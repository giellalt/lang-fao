# hfst.m4 - Macros to locate and utilise HFST scripts -*- Autoconf -*-
# serial 1 (gtsvn-1)
# 
# Copyright Â© 2011 Divvun/Samediggi/UiT <bugs@divvun.no>.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; version 3 of the License.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#
# As a special exception to the GNU General Public License, if you
# distribute this file as part of a program that contains a
# configuration script generated by Autoconf, you may include it under
# the same distribution terms that you use for the rest of that program.

# gt_PROG_HFST([MIN-VERSION])
#
# Check to see that workable HFST installation exists near $PATH and is new
# enough to support gt building.
#
# MIN-VERSION should be a version vector with one to three digit series
#   separated by full stops. A default is 3.3 since it introduced ability to
#   check the current version.
# ---------------------------
AC_DEFUN([gt_PROG_HFST],
[AC_PATH_PROG(HFST_INFO, hfst-info, false, $PATH$PATH_SEPARATOR$with_hfst)
_gt_hfst_min_version=m4_default([$1], [3.3])
AC_MSG_CHECKING([hfst is at least $_gt_hfst_min_version])
if test x$HFST_INFO != xfalse; then
    if $HFST_INFO --atleast-version=$_gt_hfst_min_version ; then
        gt_prog_hfst=yes
        AC_MSG_RESULT([yes])
    else
        gt_prog_hfst=no
        AC_MSG_RESULT([no])
    fi
else
    gt_prog_hfst=no
    AC_MSG_RESULT([unknown])
    AC_MSG_WARN([Unable to determine hfst version, assuming uninstalled or too old])
fi
]) # gt_PROG_HFST

# gt_PROG_HFST_PATH
#
# check and define paths for all hfst programs. The variables usable for HFST
# building use the name of the tool capitalised with hyphens replaced by
# underscores, to fit sh syntax.
# --------------------------------
AC_DEFUN([gt_PROG_HFST_PATH],
[AC_ARG_WITH([hfst], 
            [AS_HELP_STRING([--with-hfst=DIRECTORY],
            [define HFST binary path if not in PATH @<:@default=PATH@:>@])],
            [],
            [with_hfst=yes])
AC_PATH_PROG(HFST_COMPOSE, hfst-compose, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_COMPOSE_INTERSECT, hfst-compose-intersect, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_CONCATENATE, hfst-minimize, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_CONJUNCT, hfst-conjunct, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_DISJUNCT, hfst-disjunct, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_FST2FST, hfst-fst2fst, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_INVERT, hfst-invert, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_LEXC, hfst-lexc, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_LOOKUP, hfst-lookup, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_MINIMIZE, hfst-minimize, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_PREPROCESS_FOR_OPTIMIZED_LOOKUP_FORMAT, hfst-preprocess-for-optimized-lookup-format, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_PROC, hfst-proc, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_PROJECT, hfst-project, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_REGEXP2FST, hfst-regexp2fst, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_REPEAT, hfst-repeat, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_SPLIT, hfst-split, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_STRINGS2FST, hfst-minimize, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_SUBTRACT, hfst-minimize, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_TWOLC, hfst-twolc, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_TXT2FST, hfst-txt2fst, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_PATH_PROG(HFST_XFST, hfst-xfst, false, $PATH$PATH_SEPARATOR$with_hfst)
AC_MSG_CHECKING([whether we can enable hfst builds])
AM_CONDITIONAL([CAN_HFST], [test "x$HFST_LEXC" != xfalse])
AS_IF([test "x$HFST_LEXC" != xfalse], [AC_MSG_RESULT([yes])], 
      [AC_MSG_RESULT([no])])
]) # gt_PROG_HFST_PATH

# vim: set ft=config:
