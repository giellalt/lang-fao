! ==================== !
! Adjective morphology !
! ==================== !


LEXICON l1	! beinur
+A: adj-a ; !XXX check!
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l2	! leysur
+A: adj-a ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l3	! bíligur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l4	! kunnugur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l5	! spiltur
+A: adj-a ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l6	! brekaður
+A:%^EDH adj-a ; ! no ð deletion
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l7	! blindur
+A: adj-a ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l8	! kaldur
+A:         a-non-umlaut ;
+A:%^UUML   a-umlaut ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l9	! spakur
+A:         a-non-umlaut ;
+A:%^UUML   a-umlaut ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l10 ! drúgvur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l11 ! flógvur
+A: adj-a ; ! -gv-
+A: adj-def ; ! -gv-
+A: adjcmp1 ;

LEXICON l12 ! snjallur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l13 ! langur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A: adj-def ;

LEXICON l14 ! harður
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l15 ! blíður
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

! Check these

LEXICON l16 ! góður
+A: adj-a ;
+A: adj-def ;

LEXICON l17 ! bláur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l18 ! keddur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l19 ! nýggjur
+A: adj-a ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l20 ! stillur
+A: adj-a ;
+A: adj-def ;

LEXICON l21 ! fastur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l22 ! klæddur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A:			 a-umlaut ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l23 ! rættur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A:			 a-umlaut ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l24 ! frískur
+A: adj-a ;
+A: adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l25 ! danskur
+A:         a-non-umlaut ;
+A:%^UUML      a-umlaut ;
+A:            adj-r-gen ;
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l26 ! vakin
+A:%^EPH adj-a ;
+A: adj-def ;

LEXICON l27 ! heiðin
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;

LEXICON l28 ! lítil
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;

LEXICON l29 ! fakur
+A:%^EPH       a-non-umlaut ;
+A:%^EPH%^UUML a-umlaut ;
+A:%^EPH adj-def ;
+A: adjcmp1 ;

LEXICON l30 ! tepur
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;
+A: adjcmp1 ;

LEXICON l31 ! trupul
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;

LEXICON l32 ! búgvin
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;

LEXICON l33 ! gamal
+A:            a-m-sg-n-null ; ! gamal
+A:%^EPH       a-non-umlaut-obl ;
+A:%^EPH%^UUML a-umlaut ;
+A:%^EPH adj-def ;

LEXICON l34 ! dottin
+A:%^EPH adj-a ;
+A:%^EPH adj-def ;

LEXICON l35 ! feingin ! not complete, needs umlaut
+A:%^EPH a-non-umlaut ;
+A:%^EPH adj-def ;

LEXICON l36 ! liðugur
+A: adj-a ; !XXX check!
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l37 ! grammur
+A: adj-a ; !XXX check!
+A: adj-def ;
+A: adjcmp1 ;

LEXICON l38 ! kollutur
+A: adj-a ; !XXX check!
+A: adj-def ;

LEXICON l39 ! settur
+A: adj-a ; !XXX check!
+A: adj-def ;
+A: adjcmp1 ;

! Irregular adjectives

LEXICON STOR
+A:stór adj-a ;
+A:stór adj-def ;
+A+Cmp:størr indef-cmp-adj-decl ;
+A+Superl:størst indef-superl-adj-decl ;

LEXICON andi
+A: HOMONYM-ADJ-FORMS ;
+Adv: # ;

LEXICON HOMONYM-ADJ-FORMS
+Msc: HOMONYM-ADJ-NUM ;
+Fem: HOMONYM-ADJ-NUM ;
+Neu: HOMONYM-ADJ-NUM ;

LEXICON HOMONYM-ADJ-NUM
+Sg: HOMONYM-ADJ-CASE ;
+Pl: HOMONYM-ADJ-CASE ;

LEXICON HOMONYM-ADJ-CASE
+Nom: HOMONYM-ADJ-DEF ;
+Acc: HOMONYM-ADJ-DEF ;
+Gen: HOMONYM-ADJ-DEF ;
+Dat: HOMONYM-ADJ-DEF ;

LEXICON HOMONYM-ADJ-DEF
+Indef: # ;
+Def:   # ;


! Irregular comaratives
! =====================

LEXICON ELDRI
eldri-m ;
eldri-f ;
eldri-n ;
adj-def ;

LEXICON ELSTUR
:%>ur eldri-m ;
      eldri-f ;
      eldri-n ;
      adj-def ;

LEXICON eldri-m
+Msc: HOMONYM-ADJ-FORMS ; 

LEXICON eldri-f
+Fem: HOMONYM-ADJ-FORMS ;

LEXICON eldri-n
+Neu: HOMONYM-ADJ-FORMS ;

LEXICON indecladj 
+A: HOMONYM-ADJ-FORMS ;

! Intermediate adjectival lexica
! ==============================

LEXICON adj-a
 a-non-umlaut ;
     a-umlaut ;
     
LEXICON a-non-umlaut
 a-m-sg-n ;
 a-non-umlaut-obl ;

LEXICON a-non-umlaut-obl
 a-m-sg-a ;
 a-m-sg-g ;

 a-m-pl-n ;
 a-m-pl-a ;
 a-m-pl-g ;

 a-f-sg-a ;
 a-f-sg-d ;
 a-f-sg-g ;

 a-f-pl-n ;
 a-f-pl-a ;
 a-f-pl-d ;
 a-f-pl-g ;

 a-n-sg-n ;
 a-n-sg-a ;

 a-n-sg-g ;
 a-n-pl-g ;

LEXICON a-umlaut
 a-m-sg-d ;
 a-m-pl-d ;

 a-f-sg-n ;

 a-n-sg-d ;
 a-n-pl-n ;
 a-n-pl-a ;
 a-n-pl-d ;


LEXICON adj-r-gen
a-m-pl-g-ra ;
a-f-sg-g-rar ;
a-f-pl-g-ra ;
a-n-pl-g-ra ;

! Adjectival case lexica
! ======================


! Indefinite declension
! ---------------------

! Msc
LEXICON a-m-sg-n
+Msc+Sg+Nom+Indef:%>ur # ;

LEXICON a-m-sg-n-null
+Msc+Sg+Nom+Indef: # ;

LEXICON a-m-sg-a
+Msc+Sg+Acc+Indef:%>an # ;

LEXICON a-m-sg-d
+Msc+Sg+Dat+Indef:%>um # ;

LEXICON a-m-sg-g
+Msc+Sg+Gen+Indef:%>s  # ; 

LEXICON a-m-pl-n
+Msc+Pl+Nom+Indef:%>ir  # ; 

LEXICON a-m-pl-a
+Msc+Pl+Acc+Indef:%>ar  # ; 

LEXICON a-m-pl-d
+Msc+Pl+Dat+Indef:%>um  # ; 

LEXICON a-m-pl-g
+Msc+Pl+Gen+Indef:%>a  # ; 

LEXICON a-m-pl-g-ra
+Msc+Pl+Gen+Indef:%>ra  # ; 


! Fem

LEXICON a-f-sg-n
+Fem+Sg+Nom+Indef:%>%^UUML  # ; 

LEXICON a-f-sg-a
+Fem+Sg+Acc+Indef:%>a  # ; 

LEXICON a-f-sg-d
+Fem+Sg+Dat+Indef:%>ari  # ; 

LEXICON a-f-sg-g
+Fem+Sg+Gen+Indef:%>ar  # ; 

LEXICON a-f-sg-g-rar
+Fem+Sg+Gen+Indef:%>rar  # ; 

LEXICON a-f-pl-n
+Fem+Pl+Nom+Indef:%>ar  # ; 

LEXICON a-f-pl-a
+Fem+Pl+Acc+Indef:%>ar  # ; 

LEXICON a-f-pl-d
+Fem+Pl+Dat+Indef:%>um  # ; 

LEXICON a-f-pl-g
+Fem+Pl+Gen+Indef:%>a  # ; 

LEXICON a-f-pl-g-ra
+Fem+Pl+Gen+Indef:%>ra  # ; 


! Neu

LEXICON a-n-sg-n
+Neu+Sg+Nom+Indef:%>t  # ; 
+Adv:%>t # ;

LEXICON a-n-sg-a
+Neu+Sg+Acc+Indef:%>t  # ; 

LEXICON a-n-sg-d
+Neu+Sg+Dat+Indef:%>um  # ; 

LEXICON a-n-sg-g
+Neu+Sg+Gen+Indef:%>s  # ; 

LEXICON a-n-pl-n
+Neu+Pl+Nom+Indef:%> # ; 

LEXICON a-n-pl-a
+Neu+Pl+Acc+Indef:%> # ; 

LEXICON a-n-pl-d
+Neu+Pl+Dat+Indef:%>um  # ; 

LEXICON a-n-pl-g
+Neu+Pl+Gen+Indef:%>a  # ; 

LEXICON a-n-pl-g-ra
+Neu+Pl+Gen+Indef:%>ra  # ; 


! Definite declension
! -------------------

LEXICON adj-def
+Msc: adj-def-msc ;
+Fem: adj-def-fem ;
+Neu: adj-def-neu ;


! Gender tags

LEXICON adj-def-msc 
+Sg: adj-def-msc-case ;
+Pl: adj-def-pl ;

LEXICON adj-def-fem 
+Sg:  adj-def-fem-case ;
+Pl: adj-def-pl ;

LEXICON adj-def-neu 
+Sg: adj-def-neu-case ;
+Pl: adj-def-pl ;


! Case tags
 
LEXICON adj-def-msc-case
+Nom+Def:%>i # ;
+Acc+Def:%>a # ;
+Dat+Def:%>a # ;
+Gen+Def:%>a # ;

LEXICON adj-def-fem-case 
+Nom+Def:%>a # ;
+Acc+Def:%>u # ;
+Dat+Def:%>u # ;
+Gen+Def:%>u # ;

LEXICON adj-def-neu-case
+Nom+Def:%>a # ;
+Acc+Def:%>a # ;
+Dat+Def:%>a # ;
+Gen+Def:%>a # ;

LEXICON adj-def-pl
 +Pl+Nom+Def:%^UUML%>u # ;
 +Pl+Acc+Def:%^UUML%>u # ;
 +Pl+Dat+Def:%^UUML%>u # ;
 +Pl+Gen+Def:%^UUML%>u # ;
 +Adv:%>a # ; 

! Comparative
! ===========

LEXICON adjcmp1
+Cmp:ar indef-cmp-adj-decl ;
+Superl:ast indef-superl-adj-decl ;

LEXICON adjcmp2
+Cmp:r indef-cmp-adj-decl ;
+Superl:st indef-superl-adj-decl ;

! Gender

LEXICON indef-cmp-adj-decl
+Msc+Sg: indef-case ;
+Fem+Sg: indef-case ;
+Neu+Sg: indef-case ;

+Msc+Sg+Nom:%>i deftag ;
+Msc+Sg+Acc:%>a deftag ;
+Msc+Sg+Dat:%>a deftag ;
+Msc+Pl:%>u deftag ;
+Fem+Sg+Nom:%>a deftag ;
+Fem+Sg+Acc:%>u deftag ;
+Fem+Sg+Dat:%>u deftag ;
+Fem+Pl:%>u deftag ;
+Neu+Sg+Nom:%>a deftag ;
+Neu+Sg+Acc:%>a deftag ;
+Neu+Sg+Dat:%>a deftag ;
+Neu+Pl:%>u deftag ;

LEXICON indef-case
+Nom: indeftag ;
+Acc: indeftag ;
+Gen: indeftag ;
+Dat: indeftag ;

LEXICON def-case
+Nom: deftag ;
+Acc: deftag ;
+Gen: deftag ;
+Dat: deftag ;


! Definiteness

LEXICON indeftag
+Indef:%>i # ;

LEXICON deftag
+Def: # ;



! Superlative
! ===========

LEXICON indef-superl-adj-decl
+Msc+Sg: indef-superl-msc-case ;
+Fem+Sg: indef-superl-femneu-case ;
+Neu+Sg: indef-superl-femneu-case ;

LEXICON indef-superl-msc-case
+Nom: indef-superl-msc ;
+Acc: indef-superl-msc ;
+Gen: indef-superl-msc ;
+Dat: indef-superl-msc ;

LEXICON indef-superl-femneu-case
+Nom: indef-superl-femneu ;
+Acc: indef-superl-femneu ;
+Gen: indef-superl-femneu ;
+Dat: indef-superl-femneu ;



LEXICON indef-superl-msc
+Indef:%>ur # ;

LEXICON indef-superl-femneu
+Indef: # ;

! fjerna 17.5.
! +Msc+Sg+Def:%>i    # ; !XXX check.
! +Fem+Sg+Def:%>a    # ;
! +Neu+Sg+Def:%>a    # ;



