# ===================== #
# Faroese disambiguator #
# ===================== #




# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<¶>";



# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

LIST N = N ;
LIST V = V ;
LIST A = A ;
LIST Prop = Prop ;
LIST Adv = Adv ;
LIST Num = Num ;
LIST CC = CC ;
LIST CS = CS ;
LIST Interj = Interj ;
LIST Abbr = Abbr ;
LIST ACR = ACR ;
LIST Pr = Pr ;
LIST Pron = Pron ;
LIST Pers = Pers ;
LIST Dem = Dem ;
LIST Det = Det ;
LIST Refl = Refl ;
LIST Recipr = Recipr ;
LIST Poss = Poss ;
LIST Interr = Interr ;
LIST Nom = Nom ;
LIST Acc = Acc ;
LIST Gen = Gen ;
LIST Dat = Dat ;
LIST Msc = Msc ;
LIST Fem = Fem ;
LIST Neu = Neu ;
LIST Sg = Sg ;
LIST Pl = Pl ;
LIST Def = Def ;
LIST Indef = Indef ;
LIST Cmp = Cmp ;
LIST Superl = Superl ;
LIST Prs = Prs ;
LIST Prt = Prt ;
LIST 1Sg = 1Sg ;
LIST 2Sg = 2Sg ;
LIST 3Sg = 3Sg ;
LIST Inf = Inf ;
LIST PrfPrc = PrfPrc ;
LIST PrsPrc = PrsPrc ;
LIST Imp = Imp ;
LIST Cmpnd = Cmpnd ;
LIST CLB = CLB ;
LIST PUNCT = PUNCT ;
LIST LEFT = LEFT ;
LIST RIGHT = RIGHT ;




LIST TAG = N V A Adv CC CS Interj Pr Pron Pers Det Refl Recipr Poss Nom Acc Gen
Dat Msc Fem Neu Sg Pl Def Indef Cmp Sup Prs Prt 1Sg 2Sg 3Sg Inf PrfPrc
PrsPrc Sup Imp CLB PUNCT LEFT RIGHT ;

# Sets
# ====

# Verb sets

LIST AUX = "vera" "verða" ;

LIST VFIN = Prs Prt Imp ;
LIST Ind = Prs Prt ;

# Number sets

SET NUMBERS = Num - ("eitt") - ("1") ;
# Preposition sets

LIST ACCPREP = "aftan" "aftanvert" "ábeint" "áraka" "foruttan" "gjøgnum" "hóast" "inntil" "ígjøgnum
íkring" "kring" "niðan" "oman" "síðan" "umframt" "umhvørvis" "umkring" "báðumegin
hasumegin" "hvørgumegin" "høgrumegin" "sínumegin" "skeivumegin" "somumegin
vinstrumegin" "øðrumegin" "eystan" "norðan" "sunnan" "vestan" ;

LIST DATPREP = "hjá" "með" "á" "aftrat" "afturat" "afturímóti" "andstøðis" "andsýnis" "mótvegis" "nær" "sambært" "viðvíkjandi" ;

LIST ACCDATPREP = "í" "á" "undir" "yfir" "eftir" "fyri" "við" "frá" ;

LIST ACCGENPREP = "innan" "uttan" "millum" "til" "vegna" ;

LIST ACCDATGENPREP = "at" "av" "hjá" "móti" "um" "undan" "úr" ;


SET SOMEACCPREP = ACCPREP | ACCDATPREP | ACCGENPREP | ACCDATGENPREP ;

SET SOMEDATPREP = DATPREP | ACCDATPREP | ACCDATGENPREP ;

SET SOMEGENPREP = ACCGENPREP | ACCDATGENPREP ;

# Boundary sets

SET S-BOUNDARY = CS | Interr ;


# Complementary set

# Case sets

SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

#SET NO

SET PRE-N =  A | Det | (N Gen) | Num ;

SET WORD = N | V | A | Pr | Pron | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  ("\,") | ("\:") | ("\;") ;
SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;
SET NOT-A = WORDMARK - A ;

# Syntax tags

LIST @SUBJ = @SUBJ ;
LIST @OBJ = @OBJ ;
LIST @N> = @N> ;
LIST @N< = @N< ;
LIST @SPRED = @SPRED ;
LIST @VFIN = @VFIN ;
LIST @ADVL = @ADVL ;
LIST @P< = @P< ;
LIST @CC = @CC ;
#LIST @CC-VP = @CC-VP ;
LIST @X = @X ;

LIST SYNTAG = (@CC) (@CS) (@SUBJ)  (@OBJ)  (@N>)  (@N<)  (@SPRED)  (@VFIN)  (@ADVL)   (@P<) ;

# ======== #
  MAPPINGS #
# ======== #

ADD (@CC) TARGET CC ;
ADD (@CS) TARGET CS ;
ADD (@SPRED) TARGET (N Nom) OR (A Nom) IF (*-1 AUX BARRIER NPNHA LINK -1 Nom);
MAP (@SUBJ) TARGET (N Nom) OR (Num Nom) OR (Pron Nom);

MAP (@OBJ) TARGET (N Acc) OR (Num Acc) OR (Pron Acc) IF (*-1 V BARRIER Pr);
MAP (@IOBJ) TARGET (N Dat) OR (Num Dat) OR (Pron Dat) IF (*1 (@OBJ) BARRIER NPNHA);
MAP (@P<) TARGET (N Dat) IF (*-1 SOMEDATPREP BARRIER NPNHA);

MAP (@P<) TARGET (Acc) IF (*-1 SOMEACCPREP BARRIER NPNHA);
MAP (@P<) TARGET (Gen) IF (*-1 SOMEGENPREP BARRIER NPNHA);

MAP (@N>) TARGET (N Gen) IF (*1 N BARRIER V OR Adv);

MAP (@N>) TARGET (A Nom) OR (Dem Nom) IF (*1 (N Nom) OR (Num Nom) BARRIER NPNHA);
MAP (@N>) TARGET (A Acc) OR (Dem Acc) IF (*1 (N Acc) OR (Num Acc) BARRIER NPNHA);
MAP (@N>) TARGET (A Dat) OR (Dem Dat) IF (*1 (N Dat) OR (Num Dat) BARRIER NPNHA);
MAP (@N>) TARGET (A Gen) OR (Dem Gen) IF (*1 (N Gen) OR (Num Gen) BARRIER NPNHA);

MAP (@VFIN) TARGET (V Prs) OR (V Prt) OR (V Imp);

MAP (@ADVL) TARGET Pr ;

MAP (@N<) TARGET Poss IF (0 Nom)(-1 (N Nom));
MAP (@N<) TARGET Poss IF (0 Acc)(-1 (N Acc));
MAP (@N<) TARGET Poss IF (0 Dat)(-1 (N Dat));
MAP (@N<) TARGET Poss IF (0 Gen)(-1 (N Gen));

MAP (@ADVL) TARGET Adv ;

MAP (@X) TARGET WORD ;

# =========== #
  CONSTRAINTS #
# =========== #

# No-syntax early
REMOVE (@X)  IF (0 SYNTAG);


# POS disambiguation
# ==================

# Pron Pers or Dem

# Det

REMOVE (Num) IF (0 ("ein" Det))(*1 N BARRIER NPNHA);
	## eina mynd

# Specific lexemes
# ================

SELECT Pr IF (0 ("á"))(1 Dat OR Acc);
SELECT Pr IF (0 ("til"))(*1 (N Gen) BARRIER NPNHA);
SELECT CS IF (0 ("at"))(1C Inf);


REMOVE Dem IF (0 Pers)(NOT 1 NPNHA OR N);

# Adverb

SELECT Adv IF (-1 DATPREP)(*1 Dat BARRIER NPNHA);


# NP internal constraints
# =======================

# Determiner disambiguation

REMOVE Neu IF (0 (Pron Dem Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Dem Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Neu);

REMOVE Msc IF (0 (Pron Dem Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Dem Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Msc);

REMOVE Fem IF (0 (Pron Dem Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Dem Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Fem);


REMOVE Neu IF (0 (Pron Det Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (Pron Det Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Neu);

REMOVE Msc IF (0 (Pron Det Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (Pron Det Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Msc);

REMOVE Fem IF (0 (Pron Det Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (Pron Det Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Fem);

# Case disambiguation
SELECT Nom IF (0 (Pron Dem))(*1C Nom BARRIER NPNHA);
SELECT Acc IF (0 (Pron Dem))(*1C Acc BARRIER NPNHA);
SELECT Dat IF (0 (Pron Dem))(*1C Dat BARRIER NPNHA);
SELECT Gen IF (0 (Pron Dem))(*1C Gen BARRIER NPNHA);
						  
REMOVE Nom IF (0 (Pron Dem))(*1 N    BARRIER NPNHA LINK NOT 0 Nom);
REMOVE Acc IF (0 (Pron Dem))(*1 N    BARRIER NPNHA LINK NOT 0 Acc);
REMOVE Dat IF (0 (Pron Dem))(*1 N    BARRIER NPNHA LINK NOT 0 Dat);
REMOVE Gen IF (0 (Pron Dem))(*1 N    BARRIER NPNHA LINK NOT 0 Gen);

SELECT Nom IF (0 (Pron Det))(*1C Nom BARRIER NPNHA);
SELECT Acc IF (0 (Pron Det))(*1C Acc BARRIER NPNHA);
SELECT Dat IF (0 (Pron Det))(*1C Dat BARRIER NPNHA);
SELECT Gen IF (0 (Pron Det))(*1C Gen BARRIER NPNHA);
						  
REMOVE Nom IF (0 (Pron Det))(*1 N    BARRIER NPNHA LINK NOT 0 Nom);
REMOVE Acc IF (0 (Pron Det))(*1 N    BARRIER NPNHA LINK NOT 0 Acc);
REMOVE Dat IF (0 (Pron Det))(*1 N    BARRIER NPNHA LINK NOT 0 Dat);
REMOVE Gen IF (0 (Pron Det))(*1 N    BARRIER NPNHA LINK NOT 0 Gen);


# Adjective disambiguation
# ------------------------

REMOVE Neu IF (0 (A Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Neu);
REMOVE Neu IF (0 (A Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Neu);
														       
REMOVE Msc IF (0 (A Nom))(*1 (N Fem Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Acc))(*1 (N Fem Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Gen))(*1 (N Fem Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Dat))(*1 (N Fem Dat) BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Msc IF (0 (A Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Msc);
														       
REMOVE Fem IF (0 (A Nom))(*1 (N Msc Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Nom))(*1 (N Neu Nom) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Acc))(*1 (N Msc Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Acc))(*1 (N Neu Acc) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Gen))(*1 (N Msc Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Gen))(*1 (N Neu Gen) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Dat))(*1 (N Msc Dat) BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Fem IF (0 (A Dat))(*1 (N Neu Dat) BARRIER NPNHA LINK NOT 0 Fem);

# Case disambiguation
SELECT Nom IF (0 A)(*1C Nom BARRIER NPNHA)(NOT 0 V);
SELECT Acc IF (0 A)(*1C Acc BARRIER NPNHA)(NOT 0 V);
SELECT Dat IF (0 A)(*1C Dat BARRIER NPNHA)(NOT 0 V);
SELECT Gen IF (0 A)(*1C Gen BARRIER NPNHA)(NOT 0 V);

REMOVE Nom IF (0 A)(*1 N    BARRIER NPNHA LINK NOT 0 Nom);
REMOVE Acc IF (0 A)(*1 N    BARRIER NPNHA LINK NOT 0 Acc);
REMOVE Dat IF (0 A)(*1 N    BARRIER NPNHA LINK NOT 0 Dat);
REMOVE Gen IF (0 A)(*1 N    BARRIER NPNHA LINK NOT 0 Gen);


# Numeral disambiguation

# Ordinals
REMOVE (A Sg @N>) IF (1 (Num Sg));
	## fyrstu tríggjar mánðirnar.

# Gender disamb of numerals
SELECT Msc IF (0 Num)(*1C (N Msc) BARRIER A);
SELECT Fem IF (0 Num)(*1C (N Fem) BARRIER A);
SELECT Neu IF (0 Num)(*1C (N Neu) BARRIER A);
	## eit sindur

# Case disamb of numerals
SELECT Nom IF (0 Num)(*1C (N Nom) BARRIER A);
SELECT Acc IF (0 Num)(*1C (N Acc) BARRIER A);
SELECT Dat IF (0 Num)(*1C (N Dat) BARRIER A);
SELECT Gen IF (0 Num)(*1C (N Gen) BARRIER A);

# Perhaps also rules that remove Neu if Msc or Fem, etc.


# Noun disambiguation
# -------------------

SELECT Msc IF (-1C (A Msc))(0 N);
SELECT Fem IF (-1C (A Fem))(0 N);
SELECT Neu IF (-1C (A Neu))(0 N);

SELECT Nom IF (-1C (A Nom))(0 N);
SELECT Acc IF (-1C (A Acc))(0 N);
SELECT Dat IF (-1C (A Dat))(0 N);
SELECT Gen IF (-1C (A Gen))(0 N);

SELECT Sg IF (-1C (A Sg))(0 N);
SELECT Pl IF (-1C (A Pl))(0 N);

# Poss disambiguation

REMOVE Msc IF (-1 N LINK NOT 0 Msc)(0 Poss);
REMOVE Fem IF (-1 N LINK NOT 0 Fem)(0 Poss);
REMOVE Neu IF (-1 N LINK NOT 0 Neu)(0 Poss);

REMOVE Msc IF (0 Poss)(*1 N BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Fem IF (0 Poss)(*1 N BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Neu IF (0 Poss)(*1 N BARRIER NPNHA LINK NOT 0 Neu);

# Number disambiguation
REMOVE Pl IF (*-1 (Num Sg) OR (Dem Sg) BARRIER NOT-A)(0 (N Sg));
# Here, we need vislcg3 and variable notation!

# PP disambiguation
# =================

# P or not?

# at
REMOVE Pr IF (0 ("at"))(NOT *1 Acc OR Gen OR Dat BARRIER V);
SELECT CS IF (0 ("at"))(*1 Nom BARRIER NPNHA LINK *1 V BARRIER OBL);

# til
SELECT Pr IF (0 ("til"))(*1 N OR NPNH OR ("at") BARRIER NPNHA);

# Case within PP phrases

SELECT Acc IF (*-1 ACCPREP BARRIER NOTACC);
REMOVE Nom IF (*-1 DATPREP BARRIER NOTDAT);
REMOVE Acc IF (*-1 DATPREP BARRIER NOTDAT);
REMOVE Gen IF (*-1 DATPREP BARRIER NOTDAT);
REMOVE Gen IF (*-1 ACCDATPREP BARRIER NOTACCDAT);
REMOVE Nom IF (*-1 ACCDATPREP BARRIER NOTACCDAT);



# VP disambiguation
# =================

# V or A

REMOVE (A @N>) IF (0 V)(NOT 1 N OR A);
# Alternatively: Demand Nom to the left or Num to the right.

REMOVE A IF (0 ("vera"))(1 (N Sg));
	## Enskt mál er móðurmál.

 
# Infinitive

SELECT Inf IF (-1 ("at"));

# Imperative

REMOVE Imp IF (-1 Adv OR Nom)(0 Prs OR Prt);

REMOVE Imp IF (0 N)(1 CC)(*2 N BARRIER NPNHA);
	## Stýrið og stjórnin hjá...

REMOVE (Imp Pl) IF (0 (N Neu))(1 VFIN);
	## Húsið er stórt.

# Present singular

SELECT (V Ind 3Sg) IF (-1 (N Prop Nom) OR (N Sg Nom) LINK NOT *-1 CC BARRIER NPNHA)(1 (Pron Refl Acc)) ;
REMOVE (V Ind 2Sg) IF (*-1 (N Sg Nom @SUBJ) BARRIER NOT-ADV);
SELECT (V Ind 3Sg) IF (-1 (N Prop Nom) OR (N Sg Nom) LINK NOT *-1 CC BARRIER NPNHA OR S-BOUNDARY) ;

# Present plural

SELECT (V Pl) IF (-1 (N Pl Nom));

# Genitive
# ========

# Genitive is marginal in faroese. The strategy should be : 
# 1. Write rules selecting genitive. (such rules are still forthcoming)

SELECT Gen IF (*-1 ACCGENPREP BARRIER NPNHA)(NOT 0 Acc);

# 2. Remove the remaining genitives. 

REMOVE Gen ;


# Pronound disambiguation
# =======================

REMOVE ("hannur") IF (0 ("hann" Nom))(1 (V 3Sg));
       ## Han var her.

# Syntactic disambiguation
# ========================

REMOVE (@SPRED) IF (NOT *-1 (@SUBJ))(NOT *1 (@SUBJ));
REMOVE (@OBJ) IF (*-1 Pr BARRIER NPNHA);
REMOVE (@IOBJ) IF (*-1 Pr BARRIER NPNHA);

REMOVE (@P<) IF (*-1 BOS OR (N Acc) OR (N Dat) OR (N Nom) OR V OR CLB OR CS BARRIER Pr);

REMOVE (@N>) IF (*-1 Pr BARRIER NPNHA);

SELECT (Sg @SPRED) IF (*-1 AUX BARRIER NPNHA LINK -1 (Sg Nom));

SELECT (Pl @SPRED) IF (*-1 AUX BARRIER NPNHA LINK -1 (Pl Nom));

REMOVE (@SUBJ) IF (*-1 VFIN BARRIER S-BOUNDARY LINK -1 (@SUBJ));



# PPs

REMOVE (@SUBJ) IF (*-1C Pr BARRIER NPNH)(1 (@P<));
REMOVE (@OBJ) IF (*-1C Pr BARRIER NPNH)(1 (@P<));
REMOVE (@ADVL) IF (*-1C Pr BARRIER NPNH)(1 (@P<));
REMOVE (@SPRED) IF (*-1C Pr BARRIER NPNH)(1 (@P<));
REMOVE (@OPRED) IF (*-1C Pr BARRIER NPNH)(1 (@P<));

	## av tilvild

# Case disambiguation

#SELECT Acc IF (*-1 ACCPREP BARRIER NPNHA)(0 (@P<));
#SELECT Dat IF (*-1 DATPREP BARRIER NPNHA)(0 (@P<));
#REMOVE Nom IF (*-1 ACCGENPREP BARRIER NPNHA)(0 (@P<));
#REMOVE Dat IF (*-1 ACCGENPREP BARRIER NPNHA)(0 (@P<));
#REMOVE Nom IF (*-1 ACCDATPREP BARRIER NPNHA)(0 (@P<));
#REMOVE Gen IF (*-1 ACCDATPREP BARRIER NPNHA)(0 (@P<));

# Number SPRED disambiguation





#REMOVE (@X)  IF (0 SYNTAG);
