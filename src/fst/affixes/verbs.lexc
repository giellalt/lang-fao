



! =============== !
!! !!!Verb morphology !
! =============== !

!LEXICON sx ! this is an ad hoc lexicon waiting for classification.
!s1 ;

LEXICON s1
!! __@LEXNAME@__ nevna
               standard_ir ; ! Verbal forms
         :%>d p7          ; ! Adjectival forms


LEXICON s2
!! __@LEXNAME@__ keypa
  standard_ir_t ;
  p39           ;

LEXICON SETA
!! __@LEXNAME@__
 +V+Ind+Prs+2Sg: urPass  ;
 +V+Ind+Prs+3Sg: urPass  ;
                     s2 ;


LEXICON s3
!! __@LEXNAME@__ leiða
               standard_ir ;
 +V+PrfPrc:%>d p18         ;

LEXICON s4
!! __@LEXNAME@__ frøa
               inf        ;
               pres_ir    ;
               prt_dd     ;
               imp_prsptc ;
               sup_tt     ;
 +V+PrfPrc:%>d p7         ;

LEXICON s5
!! __@LEXNAME@__ senda
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s6
!! __@LEXNAME@__ hirða
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s7
!! __@LEXNAME@__ gista
  standard_ir_t ;
  p5            ;


LEXICON s8
!! __@LEXNAME@__ kenna
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s9
!! __@LEXNAME@__ klippa
  standard_ir_t ;
  p5            ;


LEXICON s10
!! __@LEXNAME@__ fylgja
               jinf       ;
               pres_ir_sg ;
          :j   pres_pl    ;
               sup        ;
               prt_d      ;
 +V+PrfPrc:%>d p7         ;
               imp        ;
          :j   prsptc     ;

LEXICON s11 !
!! __@LEXNAME@__  roykja
               jinf       ;
               pres_ir_sg ;
          :j   pres_pl    ;
               sup        ;
               prt_t      ;
 +V+PrfPrc:%>t p5         ;
               imp        ;
          :j   prsptc     ;


LEXICON s12 !
!! __@LEXNAME@__ boyggja
               jinf       ;
               pres_ir_sg ;
          :j   pres_pl    ;
               sup        ;
               prt_d      ;
 +V+PrfPrc:%>d p7         ;
               imp        ;
          :j   prsptc     ;


LEXICON s13 !
!! __@LEXNAME@__ søkkja
    jinf       ;
    pres_ir_sg ;
 :j pres_pl    ;
    sup_t      ;
    prt_t      ;
    p5         ;
    imp        ;
 :j prsptc     ;


LEXICON s14 !
!! __@LEXNAME@__ heingja
                    jinf       ;
          :%^EIO    jinf       ;
                    pres_ir_sg ;
          :%^EIO    pres_ir_sg ;
          :j        pres_pl    ;
          :%^EIO%>j pres_pl    ;
          :%^EIO    prt_d      ;
          :%^EIO    imp        ;
                    imppl      ;
          :%^EIO    sup        ;
 +V+PrfPrc:%^EIO%>d p7         ;
          :j        prsptc     ;
          :%^EIO%>j prsptc     ;


LEXICON s15 !
!! __@LEXNAME@__ skeinkja
           jinf       ;
 :%^EIO    jinf       ;
           pres_ir_sg ;
 :%^EIO    pres_ir_sg ;
 :j        pres_pl    ;
 :%^EIO%>j pres_pl    ;
 :%^EIO    prt_t      ;
 :%^EIO    imp        ;
           imppl      ;
 :%^EIO    sup_t      ;
           p5         ;
 :j        prsptc     ;
 :%^EIO%>j prsptc     ;

LEXICON s15_2 !
!! __@LEXNAME@__ steikja
        jinf       ;
        pres_ir_sg ;
 :j     pres_pl    ;
 :%^EIO prt_t      ;
        imp        ;
 :%^EIO sup_t      ;
        p5         ;
 :j     prsptc     ;

LEXICON s16 !
!! __@LEXNAME@__ flekja
        jinf       ;
        pres_ir_sg ;
 :j     pres_pl    ;
 :%^WVV prt_d      ;
        imp        ;
 :j     prsptc     ;
 :%^WVV sup        ;
        p5         ;

LEXICON s17 !
!! __@LEXNAME@__ berja
                 jinf     ;
                 pres_jir ;
          :%^WVV prt_d    ;
          :j     impsg    ;
 +V+Imp+Pl:jið   #     ;
          :j     prsptc   ;
          :%^WVV sup      ;
                 p7       ;

LEXICON s18 !
!! __@LEXNAME@__ krevja
                 jinf      ;
                 pres_ur_j ;
          :%^WVV prt_d     ;
                 impsg     ;
 +V+Imp+Pl:jið   #      ;
          :j     prsptc    ;
          :%^WVV sup       ;
                 p7        ;

LEXICON s19 !
!! __@LEXNAME@__ dylja
                 jinf      ;
                 pres_ur_j ;
          :%^WVV prt_d     ;
          :      impsg     ;
 +V+Imp+Pl:jið   #      ;
          :j     prsptc    ;
          :%^WVV sup       ;
                 p7        ;

LEXICON s20 !
!! __@LEXNAME@__ leggja
 :egg  jinf       ;
 :egg  pres_ur_j2 ;
 :eg   prt_ði     ;
 :øg   prt_du     ;
 :egg  imp        ;
 :eggj prsptc     ;
 :ag   sup        ;
       p8         ;

LEXICON s21 !
!! __@LEXNAME@__ selja
               jinf      ;
               pres_ur_j ;
          :    prt_d     ;
          :    impsg     ;
 +V+Imp+Pl:jið #      ;
          :j   prsptc    ;
          :    sup       ;
               p7        ;

LEXICON s22 !
!! __@LEXNAME@__ ryðja
 :      jinf       ;
 :      pres_ur_j2 ;
 :%^WVV prt_d      ;
 :      imp        ;
 :j     prsptc     ;
 :%^WVV sup_tt     ;
 :      p18        ;

!LEXICON s22_1 !
!! __@LEXNAME@__ ýðja
! :ýð  jinf       ;
! :ýð  pres_ur_j2 ;
! :ýð  pres_ir_j2 ;
! :u   prt_dd     ;
! :ý   prt_dd     ;
! :ýð  imp        ;
! :ýðj prsptc     ;
! :u   sup_tt     ;
! :ý   sup_tt     ;
 !	p18        ; " Needs stem to go to p18

LEXICON s23 !
!! __@LEXNAME@__ smyrja
               :yr     jinf    ;
 +V+Ind+Prs+1Sg:%>yrji Pass    ;
 +V+Ind+Prs+2Sg:%>yr   Pass    ;
 +V+Ind+Prs+2Sg:%>yrt  Pass    ;
 +V+Ind+Prs+3Sg:%>yr   Pass    ;
  +V+Ind+Prs+Pl:%>yrja Pass    ;
               :ur     prt_d   ;
               :yr     impsg   ;
               :yrj    imppl_j ;
               :yrj    prsptc  ;
               :ur     sup     ;
                       p7      ;

LEXICON s24 !
!! __@LEXNAME@__ flysa
        inf        ;
        pres_ur    ; ! XXX not pres_null
 :%^WVV prt_d      ;
        imp_prsptc ;
 :%^WVV sup        ;
        p5         ;

LEXICON s25 !l
!! __@LEXNAME@__ iva
               inf        ;
               pres_ir    ;
               prt_d      ;
               imp_prsptc ;
               sup_a      ;
 +V+PrfPrc:%>d p6         ;

LEXICON s26 !
!! __@LEXNAME@__ plaga
             inf     ;
             pres_ar ;
             prt_d   ;
             sup     ;
 +V+Sup:%>að #    ;


LEXICON s26_1 !
!! __@LEXNAME@__ mála->máldi
             inf        ;
             pres_ar    ;
             prt_d      ;
             imp_prsptc ;
             sup        ;
 +V+Sup:%>að #       ;

LEXICON s27 !s
!! __@LEXNAME@__ páa
             inf        ;
             pres_ar    ;
             prt_dd     ;
             imp_prsptc ;
 +V+Imp+Sg:a #       ;
             sup_tt     ;
             p18        ;

LEXICON s28 !
!! __@LEXNAME@__ skaða
  inf        ;
  pres_ar    ;
  prt_d      ;
  imp_prsptc ;
  sup        ;
  p18        ;

LEXICON s29 !
!! __@LEXNAME@__ brúka
             inf        ;
             pres_ar    ;
             prt_d      ;
             imp_prsptc ;
 +V+Imp+Sg:a #       ;
             sup        ;
             p5         ;

LEXICON s30 !
!! __@LEXNAME@__ kalla
    inf     ;
    pres_ar ;
    prt_a   ;
 :a impsg   ; !deviant kalla
    imppl   ;
    prsptc  ;
    sup_a   ;
    p6      ;

LEXICON s31 !
!! __@LEXNAME@__ only gera and *gera
               :er    inf        ;
 +V+Ind+Prs+1Sg:%>eri Pass       ;
 +V+Ind+Prs+2Sg:%>ert Pass       ;
 +V+Ind+Prs+2Sg:%>er  Pass       ;
 +V+Ind+Prs+3Sg:%>er  Pass       ;
               :er    pres_pl    ;
               :er    imp_prsptc ;
               :jør   prt_d      ;
               :jør   sup        ;
                      p7         ;

LEXICON s32 !
!! __@LEXNAME@__ búgva
               :gv        inf                ;
               :gv        pres_strong_s1     ;
               :%^IUML    pres_strong_s23_t1 ;
                          prt_ð              ;
               :gv        pres_pl            ;
               :gv        imp_prsptc         ;
                          sup_in             ;
                          p32                ;

LEXICON s33 !
!! __@LEXNAME@__ rógva
               :gv        inf                ;
               :gv        pres_strong_s1     ;
               :%^IUML    pres_strong_s23_t1 ;
               :gv        pres_pl            ;
                          prt_ð              ;
               :gv        imp_prsptc         ;
                          sup_in             ;
                          p32                ;


LEXICON s34 !
!! __@LEXNAME@__ goyggja
               :ggj inf        ;
 +V+Ind+Prs+1Sg:ggi Pass       ;
 +V+Ind+Prs+2Sg:    rtPass       ;
 +V+Ind+Prs+2Sg:    rPass       ;
 +V+Ind+Prs+3Sg:    rPass       ;
               :ggj pres_pl    ;
                    prt_ð      ;
               :ggj imp_prsptc ;
         +V+Sup:%>ð #       ;




!! !!Strong verbs starting here
! ==========================

LEXICON s35 !
!! __@LEXNAME@__ bíta
                     inf        ;
                     pres_ur    ;
          :%^AB1     prt_null_s ;
          :%^AB2     prt_u_p    ;
                     imp_prsptc ;
          :%^VSH     sup_null   ;
 +V+PrfPrc:%^VSH%>in #       ;
                     p26        ;

LEXICON s36 !
!! __@LEXNAME@__ svíkja
          :        jinf       ;
                   pres_ur_j2 ;
          :%^AB1   prt_null_s ;
          :%^AB2   prt_u_p    ;
          :        imp        ;
          :j       prsptc     ;
          :%^VSH   sup_null   ;
 +V+PrfPrc:%^VSHin p26        ;


LEXICON s37 !
!! __@LEXNAME@__ bróta
        inf             ;
        pres_strong_s1  ;
 :%^AB7 pres_strong_s23 ;
 :%^AB1 prt_null_s      ;
 :%^AB4 prt_u_p         ;
        imp_prsptc      ;
 :%^AB5 sup_ið_in       ;
        p26             ;

LEXICON s38 !
!! __@LEXNAME@__ skjóta present tense
  :jót inf             ;
  :jót pres_strong_s1  ;
  :jýt pres_strong_s23 ;
  :eyt prt_null_s      ;
  :ut  prt_u_p         ;
  :jót imp_prsptc      ;
  :ot  sup_ið_in       ;
       p26             ;

!LEXICON s39/30
! :jóð s30             ; ! weak infl + common forms
!      s39             ; ! umlauted forms, XXX check.

LEXICON s39d ! bjóða
!! __@LEXNAME@__
          :jóð   inf             ;
          :jóð   pres_strong_s1  ;
          :ýð    pres_strong_s23 ;
          :eyð   prt_null_s      ;
          :uð    prt_u_p         ;
          :jóða  impsg           ;
 +V+Imp+Pl:jóðið #            ;
          :jóð   prsptc          ;
          :oð    sup_ið_in       ;
                 p26             ;

LEXICON s39s ! gjósa
!! __@LEXNAME@__
          :jós   inf             ;
          :jós   pres_strong_s1  ;
          :ýs    pres_strong_s23 ;
          :eys   prt_null_s2     ;
          :us    prt_u_p         ;
          :jósa  impsg           ;
 +V+Imp+Pl:jósið #            ;
          :jós   prsptc          ;
          :os    sup_ið_in       ;
                 p26             ;

LEXICON s39 ! fljóta
!! __@LEXNAME@__ bjóða umlaut forms
          :jót   inf             ;
          :jót   pres_strong_s1  ;
          :ýt    pres_strong_s23 ;
          :eyt   prt_null_s      ;
          :ut    prt_u_p         ;
          :jóta  impsg           ;
 +V+Imp+Pl:jótið #            ;
          :jót   prsptc          ;
          :ot    sup_ið_in       ;
                 p26             ;

LEXICON s40 !
!! __@LEXNAME@__ fúka
          inf             ;
          pres_strong_s1  ;
  :%^IUML pres_strong_s23 ;
! :%^IUML pres_ur         ;
  :%^AB1  prt_null_s      ;
  :%^AB4  prt_u_p         ;
          imp_prsptc      ;
  :%^AB5  sup_ið_in       ;
          p26             ;

LEXICON s41 !
!! __@LEXNAME@__ flúgva
               inf             ;
               pres_strong_s1  ;
 :%^IUML%^VDEL pres_strong_s23 ;
 :%^AB1%^VDEL  prt_null_s      ;
 :%^VSH%^VDEL  prt_u_p         ;
               imp_prsptc      ;
 :%^AB5%^VDEL  sup_ið_in       ;
               p26             ;

LEXICON s42 !
!! __@LEXNAME@__ klúgva
               inf             ;
               pres_strong_s1  ;
 :%^IUML%^GDEL pres_strong_s23 ;
 :%^AB1%^GDEL  prt_null_s      ;
 :%^VSH%^GDEL  prt_u_p         ;
               imp_prsptc      ;
 :%^AB5%^GDEL  sup_ið_in       ;
               p26             ;

LEXICON s44 !
!! __@LEXNAME@__ finna
        inf        ;
        pres_ur    ;
 :%^AB3 prt_null_s ;
 :%^AB4 prt_u_p    ;
        imp_prsptc ;
 :%^AB4 sup_ið_in  ;
        p26        ;

LEXICON s45 !
!! __@LEXNAME@__ binda
 :d        inf        ;
 :d        pres_ur    ;
 :t%^aAB   prt_null_s ;
 :%^AB4%>d prt_u_p    ;
 :d        imp_prsptc ;
 :%^AB4%>d sup_ið_in  ;
 :d        p26        ;

LEXICON s46 !
!! __@LEXNAME@__ stinga
              inf        ;
              pres_ur    ;
 :%^NGKK%^aAB prt_null_s ;
 :%^AB4       prt_u_p    ;
              imp_prsptc ;
 :%^NGKK      imp	     ;
 :%^AB4       sup_ið_in  ;
              p26        ;

LEXICON s47 !
!! __@LEXNAME@__ svimja
           :imj     inf        ;
           :im      pres_ur_j  ;
           :am      prt_null_s ;
           :um      prt_u_p    ;
           :im      impsg      ;
           :imj		imppl_j	   ;
           :imj		prsptc	   ;
           :om      sup_ið_in  ;
  		   :omin  	p26        ;

!LEXICON s47 ! svimja
!                   inf        ;
!                   pres_ur    ;
!          :%^aAB   prt_null_s ;
!          :%^AB4   prt_u_p    ;
!                   imp_prsptc ;
!          :%^oAB   sup_null   ;
! +V+PrfPrc:%^oABin p26        ;

LEXICON s48 !
!! __@LEXNAME@__ drekka
                 inf        ;
                 pres_ur    ;
          :%^AB3 prt_null_s ;
          :%^AB4 prt_u_p    ;
                 imp_prsptc ;
          :%^AB4 sup_ið_in  ;
 +V+PrfPrc:%>in  p26        ;

LEXICON s49 !
!! __@LEXNAME@__ detta
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :%^AB4   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_ið_in  ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s50 !
!! __@LEXNAME@__ røkka
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :        prt_null_s ;
          :%^AB4   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_ið_in  ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s51 !
!! __@LEXNAME@__ ganga
          :ang     inf             ;
          :ang     pres_strong_s1  ;
          :ong     pres_strong_s23 ; !2?
          :ekk     prt_null_s      ;
          :ing     prt_u_p         ;
          :akk	   impsg		   ;
          :ang     imp_prsptc      ;
          :ing     sup_ið_in       ;
 +V+PrfPrc:ing%>in p26             ;


LEXICON s52 !
!! __@LEXNAME@__ veva
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :%^AB2   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_ið_in  ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s53 !
!! __@LEXNAME@__ leypa
          :leyp  inf             ;
          :leyp  pres_strong_s1  ;
          :loyp  pres_strong_s23 ;
          :leyp  prt_null_s      ;
          :leyp  imp_prsptc      ;
          :lup   prt_u_p         ;
          :lop   sup_ið_in       ;
 +V+PrfPrc:lopin p26             ;

LEXICON s54 !
!! __@LEXNAME@__ bera
                   		 inf                ;
                   		 pres_strong_s1     ;
                   		 pres_strong_s23_t0 ;
  +V+Ind+Prs+2Sg:   	 Pass			    ;
          		:%^AB3   prt_null_s2        ;
         		:%^AB2   prt_u_p            ;
                	     imp_prsptc         ;
          		:%^AB5   sup_ið_in          ;
	 			:%^AB5in p26        ;

LEXICON s55 !
!! __@LEXNAME@__ fara
                  		inf                ;
                  		pres_strong_s1     ;
          		:%^IUML pres_strong_s23_t0 ;
  +V+Ind+Prs+2Sg:%^IUML Pass   			   ;
          		:%^AB3  prt_null_s2        ;
  		        :%^AB3  prt_u_p            ;
                  		sup_ið_in          ;
                  		imp_prsptc         ;
 				:%>in   p26                ;

LEXICON s56 !
!! __@LEXNAME@__ geva
                     inf        ;
                     pres_ur    ;
          :%^AB3     prt_null_s ;
          :%^AB2     prt_u_p    ;
          :%^AB6     sup_ið_in  ;
                     imp_prsptc ;
 +V+PrfPrc:%^AB6%>in p26        ;

LEXICON s57 !
!! __@LEXNAME@__ sita
                 inf        ;
                 pres_ur    ;
          :%^AB3 prt_null_s ;
          :%^AB2 prt_u_p    ;
                 sup_ið_in  ;
                 imp_prsptc ;
 +V+PrfPrc:%>in  p26        ;

LEXICON s58 !
!! __@LEXNAME@__ mala
                  inf             ;
                  pres_strong_s1  ;
          :%^IUML pres_strong_s23 ;
          :%^AB3  prt_null_s      ;
          :%^AB3  prt_u_p         ;
                  imp_prsptc      ;
                  sup_ið_in       ;
 +V+PrfPrc:%>in   p26             ;

LEXICON s59 !
!! __@LEXNAME@__ stjala
          :jal   inf             ;
          :jal   pres_strong_s1  ;
          :jel    pres_strong_s23 ;
          :jól   prt_null_s      ;
          :jól   prt_u_p         ;
          :jal   imp_prsptc      ;
          :ol    sup_ið_in       ;
          :jol   sup_ið_in       ;
          		 p26			 ;

LEXICON s60 !
!! __@LEXNAME@__ taka, aka
                   inf             ;
                   pres_strong_s1  ;
          :%^IUML  pres_strong_s23 ;
          :%^AB3   prt_null_s      ;
          :%^AB3   prt_u_p         ;
                   imp_prsptc      ;
          :%^AB6   sup_ið_in       ;
 +V+PrfPrc:%^AB6in p26             ;

LEXICON s61 !
!! __@LEXNAME@__ halda
          :hald   inf             ;
          :hald   pres_strong_s1  ;
          :held   pres_strong_s23 ;
          :helt   prt_null        ;
          :hild   prt_u_p         ;
          :hild   sup_ið_in       ;
          :halt   impsg			  ;
          :hald   imp_prsptc      ;
 		  :hildin p26             ;

LEXICON s62 !
!! __@LEXNAME@__ sova
          :sov  inf             ;
          :sov  pres_strong_s1  ;
          :svev pres_strong_s23 ;
          :svav prt_null_s      ;
          :svóv prt_u_p         ;
          :sov  sup_ið_in       ;
          :sov  imp_prsptc      ;
 +V+PrfPrc:%>in p26             ;

LEXICON s63 !
!! __@LEXNAME@__ koma
                   inf             ;
                   pres_strong_s1  ;
          :%^eIUML pres_strong_s23 ;
                   prt_null_s      ;
                   prt_u_p         ;
                   sup_ið_in       ;
                   imp_prsptc      ;
 +V+PrfPrc:%>in    p26             ;

LEXICON s64 !
!! __@LEXNAME@__ lata
                   inf             ;
                   pres_strong_s1  ;
          :%^IUML  pres_strong_s23 ;
          :%^   prt_null_s      ;
          :%^AB2   prt_u_p         ;
                   sup_ið_in       ;
                   imp_prsptc      ;
 +V+PrfPrc:%>in    p26             ;

LEXICON s64_1 !
!! __@LEXNAME@__ láta
                   inf             ;
                   pres_strong_s1  ;
          :%^eIUML pres_strong_s23 ;
          :%^AB3   prt_null_s      ;
          :%^AB2   prt_u_p         ;
                   sup_ið_in       ;
                   imp_prsptc      ;
 +V+PrfPrc:%>in    p26             ;

LEXICON s65 !
!! __@LEXNAME@__ standa
 :and inf             ;
 :and pres_strong_s1  ;
 :end pres_strong_s23 ;
 :óð  prt_null_s      ;
 :óð  prt_u_p         ;
 :að  sup_ið_in       ;
 :and imp_prsptc      ;
 :att impsg           ;

LEXICON s66 !
!! __@LEXNAME@__ biðja
                 jinf       ;
                 pres_ur_j2 ;
          :%^AB3 prt_null_s ;
          :%^AB2 prt_u_p    ;
				 imp		;
		  :j	 prsptc	 	;
                 sup_ið_in  ;
 		  :%>in  p26        ;

LEXICON s67 !
!! __@LEXNAME@__ draga
          :ag       inf             ;
          :ag       pres_strong_s1  ;
          :eg       pres_strong_s23 ;
          :ó        prt_null_s      ; !dróg, should be dró
          :óg       prt_u_p         ;
          :ig       sup_ið_in       ; ! %^AB2
          :ag       imp_prsptc      ;
 		  :%>igin  	p26        		;

LEXICON s68 !
!! __@LEXNAME@__ hvørva
          :vørv    inf        ;
          :vørv    pres_ur    ;
          :varv    prt_null_s ;
          :vurv    prt_u_p    ;
          :orv     sup_ið_in  ;
   +Use/NG:vorv    sup_ið_in  ;
          :vørv    imp_prsptc ;
		  :orv%>in p26        ;

LEXICON s69 !
!! __@LEXNAME@__ sláa
          		inf                	;
          		pres_strong_s1     	;
  :%^IUML 		pres_strong_s23_t1 	;
  :%^AB2  		prt_null_s         	;
  :g%^AB2 	prt_u_p		  	   	;
  :%^eIUML%>r	prt_d				;
  				imp_prsptc			;
  :g^AB6		sup_ið_in			;
  
! :slerdi prt_null_s         ;
! :slóg   prt_u_p            ;
! :slerd  prt_u_p            ;
! :sligi  sup_a              ;
! :sligin p26                ;

LEXICON s70 !
!! __@LEXNAME@__ siga
           :sig    inf        ;
           :sig    pres_ur    ;
           :segði  prt_null   ;
           :søgd   prt_u_p    ;
           :sig    imp_prsptc ;
           :sag    sup        ;
  +V+PrfPrc:sagd%> p8         ;

LEXICON s71 !
!! __@LEXNAME@__ skerja
            	jinf       		;
          :j  	pres_ir    		;
          :  	prt_d      		;
          	 	sup      		;
          :j  	imp_prsptc_j 	;
 		  :d 	p7         		;

LEXICON s72 !
!! __@LEXNAME@__ eta
          :et     inf        ;
          :et     pres_ur    ;
          :át     prt_null_s ;
          :ót     prt_u_p    ;
          :et     sup_ið_in  ;
          :et     imp_prsptc ;
 		  :et%>in p26        ;

LEXICON s73 !
!! __@LEXNAME@__ læa
          :læ   inf        			;
          :læ	pres_strong_s1 		;
          :læ   pres_strong_s23_t1  ;
          :læ   prt_ði 				;
          :ló	prt_null			;
          :læð	prt_u_p				;
          :læð  sup_ið_in      		;
          :læ   imp_prsptc 			;
		  :læin p26        			;
 

!! !!Ad hoc, irregular
! =================

LEXICON BLÍVA
!! __@LEXNAME@__
         +V+Inf:blíva    Pass  ;
 +V+Ind+Prs+1Sg:blívi    #  ;
 +V+Ind+Prs+2Sg:blív     urPass  ;
 +V+Ind+Prs+3Sg:blív     urPass  ;
  +V+Ind+Prs+Pl:blíva    Pass  ;
 +V+Ind+Prt+1Sg:bleiv    Pass  ;
 +V+Ind+Prt+2Sg:bleiv    #  ;
 +V+Ind+Prt+2Sg:bleiv    stPass  ;
 +V+Ind+Prt+3Sg:bleiv    Pass  ;
  +V+Ind+Prt+Pl:blivu    Pass  ;
      +V+Imp+Sg:blív     #  ;
      +V+Imp+Pl:blívið   #  ;
      +V+PrsPrc:blívandi VANDI ;
      +V+PrfPrc:blivin   #  ;
         +V+Sup:blívið   #  ;

LEXICON EIGA
!! __@LEXNAME@__
         +V+Inf:eiga    #  ;
 +V+Ind+Prs+1Sg:eigi    Pass  ;
 +V+Ind+Prs+2Sg:eig     urPass ;
 +V+Ind+Prs+3Sg:eig     urPass ;
  +V+Ind+Prs+Pl:eiga    Pass  ;
 +V+Ind+Prt+1Sg:átti    Pass  ;
 +V+Ind+Prt+2Sg:átti    Pass  ;
 +V+Ind+Prt+3Sg:átti    Pass  ;
  +V+Ind+Prt+Pl:áttu    Pass  ;
      +V+PrsPrc:eigandi VANDI ;
      +V+PrfPrc:áttur   #  ;
         +V+Sup:átt     #  ;

LEXICON EITA
!! __@LEXNAME@__
         +V+Inf:eita    #  ;
 +V+Ind+Prs+1Sg:eiti    Pass  ;
 +V+Ind+Prs+2Sg:eit     urPass ;
 +V+Ind+Prs+2Sg:eit     urPass ;
  +V+Ind+Prs+Pl:eita    Pass  ;
 +V+Ind+Prt+1Sg:æt      Pass  ;
 +V+Ind+Prt+2Sg:æt      Pass  ;
 +V+Ind+Prt+3Sg:æt      Pass  ;
  +V+Ind+Prt+Pl:itu     Pass  ;
      +V+PrsPrc:itandi  VANDI ; ! XXX check
      +V+PrfPrc:itin    #  ;
         +V+Sup:itið    #  ;

LEXICON GRÁTA
!! __@LEXNAME@__
         +V+Inf:áta    #  ;
 +V+Ind+Prs+1Sg:áti    Pass  ;
 +V+Ind+Prs+2Sg:æt     urPass ;
 +V+Ind+Prs+3Sg:æt     urPass ;
  +V+Ind+Prs+Pl:áta    Pass  ;
 +V+Ind+Prt+1Sg:æt     Pass  ;
 +V+Ind+Prt+2Sg:æt     Pass  ;
 +V+Ind+Prt+2Sg:ætst   #  ;
 +V+Ind+Prt+3Sg:æt     Pass  ;
  +V+Ind+Prt+Pl:ótu    Pass  ;
      +V+Imp+Sg:át     #  ;
      +V+Imp+Pl:átið   #  ;
      +V+PrsPrc:átandi VANDI ;
      +V+PrfPrc:átin   #  ;
         +V+Sup:átið   #  ;

LEXICON HAVA
!! __@LEXNAME@__
         +V+Inf:hava     #  ;
 +V+Ind+Prs+1Sg:havi     Pass  ;
 +V+Ind+Prs+2Sg:hev      urPass ;
 +V+Ind+Prs+3Sg:hev      urPass ;
  +V+Ind+Prs+Pl:hava     Pass  ;
 +V+Ind+Prt+1Sg:hevði    Pass  ;
 +V+Ind+Prt+2Sg:hevði    Pass  ;
 +V+Ind+Prt+3Sg:hevði    Pass  ;
  +V+Ind+Prt+Pl:høvdu    #  ;
      +V+Imp+Sg:hav      #  ;
      +V+Imp+Pl:havið    #  ;
      +V+PrsPrc:havandi  VANDI ;
      +V+PrfPrc:havd     urPass ;
         +V+Sup:havt     #  ;

LEXICON KUNNA
!! __@LEXNAME@__
         +V+Inf:kunna  # ;
 +V+Ind+Prs+1Sg:kann   # ;
 +V+Ind+Prs+2Sg:kanst  # ;
 +V+Ind+Prs+3Sg:kann   # ;
  +V+Ind+Prs+Pl:kunnu  # ;
  +V+Ind+Prs+Pl:kunna  # ;
 +V+Ind+Prt+1Sg:kundi  # ;
 +V+Ind+Prt+2Sg:kundi  # ;
 +V+Ind+Prt+3Sg:kundi  # ;
  +V+Ind+Prt+Pl:kundu  # ;
         +V+Sup:kunnað # ;

LEXICON MEGA
!! __@LEXNAME@__
         +V+Inf:mega  # ;
 +V+Ind+Prs+1Sg:má    # ;
 +V+Ind+Prs+2Sg:mást  # ;
 +V+Ind+Prs+3Sg:má    # ;
  +V+Ind+Prs+Pl:mugu  # ;
  +V+Ind+Prs+Pl:muga  # ;
 +V+Ind+Prt+1Sg:mátti # ;
 +V+Ind+Prt+2Sg:mátti # ;
 +V+Ind+Prt+3Sg:mátti # ;
  +V+Ind+Prt+Pl:máttu # ;

LEXICON MUNNA
!! __@LEXNAME@__
         +V+Inf:munna  # ;
 +V+Ind+Prs+1Sg:man    # ;
 +V+Ind+Prs+2Sg:manst  # ;
 +V+Ind+Prs+3Sg:man    # ;
  +V+Ind+Prs+Pl:munnu  # ;
  +V+Ind+Prs+Pl:munna  # ;
 +V+Ind+Prt+1Sg:mundi  # ;
 +V+Ind+Prt+2Sg:mundi  # ;
 +V+Ind+Prt+3Sg:mundi  # ;
  +V+Ind+Prt+Pl:mundu  # ;
         +V+Sup:munnað # ;

LEXICON SKULA
!! __@LEXNAME@__
         +V+Inf:skula  # ;
 +V+Ind+Prs+1Sg:skal   # ;
 +V+Ind+Prs+2Sg:skalt  # ;
 +V+Ind+Prs+3Sg:skal   # ;
  +V+Ind+Prs+Pl:skulu  # ;
  +V+Ind+Prs+Pl:skula  # ;
 +V+Ind+Prt+1Sg:skuldi # ;
 +V+Ind+Prt+2Sg:skuldi # ;
 +V+Ind+Prt+3Sg:skuldi # ;
  +V+Ind+Prt+Pl:skuldu # ;
         +V+Sup:skulað # ;

LEXICON TYKJA
!! __@LEXNAME@__
         +V+Inf:tykja   # ;
 +V+Ind+Prs+1Sg:tyki   rPass ;
 +V+Ind+Prs+2Sg:tyki   rPass ;
 +V+Ind+Prs+3Sg:tyki   rPass ;
  +V+Ind+Prs+Pl:tyki   rPass ;
 +V+Ind+Prt+1Sg:tókti   # ;
 +V+Ind+Prt+1Sg:tykti   # ;
 +V+Ind+Prt+2Sg:tókti   # ;
 +V+Ind+Prt+2Sg:tykti   # ;
 +V+Ind+Prt+2Sg:tóktist # ;
 +V+Ind+Prt+2Sg:tyktist # ;
 +V+Ind+Prt+3Sg:tókti   # ;
 +V+Ind+Prt+3Sg:tykti   # ;
  +V+Ind+Prt+Pl:tóktu   # ;
  +V+Ind+Prt+Pl:tyktu   # ;
         +V+Sup:tykt    # ;

LEXICON VERA
!! __@LEXNAME@__
         +V+Inf:vera  # ;
 +V+Ind+Prs+1Sg:eri   # ;
 +V+Ind+Prs+2Sg:ert   # ;
 +V+Ind+Prs+3Sg:er    # ;
  +V+Ind+Prs+Pl:eru   # ;
 +V+Ind+Prt+1Sg:var   # ;
 +V+Ind+Prt+2Sg:varst # ;
 +V+Ind+Prt+3Sg:var   # ;
  +V+Ind+Prt+Pl:vóru  # ;
         +V+Sup:verið # ;

LEXICON VERÐA
!! __@LEXNAME@__
         +V+Inf:verða    #   ;
 +V+Ind+Prs+1Sg:verði    #   ;
 +V+Ind+Prs+2Sg:verð%>ur #   ;
 +V+Ind+Prs+3Sg:verð%>ur #   ;
  +V+Ind+Prs+Pl:verða    #   ;
 +V+Ind+Prt+1Sg:varð     #   ;
 +V+Ind+Prt+2Sg:varðst   #   ;
 +V+Ind+Prt+3Sg:varð     #   ;
  +V+Ind+Prt+Pl:vórðu    #   ;
      +V+Imp+Sg:verð     #   ;
     +V+Sbj+3Sg:verði    #   ; !XXX verði ljós
      +V+Imp+Pl:verðið   #   ;
      +V+PrsPrc:verðandi #   ;
      +V+PrfPrc:vorðin   p26 ;


LEXICON VILJA
!! __@LEXNAME@__
         +V+Inf:vilja  # ;
 +V+Ind+Prs+1Sg:vil    # ;
 +V+Ind+Prs+2Sg:vilt   # ;
 +V+Ind+Prs+3Sg:vil    # ;
  +V+Ind+Prs+Pl:vilja  # ;
 +V+Ind+Prt+1Sg:vildi  # ;
 +V+Ind+Prt+2Sg:vildi  # ;
 +V+Ind+Prt+3Sg:vildi  # ;
  +V+Ind+Prt+Pl:vildu  # ;
         +V+Sup:viljað # ;

LEXICON VITA
!! __@LEXNAME@__
         +V+Inf:vita   # ;
 +V+Ind+Prs+1Sg:veit   Pass ;
 +V+Ind+Prs+2Sg:veitst # ;
 +V+Ind+Prs+3Sg:veit   Pass ;
  +V+Ind+Prs+Pl:vita   Pass ;
 +V+Ind+Prt+1Sg:visti  Pass ;
 +V+Ind+Prt+2Sg:visti  Pass ;
 +V+Ind+Prt+3Sg:visti  Pass ;
  +V+Ind+Prt+Pl:vistu  Pass ;
         +V+Sup:vitað  # ;


LEXICON SÍGGJA
!! __@LEXNAME@__
         +V+Inf:síggja    #  ;
 +V+Ind+Prs+1Sg:síggi     Pass  ;
 +V+Ind+Prs+2Sg:sært      #  ;
 +V+Ind+Prs+3Sg:sær       Pass  ;
  +V+Ind+Prs+Pl:síggja    Pass  ;
 +V+Ind+Prt+1Sg:sá        Pass  ;
 +V+Ind+Prt+2Sg:sást      #  ;
 +V+Ind+Prt+3Sg:sá        Pass  ;
  +V+Ind+Prt+Pl:sóu       Pass  ;
      +V+Imp+Sg:síggj     #  ;
      +V+Imp+Sg:sí        #  ;
      +V+Imp+Pl:síggið    #  ;
      +V+PrsPrc:síggjandi VANDI ;
      +V+PrfPrc:sædd%>ur  #  ;
         +V+Sup:sæð       #  ;
         +V+Sup:sætt      #  ;

LEXICON FÁA
!! __@LEXNAME@__
              +V+Inf:fáa    # ;
      +V+Ind+Prs+1Sg:fái    Pass ;
      +V+Ind+Prs+2Sg:fært   # ;
      +V+Ind+Prs+2Sg:fær    Pass ;
      +V+Ind+Prs+3Sg:fær    Pass    ;
       +V+Ind+Prs+Pl:fáa    Pass ;
      +V+Ind+Prt+1Sg:fekk   Pass ;
      +V+Ind+Prt+2Sg:fekst  # ;
      +V+Ind+Prt+3Sg:fekk   Pass ;
       +V+Ind+Prt+Pl:fingu  Pass ;
           +V+Imp+Sg:fá     # ;
           +V+Imp+Pl:fáið   # ;
           +V+PrfPrc:fingin # ;
              +V+Sup:fingið # ;

LEXICON NÁA !
!! __@LEXNAME@__ XXX check
              +V+Inf:náa  # ;
      +V+Ind+Prs+1Sg:nær  Pass ;
      +V+Ind+Prs+2Sg:nært # ;
      +V+Ind+Prs+2Sg:nær  Pass ;
      +V+Ind+Prs+3Sg:nær  Pass ;
       +V+Ind+Prs+Pl:náa  Pass ;
      +V+Ind+Prt+1Sg:nátt # ;
      +V+Ind+Prt+2Sg:nátt # ;
      +V+Ind+Prt+3Sg:nátt # ;
       +V+Ind+Prt+Pl:nátt # ;
           +V+Imp+Sg:ná   # ;
           +V+Imp+Pl:náið # ;
           +V+PrfPrc:nátt # ;
              +V+Sup:nátt # ;

LEXICON LIGGJA
!! __@LEXNAME@__
         +V+Inf:iggja    #  ;
 +V+Ind+Prs+1Sg:iggi     Pass  ;
 +V+Ind+Prs+2Sg:igg      urPass ;
 +V+Ind+Prs+3Sg:igg      urPass ;
  +V+Ind+Prs+Pl:iggja    Pass  ;
 +V+Ind+Prt+1Sg:á        Pass  ;
 +V+Ind+Prt+2Sg:ást      #  ;
 +V+Ind+Prt+3Sg:á        Pass  ;
  +V+Ind+Prt+Pl:ógu      Pass  ;
      +V+Imp+Sg:igg      #  ;
      +V+Imp+Pl:iggið    #  ;
      +V+PrsPrc:iggjandi VANDI ;
      +V+PrfPrc:igin     #  ;
         +V+Sup:igið     #  ;


LEXICON RADA
!! __@LEXNAME@__
          +V+Inf:ráða    #   ;
  +V+Ind+Prs+1Sg:ráði    Pass   ;
  +V+Ind+Prs+2Sg:ræð     urPass   ;
  +V+Ind+Prs+3Sg:ræð     urPass   ;
   +V+Ind+Prs+Pl:ráða    Pass   ;
  +V+Ind+Prt+1Sg:ráddi   Pass   ;
  +V+Ind+Prt+2Sg:ráddi   Pass   ;
  +V+Ind+Prt+3Sg:ráddi   Pass   ;
   +V+Ind+Prt+Pl:ráddu   Pass   ;
       +V+Imp+Sg:ráð     #   ;
       +V+Imp+Pl:ráðið   #   ;
       +V+PrsPrc:ráðandi VANDI  ;
              :rá      sup_tt ;

LEXICON SYNGJA !
!! __@LEXNAME@__ XXX check
         +V+Inf:syngja    Pass  ;
 +V+Ind+Prs+1Sg:syngi     Pass  ;
 +V+Ind+Prs+2Sg:syng      urPass  ;
 +V+Ind+Prs+3Sg:syng      urPass  ;
  +V+Ind+Prs+Pl:syngja    Pass  ;
 +V+Ind+Prt+1Sg:sang      Pass  ;
 +V+Ind+Prt+2Sg:sang      stPass  ;
 +V+Ind+Prt+2Sg:sang      Pass  ;
 +V+Ind+Prt+3Sg:sang      Pass  ;
  +V+Ind+Prt+Pl:sungu     Pass  ;
      +V+Imp+Sg:syng      #  ;
      +V+Imp+Pl:syngið    #  ;
      +V+PrsPrc:syngjandi VANDI ;
      +V+PrfPrc:sungið    #  ;
         +V+Sup:sungin    #  ;



LEXICON HOGGA !
!! __@LEXNAME@__ høgga
         +V+Inf:høgga    #  ;
 +V+Ind+Prs+1Sg:høggi    Pass  ;
 +V+Ind+Prs+2Sg:høgg     urPass  ;
 +V+Ind+Prs+3Sg:høgg     urPass  ;
  +V+Ind+Prs+Pl:høgga    Pass  ;
 +V+Ind+Prt+1Sg:høgdi    Pass  ;
 +V+Ind+Prt+2Sg:høgdi    Pass  ;
 +V+Ind+Prt+3Sg:høgdi    Pass  ;
  +V+Ind+Prt+Pl:høgdu    Pass  ;
      +V+Imp+Sg:høgg     #  ;
      +V+Imp+Pl:høggið   #  ;
      +V+PrsPrc:høggandi VANDI ;
      +V+PrfPrc:høgdur   #  ;
         +V+Sup:høgt     #  ;

LEXICON KVODA
!! __@LEXNAME@__
         +V+Inf:kvøða    	#  ;
 +V+Ind+Prs+1Sg:kvøði    	Pass  ;
 +V+Ind+Prs+2Sg:kvøð     	urPass  ;
 +V+Ind+Prs+3Sg:kvøð     	urPass  ;
  +V+Ind+Prs+Pl:kvøða    	Pass  ;
 +V+Ind+Prt+1Sg:kvað   		Pass  ;
 +V+Ind+Prt+2Sg:kvað   		Pass  ;
 +V+Ind+Prt+2Sg:kvað     	stPass  ;
 +V+Ind+Prt+3Sg:kvað     	Pass  ;
  +V+Ind+Prt+Pl:kvóðu    	Pass  ;
      +V+Imp+Sg:kvøð    	#  ;
      +V+Imp+Pl:kvøðið   	#  ;
      +V+PrsPrc:kvøðandi 	VANDI ;
      +V+PrfPrc:kvøðin  	#  ;
         +V+Sup:kvøðið  	#  ;



LEXICON FLYGGJA
!! __@LEXNAME@__
         +V+Inf:flýggja    #  ;
 +V+Ind+Prs+1Sg:flýggi     Pass  ;
 +V+Ind+Prs+2Sg:flýggja   rPass  ;
 +V+Ind+Prs+3Sg:flýggja   rPass  ;
  +V+Ind+Prs+Pl:flýggja    Pass  ;
 +V+Ind+Prt+1Sg:flýddi     Pass  ;
 +V+Ind+Prt+2Sg:flýddi     Pass  ;
 +V+Ind+Prt+3Sg:flýddi     Pass  ;
  +V+Ind+Prt+Pl:flýddu     Pass  ;
      +V+Imp+Sg:flýggja    #  ;
      +V+Imp+Pl:flýggið    #  ;
      +V+PrsPrc:flýggjandi VANDI ;
      +V+PrfPrc:flýddur    #  ;
         +V+Sup:flýtt      #  ;


LEXICON VAKSA
!! __@LEXNAME@__
                 +V+Inf:vaksa    #  ;
         +V+Ind+Prs+1Sg:vaksi    Pass  ;
         +V+Ind+Prs+2Sg:veks     urPass  ;
         +V+Ind+Prs+3Sg:veks     urPass  ;
          +V+Ind+Prs+Pl:vaksa    Pass  ;
         +V+Ind+Prt+1Sg:vaks     #  ;
         +V+Ind+Prt+2Sg:vaks     #  ;
         +V+Ind+Prt+3Sg:vaks     #  ;
          +V+Ind+Prt+Pl:vuksu    Pass  ;
              +V+Imp+Sg:vaks     #  ;
              +V+Imp+Pl:vaksið   #  ;
              +V+PrsPrc:vaksandi VANDI ;
              +V+PrfPrc:vaksin   #  ;
                 +V+Sup:vaksið   #  ;

LEXICON VEKSA
!! __@LEXNAME@__
                 +V+Inf:eksa    #  ;
         +V+Ind+Prs+1Sg:eksi    Pass  ;
         +V+Ind+Prs+2Sg:eksur   #  ;
         +V+Ind+Prs+3Sg:eksur   #  ;
          +V+Ind+Prs+Pl:eksa    Pass  ;
         +V+Ind+Prt+1Sg:aks     #  ;
         +V+Ind+Prt+2Sg:aks     #  ;
         +V+Ind+Prt+3Sg:aks     #  ;
          +V+Ind+Prt+Pl:uksu    Pass  ;
              +V+Imp+Sg:eks     #  ;
              +V+Imp+Pl:eksið   #  ;
              +V+PrsPrc:eksandi VANDI ;
              +V+PrfPrc:aksin   #  ;
                 +V+Sup:aksið   #  ;


LEXICON s30/26_1
!! __@LEXNAME@__ dáma
		s30		;
		s26_1	;
      	p7      ;

LEXICON HYGGJA
!! __@LEXNAME@__
         +V+Inf:hyggja   	#       ;
 +V+Ind+Prs+1Sg:hyggi   	Pass       ;
 +V+Ind+Prs+2Sg:hygg     	urPass       ;
 +V+Ind+Prs+3Sg:hygg     	urPass       ;
  +V+Ind+Prs+Pl:hyggja   	Pass       ;
 +V+Ind+Prt+1Sg:hugdi   	Pass       ;
 +V+Ind+Prt+2Sg:hugdi   	Pass       ;
 +V+Ind+Prt+3Sg:hugdi   	Pass       ;
  +V+Ind+Prt+Pl:hugdu   	Pass       ;
      +V+Imp+Sg:hygg   		#       ;
      +V+Imp+Pl:hyggið   	#       ;
      +V+PrsPrc:hyggjandi   VANDI   ;
      +V+PrfPrc:hugdur   	#       ;
         +V+Sup:hugt   		#       ;

LEXICON MYLA
!! __@LEXNAME@__
         +V+Inf:myla    #       ;
 +V+Ind+Prs+1Sg:myli     Pass       ;
 +V+Ind+Prs+2Sg:mylur   #       ;
 +V+Ind+Prs+3Sg:mylur   #       ;
  +V+Ind+Prs+Pl:myla    Pass       ;
  +V+Ind+Prt+1Sg:myldi  Pass       ;
  +V+Ind+Prt+2Sg:myldi  Pass       ;
  +V+Ind+Prt+3Sg:myldi  Pass       ;
  +V+Ind+Prt+Pl:myldu   Pass       ;
      +V+Imp+Sg:myl     #       ;
      +V+Imp+Pl:mylið   #       ;
      +V+PrsPrc:mylandi	VANDI   ;
      +V+PrfPrc:myldur  #       ;
         +V+Sup:mylt   	#       ;

LEXICON TYSJA
!! __@LEXNAME@__
         +V+Inf:ysja   	#       ;
 +V+Ind+Prs+1Sg:ysi   	Pass       ;
 +V+Ind+Prs+2Sg:ysur   	#       ;
 +V+Ind+Prs+3Sg:ysur   	#       ;
  +V+Ind+Prs+Pl:ysja   	Pass       ;
  +V+Ind+Prt+1Sg:usti   Pass       ;
  +V+Ind+Prt+2Sg:usti   Pass       ;
  +V+Ind+Prt+3Sg:usti  	Pass       ;
  +V+Ind+Prt+Pl:ustu   	Pass       ;
      +V+Imp+Sg:ys   	#       ;
      +V+Imp+Pl:ysið   	#       ;
      +V+PrsPrc:ysjandi VANDI   ;
      +V+PrfPrc:ustur   #       ;
         +V+Sup:ust   	#       ;

LEXICON GROA
!! __@LEXNAME@__
         +V+Inf:gróa   	#       ;
 +V+Ind+Prs+1Sg:grói   	Pass       ;
 +V+Ind+Prs+2Sg:gró   	rPass       ;
 +V+Ind+Prs+2Sg:gró     rtPass       ;
 +V+Ind+Prs+3Sg:gró    	rPass       ;
  +V+Ind+Prs+Pl:gróa   	Pass       ;
  +V+Ind+Prt+1Sg:gróði  Pass       ;
  +V+Ind+Prt+2Sg:gróði  Pass       ;
  +V+Ind+Prt+3Sg:gróði  Pass       ;
  +V+Ind+Prt+Pl:gróðu   Pass       ;
      +V+Imp+Sg:gró   	#       ;
      +V+Imp+Pl:gróið   #       ;
      +V+PrsPrc:gróandi VANDI   ;
      +V+PrfPrc:gróin   #       ;
         +V+Sup:gróð   	#       ;
         +V+Sup:gróðið  #       ;

LEXICON KVOTTA
!! __@LEXNAME@__
         +V+Inf:kvøtta   	#       ;
 +V+Ind+Prs+1Sg:kvøtti   	Pass       ;
 +V+Ind+Prs+2Sg:kvøtt   	urPass       ;
 +V+Ind+Prs+2Sg:kvøtti   	rPass       ;
 +V+Ind+Prs+3Sg:kvøtt   	urPass       ;
 +V+Ind+Prs+3Sg:kvøtti   	rPass       ;
  +V+Ind+Prs+Pl:kvøtta   	Pass       ;
  +V+Ind+Prt+1Sg:kvøtti   	Pass       ;
  +V+Ind+Prt+1Sg:kvatt   	Pass       ;
  +V+Ind+Prt+2Sg:kvøtti   	Pass       ;
  +V+Ind+Prt+2Sg:kvatt   	Pass       ;
  +V+Ind+Prt+3Sg:kvøtti  	Pass       ;
  +V+Ind+Prt+3Sg:kvatt  	Pass       ;
  +V+Ind+Prt+Pl:kvøttu   	Pass       ;
      +V+Imp+Sg:kvøtt   	#       ;
      +V+Imp+Sg:kvøtta   	#       ;
      +V+Imp+Pl:kvøttið   	#       ;
      +V+PrsPrc:kvøttandi 	VANDI   ;
      +V+PrfPrc:kvøttur   	#       ;
      +V+PrfPrc:kvøttin   	#       ;
         +V+Sup:kvøtt   	#       ;
         +V+Sup:kvøttið   	#       ;


!LEXICON
!         +V+Inf:   	#       ;
! +V+Ind+Prs+1Sg:   	#       ;
! +V+Ind+Prs+2Sg:   	#       ;
! +Ind+Prs+3Sg:   		#       ;
!  +V+Ind+Prs+Pl:   	#       ;
!  +V+Ind+Prt+1Sg:   	#       ;
!  +V+Ind+Prt+2Sg:   	#       ;
!  +V+Ind+Prt+3Sg:  	#       ;
!  +V+Ind+Prt+Pl:   	#       ;
!      +V+Imp+Sg:   	#       ;
!      +V+Imp+Pl:   	#       ;
!      +V+PrsPrc:   	VANDI   ;
!      +V+PrfPrc:   	#       ;
!         +V+Sup:   	#       ;

!         +V+Inf+Pass:   #       ;
! +V+Ind+Prs+1Sg+Pass:   #       ;
! +V+Ind+Prs+2Sg+Pass:   #       ;
! +V+Ind+Prs+3Sg+Pass:   #       ;
!  +V+Ind+Prs+Pl+Pass:   #       ;
!  +V+Ind+Prt+1Sg+Pass:   #       ;
!  +V+Ind+Prt+2Sg+Pass:   #       ;
!  +V+Ind+Prt+3Sg+Pass:   #       ;
!  +V+Ind+Prt+Pl+Pass:   #       ;
!      +V+Imp+Sg+Pass:   #       ;
!      +V+Imp+Pl+Pass:   #       ;
!      +V+PrsPrc+Pass:   #       ;
!      +V+PrfPrc+Pass:   #       ;
!         +V+Sup+Pass:   #       ;



LEXICON TAKAST !
!! __@LEXNAME@__ 
	:%>tak		reflinf		;
	:%>tak		pres_1ist	;
	:%>tek		pres_23st	;
	:%>tak		pres_plast	;
	:%>tók		pret_st		;
	:%>tók		pret_plust	;

LEXICON LOYPAST !
!! __@LEXNAME@__ loypast
			reflinf		;
			pres_ist	;
			pres_plast	;
			pret_tist	;
			pret_pltust	;

LEXICON sxrefl !
!! __@LEXNAME@__ This is an ad hoc lexicon
  s74 ;

LEXICON s74 !
!! __@LEXNAME@__ grindast
  			reflinf  	;
  			pres_ast 	;
  			pres_plast	;
  			pret_adist  ;
  :%>að		pret_plust	;

LEXICON s75 !
!! __@LEXNAME@__ balast
  			reflinf   	;
  			pres_ist  	;
  			pres_plast	;
  			pret_dist 	;
  :%>d		pret_plust	;

LEXICON s76 !
!! __@LEXNAME@__ ræðast
  			reflinf 	;
  			pres_ist  	;
  			pres_plast	;
  			pret_dist 	;
  :%>d		pret_plust	;

LEXICON s77 !
!! __@LEXNAME@__ skiftast
  			reflinf 	;
  			pres_ist  	;
  			pres_plast	;
  			pret_ist	;
 			pret_plust	;

LEXICON s78 !
!! __@LEXNAME@__ farast
  			reflinf 	;
  			pres_1ist	;
  :%^IUML	pres_23st	;
  			pres_plast	;
  :%^AB3	pret_st		;
  :%^AB3	pret_plust	;

LEXICON s79 !
!! __@LEXNAME@__ skjótast
  :%>jót	reflinf 	;
  :%>jót	pres_1ist	;
  :%>jýt	pres_23st	;
  :%>jót	pres_plast	;
  :%>eyt	pret_st		;
  :%>ut		pret_plust	;

LEXICON s80 !
!! __@LEXNAME@__ trivast
  			reflinf 	;
  			pres_ist	;
  			pres_23st	;
    		pres_plast	;
  :%^AB1	pret_st		;
  :%^VSH	pret_plust	;

LEXICON s81 !
!! __@LEXNAME@__ kíkjast
  :%>j		reflinf 	;
  			pres_ist	;
  :%>j 		pres_plast	;
  			pret_tist	;
  			pret_pltust	;

LEXICON s82 !
!! __@LEXNAME@__ fýlast
			reflinf 	;
  			pres_ist	;
  			pres_plast	;
  			pret_tist	;
  			pret_pltust	;

LEXICON s83 !
!! __@LEXNAME@__ samsinnast
  			reflinf 	;
  			pres_ist	;
		  	pres_plast	;
			pret_tist	;
  			pret_pltust	;

LEXICON FYRIB !
!! __@LEXNAME@__ kopi, s83
  			reflinf 	;
  			pres_ist	;
		  	pres_plast	;
			pret_tist	;
  			pret_pltust	;



!! !!Split lexica
! ============

! The lexicon names are, as always, from FO.
! Now, these are just mechanically split.
! They should be split more precisely,
! just like s2/1, in order to keep
! things minimal.

!LEXICON s1/26
!            s1    ;
!            s26   ;

!LEXICON s1/30
!            s1    ;
!            s30   ;

!LEXICON s11/15
!            s11   ;
!            s15   ;

!LEXICON s15/11
!            s15   ;
!            s11   ;

!LEXICON s18/16
!            s18   ;
!            s16   ;

!LEXICON s19/30
!            s19   ;
!            s30   ;

!LEXICON s2/1
!            s2    ;
!            prt_d ; ! this is the s1 part

!LEXICON s2/30
!            s2    ;
!            s30   ;

!LEXICON s21/11
!            s21   ;
!            s11   ;
!
!LEXICON s21/18
!            s21   ;
!            s18   ;
!
!LEXICON s24/2
!            s24   ;
!            s2    ;
!
!LEXICON s25/1
!            s25   ;
!            s1    ;
!
!LEXICON s27/30
!            s27   ;
!            s30   ;
!
!LEXICON s28/30
!            s28   ;
!            s30   ;
!
!LEXICON s29/2
!            s29   ;
!            s2    ;
!
!LEXICON s29/30
!            s29   ;
!            s30   ;
!
!LEXICON s3/30
!            s3    ;
!            s30   ;
!
!LEXICON s30/1
!            s30   ;
!            s1    ;
!
!LEXICON s30/11
!            s30   ;
!            s11   ;
!
!LEXICON s30/19
!            s30   ;
!            s19   ;
!
!LEXICON s30/2
!            s30   ;
!            s2    ;
!
!LEXICON s30/22
!            s30   ;
!            s22   ;
!
!LEXICON s30/27
!            s30   ;
!            s27   ;
!
!LEXICON s30/28
!            s30   ;
! +Err/Orth: s28   ;
!
!LEXICON s30/29
!            s30   ;
!   +Use/NG: s29   ;
!
!LEXICON s30/40
!            s30   ;
!            s40   ;
!
!LEXICON s30/5
!            s30   ;
!            s5    ;
!
!LEXICON s30/6
!            s30   ;
!            s6    ;
!
!LEXICON s30/9
!            s30   ;
!            s9    ;
!
!LEXICON s36/2
!            s36   ;
!            s2    ;

!LEXICON s39/30
             !s30  ;
             !s39  ;

!LEXICON s47/19
!            s47   ;
!            s19   ;
!
!LEXICON s49/52
!            s49   ;
!            s52   ;
!
!LEXICON s49/9
!            s49   ;
!            s9    ;
!
!LEXICON s5/30
!            s5    ;
!            s30   ;
!
!LEXICON s52/30
!            s52   ;
!            s30   ;
!
!LEXICON s6/30
!            s6    ;
!            s30   ;
!
!LEXICON s60/30
!            s60   ;
!            s30   ;
!
!LEXICON s7/49
!            s7    ;
!            s49   ;
!
!LEXICON s74/75
!            s74   ;
!            s75   ;
!
!LEXICON s77/74
!            s77   ;
!            s74   ;
!
!LEXICON s8/30
!            s8    ;
!            s30   ;
!
LEXICON s8/48
!! __@LEXNAME@__
  s8  ;
  s48 ;
LEXICON s9/30
!! __@LEXNAME@__
  s9  ;
  s30 ;





!! !!Intermediate lexicon groups
! ===========================

LEXICON standard_ir
!! __@LEXNAME@__
  inf     ;
  prt_d   ;
  ir_verb ;

LEXICON standard_ir_t
!! __@LEXNAME@__
   inf                  ;
   prt_t                ; !XXX skilti
   ir_verb_t            ;

!LEXICON standard_j_ir_t
!  prt_t                ; !XXX skilti
!  standard_j_ir_nopret ;

! possibly also (no, handled by s1/2
!LEXICON standard_j_ir_td
!  prt_t                ;
!  prt_d                ;
!  standard_j_ir_nopret ;


!LEXICON standard_j_ir !
!!! __@LEXNAME@__ the prt_d
!  standard_j_ir_nopret ;
!  prt_d                ;

!LEXICON standard_j_ir_nopret
!!! __@LEXNAME@__
!    jinf       ;
!    pres_ir_sg ;
! :j pres_pl    ;
!    imp_prsptc ;
!    sup        ;

LEXICON ir_verb
!! __@LEXNAME@__
  pres_ir    ;
  imp_prsptc ;
  sup        ;

LEXICON ir_verb_t
!! __@LEXNAME@__
  pres_ir    ;
  imp_prsptc ;
  sup_t      ;

!! !!Suffix lexica
! =============

!! !Infinitive
! __________

LEXICON jinf
!! __@LEXNAME@__
      +V+Inf:%>ja   # ;

LEXICON inf
!! __@LEXNAME@__
      +V+Inf:%>a   # ;

LEXICON reflinf
!! __@LEXNAME@__
 +V+Inf+Pass:%>ast # ;


!! !Present
! _______

LEXICON pres_ir
!! __@LEXNAME@__
  pres_ir_sg ;
  pres_pl    ;

!LEXICON pres_ir_j2
!!! __@LEXNAME@__
!  +V+Ind+Prs+1Sg:%>i  # ;
!  +V+Ind+Prs+2Sg:%>ir # ;
!  +V+Ind+Prs+3Sg:%>ir # ;
! +V+Subj+Prs+3Sg:%>i  # ;
!   +V+Ind+Prs+Pl:%>ja # ;

LEXICON pres_jir
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>ji  Pass ;
 +V+Ind+Prs+2Sg:%>ji  rPass ;
 +V+Ind+Prs+3Sg:%>ji  rPass ;
  +V+Ind+Prs+Pl:%>ja  Pass ;

LEXICON pres_ir_sg
!! __@LEXNAME@__
  +V+Ind+Prs+1Sg:%>i  Pass ;
  +V+Ind+Prs+2Sg:%>i  rPass ;
  +V+Ind+Prs+3Sg:%>i  rPass ;
 +V+Subj+Prs+3Sg:%>i  Pass ;

LEXICON pres_ar
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>i  Pass    ; ! XXX sjekk
 +V+Ind+Prs+2Sg:%>a  rPass    ;
 +V+Ind+Prs+3Sg:%>a  rPass    ;
                     pres_pl ;

LEXICON pres_ur
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>i  Pass    ;
 +V+Ind+Prs+2Sg:     urPass    ;
 +V+Ind+Prs+3Sg:     urPass    ;
                     pres_pl ;

LEXICON pres_ur_j
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>ji Pass    ;
 +V+Ind+Prs+2Sg:     iurPass    ;
 +V+Ind+Prs+3Sg:     prPass    ;
               :j    pres_pl ;

LEXICON pres_ur_j2
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>i  Pass    ;
 +V+Ind+Prs+2Sg:     urPass    ;
 +V+Ind+Prs+3Sg:     urPass    ;
               :j    pres_pl ;

!LEXICON pres_null
!!! __@LEXNAME@__
! +V+Ind+Prs+1Sg:%> #    ;
! +V+Ind+Prs+2Sg:%> #    ;
! +V+Ind+Prs+3Sg:%> #    ;
                   pres_pl ;

LEXICON pres_strong_s1
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg:%>i Pass    ;
                    pres_pl ; !XXX

LEXICON pres_strong_s23
!! __@LEXNAME@__
 +V+Ind+Prs+2Sg:     urPass     ;
 +V+Ind+Prs+3Sg:     urPass     ;
                     !pres_pl ;

LEXICON pres_strong_s23_t0
!! __@LEXNAME@__
 +V+Ind+Prs+2Sg:    tPass     ;
 +V+Ind+Prs+3Sg:    Pass     ;
                    !pres_pl ;

LEXICON pres_strong_s23_t1
!! __@LEXNAME@__
 +V+Ind+Prs+2Sg:     rPass     ;
 +V+Ind+Prs+2Sg:     rtPass     ;
 +V+Ind+Prs+3Sg:     rPass    ;
                     !pres_pl ;

LEXICON pres_pl
!! __@LEXNAME@__
 +V+Ind+Prs+Pl:%>a Pass ;
 
 
 
 
 

LEXICON pres_ast ! Desse kunne ha gått til Pass.
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg+Pass:%>ist # ;
 +V+Ind+Prs+2Sg+Pass:%>ast # ;
 +V+Ind+Prs+3Sg+Pass:%>ast # ;

LEXICON pres_ist
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg+Pass:%>ist # ;
 +V+Ind+Prs+2Sg+Pass:%>ist # ;
 +V+Ind+Prs+3Sg+Pass:%>ist # ;

LEXICON pres_1ist
!! __@LEXNAME@__
 +V+Ind+Prs+1Sg+Pass:%>ist # ;

LEXICON pres_23st
!! __@LEXNAME@__
 +V+Ind+Prs+2Sg+Pass:%>st # ;
 +V+Ind+Prs+3Sg+Pass:%>st # ;

LEXICON pres_plast
!! __@LEXNAME@__
  +V+Ind+Prs+Pl+Pass:%>ast # ;
  
  
  

LEXICON pret_adist
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg+Pass:%>aðist # ;
 +V+Ind+Prt+2Sg+Pass:%>aðist # ;
 +V+Ind+Prt+3Sg+Pass:%>aðist # ;

LEXICON pret_dist
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg+Pass:%>dist # ;
 +V+Ind+Prt+2Sg+Pass:%>dist # ;
 +V+Ind+Prt+3Sg+Pass:%>dist # ;

LEXICON pret_tist
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg+Pass:%>tist # ;
 +V+Ind+Prt+2Sg+Pass:%>tist # ;
 +V+Ind+Prt+3Sg+Pass:%>tist # ;

LEXICON pret_ist
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg+Pass:%>ist # ;
 +V+Ind+Prt+2Sg+Pass:%>ist # ;
 +V+Ind+Prt+3Sg+Pass:%>ist # ;

LEXICON pret_st
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg+Pass:%>st # ;
 +V+Ind+Prt+2Sg+Pass:%>st # ;
 +V+Ind+Prt+3Sg+Pass:%>st # ;

LEXICON pret_plust
!! __@LEXNAME@__
  +V+Ind+Prt+Pl+Pass:%>ust # ;

LEXICON pret_pltust
!! __@LEXNAME@__
  +V+Ind+Prt+Pl+Pass:%>tust # ;

!! !Preterite

LEXICON prt_d
!! __@LEXNAME@__
  prt_di ;
  prt_du ;

LEXICON prt_ð
!! __@LEXNAME@__
  prt_ði ;
  prt_ðu ;

LEXICON prt_t
!! __@LEXNAME@__
  prt_ti ;
  prt_tu ;

LEXICON prt_di
 +V+Ind+Prt+1Sg:%>di Pass ;
 +V+Ind+Prt+2Sg:%>di Pass ;
 +V+Ind+Prt+3Sg:%>di Pass ;

LEXICON prt_ði
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:%>ði Pass ;
 +V+Ind+Prt+2Sg:%>ði Pass ;
 +V+Ind+Prt+3Sg:%>ði Pass ;

LEXICON prt_ti
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:%>ti Pass ;
 +V+Ind+Prt+2Sg:%>ti Pass ;
 +V+Ind+Prt+3Sg:%>ti Pass ;

LEXICON prt_du
!! __@LEXNAME@__
 +V+Ind+Prt+Pl:%>du Pass ;

LEXICON prt_tu
!! __@LEXNAME@__
 +V+Ind+Prt+Pl:%>tu Pass ;

LEXICON prt_ðu
!! __@LEXNAME@__
 +V+Ind+Prt+Pl:%>ðu Pass ;

LEXICON prt_dd
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:%>ddi Pass ;
 +V+Ind+Prt+2Sg:%>ddi Pass ;
 +V+Ind+Prt+3Sg:%>ddi Pass ;
  +V+Ind+Prt+Pl:%>ddu Pass ;

LEXICON prt_a
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:%>aði Pass ;
 +V+Ind+Prt+2Sg:%>aði Pass ;
 +V+Ind+Prt+3Sg:%>aði Pass ;
  +V+Ind+Prt+Pl:%>aðu Pass ;

LEXICON prt_null
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg: Pass ;
 +V+Ind+Prt+2Sg: Pass ;
 +V+Ind+Prt+3Sg: Pass ;

LEXICON prt_null_s
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:     Pass ;
 +V+Ind+Prt+2Sg:     stPass ; ! NB ist ?!
 +V+Ind+Prt+2Sg:     Pass ;
 +V+Ind+Prt+3Sg:     Pass ;

LEXICON prt_null_s2
!! __@LEXNAME@__
 +V+Ind+Prt+1Sg:    Pass ;
 +V+Ind+Prt+2Sg:    tPass ;
 +V+Ind+Prt+2Sg:    Pass ;
 +V+Ind+Prt+3Sg:    Pass ;

LEXICON prt_u_p
!! __@LEXNAME@__
 +V+Ind+Prt+Pl:%>u Pass ;


! =================
!! !!Passive lexica
!  ================

LEXICON iurPass
     :%>ur # ;
+Pass:%>ist # ;

LEXICON urPass
     :%>ur # ;
+Pass:%>st # ;

LEXICON rPass
     :%>r # ;
+Pass:%>st # ;

LEXICON tPass
     :%>t # ;
+Pass:%>st # ;

LEXICON rtPass
     :%>rt # ;
+Pass:%>st # ;

LEXICON stPass
     :%>st # ;
+Pass:%>st # ;

LEXICON Pass
 # ;
 +Pass:%>st # ;

!! !Imperative and present participle
!  ==================================

LEXICON imp_prsptc
!! __@LEXNAME@__
  imp    ;
  prsptc ;

LEXICON imp_prsptc_j
!! __@LEXNAME@__
  imp_j  ;
  prsptc ;

LEXICON imp
!! __@LEXNAME@__
  impsg ;
  imppl ;

LEXICON imp_j
!! __@LEXNAME@__
  impsg ;
  imppl_j ;

LEXICON impsg
!! __@LEXNAME@__
 +V+Imp+Sg: # ;

LEXICON imppl
!! __@LEXNAME@__
 +V+Imp+Pl:%^JI%>ið # ;

LEXICON imppl_j
!! __@LEXNAME@__
 +V+Imp+Pl:%>ið # ;

LEXICON prsptc
!! __@LEXNAME@__
 +V+PrsPrc:%>andi VANDI             ;
        +A:%>andi HOMONYM_ADJ_FORMS ;

!! !Supine and preterite participle

LEXICON sup
!! __@LEXNAME@__
    +V+Sup:%>t     # ;
 +V+PrfPrc:%>d%>ur # ;

LEXICON sup_t
!! __@LEXNAME@__
    +V+Sup:%>t     # ;
 +V+PrfPrc:%>t%>ur # ;

LEXICON sup_tt
!! __@LEXNAME@__
    +V+Sup:%>tt     # ;
 +V+PrfPrc:%>dd%>ur # ;

LEXICON sup_a !
!! __@LEXNAME@__ kalla
    +V+Sup:%>að     # ;
 +V+PrfPrc:%>að%>ur # ;

LEXICON sup_null !
!! __@LEXNAME@__  stungið
 +V+Sup:%>ið # ;

LEXICON sup_in !
!! __@LEXNAME@__ kalla
    +V+Sup:%>ð    # ;
 +V+PrfPrc:%>gvin # ;

LEXICON sup_ið_in !
!! __@LEXNAME@__  stungið
    +V+Sup:%>ið # ;
 +V+PrfPrc:%>in # ;





!! !Middle lexicon

LEXICON VANDI
!! __@LEXNAME@__
  #    ; ! Verbs as usual
  andi ; ! participles



! =================== !
!! !!Perfect Participles !
! =================== !




LEXICON p18
!! __@LEXNAME@__
  adj_a ;
  :ð l18   ;

LEXICON p26
!! __@LEXNAME@__
 :%^EPH a_m_sg_n_null    ;
 :%^EPH a_non_umlaut_obl ;
 :%^EPH a_umlaut         ;
 :ð       l26              ;

LEXICON p32
!! __@LEXNAME@__
 :%^EPH adj_a ;
 :ð       l32   ;

LEXICON p39
!! __@LEXNAME@__
  adj_a ; !XXX check!
  :ð l39   ;

LEXICON p5
!! __@LEXNAME@__
  adj_a     ;
  adj_r_gen ;
  :ð l5        ;

!LEXICON p5x
!!! __@LEXNAME@__
!  l5 ;


LEXICON p6
!! __@LEXNAME@__
 :ð%^EDH adj_a ; ! no ð deletion
 :ð       l6    ;

LEXICON p7
!! __@LEXNAME@__
  +V+PrfPtc+Der/A+A: adj_a     ;
  +V+PrfPtc+Der/A+A:  adj_r_gen ;
  +V+PrfPtc+Der/A:  l7        ;

LEXICON p8
!! __@LEXNAME@__
         a_non_umlaut ;
 :%^UUML a_umlaut     ;
         adj_r_gen    ;
  :ð       l8           ;





