! ======== !
!! Faroese Numerals
! ======== !



LEXICON Numeral
!! **@LEXNAME@** splitting in types
Textual ;  !!= * @CODE@
ARABICS ;  !!= * @CODE@
ARABICORD ;  !!= * @CODE@
ROMAN ;  !!= * @CODE@
ISOLATED-NUMEXP ;  !!= * @CODE@
NUM-PREFIXES ;  !!= * @CODE@


LEXICON Textual
 !!= **@LEXNAME@** for the numerals with letters
1-9 ;
TEXTTENS ;
TEXTTEENS ;
basic ;
ordinals ;


LEXICON 1-9
 !!= **@LEXNAME@** 
eitt:ei EITTsplit ;
tvey:tv TVEYsplit ;
trý:tr TRÝsplit ;
einir+Num:ein PAIRNUM ;
tvinnir+Num:tvinn PAIRNUM ;
trinnir+Num:trinn PAIRNUM ;
fýra nsplit ;
fimm nsplit ;
seks nsplit ;
sjey nsplit ;
átta nsplit ;
áttir nsplit ;
níggju nsplit ;

LEXICON EITTsplit
 !!= **@LEXNAME@** 
+Num: EITT ;
nog:nog TEXTTENS ;

LEXICON TVEYsplit
 !!= **@LEXNAME@** 
+Num: TVEY ;
og:og TEXTTENS ;

LEXICON TRÝsplit
 !!= **@LEXNAME@** 
+Num: TRÝ ;
ýog:ýog TEXTTENS ;

LEXICON nsplit
 !!= **@LEXNAME@** 
n ;
og:og TEXTTENS ;

LEXICON TEXTTENS
 !!= **@LEXNAME@** 
tjúgu n ;
tretivu n ;
tríati n ;     ! 1Mos
fjøruti n ;
hálvtrýss n ;
fimmti n ;     ! 1Mos
trýss n ;
seksti n ;     ! 1Mos
hálvfjerðs n ;
sjeyti n ;     ! 1Mos
fýrs n ;
áttati n ;     ! 1Mos
hálvfems n ;
níti n ;       ! 1Mos


LEXICON TEXTTEENS
 !!= **@LEXNAME@** 
ellivu n ;
tólv n ;
trettan n ;
fjúrtan n ;
fimtan n ;
sekstan n ;
seytan n ; ! XXX check
seytjan n ;
átjan n ;
nítjan n ;

LEXICON basic
 !!= **@LEXNAME@** 
eini n ;
tíggju n ;
hundrað n ;
túsund n ;
millión n ;
átjanhundrað n ;
ein n ;
nítjanhundrað n ;
tveytúsund n ;
níggjundi n ;
tjúgur n ;
fýrsur n ;



LEXICON EITT
 !!= **@LEXNAME@** 
+Msc+Sg+Nom:n # ;
+Msc+Sg+Acc:n # ;
+Msc+Sg+Dat:num # ;
+Msc+Sg+Gen:ns # ;
+Fem+Sg+Nom:n # ;
+Fem+Sg+Acc:na # ;
+Fem+Sg+Dat:ni # ;
+Fem+Sg+Dat:nari # ;
+Fem+Sg+Gen:nar # ;
+Neu+Sg+Nom:tt # ;
+Neu+Sg+Acc:tt # ;
+Neu+Sg+Dat:num # ;
+Neu+Sg+Gen:ns # ;

LEXICON TVEY
 !!= **@LEXNAME@** 
+Msc+Sg+Nom:eir # ;
+Msc+Sg+Acc:eir # ;
+Msc+Sg+Dat:eimum # ;
+Msc+Sg+Gen:eggja # ;
+Fem+Sg+Nom:ær # ;
+Fem+Sg+Acc:ær # ;
+Fem+Sg+Dat:eimum # ;
+Fem+Sg+Gen:ggja # ;
+Neu+Sg+Nom:ey # ;
+Neu+Sg+Acc:ey # ;
+Neu+Sg+Dat:eimum # ;
+Neu+Sg+Gen:ggja # ;

LEXICON TRÝ
 !!= **@LEXNAME@** 
+Msc+Sg+Nom:íggir # ;
+Msc+Sg+Acc:íggjar # ;
+Msc+Sg+Dat:imum # ;
+Msc+Sg+Gen:íggja # ;
+Fem+Sg+Nom:íggjar # ;
+Fem+Sg+Acc:íggjar # ;
+Fem+Sg+Dat:imum # ;
+Fem+Sg+Gen:íggja # ;
+Neu+Sg+Nom:ý # ;
+Neu+Sg+Acc:ý # ;
+Neu+Sg+Dat:imimum # ;
+Neu+Sg+Gen:íggja # ;

LEXICON PAIRNUM
 !!= **@LEXNAME@** 
+Msc+Sg+Pl+Nom:ir # ;
+Msc+Sg+Pl+Acc:ar # ;
+Msc+Sg+Pl+Dat:um # ;
+Msc+Sg+Pl+Gen:a # ;
+Fem+Sg+Pl+Nom:ar # ;
+Fem+Sg+Pl+Acc:ar # ;
+Fem+Sg+Pl+Dat:um # ;
+Fem+Sg+Pl+Gen:a # ;
+Neu+Sg+Pl+Nom:i # ;
+Neu+Sg+Pl+Acc:i # ;
+Neu+Sg+Pl+Dat:um # ;
+Neu+Sg+Pl+Gen:a # ;

LEXICON n
 !!= **@LEXNAME@** 
+Num: # ;


!! ## Ordinals
! ========

LEXICON ordinals
 !!= **@LEXNAME@** 
fyrsti:fyrst ord_decl ;
annar: ANNAR ;
triði:trið ord_decl ;
triðji:triðj ord_decl ;
fjórði:fjórð ord_decl ;
fimti:fimt ord_decl ;
sætti:sætt ord_decl ;
sjeyndi:sjeynd ord_decl ;
áttandi:áttand ord_decl ;
níggjundi:níggjund ord_decl ;
tíggjundi:tíggjund ord_decl ;
ellivti:ellivt ord_decl ;
ellivnti:ellivnt ord_decl ;
tólvti:tólvt ord_decl ;
trettandi:trettand ord_decl ;
fjúrtandi:fjúrtand ord_decl ;
fimtandi:fimtand ord_decl ;
sekstandi:sekstand ord_decl ;
seytjandi:seytjand ord_decl ;
átjandi:átjand ord_decl ;
nítjandi:nítjand ord_decl ;
tjúgundi:tjúgund ord_decl ;
tretivti:tretivt ord_decl ;
tretivundi:tretivund ord_decl ;
fjørutiundi:fjørutiund  ord_decl ;
hálvtrýssinstjúgundi:hálvtrýssinstjúgund ord_decl ;
hálvtrýssundi:hálvtrýssund ord_decl ;
trýssinstjúgundi:trýssinstjúgund ord_decl ;
trýssundi:trýssund ord_decl ;
hálvfjerðsinstjúgundi:hálvfjerðsinstjúgund ord_decl ;
hálvfjerðsundi:hálvfjerðsund ord_decl ;
fýrsinstjúgundi:fýrsinstjúgund ord_decl ;
fýrsundi:fýrsund ord_decl ;
hálvfemsinstjúgundi:hálvfemsinstjúgund ord_decl ;
hálvfemsundi:hálvfemsund ord_decl ;
hundraðndi:hundraðnd ord_decl ;
túsundi:túsund ord_decl ;

LEXICON ord_decl
 !!= **@LEXNAME@** 
+A: adj_def ;

LEXICON ANNAR
 !!= **@LEXNAME@** 
+A: ANNARMORPH ;
+Pron+Indef: ANNARMORPH ;

LEXICON ANNARMORPH
 !!= **@LEXNAME@** 
+Msc+Nom:annar # ;
+Msc+Acc:annan # ;
+Msc+Dat:øðrum # ;
+Msc+Gen:annars # ;
+Fem+Nom:onnur # ;
+Fem+Acc:aðra # ;
+Fem+Acc:aðru # ;
+Fem+Dat:aðri # ;
+Fem+Dat:aðru # ;
+Fem+Gen:annarar # ;
+Neu+Nom:annað # ;
+Neu+Acc:annað # ;
+Neu+Dat:øðrum # ;
+Neu+Gen:annars # ;
+Msc+Pl+Nom:aðrir # ;
+Msc+Pl+Acc:aðrar # ;
+Msc+Pl+Dat:øðrum # ;
+Msc+Pl+Gen:annara # ;
+Fem+Pl+Nom:aðrar # ;
+Fem+Pl+Acc:aðrar # ;
+Fem+Pl+Dat:øðrum # ;
+Fem+Pl+Gen:annara # ;
+Neu+Pl+Nom:onnur # ;
+Neu+Pl+Acc:onnur # ;
+Neu+Pl+Dat:øðrum # ;
+Neu+Pl+Gen:annara # ;

+Msc+Pl+Acc:aðrari # ; ! XXX


!LEXICON NUMBERCMPND
!ár:ár kv6 ;
!tal:tal h5 ;
!mannafar:mannafar h5 ;
!metri:metr k1 ;
!oyra:oyr h1 ;
!siffur:siffur h13 ;
!partur:part k6 ;
!ferð:ferð kv2e ;
!pláta:plát kv1 ;
