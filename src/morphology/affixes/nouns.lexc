! =============== !
! Noun morphology !
! =============== !


! ----------------- !
! Basic noun lexica !
! ----------------- !

! Taken from the dictionary

! The nominal morphology is added in three layers.
! In this first one we add gender tags and morpho-
! phonological diacritics.
! The next two layers are for indefinite and definite
! suffixes, respectively.

! still to be allocated

LEXICON xur ! TOOD: classify xur. They are all m
k6 ;

LEXICON xi ! TOOD: classify xi. They are all m
k1 ;

LEXICON xk21 ! TOOD: classify xk21. They are not checked, go to k21.
k21 ;

LEXICON xk22 ! TOOD: classify xk21. They are not checked, go to k21.
k22 ;

!LEXICON xkv1 ! TOOD: classify xkv1. They are all f and end in a
!kv1 ;

LEXICON xkv2 ! TOOD: classify xkv1. They are all f and end in a
kv2 ;

LEXICON xh1 ! TOOD: classify xkv2. They are all f and end in a consonant
h1 ;

LEXICON xh3 ! TOOD: classify xkv2. They are all f and end in a consonant
h3 ;

LEXICON xh25 ! TOOD: classify xkv2. They are all f and end in a consonant
h25 ;

! Irregular

LEXICON k0 ! januar
 +N+Msc: irregular_nouns ;
LEXICON kv0 ! ommudidd
 +N+Fem: irregular_nouns ;
LEXICON h0 ! A-fýra
 +N+Neu: irregular_nouns ;

LEXICON irregular_nouns
 +Sg+Nom+Indef: ENDLEX ; ! januar
 +Sg+Acc+Indef: ENDLEX ; ! januar
 +Sg+Dat+Indef: ENDLEX ; ! januar
 +Sg+Gen+Indef: ENDLEX ; ! januar


LEXICON h11/41 
  h11 ;
  h41  ;
LEXICON h3/h41 
  h3  ;
  h41 ; 
LEXICON h4/41 
  h4  ;
  h41  ;
LEXICON h7/h3 
  h7  ;
  h3  ;
LEXICON k11/kv6 
  k11 ;
  kv6 ; 
LEXICON k19/12 
  k19 ;
  k12  ;
LEXICON k25/17f 
  k25 ;
  k_flt17 ; 
LEXICON k52/kv2 
  k52 ;
  kv2 ; 
LEXICON k9/10 
  k9  ;
  k10  ;
LEXICON k9/16 
  k9  ;
  k16  ;



! Weak masculines.

LEXICON k1/3e
  k1e ;
  k3e ;

LEXICON k1/3
  k1 ;
  k3 ;

LEXICON k1/4
  k1 ;
  k4 ;

LEXICON k1e/48f !k1e/f84b Heini
  k1e     ;
  k_flt48 ;

LEXICON k1/11
 k1 ;
 k11 ;

LEXICON k1 ! risi
  k1e    ;
  k_flt1 ;

LEXICON k1e
 +N+Msc: W_M_SGNOM ;
 +N+Msc: W_M_SGACC ;
 +N+Msc: W_M_SGDAT ;
 +N+Msc: W_M_SGGEN ;

LEXICON k_flt1
 +N+Msc: W_M_PLNOM ;
 +N+Msc: W_M_PLACC ;
 +N+Msc: W_M_PLDAT ;
 +N+Msc: W_M_PLGEN ;


LEXICON k1_3stem !! * @CODE* felagi
 +N+Msc:       W_M_SGNOM       ;
 +N+Msc:       W_M_SGACC       ;
 +N+Msc:       W_M_SGDAT_mixed ;
 +N+Msc:       W_M_SGGEN       ;
 +N+Msc:       W_M_PLNOM       ;
 +N+Msc:       W_M_PLACC       ;
 +N+Msc:       W_M_PLDAT       ;
 +N+Msc:%^UUML W_M_PLDAT       ;
 +N+Msc:       W_M_PLGEN       ;


LEXICON k2 ! beiggi
 +N+Msc:%^JI W_M_SGNOM ;
 +N+Msc:     W_M_SGACC ;
 +N+Msc:     W_M_SGDAT ;
 +N+Msc:     W_M_SGGEN ;
 +N+Msc:     W_M_PLNOM ;
 +N+Msc:     W_M_PLACC ;
 +N+Msc:     W_M_PLDAT ;
 +N+Msc:     W_M_PLGEN ;

LEXICON k3 ! hagi
  k3e    ;
  k_flt3 ;

LEXICON k3e
 +N+Msc: W_M_SGNOM ;
 +N+Msc: W_M_SGACC ;
 +N+Msc: W_M_SGDAT ;
 +N+Msc: W_M_SGGEN ;

LEXICON k_flt3
 +N+Msc:       W_M_PLNOM ;
 +N+Msc:       W_M_PLACC ;
 +N+Msc:%^UUML W_M_PLDAT ;
 +N+Msc:       W_M_PLGEN ;

LEXICON k4 ! tanki
  k3 ; ! Same u_umlaut, but nasal cns

LEXICON k5 ! bóndi
 +N+Msc:       W_M_SGNOM    ;
 +N+Msc:       W_M_SGACC    ;
 +N+Msc:       W_M_SGDAT    ;
 +N+Msc:       W_M_SGGEN    ;
 +N+Msc:%^IUML W_M_PLNOM_UR ;
 +N+Msc:%^IUML W_M_PLACC_UR ;
 +N+Msc:       W_M_PLDAT    ;
 +N+Msc:       W_M_PLGEN    ;


! Strong masculines

LEXICON k6/7
               k6          ;
               k7          ;

LEXICON k6/8
               k6          ;
               k8          ;

!LEXICON k6e/12e
               !k6e         ;
               !k12e        ;

LEXICON k6e/19e
               k6e         ;
               k19e        ;

LEXICON k7/6
               k7          ;
               k6          ;

!LEXICON k8/11
               !k8          ;
               !k11         ;

LEXICON k8/17
  k8    ;
  k17   ;

!LEXICON k8/17e
  !k8   ;
  !k17e ;

LEXICON k8/6
  k8   ;
  k6   ;

!LEXICON k9/16
  !k9   ;
  !k16  ;

LEXICON k6_null ! antikrist
  k6e_null ;
  k_flt6   ;

LEXICON k6e_null
 +N+Msc: S_M_SGNOM_NULL ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGGEN      ;

LEXICON k6 ! úlvur
  k6e    ;
  k_flt6 ;

LEXICON k6e
 +N+Msc: S_M_SGNOM ;
 +N+Msc: S_M_SGACC ;
 +N+Msc: S_M_SGDAT ;
 +N+Msc: S_M_SGGEN ;

LEXICON k_flt6
 +N+Msc: S_M_PLNOM ;
 +N+Msc: S_M_PLACC ;
 +N+Msc: S_M_PLDAT ;
 +N+Msc: S_M_PLGEN ;


LEXICON k7 ! sandur
  k7e    ;
  k_flt7 ;

LEXICON k7e
 +N+Msc: S_M_SGNOM ;
 +N+Msc: S_M_SGACC ;
 +N+Msc: S_M_SGDAT ;
 +N+Msc: S_M_SGGEN ;


LEXICON k_flt7
 +N+Msc:       S_M_PLNOM ;
 +N+Msc:       S_M_PLACC ;
 +N+Msc:%^UUML S_M_PLDAT ;
 +N+Msc:       S_M_PLGEN ;

LEXICON k_flt8
  k_flt7 ;

LEXICON k8e
  k7e   ;

LEXICON k8 ! garður
  k7 ; ! Different u_umlaut

LEXICON k_flt9
  k_flt6 ;

LEXICON k9e
  k6e ;

LEXICON k9
  k6   ; ! Double consonant deletion in front of s

LEXICON k10/11
  k10 ;
  k11 ;

LEXICON k10
  k10e    ;
  k_flt10 ;

LEXICON k10e
 +N+Msc: S_M_SGNOM ;
 +N+Msc: S_M_SGACC ;
 +N+Msc: S_M_SGDAT ;
 +N+Msc: S_M_SGGEN ;

LEXICON k_flt10
 +N+Msc:       S_M_PLNOM ;
 +N+Msc:       S_M_PLACC ;
 +N+Msc:       S_M_PLDAT ; ! Double dative.
 +N+Msc:%^UUML S_M_PLDAT ;
 +N+Msc:       S_M_PLGEN ;

LEXICON k11/18
               k11      ;
               k18      ;

LEXICON k11 ! ísur
  k11e     ;
  k_flt11  ;

!LEXICON k11e/17f
  !k11e    ;
  !k_flt17 ;

!LEXICON k11e/26f
  !k11e    ;
  !k_flt26 ;

LEXICON k11e
 +N+Msc: S_M_SGNOM      ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGGEN_NULL ;

LEXICON k_flt11
 +N+Msc: S_M_PLNOM ;
 +N+Msc: S_M_PLACC ;
 +N+Msc: S_M_PLDAT ;
 +N+Msc: S_M_PLGEN ;


! ir_stems

!LEXICON k12/15
         !k12      ;
         !k15      ;

!LEXICON k12/45
         !k12      ;
         !k45      ;

LEXICON k12/6f
         k12      ;
         k_flt6   ;

LEXICON k12 ! vinur
  k12e    ;
  k_flt12 ;

LEXICON k12_boe
+N+Msc+Sg+Nom+Indef:øur MscNom_Flag ;
+N+Msc+Sg+Acc+Indef:ø MscNom_Flag ;
+N+Msc+Sg+Dat+Indef:ø MscNom_Flag ;
+N+Msc+Sg+Gen+Indef:íggjar MscNom_Flag ;

LEXICON k12e
 +N+Msc: S_M_SGNOM    ;
 +N+Msc: S_M_SGACC    ;
 +N+Msc: S_M_SGDAT    ;
 +N+Msc: S_M_SGGEN_AR ;

LEXICON k_flt12
 +N+Msc: S_M_PLNOM_IR ;
 +N+Msc: S_M_PLACC_IR ;
 +N+Msc: S_M_PLDAT    ;
 +N+Msc: S_M_PLGEN    ;

LEXICON k13e
         k12e           ;
 +N+Msc: S_M_SGDAT_NULL ;

LEXICON k13 ! vegur
         k12            ; ! As k12, plus a zero dative
 +N+Msc: S_M_SGDAT_NULL ;

LEXICON k14 ! staður
  k14e    ;
  k_flt14 ;

LEXICON k14e
 +N+Msc: S_M_SGNOM      ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGDAT_NULL ;
 +N+Msc: S_M_SGGEN_AR   ;

LEXICON k_flt14
 +N+Msc:       S_M_PLNOM_IR ;
 +N+Msc:       S_M_PLACC_IR ;
 +N+Msc:%^UUML S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k15/6
              k15         ;
              k6          ;

LEXICON k15 ! gestur
  k15e    ;
  k_flt15 ;

LEXICON k15e
 +N+Msc: S_M_SGNOM ;
 +N+Msc: S_M_SGACC ;
 +N+Msc: S_M_SGDAT ;
 +N+Msc: S_M_SGGEN ; !s_gen

LEXICON k_flt15
 +N+Msc: S_M_PLNOM_IR ;
 +N+Msc: S_M_PLACC_IR ;
 +N+Msc: S_M_PLDAT    ;
 +N+Msc: S_M_PLGEN    ;

!LEXICON k16/27
         !k16         ;
         !k27         ;

LEXICON k16/9
         k16         ;
         k9          ;

LEXICON k16e
         k15e        ;

LEXICON k16
  k15 ; ! Double consonant

LEXICON k_flt17
                k_flt15   ;
  +N+Msc:%^UUML S_M_PLDAT ;

!LEXICON k17/8/6
                !k17      ;
                !k8       ;
                !k6       ;

!LEXICON k17e
!               k15e      ;

LEXICON k17
               k15       ; ! Standard, but also u_umlaut
 +N+Msc:%^UUML S_M_PLDAT ;

LEXICON k18/11
  k18 ;
  k11 ;

LEXICON k18 ! dansur
  k18e    ;
  k_flt18 ;

LEXICON k18e
 +N+Msc: S_M_SGNOM      ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGGEN_NULL ; ! s_final

LEXICON k_flt18
 +N+Msc: S_M_PLNOM_IR ;
 +N+Msc: S_M_PLACC_IR ;
 +N+Msc: S_M_PLDAT    ;
 +N+Msc: S_M_PLGEN    ;

!LEXICON k19/12
         !k19         ;
         !k12         ;

LEXICON k19 ! meldur
  k19e    ;
  k_flt19 ;

LEXICON k19e/15e
  k19e   ;
  k15e   ;

LEXICON k19e/15
  k19e   ;
  k15   ;

LEXICON k19e
 +N+Msc:      S_M_SGNOM_NULL ;
 +N+Msc:      S_M_SGACC      ;
 +N+Msc:%^EPH S_M_SGDAT      ;
 +N+Msc:      S_M_SGGEN      ;

LEXICON k_flt19
 +N+Msc:%^EPH S_M_PLNOM ;
 +N+Msc:%^EPH S_M_PLACC ;
 +N+Msc:%^EPH S_M_PLDAT ;
 +N+Msc:%^EPH S_M_PLGEN ;


LEXICON k20 ! akur
  k20e    ;
  k_flt20 ;

LEXICON k20e
 +N+Msc:      S_M_SGNOM_NULL ;
 +N+Msc:      S_M_SGACC      ;
 +N+Msc:%^EPH S_M_SGDAT      ;
 +N+Msc:      S_M_SGGEN      ;

LEXICON k_flt20
 +N+Msc:%^EPH       S_M_PLNOM ;
 +N+Msc:%^EPH       S_M_PLACC ;
 +N+Msc:%^EPH%^UUML S_M_PLDAT ;
 +N+Msc:%^EPH       S_M_PLGEN ;

LEXICON k_flt21
  k_flt19 ;

LEXICON k21/20
  k21 ;
  k20 ;

LEXICON k21e
  k19e ;

LEXICON k21 ! stuðul
         k19       ; ! Can't see the diffrence ==> but:
 +N+Msc: S_M_SGDAT ; ! here is the difference

LEXICON k22e
  k19e ;

!LEXICON k_flt22
!  k_flt19 ;

LEXICON k22 ! himmal
         k19       ; ! himlinum etc.
 +N+Msc: S_M_SGDAT ; ! himmalinum

LEXICON k23/19
  k23   ;
  k19   ;

!LEXICON k23e/19e
  !k23e ;
  !k19e ;

LEXICON k23 ! róður
  k23e    ;
  k_flt23 ;

LEXICON k23e
 +N+Msc:      S_M_SGNOM_NULL ;
 +N+Msc:      S_M_SGACC      ;
 +N+Msc:%^EPH S_M_SGDAT      ;
 +N+Msc:%^EPH S_M_SGGEN_AR   ;

LEXICON k_flt23
 +N+Msc:%^EPH S_M_PLNOM ;
 +N+Msc:%^EPH S_M_PLACC ;
 +N+Msc:%^EPH S_M_PLDAT ;
 +N+Msc:%^EPH S_M_PLGEN ;

LEXICON k24/25
              k24      ;
              k25      ;

!LEXICON k25/17f
              !k25      ;
              !k_flt17  ;

LEXICON k25/24
              k25      ;
              k24      ;

LEXICON k26/12
    k26     ;
    k12     ;

!LEXICON k26/45
    !k26    ;
    !k45    ;

LEXICON k26/6f
    k26    ;
    k_flt6 ;

LEXICON k27/25
    k27     ;
    k25    ;

LEXICON k28/12
    k28    ;
    k12    ;

LEXICON k28/23
    k28    ;
    k23    ;

LEXICON k28e/12e
    k28e   ;
    k12e   ;

LEXICON k3/1
  k3   ;
  k1   ;

!LEXICON k32/9
  !k32 ;
  !k9  ;

LEXICON k4/1
  k4      ;
  k1      ;

LEXICON k45/6f
  k45    ;
  k_flt6 ;

LEXICON k6/12e
  k6     ;
  k12e   ;

LEXICON k6/15
  k6   ;
  k15  ;

!LEXICON k6e/12
  !k6e ;
  !k12 ;

LEXICON k3e/14f
  k3e     ;
  k_flt14 ;

LEXICON k24 ! fløttur
 +N+Msc:      S_M_SGNOM    ;
 +N+Msc:      S_M_SGACC    ;
 +N+Msc:      S_M_SGDAT    ;
 +N+Msc:%^OEA S_M_SGGEN_AR ;
 +N+Msc:      S_M_PLNOM    ;
 +N+Msc:      S_M_PLACC    ;
 +N+Msc:      S_M_PLDAT    ;
 +N+Msc:%^OEA S_M_PLGEN    ;

LEXICON k25 ! vøllur
  k25e    ;
  k_flt25 ;

LEXICON k25e
 +N+Msc:      S_M_SGNOM    ;
 +N+Msc:      S_M_SGACC    ;
 +N+Msc:      S_M_SGDAT    ;
 +N+Msc:%^OEA S_M_SGGEN_AR ;

LEXICON k_flt25
 +N+Msc:      S_M_PLNOM_IR ;
 +N+Msc:      S_M_PLACC_IR ;
 +N+Msc:      S_M_PLDAT    ;
 +N+Msc:%^OEA S_M_PLGEN    ;

LEXICON k26 ! táttur
  k26e    ;
  k_flt26 ;

LEXICON k26e
 +N+Msc: S_M_SGNOM    ;
 +N+Msc: S_M_SGACC    ;
 +N+Msc: S_M_SGDAT    ;
 +N+Msc: S_M_SGGEN_AR ;

LEXICON k_flt26
 +N+Msc:%^IUML S_M_PLNOM_IR ;
 +N+Msc:%^IUML S_M_PLACC_IR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k27 ! vøkstur
 +N+Msc:           S_M_SGNOM_NULL ;
 +N+Msc:           S_M_SGACC      ;
 +N+Msc:%^EPH      S_M_SGDAT      ;
 +N+Msc:%^EPH%^OEA S_M_SGGEN_AR   ;
 +N+Msc:%^EPH      S_M_PLNOM_IR   ;
 +N+Msc:%^EPH      S_M_PLACC_IR   ;
 +N+Msc:%^EPH      S_M_PLDAT      ;
 +N+Msc:%^EPH%^OEA S_M_PLGEN      ;

LEXICON k28 ! dráttur
  k28e    ;
  k_flt28 ;

LEXICON k28e
 +N+Msc:      S_M_SGNOM_NULL ;
 +N+Msc:      S_M_SGACC      ;
 +N+Msc:%^EPH S_M_SGDAT      ;
 +N+Msc:%^EPH S_M_SGGEN_AR   ;

LEXICON k_flt28
 +N+Msc:%^EPH S_M_PLNOM_IR ;
 +N+Msc:%^EPH S_M_PLACC_IR ;
 +N+Msc:%^EPH S_M_PLDAT    ;
 +N+Msc:%^EPH S_M_PLGEN    ;

LEXICON k29 ! tráður

 +N+Msc:          S_M_SGNOM      ;
 +N+Msc:          S_M_SGACC      ;
 +N+Msc:          S_M_SGDAT      ;
 +N+Msc:          S_M_SGDAT_NULL ;
 +N+Msc:%^IUMLr   S_M_SGDAT      ;
 +N+Msc:          S_M_SGGEN_AR   ;
 +N+Msc:%^IUML%>r S_M_PLNOM_IR   ;
 +N+Msc:%^IUML%>r S_M_PLACC_IR   ;
 +N+Msc:          S_M_PLDAT      ;
 +N+Msc:%^IUML%>r S_M_PLDAT      ;
 +N+Msc:          S_M_PLGEN      ;

LEXICON k30 ! fótur
  k30e    ;
  k_flt30 ;

LEXICON  k30e
 +N+Msc: S_M_SGNOM    ;
 +N+Msc: S_M_SGACC    ;
 +N+Msc: S_M_SGDAT    ;
 +N+Msc: S_M_SGGEN_AR ;

LEXICON k_flt30
 +N+Msc:%^IUML S_M_PLNOM_UR ;
 +N+Msc:%^IUML S_M_PLACC_UR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k31 ! veggur
  k31e    ;
  k_flt31 ;

LEXICON k31e
 +N+Msc:  S_M_SGNOM    ;
 +N+Msc:  S_M_SGACC    ;
 +N+Msc:  S_M_SGDAT    ;
 +N+Msc:j S_M_SGGEN_AR ;

LEXICON k_flt31
 +N+Msc:  S_M_PLNOM_IR ;
 +N+Msc:  S_M_PLACC_IR ;
 +N+Msc:j S_M_PLDAT    ;
 +N+Msc:j S_M_PLGEN    ;

LEXICON k32 ! ryggur
           k31e         ;
 +N+Msc:  S_M_SGNOM    ;
 +N+Msc:  S_M_SGACC    ;
 +N+Msc:  S_M_SGDAT    ;
 +N+Msc:j S_M_SGGEN_AR ;
 +N+Msc:  S_M_SGGEN    ;

           k_flt31      ;
 +N+Msc:  S_M_PLNOM_IR ;
 +N+Msc:  S_M_PLACC_IR ;
 +N+Msc:j S_M_PLDAT    ;
 +N+Msc:j S_M_PLGEN    ;

LEXICON k33 ! hylur
 +N+Msc:    S_M_SGNOM    ;
 +N+Msc:    S_M_SGACC    ;
 +N+Msc:    S_M_SGDAT    ;
 +N+Msc:%>j S_M_SGGEN_AR ;
 +N+Msc:    S_M_SGGEN    ;
 +N+Msc:%>j S_M_PLNOM    ;
 +N+Msc:%>j S_M_PLACC    ;
 +N+Msc:%>j S_M_PLDAT    ;
 +N+Msc:%>j S_M_PLGEN    ;

LEXICON k34 ! drongur
  k34e    ;
  k_flt34 ;

LEXICON k34e
 +N+Msc:%^EIO S_M_SGNOM    ;
 +N+Msc:%^EIO S_M_SGACC    ;
 +N+Msc:%^EIO S_M_SGDAT    ;
 +N+Msc:      S_M_SGDAT    ;
 +N+Msc:%>j   S_M_SGGEN_AR ;
 +N+Msc:%^EIO S_M_SGGEN    ;

LEXICON k_flt34
 +N+Msc:    S_M_PLNOM_IR ;
 +N+Msc:    S_M_PLACC_IR ;
 +N+Msc:%>j S_M_PLDAT    ;
 +N+Msc:%>j S_M_PLGEN    ;


LEXICON k35 ! bonkur
 +N+Msc:%^EIO S_M_SGNOM    ;
 +N+Msc:%^EIO S_M_SGACC    ;
 +N+Msc:%^EIO S_M_SGDAT    ;
 +N+Msc:      S_M_SGDAT    ;
 +N+Msc:%>j   S_M_SGGEN_AR ;
 +N+Msc:%^EIO S_M_SGGEN_AR ;
 +N+Msc:      S_M_PLNOM_IR ;
 +N+Msc:      S_M_PLACC_IR ;
 +N+Msc:%>j   S_M_PLDAT    ;
 +N+Msc:%>j   S_M_PLGEN    ;

LEXICON k36 ! heyggjur
 +N+Msc:       S_M_SGNOM ;
 +N+Msc:       S_M_SGACC ;
 +N+Msc:       S_M_SGDAT ;
 +N+Msc:%^GDEL S_M_SGGEN ;
 +N+Msc:       S_M_PLNOM ;
 +N+Msc:       S_M_PLACC ;
 +N+Msc:       S_M_PLDAT ;
 +N+Msc:       S_M_PLGEN ;

LEXICON k37 ! skógvur
  k37e    ;
  k_flt37 ;

LEXICON k37e
 +N+Msc:        S_M_SGNOM ;
 +N+Msc:        S_M_SGACC ;
 +N+Msc:        S_M_SGDAT ;
 +N+Msc:%^GVDEL S_M_SGGEN ;


LEXICON k_flt37
 +N+Msc:        S_M_PLNOM  ;
 +N+Msc:        S_M_PLACC  ;
 +N+Msc:%^GVDEL S_M_PLDATm ;
 +N+Msc:        S_M_PLGEN  ;

LEXICON k38 ! bógvur
  k38e    ;
  k_flt38 ;

LEXICON k38e
 +N+Msc:       S_M_SGNOM ;
 +N+Msc:       S_M_SGACC ;
 +N+Msc:%^IUML S_M_SGDAT ;
 +N+Msc:       S_M_SGDAT ;
 +N+Msc:%^VDEL S_M_SGGEN ;

LEXICON k_flt38
 +N+Msc:       S_M_PLNOM    ;
 +N+Msc:%^IUML S_M_PLNOM_IR ;
 +N+Msc:       S_M_PLACC    ;
 +N+Msc:%^IUML S_M_PLACC_IR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k39 ! sjógvur
  k39e    ;
  k_flt39 ;

LEXICON k39e
 +N+Msc:        S_M_SGNOM    ;
 +N+Msc:        S_M_SGACC    ;
 +N+Msc:        S_M_SGDAT_2  ;
 +N+Msc:%^GDEL  S_M_SGGEN_AR ;
 +N+Msc:%^GVDEL S_M_SGGEN    ;

LEXICON k_flt39
 +N+Msc: S_M_PLNOM ;
 +N+Msc: S_M_PLACC ;
 +N+Msc: S_M_PLDAT ;
 +N+Msc: S_M_PLGEN ;

LEXICON k40 ! hógvur
  k40e    ;
  k_flt40 ;

LEXICON k40e
 +N+Msc:       S_M_SGNOM ;
 +N+Msc:       S_M_SGACC ;
 +N+Msc:       S_M_SGDAT ;
 +N+Msc:%^GDEL S_M_SGGEN ;

LEXICON k_flt40
 +N+Msc: S_M_PLNOM ;
 +N+Msc: S_M_PLACC ;
 +N+Msc: S_M_PLDAT ;
 +N+Msc: S_M_PLGEN ;

LEXICON k41 ! maður
  k41e    ;
  k_flt41 ;

LEXICON k41e
 +N+Msc+Sg+Nom:maður   DF_N_SGm ;
              :mann    k41_obl  ;
 +N+Msc+Pl+Dat:monn%>u DF_D_PL  ;

LEXICON k41_obl
 +N+Msc: S_M_SGACC ;
 +N+Msc: S_M_SGDAT ;
 +N+Msc: S_M_SGGEN ;

LEXICON k_flt41
 +N+Msc:menn S_M_PLNOM_NULL ;
 +N+Msc:menn S_M_PLACC_NULL ;
 +N+Msc:mann S_M_PLGEN      ;

LEXICON k42 ! dagur
  k42e    ;
  k_flt42 ;

LEXICON k42e
 +N+Msc:       S_M_SGNOM ;
 +N+Msc:       S_M_SGACC ;
 +N+Msc:%^IUML S_M_SGDAT ;
 +N+Msc:       S_M_SGGEN ;

LEXICON k_flt42
 +N+Msc:       S_M_PLNOM ;
 +N+Msc:       S_M_PLACC ;
 +N+Msc:%^UUML S_M_PLDAT ;
 +N+Msc:       S_M_PLGEN ;

LEXICON k43 ! faðir
  k43e    ;
  k_flt43 ;

LEXICON k43e
! +N+Msc:      S_M_SGNOM_NULL ;
! +N+Msc:      S_M_SGACC      ;
! +N+Msc:      S_M_SGDAT_NULL ;
! +N+Msc:      S_M_SGGEN      ;

  +N+Msc:faðir S_M_SGNOM_NULL ;
  +N+Msc:faðir S_M_SGACC      ;
  +N+Msc:faðir S_M_SGDAT_NULL ;
  +N+Msc:faðir S_M_SGGEN      ;

LEXICON k_flt43
! +N+Msc:%^IUML S_M_PLNOM ;
! +N+Msc:%^IUML S_M_PLACC ;
! +N+Msc:%^IUML S_M_PLDAT ;
! +N+Msc:%^IUML S_M_PLGEN ;

  +N+Msc:feðr   S_M_PLNOM ;
  +N+Msc:feðr   S_M_PLACC ;
  +N+Msc:feðr   S_M_PLDAT ;
  +N+Msc:feðr   S_M_PLGEN ;


LEXICON k44 ! bróðir, stem is bróð
       +N+Msc+Sg+Nom:ir     DF_N_SGm       ;
              +N+Msc:ur     S_M_SGACC      ;
 +N+Msc+Sg+Dat+Indef:ur     ENDLEX          ;
   +N+Msc+Sg+Dat+Def:urinum ENDLEX          ;
   +N+Msc+Sg+Dat+Def:rinum  ENDLEX          ;
              +N+Msc:ur     S_M_SGGEN_NULL ;
                    :ur     k_flt44        ;

LEXICON k_flt44
 +N+Msc:%^IUML      S_M_PLNOM_NULL_NULL ;
 +N+Msc:%^IUML      S_M_PLACC_NULL_NULL ;
 +N+Msc:%^EPH%^IUML S_M_PLDAT           ;
 +N+Msc:%^EPH%^IUML S_M_PLGEN           ;

LEXICON k45 ! spónur
  k45e    ;
  k_flt45 ;

LEXICON k45e
 +N+Msc: S_M_SGNOM    ;
 +N+Msc: S_M_SGACC    ;
 +N+Msc: S_M_SGDAT    ;
 +N+Msc: S_M_SGGEN_AR ;

LEXICON k_flt45
 +N+Msc:%^IUML S_M_PLNOM_IR ;
 +N+Msc:%^IUML S_M_PLACC_IR ;
 +N+Msc:%^IUML S_M_PLDAT    ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k46 ! fjørður ! XXX start her.
k46e ;
k_flt46 ;

LEXICON k46e
 +N+Msc:       S_M_SGNOM    ;
 +N+Msc:       S_M_SGACC    ;
 +N+Msc:%^IUML S_M_SGDAT    ;
 +N+Msc:%^OEA  S_M_SGGEN_AR ;

LEXICON k_flt46
 +N+Msc:%^IUML S_M_PLNOM_IR ;
 +N+Msc:%^IUML S_M_PLACC_IR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:%^IUML S_M_PLDAT    ;
 +N+Msc:%^OEA  S_M_PLGEN    ;

LEXICON k47 ! sonur
  k47e    ;
  k_flt47 ;

LEXICON k47e
 +N+Msc:       S_M_SGNOM    ;
 +N+Msc:       S_M_SGACC    ;
 +N+Msc:       S_M_SGDAT    ;
 +N+Msc:%^IUML S_M_SGDAT    ;
 +N+Msc:       S_M_SGGEN_AR ;

LEXICON k_flt47
 +N+Msc:%^IUML S_M_PLNOM_IR ;
 +N+Msc:%^IUML S_M_PLACC_IR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:%^IUML S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k48 ! hamar
  k48e    ;
  k_flt48 ;

LEXICON k48e
 +N+Msc:      S_M_SGNOM_NULL ;
 +N+Msc:      S_M_SGACC      ;
 +N+Msc:%^EPH S_M_SGDAT      ;
 +N+Msc:      S_M_SGGEN      ;

LEXICON k_flt48
 +N+Msc:%^EPH       S_M_PLNOM ;
 +N+Msc:%^EPH       S_M_PLACC ;
 +N+Msc:%^EPH%^UUML S_M_PLDAT ;
 +N+Msc:%^EPH       S_M_PLDAT ;
 +N+Msc:%^EPH       S_M_PLGEN ;

LEXICON k49 ! verkur
  k49e    ;
  k_flt49 ;

LEXICON k49e
 +N+Msc:%^JDEL S_M_SGNOM ;
 +N+Msc:%^JDEL S_M_SGACC ;
 +N+Msc:%^JDEL S_M_SGDAT ;
 +N+Msc:%^JDEL S_M_SGGEN ;

LEXICON k_flt49
 +N+Msc:%^JDEL S_M_PLNOM_IR ;
 +N+Msc:%^JDEL S_M_PLACC_IR ;
 +N+Msc:       S_M_PLDAT    ;
 +N+Msc:       S_M_PLGEN    ;

LEXICON k50 ! skjøldur (non_poetic)
 +N+Msc:           S_M_SGNOM_NULL ;
 +N+Msc:           S_M_SGACC      ;
 +N+Msc:%^EPH      S_M_SGDAT      ;
 +N+Msc:%^EPH%^OEA S_M_SGGEN_AR   ;
 +N+Msc:%^EPH      S_M_PLNOM      ;
 +N+Msc:%^EPH      S_M_PLACC      ;
 +N+Msc:%^EPH      S_M_PLDAT      ;
 +N+Msc:%^EPH%^OEA S_M_PLGEN      ;

LEXICON k51 ! luður
 +N+Msc:ð  S_M_SGNOM ;
 +N+Msc:nn S_M_SGACC ;
 +N+Msc:nn S_M_SGDAT ;
 +N+Msc:nn S_M_SGGEN ;
 +N+Msc:nn S_M_PLNOM ;
 +N+Msc:nn S_M_PLACC ;
 +N+Msc:nn S_M_PLDAT ;
 +N+Msc:nn S_M_PLGEN ;

LEXICON k52 ! primus
  k52e    ;
  k_flt52 ;

LEXICON k52e
 +N+Msc: S_M_SGNOM_NULL ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGGEN      ;

LEXICON k_flt52
 +N+Msc: S_M_PLNOM ;
 +N+Msc: S_M_PLACC ;
 +N+Msc: S_M_PLDAT ;
 +N+Msc: S_M_PLGEN ;

LEXICON k53 ! aðal
 +N+Msc: S_M_SGNOM_NULL ;
 +N+Msc: S_M_SGACC      ;
 +N+Msc: S_M_SGDAT      ;
 +N+Msc: S_M_SGGEN      ;



! Feminines
! __________

!LEXICON kv1/2 not in use
!  kv1 ;
!  kv2 ;

LEXICON kv_flt0
  +N+Pl+Nom: # ;
  +N+Pl+Acc: # ;
  +N+Pl+Dat: # ;
  +N+Pl+Gen: # ;
  
!LEXICON kv_flt ! gætur, paradigm missing in dictionary
!  +N+Pl+Nom:%>ur # ;

LEXICON kv1/6f
  kv1 ;
  kv6 ;

LEXICON kv1 ! genta
  kv1e    ;
  kv_flt1 ;

LEXICON kv1e
 +N+Fem: W_F_SGNOM ;
 +N+Fem: W_F_SGOBL ;

LEXICON kv_flt1
 +N+Fem: F_PLNA_UR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

!LEXICON kv2/22
!  kv2  ;
!  kv22 ;


LEXICON kv2/6
  kv2 ;
  kv6 ;

LEXICON kv2/27
  kv2  ;
  kv27 ;

LEXICON kv2/3
  kv2 ;
  kv3 ;

LEXICON kv2e/17e
  kv2e  ;
  kv17e ;

LEXICON kv2 ! bygd
  kv2e    ;
  kv_flt2 ;

LEXICON kv35 ! á
+N+Fem+Sg+Acc+Def:%>nna FemObl_Flag ;
+N+Fem+Sg+Dat+Def:%>nni FemObl_Flag ;
RESTFORMS_SHORT_kv2e ;
  kv_flt2 ;


LEXICON kv36 
  kv36e ;
  kv_flt2 ;

LEXICON kv36e ! støð
+N+Fem+Sg+Acc+Def:%>na FemObl_Flag ;
+N+Fem+Sg+Dat+Def:%>ni FemObl_Flag ;
RESTFORMS_SHORT_kv2e ;


LEXICON RESTFORMS_SHORT_kv2e
+N+Fem+Sg+Nom: DF_N_SGf_S ;
+N+Fem+Sg+Acc+Indef: FemNom_Flag ;
+N+Fem+Sg+Dat+Indef: FemObl_Flag ;
+N+Fem+Sg+Acc+Def:%>nna FemObl_Flag ;
+N+Fem+Sg+Dat+Def:%>nni FemObl_Flag ;
+N+Fem+Sg+Gen: DF_G_SGf_W ;



LEXICON kv_bd2e
  kv2e ;

LEXICON kv2e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt2
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

LEXICON kv3/2
  kv3 ;
  kv2 ;

LEXICON kv3/5
  kv3 ;
  kv5 ;

LEXICON kv3/7
  kv3 ;
  kv7 ;

LEXICON kv3 ! søgn
  kv3e    ;
  kv_flt3 ;

LEXICON kv3e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^OEA S_F_SGGEN ;

LEXICON kv_flt3
 +N+Fem:%^OEA F_PLNA_IR ;
 +N+Fem:%^OEA F_PLDAT   ;
 +N+Fem:      F_PLDAT   ;
 +N+Fem:%^OEA F_PLGEN   ;


LEXICON kv4 ! fonn
  kv4e    ;
  kv_flt4 ;

LEXICON kv4e
 +N+Fem:     S_F_SGNAD ;
 +N+Fem:%^OA S_F_SGGEN ;

LEXICON kv_flt4
 +N+Fem:%^OA F_PLNA_IR ;
 +N+Fem:     F_PLDAT   ;
 +N+Fem:%^OA F_PLGEN   ;

LEXICON kv5 ! øld
  kv5e    ;
  kv_flt5 ;

LEXICON kv5e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^OEA S_F_SGGEN ;

LEXICON kv_flt5
 +N+Fem:      F_PLNA_IR ;
 +N+Fem:      F_PLDAT   ;
 +N+Fem:%^OEA F_PLGEN   ;

LEXICON kv6/2
  kv6 ;
  kv2 ;

LEXICON kv6/h16
 kv6 ;
 h16 ;
 

LEXICON kv6 ! ár, oy
  kv6e    ;
  kv_flt6 ;
  
LEXICON kv6e/h16e
 kv6e ;
 h16e ;
 
LEXICON kv6e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt6
 +N+Fem: F_PLNA_AR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

LEXICON kv7/3
  kv7 ;
  kv3 ;

LEXICON kv7 ! vørr
  kv7e    ;
  kv_flt7 ;

LEXICON kv7e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^OEA S_F_SGGEN ;

LEXICON kv_flt7
 +N+Fem:%^OEA F_PLNA_AR ;
 +N+Fem:      F_PLDAT   ;
 +N+Fem:%^OEA F_PLDAT   ;
 +N+Fem:%^OEA F_PLGEN   ;

LEXICON kv8 ! egg
  kv8e    ;
  kv_flt8 ;

LEXICON kv8e
 +N+Fem:%^JDEL S_F_SGNAD ;
 +N+Fem:       S_F_SGGEN ;

LEXICON kv_flt8
 +N+Fem: F_PLNA_AR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

LEXICON kv9/2
  kv9 ;
  kv2 ;

LEXICON kv9 ! stong
  kv9e    ;
  kv_flt9 ;

LEXICON kv9e
 +N+Fem:     S_F_SGNAD ;
 +N+Fem:%^OA S_F_SGGEN ;

LEXICON kv_flt9
 +N+Fem:%^eIUML F_PLNA_UR ;
 +N+Fem:        F_PLDAT   ;
 +N+Fem:%^OA    F_PLGEN   ;

LEXICON kv10 ! dorg
 +N+Fem:       S_F_SGNAD ;
 +N+Fem:       S_F_SGGEN ;
 +N+Fem:%^IUML F_PLNA_UR ;
 +N+Fem:       F_PLDAT   ;
 +N+Fem:       F_PLGEN   ;

LEXICON kv11 ! song
  kv11e    ;
  kv_flt11 ;

LEXICON kv11e
 +N+Fem:%^JDEL S_F_SGNAD ;
 +N+Fem:%^JDEL S_F_SGGEN ;
 +N+Fem:%^EIO  S_F_SGGEN ;


LEXICON kv_flt11
 +N+Fem:%^EIO  F_PLNA_IR ;
 +N+Fem:%^IUML F_PLNA_UR ;
 +N+Fem:%^EIO  F_PLDAT   ;
 +N+Fem:%^EIO  F_PLGEN   ;

LEXICON kv12 ! bók
  kv12e    ;
  kv_flt12 ;

LEXICON kv12e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

!LEXICON kv12f
!  kv_flt12 ;

LEXICON kv_flt12
 +N+Fem:%^IUML F_PLNA_UR ;
 +N+Fem:       F_PLDAT   ;
 +N+Fem:       F_PLGEN   ;

LEXICON kv13 ! mørk
  kv13e    ;
  kv_flt13 ;

LEXICON kv13e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^OEA S_F_SGGEN ;

LEXICON kv_flt13
 +N+Fem:%^IUML F_PLNA_UR ;
 +N+Fem:       F_PLDAT   ;
 +N+Fem:%^OEA  F_PLGEN   ;

LEXICON kv14 ! nátt
  kv14e    ;
  kv_flt14 ;

LEXICON kv14e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt14
 +N+Fem:%^IUML F_PLNA_UR ; ! XXX one t, nætur
 +N+Fem:       F_PLDAT   ;
 +N+Fem:       F_PLGEN   ;

LEXICON kv15 ! tonn
  kv15e    ;
  kv_flt15 ;

LEXICON kv15e
 +N+Fem:     S_F_SGNAD ;
 +N+Fem:%^OA S_F_SGGEN ;

LEXICON kv_flt15f
  kv_flt15 ;

LEXICON kv_flt15
 +N+Fem:%^IUML F_PLNA_NULL ;
 +N+Fem:       F_PLDAT     ;
 +N+Fem:%^OEA  F_PLGEN     ;

LEXICON kv16 ! mús
 +N+Fem:       S_F_SGNAD   ;
 +N+Fem:       S_F_SGGEN   ;
 +N+Fem:%^IUML F_PLNA_NULL ;
 +N+Fem:       F_PLDAT     ;
 +N+Fem:       F_PLGEN     ;

LEXICON kv17 ! mastur
  kv17e    ;
  kv_flt17 ;

LEXICON kv17e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^EPH S_F_SGGEN ;

LEXICON kv_flt17
 +N+Fem:%^EPH F_PLNA_AR ;
 +N+Fem:%^EPH F_PLDAT   ;
 +N+Fem:%^EPH F_PLGEN   ;

LEXICON kv18/17
  kv18 ;
  kv17 ;

LEXICON kv18 ! brúður
 +N+Fem:            S_F_SGNAD ;
 +N+Fem:%^EPH%^RDEL S_F_SGGEN ;
 +N+Fem:%^EPH       F_PLNA_AR ;
 +N+Fem:%^EPH       F_PLDAT   ;
 +N+Fem:%^EPH       F_PLGEN   ;


LEXICON kv19 ! fjøður
  kv19e    ;
  kv_flt19 ;

LEXICON kv19e
 +N+Fem:      S_F_SGNAD ;
 +N+Fem:%^EPH S_F_SGGEN ;

LEXICON kv_flt19f
  kv_flt19f ;

LEXICON kv_flt19
 +N+Fem:%^EPH%^OEA F_PLNA_AR ;
 +N+Fem:%^EPH      F_PLDAT   ;
 +N+Fem:%^EPH%^OEA F_PLDAT   ;
 +N+Fem:%^EPH%^OEA F_PLGEN   ;


LEXICON kv20 ! ær (only)
 +N+Fem:ær S_F_SGNAD      ;
 +N+Fem:ær S_F_SGGEN_NULL ;
 +N+Fem:ær F_PLNA_NULL    ;
 +N+Fem:ó  F_PLDAT        ;
 +N+Fem:á  F_PLGEN        ;


LEXICON kv21 ! gjógv
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;


LEXICON kv22 ! klógv
  kv22e    ;
  kv_flt22 ;

LEXICON kv22e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt22
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;


LEXICON kv23 ! trúgv
  kv23e    ;
  kv_flt23 ;

LEXICON kv23e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt23
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

LEXICON kv24/2
  kv24 ;
  kv2  ;

LEXICON kv24/6
  kv24 ;
  kv6  ;
  
!LEXICON kv24/6e
!  kv24 ;
!  kv6e  ;

!LEXICON kv24e/6e
!  kv24e ;
!  kv6e  ;

LEXICON kv24 ! kúgv
  kv24e    ;
  kv_flt24 ;

LEXICON kv24e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt24f
  kv_flt24 ;

LEXICON kv_flt24
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;


LEXICON kv25 ! gás
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;


LEXICON kv26 ! tá
  kv26e    ;
  kv_flt26 ;

LEXICON kv26e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt26
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;


LEXICON kv27 ! rás
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;
 +N+Fem: F_PLNA_IR ;
 +N+Fem: F_PLDAT   ;
 +N+Fem: F_PLGEN   ;

LEXICON kv28/12
kv28 ;
kv12 ;

LEXICON kv28 ! dyr
!  kv28e    ;
   kv_flt28 ;

!LEXICON kv28e ! no ex
   !#       ;

LEXICON kv_flt28
 +N+Fem:%^IUML F_PLNA_NULL ;
 +N+Fem:       F_PLDAT     ;
 +N+Fem:       F_PLGEN     ;

LEXICON kv29 ! móðir
 +N+Fem+Sg:móð kv29e    ;
 +N+Fem+Pl:møð kv_flt29 ;


LEXICON kv29e
 +Nom:%>ir DF_N_SGf_S ;
 +Acc:%>ur DF_A_SGf_S ;
 +Dat:%>ur DF_D_SGf_S ;
 +Gen:%>ur DF_G_SGf_S ;

LEXICON kv_flt29
 +Nom:%>ur DF_NA_PLf ;
 +Acc:%>ur DF_NA_PLf ;
 +Dat:r%>u DF_D_PL   ;
 +Gen:r%>a DF_G_PL   ;

LEXICON kv30 ! dóttir
 +N+Fem+Sg:dótt kv30e    ;
 +N+Fem+Pl:døt  kv_flt30 ;

LEXICON kv30e ! Remove for kv29e?
  kv29e ;

LEXICON kv_flt30
  kv_flt29 ;

LEXICON kv31 ! systir
 +N+Fem+Sg+Nom:%>ir  DF_N_SGf_S ;
 +N+Fem+Sg+Acc:%>ur  DF_A_SGf_S ;
 +N+Fem+Sg+Dat:%>ur  DF_D_SGf_S ;
 +N+Fem+Sg+Gen:%>ur  DF_G_SGf_S ;
 +N+Fem+Sg+Nom:%>rar DF_NA_PLf  ;
 +N+Fem+Sg+Acc:%>rar DF_NA_PLf  ;
 +N+Fem+Sg+Dat:%>rum DF_D_PL    ;
 +N+Fem+Sg+Gen:%>ra  DF_G_PL    ;

LEXICON kv32 ! mýri
  kv32e    ;
  kv_flt32 ;

LEXICON kv32e
 +N+Fem+Sg+Nom+Indef:%>i  ENDLEX ;
 +N+Fem+Sg+Acc+Indef:%>i  ENDLEX ;
 +N+Fem+Sg+Dat+Indef:%>i  ENDLEX ;
 +N+Fem+Sg+Gen+Indef:%>ar ENDLEX ;

LEXICON kv_flt32
 +N+Fem+Pl+Nom+Indef:%>ar ENDLEX ;
 +N+Fem+Pl+Acc+Indef:%>ar ENDLEX ;
 +N+Fem+Pl+Dat+Indef:%>um ENDLEX ;
 +N+Fem+Pl+Gen+Indef:%>a  ENDLEX ;

LEXICON kv33e/h24e
 kv33e ;
 h24e ;

LEXICON kv33 ! gleði
  kv33e    ;
  kv_flt33 ;

LEXICON kv33e
 +N+Fem+Sg+Nom+Indef: ENDLEX ;
 +N+Fem+Sg+Acc+Indef: ENDLEX ;
 +N+Fem+Sg+Dat+Indef: ENDLEX ;
 +N+Fem+Sg+Gen+Indef: ENDLEX ;

LEXICON kv_flt33
 +N+Fem+Sg+Nom+Indef:%>r ENDLEX ;
 +N+Fem+Sg+Acc+Indef:%>r ENDLEX ;

LEXICON kv34 ! kraft
  kv34e    ;
  kv_flt34 ;

LEXICON kv34e
 +N+Fem: S_F_SGNAD ;
 +N+Fem: S_F_SGGEN ;

LEXICON kv_flt34
 +N+Fem:%^IUML F_PLNA_IR ;
 +N+Fem:%^IUML F_PLDAT   ;
 +N+Fem:%^IUML F_PLGEN   ;

LEXICON h1 ! eyga
  h1e    ; !eyga
  h_flt1 ;

LEXICON h1e
 +N+Neu+Sg+Nom:%>a DF_NA_SGn_indef ;
 +N+Neu+Sg+Nom:%>a DF_NA_SGn_def   ;
 +N+Neu+Sg+Acc:%>a DF_NA_SGn_indef ;
 +N+Neu+Sg+Acc:%>a DF_NA_SGn_def   ;
 +N+Neu+Sg+Dat:%>a DF_D_SGn        ;
 +N+Neu+Sg+Gen:%>a DF_G_SGn_a      ;

LEXICON h_flt1
 +N+Neu: N_PLNA_u_ur ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG_na    ;

LEXICON h2 ! hjarta
               h1e         ;
 +N+Neu:%^UUML N_PLNA_u_ur ;
 +N+Neu:%^UUML N_PLD       ;
 +N+Neu:%^UUML N_PLG_na    ;

LEXICON h2e
h1e ;

LEXICON h3/41
 h3 ;
 h41 ;
 
LEXICON h3/5
  h3 ;
  h5 ;

LEXICON h3/22
  h3  ;
  h22 ;


LEXICON h3_s ! universitet
         h3e         ;
! +N+Neu: S_N_S_CMPND ;
         h_flt3      ;

LEXICON h3 ! orð
         h3e         ;
! +N+Neu: S_N_A_CMPND ;
         h_flt3      ;

LEXICON h3e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;


LEXICON h_flt3f
  h_flt3 ;

LEXICON h_flt3
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;

!LEXICON h4/5
!  h4 ;
!  h5 ;

LEXICON h4 ! boð
  h4e    ;
  h_flt4 ;

LEXICON h4e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGG     ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt4
  +N+Neu: N_PLNA    ;
  +N+Neu: N_PLD_0_U ; ! liðnum
! +N+Neu: N_PLD     ; ! liðunum, comment in if OK
  +N+Neu: N_PLG     ;

LEXICON h5/3
  h5 ;
  h3 ;

LEXICON h5/6
  h5 ;
  h6 ;

LEXICON h5 ! barn
  h5e    ;
  h_flt5 ;

LEXICON h5e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt5
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;

LEXICON h6/4
  h6 ;
  h4 ;

LEXICON h6 ! lag
  h6e    ;
  h_flt6 ;

LEXICON h6e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGG     ;
 +N+Neu: S_N_SGDG_is ; ! blaðinum
 +N+Neu: S_N_SGD     ; ! blaðnum XXX check

LEXICON h_flt6
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;

LEXICON h7/3
h7 ;
h3 ;

LEXICON h7/3e
h7 ;
h3e ;

LEXICON h7/4
h7 ;
h4 ;

LEXICON h7 ! bræv
 +N+Neu:       S_N_SGNA    ;
 +N+Neu:       S_N_SGDG_is ;
 +N+Neu:%^UUML N_PLNA      ;
 +N+Neu:%^UUML N_PLD       ;
 +N+Neu:       N_PLG       ;

LEXICON h8 ! land
  h8e    ;
  h_flt8 ;

LEXICON h8e ! land
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt8 ! land
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;

LEXICON h9/10
  h9  ;
  h10 ;

LEXICON h9/kv2
  h9  ;
  kv2 ;

LEXICON h9/41
  h9  ;
  h41 ;

LEXICON h9 ! blikk
  h9e    ;
  h_flt9 ;

LEXICON h9e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt9
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;

LEXICON h10 ! fall
  h10e    ;
  h_flt10 ;

LEXICON h10e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt10
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;


LEXICON h11e/22f
  h11e    ;
  h_flt22 ;

LEXICON h_flt22/11e
 h_flt22 ;
 h11e ;

LEXICON h11 ! hús
  h11e    ;
  h_flt11 ;

LEXICON h11e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt11
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;

LEXICON h12 ! glas
  h12e    ;
  h_flt12 ;

LEXICON h12e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt12
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;


LEXICON h13 ! setur
  h13e    ;
  h_flt13 ;

LEXICON h13e
 +N+Neu:%^EPH S_N_SGNA    ;
 +N+Neu:%^EPH S_N_SGDG_is ;

LEXICON h_flt13
 +N+Neu:%^EPH N_PLNA ;
 +N+Neu:%^EPH N_PLD  ;
 +N+Neu:%^EPH N_PLG  ;


LEXICON h14 ! pistr
  h14e    ;
  h_flt14 ;

LEXICON h14e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt14
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h15 ! tjaldur
  h15e ;
  h15f ;

LEXICON h15e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h15f
 +N+Neu:%^UUML N_PLNA ;
 +N+Neu:%^UUML N_PLD  ;
 +N+Neu:       N_PLG  ;

! Start here.
LEXICON h16 ! skýggj
  h16e    ;
  h_flt16 ;

LEXICON h16e
 +N+Neu:        S_N_SGNA  ;
 +N+Neu:%^JI    S_N_SGD_i ; !skýggi
 +N+Neu:        S_N_SGD   ; !skýggj
 +N+Neu:%^GGDEL S_N_SGG_s ; !krýs

!LEXICON h_flt16f
!  h_flt16 ;

LEXICON h_flt16
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;

LEXICON h17 ! kríggj
  h17e    ;
  h_flt17 ;

LEXICON h17e 
 +N+Neu:       S_N_SGNA  ;
 +N+Neu:%^JI   S_N_SGD_i ; !kríggi
 +N+Neu+Sg+Dat+Indef:   Neu_Flag ; !kríggj
 +N+Neu+Sg+Dat+Def:%^GDEL%>num Neu_Flag ; !krígnum
 +N+Neu:%^GDEL S_N_SGG_s ; !krígs

LEXICON h_flt17
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h18 ! týggi
  h18e    ;
  h_flt18 ;

LEXICON h18e
 +N+Neu: 0_N_SGNA    ;
 +N+Neu:%>is S_N_SGDG ;

LEXICON h_flt18
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h19 ! prógv
  h19e    ;
  h_flt19 ;

LEXICON h19e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt19
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h20 ! búgv
  h20e    ;
  h_flt20 ;

LEXICON h20e
 +N+Neu:        S_N_SGNA  ;
 +N+Neu:        S_N_SGD_i ;
 +N+Neu:%^GVDEL S_N_SGG_s ;

LEXICON h_flt20
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h21 ! plógv
  h21e    ;
  h_flt21 ;

LEXICON h21e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt21
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h22 ! ber
  h22e    ;
  h_flt22 ;

LEXICON h22e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt22
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h23 ! egg
  h23e    ;
  h_flt23 ;

LEXICON h23e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt23
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h24 ! kvæði
  h24e     ;
  h_flt24r ;

!LEXICON h_flt24e
!  h24e ;

LEXICON h24e
 +N+Neu: i_N_SGNA   ;
 +N+Neu: S_N_SGD_i  ;
 +N+Neu: S_N_SGG_is ;

LEXICON h_flt24r
  +N+Neu:  N_PLNA_i_ir ;
!          h_flt24     ; ! Ordinary pl
! +N+Neu:r N_PLNA      ; ! "Danish" r pl.
  +N+Neu:  N_PLD       ;
  +N+Neu:  N_PLG       ;

!LEXICON h_flt24
! +N+Neu:  N_PLNA      ;
! +N+Neu:  N_PLD       ;
! +N+Neu:  N_PLG       ;


LEXICON h25 ! merki
  h25e    ;
  h_flt25 ;

LEXICON h25e
 +N+Neu:i S_N_SGNA    ;
 +N+Neu:  S_N_SGDG_is ;

LEXICON h_flt25
 +N+Neu: N_PLNA ;
 +N+Neu:j N_PLD  ;
 +N+Neu:j N_PLG  ;


LEXICON h26 ! tiðindi
 h26e ;
 h_flt26 ;

LEXICON h26e 
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt26 
 +N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h27 ! systkin
  h27e    ;
  h_flt27 ;

LEXICON h27e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt27
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h28 ! bakari
  h28e    ;
  h_flt28 ;

LEXICON h28e
 +N+Neu: S_N_SGNA  ; !bakarí
 +N+Neu: S_N_SGD   ; ! bakarí
 +N+Neu: S_N_SGD_i ; ! bakaríi
 +N+Neu: S_N_SGG_s ; ! bakarís

LEXICON h_flt28
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;


LEXICON h29 ! kamar
  h29e    ;
  h_flt29 ;

LEXICON h29e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt29
 +N+Neu: N_PLNA ;
 +N+Neu: N_PLD  ;
 +N+Neu: N_PLG  ;

LEXICON h30 ! summar
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h31 ! nummar
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h32 ! høvd
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h33 ! høvur
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h34 ! fæ
h34e ;
h_flt34 ;

LEXICON h34e
+N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt34
+N+Neu: N_PLNA      ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;

! Leksikona h35 til og med h41 er __ikkje__ ferdig.


LEXICON h35e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h36 
 +N+Neu:%>um S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: F_PLNA_IR ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;
 
LEXICON h37
h37e ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;

LEXICON h37e
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGD_i ;
 +N+Neu: S_N_SGG_s ;

LEXICON h38
h38e ;
h_flt38 ;

LEXICON h38e 
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGD_i ;
 +N+Neu: S_N_SGG_s ;

LEXICON h_flt38 
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;

LEXICON h39
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG       ;


LEXICON h40
 +N+Neu:fur S_N_SGNA    ;
 +N+Neu:fr S_N_SGD_i ;
 +N+Neu: S_N_SGG_s ;
 +N+Neu:r N_PLD       ;
 +N+Neu:r N_PLG       ;

LEXICON h41/h9
 h41 ;
 h9 ;

LEXICON h41
h41e ;
h_flt41 ;

LEXICON h41e 
 +N+Neu: S_N_SGNA    ;
 +N+Neu: S_N_SGDG_is ;

LEXICON h_flt41 
 +N+Neu: N_PLD       ;
 +N+Neu: N_PLG_NULL  ;




LEXICON h42
 +N+Neu+Sg+Indef: ENDLEX ; !check.


! =============== !
! Case inflection !
! =============== !

! This is the second layer. Here we do indefinite
! forms and compounds.

! Masculine forms
! _______________

! Weak case suffixes.

LEXICON W_M_SGNOM
 +Sg+Nom:%>i DF_N_SGm_indef ;
 +Sg+Nom:%>i DF_N_SGm_def   ;

LEXICON W_M_SGACC
 +Sg+Acc:%>a DF_A_SGm ; ! risan

LEXICON W_M_SGDAT
 +Sg+Dat:%>a DF_D_SGm ;

LEXICON W_M_SGDAT_mixed !felagnum
 +Sg+Dat+Indef:%>a   ENDLEX ;
   +Sg+Dat+Def:%>num ENDLEX ;

LEXICON W_M_SGGEN
 +Sg+Gen+Indef:%>a   ENDLEX ;
 +Sg+Gen+Indef:%>a   R ; ! compound
   +Sg+Gen+Def:%>ans ENDLEX ; !XXX ans?



! Plural

LEXICON W_M_PLNOM
 +Pl+Nom:%>ar DF_N_PLm ;

LEXICON W_M_PLNOM_UR
 +Pl+Nom:%>ur DF_N_PLm ;

LEXICON W_M_PLACC
 +Pl+Acc:%>ar DF_A_PLm ;

LEXICON W_M_PLACC_UR
 +Pl+Acc:%>ur DF_A_PLm ;

LEXICON W_M_PLDAT
 +Pl+Dat:%>u DF_D_PL ;

LEXICON W_M_PLGEN
 +Pl+Gen:%>a DF_G_PL ;




! Strong case suffixes
! ____________________


! Nominative Sg

LEXICON S_M_SGNOM
 +Sg+Nom:%>ur  DF_N_SGm_indef ;
 +Sg+Nom:%>uri DF_N_SGm_def   ;

LEXICON S_M_SGNOM_NULL
 +Sg+Nom: DF_N_SGm ;
 +Sg+Nom: DF_N_SGm ;


! Accusative Sg

LEXICON S_M_SGACC
 +Sg+Acc:    DF_A_SGm_indef ;
 +Sg+Acc:%>i DF_A_SGm_def   ; !sandin
 +Sg+Acc:    R ; ! stem compound

! Dative Sg

LEXICON S_M_SGDAT
 +Sg+Dat:%>i DF_D_SGm ;

LEXICON S_M_SGDAT_2
     +Sg+Dat:%>i          DF_D_SGm ;
 +Sg+Dat+Def:%^GVDEL%>num ENDLEX        ;

LEXICON S_M_SGDAT_NULL
 +Sg+Dat: DF_D_SGm ;

! Genitive Sg

LEXICON S_M_SGGEN
 +Sg+Gen:%>s DF_G_SGm ;
 +Sg+Gen:%>s R ; ! gen compound

LEXICON S_M_SGGEN_NULL
 +Sg+Gen: DF_G_SGm ;
 +Sg+Gen: R ; ! gen compound

LEXICON S_M_SGGEN_AR
 +Sg+Gen:%>ar DF_G_SGm ;
 +Sg+Gen:%>ar R ; ! gen compound

! Plural forms

! Nominative

LEXICON S_M_PLNOM
 +Pl+Nom:%>ar DF_N_PLm ;

LEXICON S_M_PLNOM_IR
 +Pl+Nom:%>ir DF_N_PLm ;

LEXICON S_M_PLNOM_UR
 +Pl+Nom:%>ur DF_N_PLm ;

LEXICON S_M_PLNOM_NULL
 +Pl+Nom:    DF_N_PLm_indef ;
 +Pl+Nom:%>i DF_N_PLm_def   ;

LEXICON S_M_PLNOM_NULL_NULL
  +Pl+Nom:     DF_N_PLm_indef ;
  +Pl+Nom:     DF_N_PLm_def   ;
  

! Accusative

LEXICON S_M_PLACC
 +Pl+Acc:%>ar DF_A_PLm ;

LEXICON S_M_PLACC_IR
 +Pl+Acc:%>ir DF_A_PLm ;

LEXICON S_M_PLACC_UR
 +Pl+Acc:%>ur DF_A_PLm ;

LEXICON S_M_PLACC_NULL
 +Pl+Acc:    DF_A_PLm_indef ;
 +Pl+Acc:%>i DF_A_PLm_def   ;

LEXICON S_M_PLACC_NULL_NULL
  +Pl+Acc:     DF_A_PLm_indef ;
  +Pl+Acc:     DF_A_PLm_def   ;


! Dative

LEXICON S_M_PLDAT
 +Pl+Dat:%>u DF_D_PL ;

LEXICON S_M_PLDATm ! skóm
 +Pl+Dat: DF_D_PL ;

! Genitive

LEXICON S_M_PLGEN
 +Pl+Gen:%>a DF_G_PL ;
 +Pl+Gen:%>a R       ; ! gen pl compound


! Feminine forms
! ______________

! Singular case suffixes.

! Nominative

LEXICON W_F_SGNOM
 +Sg+Nom:%>a DF_N_SGf_W ;

LEXICON S_F_SGNAD
 +Sg+Nom: DF_N_SGf_S ;
 +Sg+Acc: DF_A_SGf_S ;
 +Sg+Acc: R ; ! stem compound
 +Sg+Dat: DF_D_SGf_S ;
 
! Oblique

LEXICON W_F_SGOBL
 +Sg+Acc:%>u DF_A_SGf_W ;
 +Sg+Dat:%>u DF_D_SGf_W ;
 +Sg+Gen:%>u DF_G_SGf_W ;

LEXICON S_F_SGGEN
 +Sg+Gen:%>ar DF_G_SGf_S ;
 +Sg+Gen:%>ar R ; ! gen compound

LEXICON S_F_SGGEN_NULL
 +Sg+Gen: DF_G_SGf_S ;




! Plural case suffixes

LEXICON F_PLNA_UR
 +Pl+Nom:%>ur DF_NA_PLf ;
 +Pl+Acc:%>ur DF_NA_PLf ;

LEXICON F_PLNA_IR
 +Pl+Nom:%>ir DF_NA_PLf ;
 +Pl+Acc:%>ir DF_NA_PLf ;

LEXICON F_PLNA_AR
 +Pl+Nom:%>ar DF_NA_PLf ;
 +Pl+Acc:%>ar DF_NA_PLf ;

LEXICON F_PLNA_NULL
 +Pl+Nom: DF_NA_PLf ;
 +Pl+Acc: DF_NA_PLf ;

LEXICON F_PLDAT
 +Pl+Dat:%>u DF_D_PL ;

LEXICON F_PLGEN
 +Pl+Gen:%>a DF_G_PL ;
 +Pl+Gen:%>a R ; ! gen compound


! Neuter forms
! ____________

LEXICON S_N_SGNA
 +Sg+Nom: DF_NA_SGn ;
 +Sg+Acc: DF_NA_SGn ;
 +Sg+Acc: R ; ! stem compound
 
LEXICON S_N_SGDG
 +Sg+Dat: DF_D_SGn ;
 +Sg+Gen: DF_G_SGn ;

LEXICON S_N_SGD
 +Sg+Dat: DF_D_SGn ;

LEXICON S_N_SGG
 +Sg+Gen: DF_G_SGn ;

LEXICON S_N_SGDG_is
  S_N_SGD_i ;
  S_N_SGG_s ;

LEXICON S_N_SGD_i
 +Sg+Dat:%>i DF_D_SGn ;

LEXICON S_N_SGG_s
 +Sg+Gen:%>s DF_G_SGn ;


LEXICON S_N_SGG_is
 +Sg+Gen:%>is DF_G_SGn ;

LEXICON 0_N_SGNA
 +Sg+Nom: DF_NA_SGn_indef ;
 +Sg+Nom: DF_NA_SGn_def   ;
 +Sg+Acc: DF_NA_SGn_indef ;
 +Sg+Acc: DF_NA_SGn_def   ;

LEXICON i_N_SGNA
 +Sg+Nom:%>i DF_NA_SGn_indef ;
 +Sg+Nom:%>i DF_NA_SGn_def   ;
 +Sg+Acc:%>i DF_NA_SGn_indef ;
 +Sg+Acc:%>i DF_NA_SGn_def   ;

!LEXICON i_N_SGDG_is
! +Sg+Gen:%>is DF_G_SGn ;
! +Sg+Dat:%>is DF_D_SGn ;

LEXICON N_PLNA_u_ur
       +Pl+Nom:%>u  DF_NA_PLn_W ;
       +Pl+Acc:%>u  DF_NA_PLn_W ;
 +Pl+Nom+Indef:%>ur ENDLEX       ;
 +Pl+Acc+Indef:%>ur ENDLEX       ;

LEXICON N_PLNA_i_ir
       +Pl+Nom:%>i  DF_NA_PLn_W ;
       +Pl+Acc:%>i  DF_NA_PLn_W ;
 +Pl+Nom+Indef:%>ir ENDLEX       ;
 +Pl+Acc+Indef:%>ir ENDLEX       ;

LEXICON N_PLNA
 +Pl+Nom: DF_NA_PLn ;
 +Pl+Acc: DF_NA_PLn ;


LEXICON N_PLD
 +Pl+Dat:%>u DF_D_PL ;

LEXICON N_PLD_0_U
 +Pl+Dat+Indef:%>um  ENDLEX ;
   +Pl+Dat+Def:%>num ENDLEX ;

LEXICON N_PLG
 +Pl+Gen:%>a DF_G_PL ;
 +Pl+Gen:%>a R ;

LEXICON N_PLG_NULL
 +Pl+Gen+Indef: ENDLEX ;
   +Pl+Gen+Def: ENDLEX ;


LEXICON N_PLG_na
 +Pl+Gen:%>na DF_G_PL ;
 +Pl+Gen:%>na R ;

! Common cases

LEXICON DF_D_PL
 +Indef:%>m   ENDLEX ;
   +Def:%>num ENDLEX ;

LEXICON DF_G_PL
 +Indef:      ENDLEX ;
   +Def:%>nna ENDLEX ;







! =================== !
! Definite inflection !
! =================== !

! This is the third layer. Here we do the definite forms.


! Masculine forms
! _______________

! Masc def sg

LEXICON DF_N_SGm
      DF_N_SGm_indef ;
 :%>i DF_N_SGm_def   ;

LEXICON DF_N_SGm_indef
 +Indef: MscNom_Flag ; !risi 

LEXICON DF_N_SGm_def
 +Def:%>n MscNom_Flag ; ! risin

LEXICON DF_A_SGm
  DF_A_SGm_indef ;
  DF_A_SGm_def   ;

LEXICON DF_A_SGm_indef
 +Indef: MscObl_Flag ;

LEXICON DF_A_SGm_def
 +Def:%>n MscObl_Flag ; ! risan, sandin

LEXICON DF_D_SGm
 +Indef:      MscObl_Flag ;
   +Def:%>num MscObl_Flag ;

LEXICON DF_G_SGm
 +Indef:      MscObl_Flag ;
   +Def:%>ins MscObl_Flag ;
              R     ;


! Masc def pl

LEXICON DF_N_PLm
  DF_N_PLm_indef ;
  DF_N_PLm_def   ;


LEXICON DF_N_PLm_indef
 +Indef: Pl_Flag ;

LEXICON DF_N_PLm_def
 +Def:%>nir Pl_Flag ; ! no _i_

LEXICON DF_A_PLm
  DF_A_PLm_indef ;
  DF_A_PLm_def   ;

LEXICON DF_A_PLm_indef
 +Indef: Pl_Flag ;

LEXICON DF_A_PLm_def
 +Def:%>nar Pl_Flag ; ! no _i



! Feminine forms
! _______________

LEXICON DF_N_SGf_W
 +Indef:    FemNom_Flag ;
   +Def:%>n FemNom_Flag ;

LEXICON DF_N_SGf_S
 +Indef:     FemNom_Flag ;
   +Def:%>in FemNom_Flag ;


LEXICON DF_A_SGf_W
 +Indef:     FemObl_Flag ;
   +Def:%>na FemObl_Flag ;

LEXICON DF_A_SGf_S
 +Indef:      FemObl_Flag ;
   +Def:%>ina FemObl_Flag ;


LEXICON DF_D_SGf_W
 +Indef:     FemObl_Flag ;
   +Def:%>ni FemObl_Flag ;

LEXICON DF_D_SGf_S
 +Indef:      FemObl_Flag ;
   +Def:%>ini FemObl_Flag ; ! árini
   +Def:%>nni FemObl_Flag ; ! oynni, consider i:n <=> Vow %>: _ n i


LEXICON DF_G_SGf_W
 +Indef:       FemObl_Flag ;
   +Def:%>nnar ENDLEX ;
               R     ;

LEXICON DF_G_SGf_S
 +Indef:        FemObl_Flag ;
   +Def:%>innar FemObl_Flag ;
                R     ;

LEXICON DF_NA_PLf
 +Indef:      Pl_Flag ;
   +Def:%>nar Pl_Flag ;


! Neuter forms
! _______________

LEXICON DF_NA_SGn
      DF_NA_SGn_indef ;
 :%>i DF_NA_SGn_def   ;

LEXICON DF_NA_SGn_indef
 +Indef: Neu_Flag ;

LEXICON DF_NA_SGn_def
 +Def:ð Neu_Flag ;

LEXICON DF_D_SGn
 +Indef:      Neu_Flag ;
   +Def:%>num Neu_Flag ;

LEXICON DF_G_SGn
 +Def:%>ins Neu_Flag     ;
            g_indef_r ;

LEXICON g_indef_r
 +Indef: Neu_Flag ;
         R     ;

LEXICON DF_G_SGn_a
 +Def:%>ns Neu_Flag     ;
           g_indef_r ;

LEXICON DF_NA_PLn ! orðini
 +Indef:      Pl_Flag ;
   +Def:%>ini Pl_Flag ;

LEXICON DF_NA_PLn_W ! eyguni, hjørtuni
 +Indef:     Pl_Flag ;
   +Def:%>ni Pl_Flag ;


! Compound flags
! --------------


LEXICON MscNom_Flag
@U.Case.MscNom@ ENDLEX ;

LEXICON MscObl_Flag
@U.Case.MscObl@ ENDLEX ;

LEXICON FemNom_Flag
@U.Case.FemNom@ ENDLEX ;

LEXICON FemObl_Flag
@U.Case.FemObl@ ENDLEX ;

LEXICON Neu_Flag
@U.Case.Neu@ ENDLEX ;

LEXICON Pl_Flag
@U.Case.Pl@ ENDLEX ;

! Debug
!LEXICON MscNom_Flag
!ENDLEX ;
!LEXICON MscObl_Flag
!ENDLEX ;
!LEXICON FemNom_Flag
!ENDLEX ;
!LEXICON FemObl_Flag
!ENDLEX ;
!LEXICON Neu_Flag
!ENDLEX ;
!LEXICON Pl_Flag
!ENDLEX ;

