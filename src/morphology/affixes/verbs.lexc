



! =============== !
!! !!!Verb morphology !
! =============== !

!LEXICON sx ! this is an ad hoc lexicon waiting for classification.
!s1 ;

LEXICON s1 
!! __@LEXNAME@__ nevna
               standard_ir ; ! Verbal forms
 +V+PrfPrc:%>d p7          ; ! Adjectival forms


LEXICON s2
!! __@LEXNAME@__ keypa
               	standard_ir_t ;
 				p39           ;

LEXICON SETA
!! __@LEXNAME@__ 
+V+Ind+Prs+Sg3:%>ur # ;
s2 ;

LEXICON s3
!! __@LEXNAME@__ leiða
               standard_ir ;
 +V+PrfPrc:%>d p18         ;

LEXICON s4
!! __@LEXNAME@__ frøa
               inf        ;
               pres_ir    ;
               prt_dd     ;
               imp_prsptc ;
               sup_tt     ;
 +V+PrfPrc:%>d p7         ;

LEXICON s5
!! __@LEXNAME@__ senda
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s6
!! __@LEXNAME@__ hirða
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s7
!! __@LEXNAME@__ gista
				standard_ir_t ;
				p5			  ;

LEXICON s8
!! __@LEXNAME@__ kenna
               standard_ir ;
 +V+PrfPrc:%>d p7          ;

LEXICON s9
!! __@LEXNAME@__ klippa
				standard_ir_t ;
				p5			  ;

 LEXICON s10
!! __@LEXNAME@__ fylgja
    jinf       ;
    pres_ir_sg ;
 :j pres_pl    ;
    sup        ;
                 prt_d		 ;
 +V+PrfPrc:%>d p7            ;
			   imp			 ;
		   :j 		prsptc		 ;
			   
LEXICON s11 !
!! __@LEXNAME@__  roykja
    jinf       ;
    pres_ir_sg ;
 :j pres_pl    ;
    sup        ;
                 prt_t		 ;
 +V+PrfPrc:%>t p5            ;
			   imp			 ;
		   :j 		prsptc		 ;

LEXICON s12 !
!! __@LEXNAME@__ boyggja
    jinf       ;
    pres_ir_sg ;
 :j pres_pl    ;
    sup        ;
                 prt_d		 ;
 +V+PrfPrc:%>d p7            ;
			   imp			 ;
		   :j 		prsptc		 ;
			   
LEXICON s13 !
!! __@LEXNAME@__ søkkja
					jinf       	 ;
    				pres_ir_sg 	 ;
  		   :j 		pres_pl   	 ;
    				sup_t        ;
              		prt_t	   	 ;
 					p5           ;
			   		imp		  	 ;
		   :j 		prsptc		 ;

LEXICON s14 !
!! __@LEXNAME@__ heingja
                     jinf       ;
           :%^EIO    jinf       ;
                     pres_ir_sg ;
           :%^EIO	 pres_ir_sg ;
           :j 		 pres_pl	;
           :%^EIO%>j pres_pl	;
           :%^EIO    prt_d      ;
           :%^EIO    imp 		;
           			 imppl 		;
           :%^EIO    sup        ;
  +V+PrfPrc:%^EIO%>d p7         ; 
		   :j 		 prsptc		;
		   :%^EIO%>j prsptc		;
 
LEXICON s15 !
!! __@LEXNAME@__ skeinkja
                     jinf       ;
           :%^EIO    jinf       ;
                     pres_ir_sg ;
           :%^EIO	 pres_ir_sg ;
           :j 		 pres_pl	;
           :%^EIO%>j pres_pl	;
           :%^EIO    prt_t      ;
           :%^EIO    imp 		;
           			 imppl 		;
           :%^EIO    sup_t      ;
  					 p5         ; 
		   :j 		 prsptc		;
		   :%^EIO%>j prsptc		;

LEXICON s15_2 !
!! __@LEXNAME@__ steikja
                     jinf       ;
                     pres_ir_sg ;
           :j 		 pres_pl	;
           :%^EIO    prt_t      ;
           		     imp 		;
           :%^EIO    sup_t      ;
  					 p5         ; 
		   :j 		 prsptc		;

LEXICON s16 !
!! __@LEXNAME@__ flekja
                 	jinf       ;
                 	pres_ir_sg ;
          :j 		pres_pl	   ;
          :%^WVV 	prt_d      ;
           			imp		   ;
          :j 		prsptc 	   ;
          :%^WVV 	sup        ;
    				p5         ;

LEXICON s17 !
!! __@LEXNAME@__ berja
                 	jinf       ;
                 	pres_jir   ;
          :%^WVV 	prt_d      ;
          :j 		impsg	   ;
 +V+Imp+Pl:jið		PASS       ;
          :j		prsptc	   ;
          :%^WVV 	sup        ;
 			     	p7         ;

LEXICON s18 !
!! __@LEXNAME@__ krevja
                 jinf       ;
                 pres_ur_j  ;
          :%^WVV prt_d      ;
          	 	 impsg		;
 +V+Imp+Pl:jið	 PASS       ;
          :j	 prsptc 	;
          :%^WVV sup        ;
 			     p7         ;

LEXICON s19 !
!! __@LEXNAME@__ dylja
                 jinf       ;
                 pres_ur_j  ;
          :%^WVV prt_d      ;
          :		 impsg		;
 +V+Imp+Pl:jið	 PASS       ;
          :j	 prsptc 	;
          :%^WVV sup        ;
				 p7         ;

LEXICON s20 !
!! __@LEXNAME@__ leggja
 :egg   jinf       ;
 :egg   pres_ur_j2 ;
 :eg    prt_ði     ;
 :øg    prt_du     ;
 :egg   imp		   ;
 :eggj	prsptc 	   ;
 :ag    sup        ;
 	    p8         ;

LEXICON s21 !
!! __@LEXNAME@__ selja
                 jinf       ;
                 pres_ur_j  ;
          :		 prt_d      ;
          :		 impsg		;
 +V+Imp+Pl:jið	 PASS       ;
          :j	 prsptc 	;
          :		 sup        ;
				 p7         ;

LEXICON s22 !
!! __@LEXNAME@__ ryðja
          :yð   jinf       ;
          :yð   pres_ur_j2 ;
          :u 	prt_dd     ;
          :yð 	imp		   ;
          :yðj	prsptc 	   ;
          :u 	sup_tt     ;
 				p18        ;

LEXICON s22_1 !
!! __@LEXNAME@__ ýðja
          :ýð   jinf       ;
          :ýð   pres_ur_j2 ;
          :u 	prt_dd     ;
          :ýð 	imp		   ;
          :ýðj	prsptc 	   ;
          :u 	sup_tt     ;
 				p18        ;

LEXICON s23 !
!! __@LEXNAME@__ smyrja
 :yr    jinf       ;
  +V+Ind+Prs+1Sg:%>yrji PASS    ;
  +V+Ind+Prs+2Sg:%>yr 	PASS    ;
  +V+Ind+Prs+2Sg:%>yrt 	PASS    ;
  +V+Ind+Prs+3Sg:%>yr 	PASS    ;
   +V+Ind+Prs+Pl:%>yrja PASS 	;
 				:ur     prt_d   ;
 				:yr     impsg   ;
 				:yrj	imppl_j	;
			 	:yrj	prsptc	;
 				:ur     sup		;
 						p7      ;

LEXICON s24 !
!! __@LEXNAME@__ flysa
                 inf        ;
                 pres_ur    ; ! XXX not pres_null
          :%^WVV prt_d      ;
          		 imp_prsptc ;
          :%^WVV sup        ;
			     p5         ;

LEXICON s25 !l
!! __@LEXNAME@__ iva
               inf        ;
               pres_ir    ;
               prt_d      ;
               imp_prsptc ;
               sup_a      ;
 +V+PrfPrc:%>d p6         ;

LEXICON s26 !
!! __@LEXNAME@__ plaga
  inf        ;
  pres_ar    ;
  prt_d      ;
  sup        ;
  +V+Sup:%>að     PASS ;
  

LEXICON s26_1 !
!! __@LEXNAME@__ mála->máldi
  inf        ;
  pres_ar    ;
  prt_d      ;
  imp_prsptc ;
  sup        ;
  +V+Sup:%>að     PASS ;

LEXICON s27 !s
!! __@LEXNAME@__ páa
               inf        ;
               pres_ar    ;
               prt_dd     ;
               imp_prsptc ;
   +V+Imp+Sg:a PASS		  ;
               sup_tt     ;
			   p18        ;

LEXICON s28 !
!! __@LEXNAME@__ skaða
               inf        ;
               pres_ar    ;
               prt_d      ;
               imp_prsptc ;
               sup        ;
 			   p18        ;

LEXICON s29 !
!! __@LEXNAME@__ brúka
               inf        ;
               pres_ar    ;
               prt_d      ;
               imp_prsptc ;
   +V+Imp+Sg:a PASS		  ;
               sup        ;
			   p5         ;

LEXICON s30 !
!! __@LEXNAME@__ kalla
                inf     ;
                pres_ar ;
                prt_a   ;
          :a    impsg   ; !deviant kalla
                imppl   ;
                prsptc  ;
                sup_a   ;
			    p6      ;

LEXICON s31 !
!! __@LEXNAME@__ only gera and *gera
          :er     inf        ;
 +V+Ind+Prs+1Sg:%>eri PASS   ;
 +V+Ind+Prs+2Sg:%>ert PASS   ;
 +V+Ind+Prs+2Sg:%>er  PASS   ;
 +V+Ind+Prs+3Sg:%>er  PASS   ;
          :er     pres_pl    ;
          :er     imp_prsptc ;
          :jør    prt_d      ;
          :jør    sup        ;
				  p7         ;

LEXICON s32 !
!! __@LEXNAME@__ búgva
          		:gv     	inf                ;
          		:gv     	pres_strong_s1     ;
          		:%^IUML 	pres_strong_s23_t1 ;
  +V+Ind+Prs+2Sg:%^IUML%>r  PASS   			   ;
	          				prt_ð 			   ;
          		:gv     	pres_pl            ;
          		:gv     	imp_prsptc         ;
							sup_in			   ;
					   		p32                ;

LEXICON s33 !
!! __@LEXNAME@__ rógva
          		:gv      	inf      	       ;
          		:gv      	pres_strong_s1 	   ;
          		:%^IUML 	pres_strong_s23_t1 ;
  +V+Ind+Prs+2Sg:%^IUML%>r  PASS   			   ;
          		:gv      	pres_pl            ;
          		        	prt_ð              ;
          		:gv      	imp_prsptc         ;
							sup_in			   ;
							p32                ;


LEXICON s34 !
!! __@LEXNAME@__ goyggja
 			   :ggj inf     	;
 +V+Ind+Prs+1Sg:ggi PASS 		;
 +V+Ind+Prs+2Sg:rt  PASS 		;
 +V+Ind+Prs+2Sg:r   PASS  		;
 +V+Ind+Prs+3Sg:r   PASS 		;
      		   :ggj pres_pl 	;
      				prt_ð   	;
 			   :ggj imp_prsptc 	;
    	 +V+Sup:%>ð PASS 		;
                 



!! !!Strong verbs starting here
! ==========================

LEXICON s35 !
!! __@LEXNAME@__ bíta
                     inf        ;
                     pres_ur    ;
          :%^AB1     prt_null_s ;
          :%^AB2   	 prt_u_p    ;
                   	 imp_prsptc ;
          :%^VSH  	 sup_null   ;
 +V+PrfPrc:%^VSH%>in PASS		;
 					 p26        ;

LEXICON s36 !
!! __@LEXNAME@__ svíkja
          :j       inf        ;
                   pres_ur    ;
          :%^AB1   prt_null_s ;
          :%^AB2   prt_u_p    ;
          :j       imp_prsptc ;
          :%^VSH   sup_null   ;
 +V+PrfPrc:%^VSHin p26        ;


LEXICON s37 !
!! __@LEXNAME@__ bróta
                   inf             ;
                   pres_strong_s1  ;
          :%^AB7   pres_strong_s23 ;
          :%^AB1   prt_null_s      ;
          :%^AB4   prt_u_p         ;
                   imp_prsptc      ;
          :%^AB5   sup_null        ;
 +V+PrfPrc:%^VSHin p26             ;


LEXICON s38 !
!! __@LEXNAME@__ skjóta present tense
          :jót  inf             ;
          :ýt   pres_strong_s1  ;
          :ýt   pres_strong_s23 ;
          :ýt   imp_prsptc      ; !XXX
          :ut   prt_u_p         ;
          :ot   sup_null        ;
 +V+PrfPrc:otin p26             ;

!LEXICON s39/30
! :jóð s30 ; ! weak infl + common forms
!      s39 ; ! umlauted forms, XXX check.

LEXICON s39d
!! __@LEXNAME@__ 
 :jóð inf      ;
      s39_rest ;

LEXICON s39s
!! __@LEXNAME@__ 
 :jós inf      ;
      s39_rest ;

LEXICON s39 !
!! __@LEXNAME@__ bjóða umlaut forms
 :jót inf      ;
      s39_rest ;

LEXICON s39_rest
!! __@LEXNAME@__ 
          :ýð   pres_strong_s1  ;
          :ýð   pres_strong_s23 ;
          :eyð  prt_null_s      ; ! Gunnar
          :úði  prt_null_s      ; ! 1Mos
          :uð   prt_u_p         ;
          :óð   sup_null        ;
 +V+PrfPrc:oðin p26             ;

LEXICON s40 !
!! __@LEXNAME@__ fúka
                    inf             ;
                    pres_strong_s1  ;
           :%^IUML  pres_strong_s23 ;
!          :%^IUML  pres_ur         ;
           :%^AB1   prt_null_s      ;
           :%^AB4   prt_u_p         ;
                    imp_prsptc      ;
           :%^AB5   sup_null        ;
  +V+PrfPrc:%^AB5in p26             ;

LEXICON s41 !
!! __@LEXNAME@__ flúgva
                         inf             ;
                         pres_strong_s1  ;
          :%^IUML%^VDEL  pres_strong_s23 ; !flýgv, should be flýg
          :%^AB1%^VDEL   prt_null_s      ;
          :%^VSH%^VDEL   prt_u_p         ;
                         imp_prsptc      ;
          :%^AB5%^VDEL   sup_null        ;
 +V+PrfPrc:%^AB5%^VDELin p26             ;

LEXICON s42 !
!! __@LEXNAME@__ klúgva
                    inf             ;
                    pres_strong_s1  ;
           :%^IUML  pres_strong_s23 ; ! klýgv, should be klýv
!          :%^IUML  pres_ur         ;
           :%^AB1   prt_null_s      ; !kleygv, should be kleyv
           :%^VSH   prt_u_p         ; !klugv, should be kluv
                    imp_prsptc      ;
           :%^AB5   sup_null        ; !klogv, should be klov
  +V+PrfPrc:%^AB5in p26             ; !klogvin, should be klovin

LEXICON s43 !
!! __@LEXNAME@__ njóta
                    inf             ;
                    pres_strong_s1  ;
           :%^IUML  pres_strong_s23 ; !njót, should be nýt
!                   pres_ur         ;
           :%^AB1   prt_null_s      ; !njeyt, should be neyt
           :%^AB4   prt_u_p         ; !njut, should be nut
                    imp_prsptc      ;
           :%^AB5   sup_null        ; !njot, should be not
  +V+PrfPrc:%^AB5in p26             ; !njotin, should be notin

LEXICON s44 !
!! __@LEXNAME@__ finna
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :%^AB4   prt_du     ; !funndu, should be fundu
                   imp_prsptc ;
          :%^AB4   sup_null   ;
 +V+PrfPrc:%^AB4in p26        ;

LEXICON s45 !
!! __@LEXNAME@__ binda
                   inf        ;
                   pres_ur    ;
          :%^aAB   prt_null_s ; !band, should be bant
          :%^AB4   prt_u_p    ;
                   imp_prsptc ;
          :%^AB4   sup_null   ;
 +V+PrfPrc:%^AB4in p26        ;

LEXICON s46 !
!! __@LEXNAME@__ stinga
                       inf        ;
                       pres_ur    ;
          :%^NGKK%^aAB prt_null_s ; !stikk, should be stakk
          :%^AB4       prt_u_p    ;
                       imp_prsptc ;
          :%^AB4       sup_null   ;
 +V+PrfPrc:%^AB4in     p26        ;

LEXICON s47 !
!! __@LEXNAME@__ svimja
           :imj     inf        ;
           :im      pres_ur_j  ;
           :am      prt_null_s ;
           :um      prt_u_p    ;
           :im      imp_prsptc ;
           :om      sup_null   ;
  +V+PrfPrc:om%>in  p26        ;

!LEXICON s47 ! svimja
!                   inf        ;
!                   pres_ur    ;
!          :%^aAB   prt_null_s ;
!          :%^AB4   prt_u_p    ;
!                   imp_prsptc ;
!          :%^oAB   sup_null   ;
! +V+PrfPrc:%^oABin p26        ;

LEXICON s48 !
!! __@LEXNAME@__ drekka
                 inf        ;
                 pres_ur    ;
          :%^AB3 prt_null_s ;
          :%^AB4 prt_u_p    ;
                 imp_prsptc ;
          :%^AB4 sup_null   ;
 +V+PrfPrc:%>in  p26        ;

LEXICON s49 !
!! __@LEXNAME@__ detta
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :%^AB4   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_null   ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s50 !
!! __@LEXNAME@__ røkka
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :        prt_null_s ;
          :%^AB4   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_null   ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s51 !
!! __@LEXNAME@__ ganga
          :ang     inf             ;
          :ang     pres_strong_s1  ;
          :ong     pres_strong_s23 ; !2?
          :ekk     prt_null_s      ;
          :ing     prt_u_p         ;
          :ang     imp_prsptc      ;
          :ing     sup_null        ;
 +V+PrfPrc:ing%>in p26             ;


LEXICON s52 !
!! __@LEXNAME@__ veva
                   inf        ;
                   pres_ur    ;
          :%^AB3   prt_null_s ;
          :%^AB2   prt_u_p    ;
                   imp_prsptc ;
          :%^AB5   sup_null   ;
 +V+PrfPrc:%^AB5in p26        ;

LEXICON s53 !
!! __@LEXNAME@__ leypa
          :leyp  inf             ;
          :loyp  pres_strong_s1  ;
          :loyp  pres_strong_s23 ;
          :leyp  prt_null_s      ;
          :leyp  imp_prsptc      ;
          :lup   prt_u_p         ;
          :lop   sup_null        ;
 +V+PrfPrc:lopin p26             ;

LEXICON s54 !
!! __@LEXNAME@__ bera
                   inf                ;
                   pres_strong_s1     ;
                   pres_strong_s23_t0 ;
          :%^AB3   prt_null_s         ;
          :%^AB2   prt_u_p            ;
                   imp_prsptc         ;
          :%^AB5   sup_null           ;
 +V+PrfPrc:%^AB5in p26                ;

LEXICON s55 !
!! __@LEXNAME@__ fara
                  inf                ;
                  pres_strong_s1     ;
          :%^IUML pres_strong_s23_t0 ; !far, should be fer
          :%^AB3  prt_null_s2        ;
          :%^AB3  prt_u_p            ;
                  sup_null           ;
                  imp_prsptc         ;
 +V+PrfPrc:%>in   p26                ;

LEXICON s56 !
!! __@LEXNAME@__ geva
                     inf        ;
                     pres_ur    ;
          :%^AB3     prt_null_s ;
          :%^AB2     prt_u_p    ;
          :%^AB6     sup_null   ;
                     imp_prsptc ;
 +V+PrfPrc:%^AB6%>in p26        ;

LEXICON s57 !
!! __@LEXNAME@__ sita
                 inf        ;
                 pres_ur    ;
          :%^AB3 prt_null_s ;
          :%^AB2 prt_u_p    ;
                 sup_null   ;
                 imp_prsptc ;
 +V+PrfPrc:%>in  p26        ;

LEXICON s58 !
!! __@LEXNAME@__ mala
                  inf             ;
                  pres_strong_s1  ;
          :%^IUML pres_strong_s23 ;
          :%^AB3  prt_null_s      ;
          :%^AB3  prt_u_p         ;
                  imp_prsptc      ;
                  sup_null        ;
 +V+PrfPrc:%>in   p26             ;

LEXICON s59 !
!! __@LEXNAME@__ stjala
                   inf             ;
                   pres_strong_s1  ;
          :%^IUML  pres_strong_s23 ;
          :%^AB3   prt_null_s      ;
          :%^AB3   prt_u_p         ;
          :%^AB5   sup_null        ; ! stjol, should be stol
                   imp_prsptc      ;
 +V+PrfPrc:%^AB5in p26             ; ! stjol, should be stol

LEXICON s60 !
!! __@LEXNAME@__ taka, aka
                   inf             ;
                   pres_strong_s1  ;
          :%^IUML  pres_strong_s23 ;
          :%^AB3   prt_null_s      ;
          :%^AB3   prt_u_p         ;
                   imp_prsptc      ;
          :%^AB6   sup_null        ;
 +V+PrfPrc:%^AB6in p26             ;

          :hild   prt_u_p          ; ! hóld ??
          :hild   sup_null         ;
 +V+PrfPrc:hildin p26              ;

LEXICON s61 !
!! __@LEXNAME@__ halda
          :hald        inf             ;
          :hald        pres_strong_s1  ;
          :held  pres_strong_s23 ;
          :helt  prt_null      ; ! hald / held%>st, should be helt / helt%>st
          :hild   prt_u_p         ;
          :hild   sup_null        ;
          :hald        imp_prsptc      ;
 +V+PrfPrc:hildin p26             ;

LEXICON s62 !
!! __@LEXNAME@__ sova
          :sov  inf             ;
          :svev pres_strong_s1  ;
          :svev pres_strong_s23 ;
          :svav prt_null_s      ;
          :svóv prt_u_p         ;
          :sovi sup_a           ;
          :sov  imp_prsptc      ;
 +V+PrfPrc:%>in p26             ;

LEXICON s63 !
!! __@LEXNAME@__ koma
                   inf             ;
                   pres_strong_s1  ;
          :%^eIUML pres_strong_s23 ;
                   prt_null_s      ;
                   prt_u_p         ;
                   sup_null        ;
                   imp_prsptc      ;
 +V+PrfPrc:%>in    p26             ;

LEXICON s64 !
!! __@LEXNAME@__ láta
                   inf             ;
                   pres_strong_s1  ;
          :%^eIUML pres_strong_s23 ;
          :%^AB3   prt_null_s      ;
          :%^AB2   prt_u_p         ;
                   sup_null        ;
                   imp_prsptc      ;
 +V+PrfPrc:%>in    p26             ;

LEXICON s65 !
!! __@LEXNAME@__ standa
 :and inf             ;
 :and pres_strong_s1  ;
 :end pres_strong_s23 ;
 :óð  prt_null_s      ;
 :óð  prt_u_p         ;
 :að  sup_null        ;
 :att imp             ;
 :and prsptc          ;

LEXICON s66 !
!! __@LEXNAME@__ biðja
                 jinf       ;
                 pres_ur_j  ;
          :%^AB3 prt_null_s ;
          :%^AB2 prt_u_p    ;
                 sup_null   ;
 +V+PrfPrc:%>in  p26        ;

LEXICON s67 !
!! __@LEXNAME@__ draga
          :ag       inf             ;
          :ag       pres_strong_s1  ;
          :eg       pres_strong_s23 ;
          :ó        prt_null_s      ; !dróg, should be dró
          :óg       prt_u_p         ;
          :ag       sup_null        ; ! %^AB2
          :ag       imp_prsptc      ;
 +V+PrfPrc:%AB2%>in p26             ;

LEXICON s68 !
!! __@LEXNAME@__ hvørva
          :vørv     inf        ;
          :vørv     pres_ur    ;
          :varv     prt_null_s ;
          :vurv     prt_u_p    ;
          :orv      sup_null   ;
   +Use/NG:vorv     sup_null   ;
          :vørv     imp_prsptc ;
 +V+PrfPrc:orv%>in p26        ; ! horvin not hvorvin

LEXICON s69 !
!! __@LEXNAME@__ sláa
          inf                ;
          pres_strong_s1     ;
  :%^IUML pres_strong_s23_t1 ;
  :%^AB2  prt_null_s         ;
! :slerdi prt_null_s         ;
! :slóg   prt_u_p            ;
! :slerd  prt_u_p            ;
! :sligi  sup_a              ;
! :sligin p26                ;

LEXICON s70 !
!! __@LEXNAME@__ siga
          :sig    inf        ;
          :sig    pres_ur    ;
          :segði  prt_null_s ;
!         :søgð   prt_u_p    ; ! yaml says ð
          :søgd   prt_u_p    ; ! and not d
          :sig    imp_prsptc ;
          :sag    sup        ;
 +V+PrfPrc:sagd%> p8         ;

LEXICON s71 !
!! __@LEXNAME@__ skerja
          :sker  jinf       ;
          :sker  pres_ir    ;
          :sker  prt_d      ;
          :skert sup_a      ;
          :sker  imp_prsptc ;
 +V+PrfPrc:skerd p7         ;

LEXICON s72 !
!! __@LEXNAME@__ eta
          :et     inf        ;
          :et     pres_ur    ;
          :át     prt_null_s ;
          :ót     prt_u_p    ;
          :et     sup_null   ;
          :et     imp_prsptc ;
 +V+PrfPrc:et%>in p26        ;

LEXICON s73 !
!! __@LEXNAME@__ læa
          :læ   inf        ;
          :lær  pres_ur    ;
          :læ   prt_d      ;
          :ló   prt_null_s ;
          :læð  sup_a      ;
          :læið sup_a      ;
          :læ   imp_prsptc ;
 +V+PrfPrc:læin p26        ; !?

LEXICON sxrefl ! 
!! __@LEXNAME@__ This is an ad hoc lexicon
s74 ;

LEXICON s74 ! 
!! __@LEXNAME@__ grindast
  reflinf  ;
  pres_ast ;
  pret_st  ;

LEXICON s75 ! 
!! __@LEXNAME@__ balast
  reflinf ;


LEXICON s76 ! 
!! __@LEXNAME@__ ræðast
  reflinf ;


LEXICON s77 ! 
!! __@LEXNAME@__ minnast
  reflinf ;


LEXICON s78 ! 
!! __@LEXNAME@__ farast
  reflinf ;


LEXICON s79 ! 
!! __@LEXNAME@__ skjótast
  reflinf ;


LEXICON s80 ! 
!! __@LEXNAME@__ trivast
  reflinf ;


LEXICON s81 ! 
!! __@LEXNAME@__ kíkjast
  reflinf ;


LEXICON s82 ! 
!! __@LEXNAME@__ fýlast
  reflinf ;

LEXICON s83 ! 
!! __@LEXNAME@__ samsinnast
  reflinf ;




!! !!Split lexica
! ============

! The lexicon names are, as always, from FO.
! Now, these are just mechanically split.
! They should be split more precisely,
! just like s2/1, in order to keep
! things minimal.

!LEXICON s1/26
!  s1  ;
!  s26 ;

!LEXICON s1/30
!  s1  ;
!  s30 ;

!LEXICON s11/15
!  s11 ;
!  s15 ;

!LEXICON s15/11
!  s15 ;
!  s11 ;

!LEXICON s18/16
!  s18 ;
!  s16 ;

!LEXICON s19/30
!  s19 ;
!  s30 ;

!LEXICON s2/1
!  s2    ;
!  prt_d ; ! this is the s1 part

!LEXICON s2/30
!  s2  ;
!  s30 ;

!LEXICON s21/11
!  s21 ;
!  s11 ;
!
!LEXICON s21/18
!  s21 ;
!  s18 ;
!
!LEXICON s24/2
!  s24 ;
!  s2  ;
!
!LEXICON s25/1
!  s25 ;
!  s1  ;
!
!LEXICON s27/30
!  s27 ;
!  s30 ;
!
!LEXICON s28/30
!  s28 ;
!  s30 ;
!
!LEXICON s29/2
!  s29 ;
!  s2  ;
!
!LEXICON s29/30
!  s29 ;
!  s30 ;
!
!LEXICON s3/30
!  s3  ;
!  s30 ;
!
!LEXICON s30/1
!  s30 ;
!  s1  ;
!
!LEXICON s30/11
!  s30 ;
!  s11 ;
!
!LEXICON s30/19
!  s30 ;
!  s19 ;
!
!LEXICON s30/2
!  s30 ;
!  s2  ;
!
!LEXICON s30/22
!  s30 ;
!  s22 ;
!
!LEXICON s30/27
!  s30 ;
!  s27 ;
!
!LEXICON s30/28
!  s30 ;
! +Err/Orth: s28 ;
!
!LEXICON s30/29
!  s30 ;
! +Use/NG: s29 ;
!
!LEXICON s30/40
!  s30 ;
!  s40 ;
!
!LEXICON s30/5
!  s30 ;
!  s5  ;
!
!LEXICON s30/6
!  s30 ;
!  s6  ;
!
!LEXICON s30/9
!  s30 ;
!  s9  ;
!
!LEXICON s36/2
!  s36  ;
!  s2   ;

!LEXICON s39/30
  !s30 ;
  !s39 ;

!LEXICON s47/19
!  s47 ;
!  s19 ;
!
!LEXICON s49/52
!  s49 ;
!  s52 ;
!
!LEXICON s49/9
!  s49 ;
!  s9  ;
!
!LEXICON s5/30
!  s5  ;
!  s30 ;
!
!LEXICON s52/30
!  s52 ;
!  s30 ;
!
!LEXICON s6/30
!  s6  ;
!  s30 ;
!
!LEXICON s60/30
!  s60 ;
!  s30 ;
!
!LEXICON s7/49
!  s7  ;
!  s49 ;
!
!LEXICON s74/75
!  s74 ;
!  s75 ;
!
!LEXICON s77/74
!  s77 ;
!  s74 ;
!
!LEXICON s8/30
!  s8  ;
!  s30 ;
!
LEXICON s8/48
!! __@LEXNAME@__ 
  s8  ;
  s48 ;
LEXICON s9/30
!! __@LEXNAME@__ 
  s9  ;
  s30 ;





!! !!Intermediate lexicon groups
! ===========================

LEXICON standard_ir
!! __@LEXNAME@__ 
  inf     ;
  prt_d   ;
  ir_verb ;

LEXICON standard_ir_t
!! __@LEXNAME@__ 
   inf                  ;
   prt_t                ; !XXX skilti
   ir_verb_t              ;

!LEXICON standard_j_ir_t
!  prt_t                ; !XXX skilti
!  standard_j_ir_nopret ;

! possibly also (no, handled by s1/2
!LEXICON standard_j_ir_td
!  prt_t                ;
!  prt_d                ;
!  standard_j_ir_nopret ;


LEXICON standard_j_ir ! 
!! __@LEXNAME@__ the prt_d
  standard_j_ir_nopret ;
  prt_d                ;

LEXICON standard_j_ir_nopret
!! __@LEXNAME@__ 
    jinf       ;
    pres_ir_sg ;
 :j pres_pl    ;
    imp_prsptc ;
    sup        ;

LEXICON ir_verb
!! __@LEXNAME@__ 
  pres_ir    ;
  imp_prsptc ;
  sup        ;

LEXICON ir_verb_t
!! __@LEXNAME@__ 
  pres_ir    ;
  imp_prsptc ;
  sup_t        ;

!! !!Suffix lexica
! =============

!! !Infinitive
! __________

LEXICON jinf
!! __@LEXNAME@__ 
      +V+Inf:%>ja   PASS ;
 +V+Inf+Pass:%>jast #    ;

LEXICON inf
!! __@LEXNAME@__ 
      +V+Inf:%>a   PASS ;
 +V+Inf+Pass:%>ast #    ;

LEXICON reflinf
!! __@LEXNAME@__ 
 +V+Inf+Pass:%>ast # ;


!! !Present
! _______

LEXICON pres_ir
!! __@LEXNAME@__ 
  pres_ir_sg ;
  pres_pl    ;

LEXICON pres_jir
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>ji  PASS ;
 +V+Ind+Prs+2Sg:%>jir PASS ;
 +V+Ind+Prs+3Sg:%>jir PASS ;
  +V+Ind+Prs+Pl:%>ja  PASS ;

LEXICON pres_ir_sg
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>i  PASS ;
 +V+Ind+Prs+2Sg:%>ir PASS ;
 +V+Ind+Prs+3Sg:%>ir PASS ;
 +V+Subj+Prs+3Sg:%>i PASS ;

LEXICON pres_ar
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>i  PASS    ; ! XXX sjekk
 +V+Ind+Prs+2Sg:%>ar PASS    ;
 +V+Ind+Prs+3Sg:%>ar PASS    ;
                     pres_pl ;

LEXICON pres_ur
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>i  PASS    ;
 +V+Ind+Prs+2Sg:%>ur PASS    ;
 +V+Ind+Prs+3Sg:%>ur PASS    ;
                     pres_pl ;

LEXICON pres_ur_j
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>ji PASS    ;
 +V+Ind+Prs+2Sg:%>ur PASS    ;
 +V+Ind+Prs+3Sg:%>ur PASS    ;
               :j    pres_pl ;

LEXICON pres_ur_j2
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>i PASS    ;
 +V+Ind+Prs+2Sg:%>ur PASS    ;
 +V+Ind+Prs+3Sg:%>ur PASS    ;
               :j    pres_pl ;

LEXICON pres_null
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%> PASS    ;
 +V+Ind+Prs+2Sg:%> PASS    ;
 +V+Ind+Prs+3Sg:%> PASS    ;
                   pres_pl ;

LEXICON pres_strong_s1
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>i PASS    ;
                    pres_pl ; !XXX

LEXICON pres_strong_s23
!! __@LEXNAME@__ 
 +V+Ind+Prs+2Sg:%>ur PASS     ;
 +V+Ind+Prs+3Sg:%>ur PASS     ;
                     !pres_pl ;

LEXICON pres_strong_s23_t0
!! __@LEXNAME@__ 
 +V+Ind+Prs+2Sg:%>t PASS     ;
 +V+Ind+Prs+3Sg:%>  PASS     ;
                    !pres_pl ;

LEXICON pres_strong_s23_t1
!! __@LEXNAME@__ 
 +V+Ind+Prs+2Sg:%>rt PASS     ;
 +V+Ind+Prs+3Sg:%>r  PASS     ;
                     !pres_pl ;

LEXICON pres_pl
!! __@LEXNAME@__ 
 +V+Ind+Prs+Pl:%>a PASS ;

LEXICON pres_ast
!! __@LEXNAME@__ 
 +V+Ind+Prs+1Sg:%>ast # ; ! XXX check
 +V+Ind+Prs+2Sg:%>ast # ;
 +V+Ind+Prs+3Sg:%>ast # ;
  +V+Ind+Prs+Pl:%>ast # ;

LEXICON pret_st
!! __@LEXNAME@__ 
 +V+Ind+Prt+1Sg:%>aðist # ;
 +V+Ind+Prt+2Sg:%>aðist # ;
 +V+Ind+Prt+3Sg:%>aðist # ;
  +V+Ind+Prt+Pl:%>aðist # ;


!! !Preterite

LEXICON prt_d
!! __@LEXNAME@__ 
  prt_di ;
  prt_du ;

LEXICON prt_ð
!! __@LEXNAME@__ 
  prt_ði ;
  prt_ðu ;

LEXICON prt_t
!! __@LEXNAME@__ 
  prt_ti ;
  prt_tu ;

LEXICON prt_di
 +V+Ind+Prt+Sg:%>di PASS ;

LEXICON prt_ði
!! __@LEXNAME@__ 
 +V+Ind+Prt+Sg:%>ði PASS ;

LEXICON prt_ti
!! __@LEXNAME@__ 
 +V+Ind+Prt+Sg:%>ti PASS ;

LEXICON prt_du
!! __@LEXNAME@__ 
 +V+Ind+Prt+Pl:%>du PASS ;

LEXICON prt_tu
!! __@LEXNAME@__ 
 +V+Ind+Prt+Pl:%>tu PASS ;

LEXICON prt_ðu
!! __@LEXNAME@__ 
 +V+Ind+Prt+Pl:%>ðu PASS ;

LEXICON prt_dd
!! __@LEXNAME@__ 
 +V+Ind+Prt+Sg:%>ddi PASS ;
 +V+Ind+Prt+Pl:%>ddu PASS ;

LEXICON prt_a
!! __@LEXNAME@__ 
 +V+Ind+Prt+Sg:%>aði PASS ;
 +V+Ind+Prt+Pl:%>aðu PASS ;

LEXICON prt_null
!! __@LEXNAME@__ 
 +V+Ind+Prt+1Sg:     PASS ;
 +V+Ind+Prt+2Sg:     PASS ;
 +V+Ind+Prt+3Sg:     PASS ;

LEXICON prt_null_s
!! __@LEXNAME@__ 
 +V+Ind+Prt+1Sg:     PASS ;
 +V+Ind+Prt+2Sg:%>st PASS ;
 +V+Ind+Prt+3Sg:     PASS ;

LEXICON prt_null_s2
!! __@LEXNAME@__ 
 +V+Ind+Prt+1Sg:    PASS ;
 +V+Ind+Prt+2Sg:%>t PASS ;
 +V+Ind+Prt+3Sg:    PASS ;

LEXICON prt_u_p
!! __@LEXNAME@__ 
 +V+Ind+Prt+Pl:%>u PASS ;



!! !Imperative and present participle

LEXICON imp_prsptc
!! __@LEXNAME@__ 
  imp    ;
  prsptc ;

LEXICON imp
!! __@LEXNAME@__ 
  impsg ;
  imppl ;

LEXICON impsg
!! __@LEXNAME@__ 
 +V+Imp+Sg: PASS ;

LEXICON imppl
!! __@LEXNAME@__ 
 +V+Imp+Pl:%^JI%>ið PASS ;

LEXICON imppl_j
!! __@LEXNAME@__ 
 +V+Imp+Pl:%>ið PASS ;

LEXICON prsptc
!! __@LEXNAME@__ 
 +V+PrsPrc:%>andi VANDI             ;
        +A:%>andi HOMONYM_ADJ_FORMS ;

!! !Supine and preterite participle

LEXICON sup
!! __@LEXNAME@__ 
    +V+Sup:%>t     PASS ;
 +V+PrfPrc:%>d%>ur PASS ;

LEXICON sup_t
!! __@LEXNAME@__ 
    +V+Sup:%>t     PASS ;
 +V+PrfPrc:%>t%>ur PASS ;

LEXICON sup_tt
!! __@LEXNAME@__ 
    +V+Sup:%>tt     PASS ;
 +V+PrfPrc:%>dd%>ur PASS ;

LEXICON sup_a ! 
!! __@LEXNAME@__ kalla
    +V+Sup:%>að     PASS ;
 +V+PrfPrc:%>að%>ur PASS ;

LEXICON sup_null !
!! __@LEXNAME@__  stungið
 +V+Sup:%>ið PASS ;

LEXICON sup_in ! 
!! __@LEXNAME@__ kalla
    +V+Sup:%>ð     	PASS ;
 +V+PrfPrc:%>gvin 	PASS ;

!! !!Ad hoc, irregular
! =================

LEXICON BLÍVA
!! __@LEXNAME@__ 
         +Inf:blíva    PASS  ;
 +Ind+Prs+1Sg:blívi    PASS  ;
 +Ind+Prs+2Sg:blív%>ur PASS  ;
 +Ind+Prs+3Sg:blív%>ur PASS  ;
  +Ind+Prs+Pl:blíva    PASS  ;
 +Ind+Prt+1Sg:bleiv    PASS  ;
 +Ind+Prt+2Sg:bleivst  PASS  ;
 +Ind+Prt+3Sg:bleiv    PASS  ;
  +Ind+Prt+Pl:blivu    PASS  ;
      +Imp+Sg:blív     PASS  ;
      +Imp+Pl:blívið   PASS  ;
      +PrsPrc:blívandi VANDI ;
      +PrfPrc:blivin   PASS  ;
         +Sup:blívið   PASS  ;

LEXICON EIGA
!! __@LEXNAME@__ 
        +V+Inf:eiga    PASS  ;
+V+Ind+Prs+1Sg:eigi    PASS  ;
+V+Ind+Prs+2Sg:eig%>ur PASS  ;
+V+Ind+Prs+3Sg:eig%>ur PASS  ;
 +V+Ind+Prs+Pl:eiga    PASS  ;
 +V+Ind+Prt+Sg:átti    PASS  ;
 +V+Ind+Prt+Pl:áttu    PASS  ;
     +V+PrsPrc:eigandi VANDI ;
     +V+PrfPrc:áttur   PASS  ;
        +V+Sup:átt     PASS  ;

LEXICON EITA
!! __@LEXNAME@__ 
         +Inf:eita    PASS  ;
 +Ind+Prs+1Sg:eiti    PASS  ;
 +Ind+Prs+2Sg:eit%>ur PASS  ;
 +Ind+Prs+2Sg:eit%>ur PASS  ;
  +Ind+Prs+Pl:eita    PASS  ;
  +Ind+Prt+Sg:æt      PASS  ;
  +Ind+Prt+Pl:itu     PASS  ;
      +PrsPrc:itandi  VANDI ; ! XXX check
      +PrfPrc:itin    PASS  ;
         +Sup:itið    PASS  ;

LEXICON GRÁTA
!! __@LEXNAME@__ 
         +Inf:áta    PASS  ;
 +Ind+Prs+1Sg:æti    PASS  ;
 +Ind+Prs+2Sg:æt%>ur PASS  ;
 +Ind+Prs+2Sg:æt%>ur PASS  ;
  +Ind+Prs+Pl:æta    PASS  ;
  +Ind+Prt+Sg:æt      PASS  ;
  +Ind+Prt+Pl:ótu     PASS  ;
      +PrsPrc:átandi  VANDI ; 
      +PrfPrc:átin    PASS  ;
         +Sup:átið    PASS  ;

LEXICON HAVA
!! __@LEXNAME@__ 
         +Inf:hava     PASS  ;
 +Ind+Prs+1Sg:havi     PASS  ;
 +Ind+Prs+2Sg:hev%>ur  PASS  ;
 +Ind+Prs+3Sg:hev%>ur  PASS  ;
  +Ind+Prs+Pl:hava     PASS  ;
  +Ind+Prt+Sg:hevði    PASS  ;
  +Ind+Prt+Pl:høvdu    PASS  ;
      +Imp+Sg:hav      PASS  ;
      +Imp+Pl:havið    PASS  ;
      +PrsPrc:havandi  VANDI ;
      +PrfPrc:havd%>ur PASS  ;
         +Sup:havt     PASS  ;

LEXICON KUNNA
!! __@LEXNAME@__ 
        +V+Inf:kunna  # ;
+V+Ind+Prs+1Sg:kann   # ;
+V+Ind+Prs+2Sg:kanst  # ;
+V+Ind+Prs+3Sg:kann   # ;
 +V+Ind+Prs+Pl:kunnu  # ;
 +V+Ind+Prs+Pl:kunna  # ;
 +V+Ind+Prt+Sg:kundi  # ;
 +V+Ind+Prt+Pl:kundu  # ;
        +V+Sup:kunnað # ;

LEXICON MEGA
!! __@LEXNAME@__ 
         +Inf:mega  # ;
 +Ind+Prs+1Sg:má    # ;
 +Ind+Prs+2Sg:mást  # ;
 +Ind+Prs+3Sg:má    # ;
  +Ind+Prs+Pl:mugu  # ;
  +Ind+Prs+Pl:muga  # ;
  +Ind+Prt+Sg:mátti # ;
  +Ind+Prt+Pl:máttu # ;

LEXICON MUNNA
!! __@LEXNAME@__ 
         +Inf:munna  # ;
 +Ind+Prs+1Sg:man    # ;
 +Ind+Prs+2Sg:manst  # ;
 +Ind+Prs+3Sg:man    # ;
  +Ind+Prs+Pl:munnu  # ;
  +Ind+Prs+Pl:munna  # ;
  +Ind+Prt+Sg:mundi  # ;
  +Ind+Prt+Pl:mundu  # ;
         +Sup:munnað # ;

LEXICON SKULA
!! __@LEXNAME@__ 
         +Inf:skula  # ;
 +Ind+Prs+1Sg:skal   # ;
 +Ind+Prs+2Sg:skalt  # ;
 +Ind+Prs+3Sg:skal   # ;
  +Ind+Prs+Pl:skulu  # ;
  +Ind+Prs+Pl:skula  # ;
  +Ind+Prt+Sg:skuldi # ;
  +Ind+Prt+Pl:skuldu # ;
         +Sup:skulað # ;

LEXICON TYKJA
!! __@LEXNAME@__ 
         +Inf:tykja PASS ;
 +Ind+Prs+3Sg:tykir PASS ;
  +Ind+Prs+Pl:tykti PASS ;
  +Ind+Prt+Sg:tókti PASS ;
         +Sup:tykt  #    ;

LEXICON VERA
!! __@LEXNAME@__ 
         +Inf:vera  # ;
 +Ind+Prs+1Sg:eri   # ;
 +Ind+Prs+2Sg:ert   # ;
 +Ind+Prs+3Sg:er    # ;
  +Ind+Prs+Pl:eru   # ;
 +Ind+Prt+1Sg:var   # ;
 +Ind+Prt+2Sg:varst # ;
 +Ind+Prt+3Sg:var   # ;
  +Ind+Prt+Pl:vóru  # ;
         +Sup:verið # ;

LEXICON VERÐA
!! __@LEXNAME@__ 
         +Inf:verða    #   ;
 +Ind+Prs+1Sg:verði    #   ;
 +Ind+Prs+2Sg:verð%>ur #   ;
 +Ind+Prs+3Sg:verð%>ur #   ;
  +Ind+Prs+Pl:verða    #   ;
 +Ind+Prt+1Sg:varð     #   ;
 +Ind+Prt+2Sg:varðst   #   ;
 +Ind+Prt+3Sg:varð     #   ;
  +Ind+Prt+Pl:vórðu    #   ;
      +Imp+Sg:verð     #   ;
     +Sbj+3Sg:verði    #   ; !XXX verði ljós
      +Imp+Pl:verðið   #   ;
      +PrsPrc:verðandi #   ;
      +PrfPrc:vorðin   p26 ;


LEXICON VILJA
!! __@LEXNAME@__ 
         +Inf:vilja  PASS ;
 +Ind+Prs+1Sg:vil    PASS ;
 +Ind+Prs+2Sg:vilt   PASS ;
 +Ind+Prs+3Sg:vil    PASS ;
  +Ind+Prs+Pl:vilja  PASS ;
  +Ind+Prt+Sg:vildi  PASS ;
  +Ind+Prt+Pl:vildu  PASS ;
         +Sup:viljað PASS ;

LEXICON VITA
!! __@LEXNAME@__ 
        +V+Inf:vita   PASS ;
+V+Ind+Prs+1Sg:veit   PASS ;
+V+Ind+Prs+2Sg:veitst PASS ;
+V+Ind+Prs+3Sg:veit   PASS ;
 +V+Ind+Prs+Pl:vita   PASS ;
 +V+Ind+Prt+Sg:visti  PASS ;
 +V+Ind+Prt+Pl:vistu  PASS ;
        +V+Sup:vitað  PASS ;


LEXICON SÍGGJA
!! __@LEXNAME@__ 
         +Inf:síggja    PASS  ;
 +Ind+Prs+1Sg:síggi     PASS  ;
 +Ind+Prs+2Sg:sært      PASS  ;
 +Ind+Prs+3Sg:sær       PASS  ;
  +Ind+Prs+Pl:síggja    PASS  ;
 +Ind+Prt+1Sg:sá        PASS  ;
 +Ind+Prt+2Sg:sást      PASS  ;
 +Ind+Prt+3Sg:sá        PASS  ;
  +Ind+Prt+Pl:sóu       PASS  ;
      +Imp+Sg:síggj     PASS  ;
      +Imp+Sg:sí        PASS  ;
      +Imp+Pl:síggið    PASS  ;
      +PrsPrc:síggjandi VANDI ;
      +PrfPrc:sædd%>ur  PASS  ;
         +Sup:sæð       PASS  ;
         +Sup:sætt      PASS  ;

LEXICON FÁA
!! __@LEXNAME@__ 
             +V+Inf:fáa    PASS ;
     +V+Ind+Prs+1Sg:fái    PASS ;
     +V+Ind+Prs+2Sg:fært   PASS ;
     +V+Ind+Prs+3Sg:fær    #    ; ! XXX special?
+V+Ind+Prs+3Sg+Pass:fæst   #    ; ! XXX special?
      +V+Ind+Prs+Pl:fáa    PASS ;
     +V+Ind+Prt+1Sg:fekk   PASS ;
     +V+Ind+Prt+2Sg:fekst  PASS ;
     +V+Ind+Prt+3Sg:fekk   PASS ;
      +V+Ind+Prt+Pl:fingu  PASS ;
          +V+Imp+Sg:fá     PASS ;
          +V+Imp+Pl:fáið   PASS ;
          +V+PrfPrc:fingin PASS ;
             +V+Sup:fingið PASS ;

LEXICON NÁA ! 
!! __@LEXNAME@__ XXX check
             +V+Inf:náa    PASS ;
     +V+Ind+Prs+1Sg:nær    PASS ;
     +V+Ind+Prs+2Sg:nært   PASS ;
     +V+Ind+Prs+3Sg:nær    #    ; ! XXX special?
+V+Ind+Prs+3Sg+Pass:næst   #    ; ! XXX special?
      +V+Ind+Prs+Pl:náa    PASS ;
     +V+Ind+Prt+1Sg:nátt   PASS ;
     +V+Ind+Prt+2Sg:nátt  PASS ;
     +V+Ind+Prt+3Sg:nátt   PASS ;
      +V+Ind+Prt+Pl:náað  PASS ;
          +V+Imp+Sg:ná     PASS ;
          +V+Imp+Pl:náið   PASS ;
          +V+PrfPrc:nátt  PASS ;
             +V+Sup:nátt  PASS ;
             
LEXICON LIGGJA
!! __@LEXNAME@__ 
         +Inf:iggja    PASS  ;
 +Ind+Prs+1Sg:iggi     PASS  ;
 +Ind+Prs+2Sg:igg%>ur  PASS  ;
 +Ind+Prs+3Sg:igg%>ur  PASS  ;
  +Ind+Prs+Pl:iggja    PASS  ;
 +Ind+Prt+1Sg:á        PASS  ;
 +Ind+Prt+2Sg:ást      PASS  ;
 +Ind+Prt+3Sg:á        PASS  ;
  +Ind+Prt+Pl:ógu      PASS  ;
      +Imp+Sg:igg      PASS  ;
      +Imp+Pl:iggið    PASS  ;
      +PrsPrc:iggjandi VANDI ;
      +PrfPrc:igin     PASS  ; ! XXX fix link to adj
         +Sup:igið     PASS  ;


LEXICON RADA
!! __@LEXNAME@__ 
          +Inf:ráða    PASS  ;
  +Ind+Prs+1Sg:ræði    PASS  ;
  +Ind+Prs+2Sg:ræður   PASS  ;
  +Ind+Prs+3Sg:ræður   PASS  ;
   +Ind+Prs+Pl:ráða    PASS  ;
   +Ind+Prt+Sg:ráddi   PASS  ;
   +Ind+Prt+Sg:ræð     PASS  ; ! old form
   +Ind+Prt+Pl:ráddu   PASS  ;
       +Imp+Sg:ráð     PASS  ;
       +Imp+Pl:ráðið   PASS  ;
       +PrsPrc:ráðandi VANDI ;
       +PrfPrc:ráðin   PASS  ;
! The following two lines are commented out because they cause
! a segmentation fault in lexc!!!
!         +Sup:rátt    PASS  ;
!         +Sup:ráðið   PASS  ; ! old form

LEXICON SYNGJA ! 
!! __@LEXNAME@__ XXX check
        +V+Inf:syngja PASS  ;
+V+Ind+Prs+1Sg:syngur PASS  ;
+V+Ind+Prs+2Sg:syngur PASS  ;
+V+Ind+Prs+3Sg:syngur PASS  ;
 +V+Ind+Prs+Pl:syngja PASS  ;
 +V+Ind+Prt+Sg:sang PASS  ;
 +V+Ind+Prt+Pl:sungu PASS  ;
     +V+Imp+Sg:syng PASS  ;
     +V+Imp+Pl:syngja PASS  ;
     +V+PrsPrc:syngjandi VANDI ;
     +V+PrfPrc:sungið PASS  ;



LEXICON HOGGA ! 
!! __@LEXNAME@__ høgga
         +V+Inf:høgga    PASS  ;
 +V+Ind+Prs+1Sg:høggi     PASS  ;
 +V+Ind+Prs+2Sg:høggur  PASS  ;
 +V+Ind+Prs+3Sg:høggur  PASS  ;
  +V+Ind+Prs+Pl:høgga    PASS  ;
 +V+Ind+Prt+1Sg:høgdi        PASS  ;
 +V+Ind+Prt+2Sg:høgdi      PASS  ;
 +V+Ind+Prt+3Sg:hjó        PASS  ;
 +V+Ind+Prt+3Sg:høgg        PASS  ;
  +V+Ind+Prt+Pl:høgdu      PASS  ;
      +V+Imp+Sg:høgg      PASS  ;
      +V+Imp+Pl:høggið    PASS  ;
      +V+PrsPrc:høggandi VANDI ;
      +V+PrfPrc:høggin     PASS  ; ! XXX fix link to adj
         +V+Sup:høggið     PASS  ;

LEXICON KVODA
!! __@LEXNAME@__ 
         +V+Inf:kvøða  PASS  ;
 +V+Ind+Prs+1Sg:kvøði     PASS  ;
 +V+Ind+Prs+2Sg:kvøður  PASS  ;
 +V+Ind+Prs+3Sg:kvøður  PASS  ;
  +V+Ind+Prs+Pl:kvøða    PASS  ;
 +V+Ind+Prt+1Sg:kvøgdi        PASS  ;
 +V+Ind+Prt+2Sg:kvøgdi      PASS  ;
 +V+Ind+Prt+3Sg:kjað        PASS  ;
 +V+Ind+Prt+3Sg:kvøddi        PASS  ;
 +V+Ind+Prt+3Sg:kvaddi        PASS  ;
  +V+Ind+Prt+Pl:kvóðu      PASS  ;
      +V+Imp+Sg:kvøgg      PASS  ;
      +V+Imp+Pl:kvøðið    PASS  ;
      +V+PrsPrc:kvøðandi VANDI ;
      +V+PrfPrc:kvøddin     PASS  ; ! XXX fix link to adj
         +V+Sup:kvøddið     PASS  ;


             
LEXICON FLYGGJA
!! __@LEXNAME@__ 
         +Inf:flýggja    PASS  ;
 +Ind+Prs+1Sg:flýggi     PASS  ;
 +Ind+Prs+2Sg:flýggjar  PASS  ;
 +Ind+Prs+3Sg:flýggjar  PASS  ;
  +Ind+Prs+Pl:flýggja    PASS  ;
 +Ind+Prt+1Sg:flýddu        PASS  ;
 +Ind+Prt+2Sg:flýddu      PASS  ;
 +Ind+Prt+3Sg:flýddu        PASS  ;
  +Ind+Prt+Pl:flýddu      PASS  ;
      +Imp+Sg:flý      PASS  ;
      +Imp+Pl:flýggið    PASS  ;
      +PrsPrc:flýggjandi VANDI ;
      +PrfPrc:flýtt     PASS  ; ! XXX fix link to adj
         +Sup:flýtt     PASS  ;


LEXICON VAKSA
!! __@LEXNAME@__ 
         +Inf:vaksa    PASS  ;
 +Ind+Prs+1Sg:veksi     PASS  ;
 +Ind+Prs+2Sg:veksur  PASS  ;
 +Ind+Prs+3Sg:veksur  PASS  ;
 +Ind+Prs+2Sg+Use/NG:veks PASS  ; ! kvæð.
 +Ind+Prs+3Sg+Use/NG:veks PASS  ; ! kvæð.
  +Ind+Prs+Pl:vaksa    PASS  ;
 +Ind+Prt+1Sg:vaks        PASS  ;
 +Ind+Prt+1Sg+Use/NGA:vóks  PASS  ; ! kvæð.
 +Ind+Prt+2Sg:vaks        PASS  ;
 +Ind+Prt+2Sg+Use/NGA:vóks  PASS  ; ! kvæð.
 +Ind+Prt+3Sg:vaks        PASS  ;
 +Ind+Prt+3Sg+Use/NGA:vóks  PASS  ; ! kvæð.
  +Ind+Prt+Pl:vuksu      PASS  ;
      +Imp+Sg:vaks      PASS  ;
      +Imp+Pl:vaksið    PASS  ;
      +PrsPrc:vaksandi VANDI ;
      +PrfPrc:vaks     PASS  ; ! XXX fix link to adj
         +Sup:vaks     PASS  ;



LEXICON DAMA
!! __@LEXNAME@__ 
                  inf        ;
                  pres_ar    ;
                  prt_a      ;
                  prt_d      ;
                  prt_t      ; ! rare
                  imp_prsptc ;
                  sup        ;
     +V+PrfPrc:%> p7         ;

!LEXICON
!         +Inf:   PASS       ;
! +Ind+Prs+1Sg:   PASS       ;
! +Ind+Prs+2Sg:   PASS       ;
! +Ind+Prs+3Sg:   PASS       ;
!  +Ind+Prs+Pl:   PASS       ;
!  +Ind+Prt+Sg:   PASS       ;
!  +Ind+Prt+Pl:   PASS       ;
!      +Imp+Sg:   PASS       ;
!      +Imp+Pl:   PASS       ;
!      +PrsPrc:   PASS       ;
!      +PrfPrc:   PASS       ;
!         +Sup:   PASS       ;



!! !Middle lexicon

LEXICON PASS
!! __@LEXNAME@__ 
                  # ; ! Verbs as usual
 +Pass:%^PASS%>st # ;

LEXICON VANDI
!! __@LEXNAME@__ 
  #    ; ! Verbs as usual
  andi ; ! participles




! =================== !
!! !!Perfect Participles !
! =================== !




LEXICON p18
!! __@LEXNAME@__
  adj_a ;
  l18   ;

LEXICON p26
!! __@LEXNAME@__ 
 :%^EPH a_m_sg_n_null    ;
 :%^EPH a_non_umlaut_obl ;
 :%^EPH a_umlaut         ;
        l26              ;

LEXICON p32
!! __@LEXNAME@__ 
 :%^EPH adj_a ;
        l32   ;

LEXICON p39
!! __@LEXNAME@__ 
  adj_a ; !XXX check!
  l39   ;

LEXICON p5
!! __@LEXNAME@__ 
  adj_a     ;
  adj_r_gen ;
  l5        ;

LEXICON p5x
!! __@LEXNAME@__ 
  l5        ;


LEXICON p6
!! __@LEXNAME@__ 
 :%^EDH adj_a ; ! no ð deletion
        l6    ;

LEXICON p7
!! __@LEXNAME@__ 
  adj_a     ;
  adj_r_gen ;
  l7        ;

LEXICON p8
!! __@LEXNAME@__ 
         a_non_umlaut ;
 :%^UUML a_umlaut     ;
         adj_r_gen    ;
         l8           ;





