!! !!!The Faroese morphophonological file 

!! !!Alphabet
!! Here we declare all symbols.

Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å   !!= * @CODE@ 
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç       !!= * @CODE@ 

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å   !!= * @CODE@ 
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð           !!= * @CODE@ 


 a2:a  !!= * @CODE@  for da2n -> dønum
 g2:g  !!= * @CODE@ for invariant g
 i2:i  !!= * @CODE@ for invariant i
 j2:j  !!= * @CODE@ for invariant j
 t2:t  !!= * @CODE@ for invariant, non-deleted t, dráttri pro *drátri
 v2:v  !!= * @CODE@ for invariant v
 
 %^UUML:0 %^IUML:0 %^eIUML:0 %^ØUML:0   !!= * @CODE@ : Umlaut types ,
 %^W:0 %^JI:0                  !!= * @CODE@ : Cns changes ,
 %^EPH:0                       !!= * @CODE@ : Epenthesis,  ,
 %^OEA:0                       !!= * @CODE@ : ø to a
 %^EDH:0 
 %^VSH:0  

 %^GDEL:0 %^GGDEL:0 %^GVDEL:0 %^VDEL:0 %^JDEL:0 %^RDEL:0 	!!= * @CODE@ : Cns deletion triggers,
 %^EIO:0 %^OA:0 %^WVV:0 %^NGKK:0 %^PASS:0
 %^AB1:0 %^AB2:0 %^AB3:0 %^AB4:0 %^AB5:0 %^AB6:0 %^AB7:0	!!= * @CODE@ : Ablaut series ,
 %^aAB:0 %^uAB:0	!!= * @CODE@ : Ablaut series subcases
 %>:0 	!!= * @CODE@ : Suffix border
 
 « » 	!!= * @CODE@ : hmm, in use?
 ;

!! !!Sets
!! Here we define some convenient sets.
Sets


 Vow = a e i o u y æ ø å                                !!= * @CODE@ 
       á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý ;    !!=  @CODE@ 
 Cns = b c d f g h j k l m n p q r s t v w x z ð þ ;    !!= * @CODE@ 
 Nas = m n ;                                            !!= * @CODE@ 
 NonNas = b c d f g h j k l p q r s t v w x z ð þ ;     !!= * @CODE@ 
 Dummy = %^UUML %^IUML %^eIUML %^W %^EPH %^JI %^OEA     !!= * @CODE@ 
 %^EDH %^VSH %^GDEL %^GGDEL %^GVDEL %^VDEL %^JDEL       !!= * @CODE@ 
 %^RDEL %^EIO %^OA %^WVV %^NGKK %^AB1 %^AB2 %^AB3       !!=  @CODE@ 
 %^AB4 %^AB5 %^AB6 %^AB7 %^aAB %^uAB %^PASS %> ;        !!=  @CODE@ 

 Special =  %^UUML %^IUML %^W %^EPH %^JI %^OEA %^GDEL %^GGDEL    !!= * @CODE@ 
            %^GVDEL %^VDEL  %^JDEL %^RDEL ;                      !!=  @CODE@ 
                                          !! Forgot why these are special...


!! !!!Rules
!! These are the rules. After each rule (or rather: after many of the rules) there are test cases that are there to test whether the rules work.

Rules


!! !!Verschärfung

"Deleting g"
!! __@RULENAME@__ 
 g:0 <=> Vow     _     v:  [ %^GVDEL: | %^GDEL: %>: ] ;       !! * Deleting the gv Verschärfung 1
         Vow     _ g:0 j:0  %^GGDEL: %>: [s|n] ;              !! * Deleting gg in ggj Genitive I
         Vow g:  _     j:0 [ %^GDEL: | %^GGDEL: ] %>: [s|n] ; !! * Deleting gg in ggj Genitive II
         Vow :k _ (%>:)  (u: r:) ([Dummy: - %^EPH:]) (Dummy:*) %>: [d|ð|s|t] ([a|i|u]) ;

! "Deleting Double Consonant kk etc. in Front of Consonant"
! !! __@RULENAME@__
!  g:0 <=> Vow :k _ (%>:)  (u: r:) ([Dummy: - %^EPH:]) %>: [d|ð|s|t] ([a|i|u]) ;

!!€ sting^NGKK^aAB>st
!!€ stak0000st


 
"ng to kk Part 1"
!! __@RULENAME@__ 
 n:k <=> Vow _ g: %^NGKK: ;

"ng to kk Part 2"
!! __@RULENAME@__ 
 g:k <=> Vow n: _ %^NGKK: (Dummy:) [#:|%>: Vow:] ;



"Deleting v in gv sequences"
!! __@RULENAME@__ 
 v:0 <=> Vow g: _ [ %^GVDEL: | [(%^AB1:)| (%^AB5:)|(%^VSH:) | (%^IUML:)] %^VDEL: | %^IUML: %>: i ] ;

!! Verschärfung tests

!!€ bógv^IUML>i
!!€ bøg000i

!!€ flúgv^IUML^VDEL
!!€ flýg000

!!€ flúgv^VSH^VDEL>u
!!€ flug0000u

!!€ búgv^GVDEL>s
!!€ bú0000s

!!€ bógv^VDEL>s
!!€ bóg000s

!!€ skógv^GVDEL>m
!!€ skó0000m

!!€ skýggj^GGDEL>s
!!€ ský00000s

!!€ kríggj^GDEL>s
!!€ kríg0000s

!!€ sjógv^GDEL>ar
!!€ sjó0v00ar

"Deleting r in Genitive of ur stems"
!! __@RULENAME@__ 
 r:0 <=> u: _ %^EPH: %^RDEL: %>: a r ;

!!€ brúður^EPH^RDEL>ar
!!€ brúð00000ar


"Deleting m in um%>num "
!! __@RULENAME@__ 
 m:0 <=> Cns: Dummy:* %>: u _ %>: n u m ; 
         g: v:  Dummy:* %>:   _ %>: n u m ; 

!!€ ris>um>num
!!€ ris0u00num

!!€ skógv^GVDEL>m>num
!!€ skó000000num



"Deleting Double Consonant in Front of Consonant"
!! __@RULENAME@__ 
 Cx:0 <=> Vow Cx _ (%>:)  (u: r:) ([Dummy: - %^EPH:]) %>: [d|ð|s|t] ([a|i|u]) ; 
! Cx:0 <=> Vow Cx _ (%>:) (u: r:) (Dummy:) %>: [d|ð|s|(s) t] ([a|i|u]) #:0 ; 
! Cx:0 <=> Vow Cx _ ( %>: u: r: ) ([Dummy: - %^PASS:]) %>: [s|t] #:0 ; 
      where Cx in ( b d f g k l m n p r s t v ) ;

!! The preceeding rule is fishy - the test cases below don't fit the context
!! requirements, and the >s# in the right context seems to indicate passive.
!! The rule conflicts with the "Cns Deletion in front of Pass" rule at the
!! end of the file - but only when using the Xerox tools! XXX - please have a look!

!!€ hjall>s
!!€ hjal00s

!!€ rygg>s
!!€ ryg00s

!!€ hjall>ar
!!€ hjall0ar



!"Double consonant simplification in front of t"
!Cx:0 <=> Vow Cx _ >: t ;
!     where Cx in ( b g l m n p r k ) ;

!!€ all>t
!!€ al00t


!"Deleting Double Consonant in Front of t + vow"
! Cx:0 <=> Vow Cx _ ( (%>:) ( u: r: ) Dummy:) %>: t [a|i|u]  ; 
!      where Cx in ( b d f g k l m n p r s t v ) ;
! added t to right context, check this!!



!! !!Verbal Sandhi rules

"Geminate Assimilation in Past Tense d"
!! __@RULENAME@__ 
 Cx:0 <=> Vow Cy _ %>: d ;
 	where Cx in ( d ð t )
 	      Cy in ( n r l )
! 	where Cx in ( d ð n p g k t ) ! removing geminates due to prev rule.
! 	      Cy in ( n r n p g k l )
 	      matched ;

"Geminate Assimilation in Past Tense t"
!! __@RULENAME@__ 
 Cx:0 <=> Vow Cy _ %>: t ;
 	where Cx in ( d ð t ) ! t:0 is covered by the next rule
 	      Cy in ( n r s ) ! l   in front of all consonants
 	      matched ; 

!!€ send>di
!!€ sen00di

!!€ hirð>di
!!€ hir00di

!"Geminate Assimilation After t Final Stem Cluster"
! t:0 <=> Vow Cns _ %>: t ;
 	      
!!€ sett>ti
!!€ set00ti 	      


"ð Assimilation in Front of Dental Past Suffix -d(i)"
!! __@RULENAME@__ 
 ð:d <=> Vow _(%^WVV:) %>: d (%>:) [ #: | Vow ] ;
 
!!€ leið>di
!!€ leid0di 

!"ð Assimilation in Front of Dental Past Suffix -d(i)"
!!! __@RULENAME@__ 
! ð:d <=> Vow  _        %>:0 d  [ #: | Vow ] ;
!         Vow: _ %^WVV: %>:0 d  [ #: | Vow ] ;
!         Vow: _        %>:0 d (%>:) [ #: | Vow ] ;
 
!!€ leið>di
!!€ leid0di

!!€ greið>di
!!€ greid0di


!!€ ryð^WVV>di
!!€ rud00di


"Deleting Double Consonant in Front of Epenthesis mark"
!! __@RULENAME@__ 
 Cx:0 <=> Cx _ [i:0|a:0|u:0] [l|r] (%^EPH:) (%^UUML:) %>: Vow ;
      where Cx in ( b d f g k l m n p r s t v ) ;

!!€ summar^EPH>i
!!€ sum00r00i

!!€ himmal^EPH^UUML>um
!!€ him00l000um

!tunnil>i
!tun00l>i

!himmal>i
!him00l0i

"Deleting stem-final s in s genitive"
!! __@RULENAME@__ 
 s:0 <=> Vow s %>: _ [#:0|%>:] ; ! added #:0

!!€ primus>s
!!€ primus00

!!€ primus>s
!!€ primus00

!!€ grís>s
!!€ grís00


"Double ð Deletion"
!! __@RULENAME@__ 
ð:0 <=> _ %>:0 ð ;


"ð Assimilation in Front of Supine Suffix -t"
!! __@RULENAME@__ 
 ð:t <=> Vow _ %>: t #: ;

!!€ leið>t
!!€ leit0t

"Adjusting Dental Past Suffix -d(i)"
!! __@RULENAME@__ 
 d:t <=> Vow ([r|l|n]) [ p | t | k | s ] (Dummy:) %>: _ (%>:) [ #: | Vow ] ;

!!€ keyp>di
!!€ keyp0ti

!!€ merk>di
!!€ merk0ti

!! !!Adjectival sandhi rules

"Adjective neuter after nlr 1"
!! __@RULENAME@__ 
t:ð <=> Vow Cns+ i [ r: | l: | n: ] ( %^EPH: ) %>: _ #:  ;

"Adjective neuter after nlr 2"
!! __@RULENAME@__ 
Cx:0 <=> Vow Cns+ i _ ( %^EPH: ) %>: :ð ;
     where Cx in ( l n r ) ; 	      

!!€ mikil^EPH>t
!!€ miki000ð

!!€ gamal^EPH>t
!!€ gamal00t

"t Deletion in Neuter"
!! __@RULENAME@__ 
 t:0 <=> [ Cns t | Vow Cns+ Vow ð %^EDH: ] %>: _ #: ;
!t:0 <=> [ r t | Vow Cns+ Vow ð %^EDH: ] %>: _ ;



!! !! j rules

"Deleting j"
!! __@RULENAME@__ 
 j:0 <=>           _     [ %^JI: | %^JDEL: ] ;          ! j for j-deletion dummies
         Vow g g:  _ %>: [ Cns | i ] ;                  ! j in j-stems
         Vow g: g: _     [ %^GDEL: | %^GGDEL: ] %>: [s|n] ; ! j in ggj genitive
             Cns   _     [ :i | :ý | :e :y ] Cns:* %^IUML: ;

!!€ kríggj^GDEL>num
!!€ kríg0000num	

!!€ beiggj^JI>i
!!€ beigg000i

!!€ verkj^JDEL>ur
!!€ verk000ur

!!€ heyggj>i
!!€ heygg00i


"Realising j in front of vowels"
!! __@RULENAME@__ 
 j2:j => _ %>: [ a | u ] ;

!!€ hylj2>ar
!!€ hylj0ar




!      ===========  !
!!  !! Vowel rules  
!      ===========  !


"Realising i2 as i"
!! __@RULENAME@__ 
 i2:i => _ %>: ;

!svii2>
!svii>

!! ! Epenthetic vowel rules

"Epenthetic deletion"
!! __@RULENAME@__ 
 Vx:0 <=> Vow: Cns:* (%>:) _ (%>:) [ r: | l | n ] %^EPH: ( Special:* ) %>: Vow ; !Vow ;
      where Vx in (a i u) ;
 !Vow was commented out?


!!€ økur^EPH^UUML>um
!!€ øk0r000um

!!€ lykil^EPH>an
!!€ lyk0l00an

!!€ aftan^EPH>
!!€ aftan00

!!€ vakin^EPH>ir
!!€ vak0n00ir



"U-umlaut of Epenthetic vowel"
!! __@RULENAME@__ 
 a:u <=> a: m _ Cns: %^EPH: %^UUML: (%^UUML:) #:0 ;
! check the double umlaut marking...
! question is whether Fem umlaut is always there.

!!€ gamal^EPH^UUML
!!€ gomul00

!!€ gamal^EPH^UUML>u
!!€ goml000u


!! !Umlaut rules

"U-umlaut in Front of Nasal"
!! __@RULENAME@__ 
 Vx:Vy <=> _ Nas (Cns:)  Vow: (Cns:)  %^EPH: %^UUML: ;
         _ Nas (Cns:)       (Cns:)         %^UUML: ;
!        _ Nas (Cns:)  Vow: (Cns:)         %^UUML: ; !Needed???
	where Vx in ( a a2 )
		  Vy in ( o ø  )
		  matched ;
	
!!€ tank^UUML
!!€ tonk0
!den skal heller ikke klare det. den skal klare tonkum, og det gjør den.

!!€ band^UUML
!!€ bond00

!!€ hamar^EPH^UUML>um
!!€ hom0r000um

"General U-umlaut"
!! __@RULENAME@__ 
  a:ø <=> _ NonNas: NonNas:* (Nas) (Vow:) %^UUML: ;
  
!!€ dag^UUML>um
!!€ døg00um

!!€ sag^UUML>a
!!€ søg00a

!!€ all^UUML>
!!€ øll00


"U-umlaut for akur"
!! __@RULENAME@__ 
  a:ø <=> _ k u:0 r (%^EPH:) %^UUML: %>: u ;

!!€ akur^EPH^UUML>um
!!€ øk0r000um

"I-umlaut"
!! __@RULENAME@__ 
 Vx:Vy <=> _ Cns:* %^IUML: ; !(Dummy:) [ %>: i | %>: u r | (%>: Cns:*) #: | %>: g ] ;
     where Vx in ( a o u ø á ó ú )
	       Vy in ( e y y i æ ø ý )
	   matched ;

!!€ dag^IUML>i
!!€ deg00i

!!€ son^IUML>i
!!€ syn00i

!!€ bógv^IUML>i
!!€ bøg000i

!!€ ung^IUMLr>i
!!€ yng0r0i

!!€ fjørð^IUML>i
!!€ f0irð00i



"eI-umlaut"
!! __@RULENAME@__ for o:e, á:e, i:e
 Vx:Vy <=> _ Cns:* %^eIUML: ;
    where Vx in ( o á i )
          Vy in ( e e e )
   matched;

"I-umlaut for bróðir" ! also for other words?
!! __@RULENAME@__ 
 ó:ø <=> _ Cns:* u: r ( %^EPH: ) %^IUML: ;


"Inverted U-umlaut from ø"
!! __@RULENAME@__ 
 ø:a <=> _ Cns* (Vow: Cns) ( %^EPH: ) %^OEA: ;

!!€ fløtt^OEAa
!!€ flatt0a

"Inverted U-umlaut from o"
!! __@RULENAME@__ 
 o:a <=> _ Cns* (Vow: Cns) ( %^EPH: ) %^OA: ;

!!€ fonn^OA>ar
!!€ fann00ar


"o/ei-Umlaut I"
!! __@RULENAME@__ 
 e:o <=> _ i: Cns:+ %^EIO: ;

"o/ei-Umlaut II"
!! __@RULENAME@__ 
 i:0 <=> e: _ Cns:+ %^EIO: ;

!!€ dreing^EIO>i
!!€ dro0ng00i

! XXX Test i dag
!"Suffix-initial vowel deletion for vowel-final stems"
!i:0 <=> _ %>: i ;

! merki>ið
! merk00ið


"æ to ø Umlaut for bræv"
æ:ø <=> _ Cns:* %^ØUML: ;



!! !Vowel deletion rules

"Vowel deletion in front of na"
!! __@RULENAME@__ 
 a:0 <=> _ %^W: n ;

!"Special Dative v Deletion"
! v:0 <=> g _ ^IUML: >: i ;

!"Plural i Deletion in faðir"
! i:0 <=> :e ð _ r ^IUML: >: ;

! faðir^IUML>ar
! feð0r00ar





!! !Verbal vowel alternation rules

"Stem vowel change in Weak Verbs"
!! __@RULENAME@__ 
 Vx:Vy <=> _ Cns:+ %^WVV: ; ! WVV = Verb
 	where Vx in (e y)
 		  Vy in (a u)
 		  matched ;
 		  
!!€ flek^WVV>t
!!€ flak00t 

!!€ flek^WVV>t
!!€ flak00t 

!!€ vel^WVV>di
!!€ val00di 


"Stem Vowel Shortening in Supine and Participle"
!! __@RULENAME@__ 
Vx:Vy <=> _ Cns:+ %^VSH:  ; !ok, men flúgv > flýg funker fremdeles ikke ...
	where Vx in ( á ó í ú )
	      Vy in ( a o i u )
		  matched ;



! Verbal ablaut rules

! Past singular

"Past tense singular diphthongs I"
!! __@RULENAME@__ 
0:Vx <=> Cns _ Vy: Cns:+ %^AB1: ;
	where Vx in ( e e e e )
	      Vy in ( í ó u ú )
!	      Vz in ( i y y y )
	matched ;

"Past tense singular diphthongs II"
!! __@RULENAME@__ 
Vy:Vz <=> Cns :Vx _  Cns:+ %^AB1: ;
	where Vx in ( e e e e )
	      Vy in ( í ó u ú )
	      Vz in ( i y y y )
	matched ;

!!€ b0ít^AB1
!!€ beit0


"Past tense singular monophthongs"
!! __@RULENAME@__ 
Vx:Vy <=> _  (Cns:+) %^AB3: ;
	where Vx in ( e ø a o á i )
	      Vy in ( a a ó a æ a )
	matched ;

!!€ gev^AB3
!!€ gav0

"Past tense plural monophthongs"
!! __@RULENAME@__ 
Vx:Vy <=> _ Cns:+ %^AB2: %>: u ;
          _ %^AB2: ;	
        where Vx in ( á í ó ú i e a ø )
    	      Vy in ( ó i u u ó ó i u )
	matched ;

!"Past tense plural monophthongs to ó"
!Vx:ó <=> _ Cns:+ %>: u ;
!	where Vx in ( i e a o á ) ;

"Past tense plural monophthongs to a"
!! __@RULENAME@__ 
Vx:a <=> _ Cns:+ (%^NGKK:) %^aAB: ;
	where Vx in ( i e ø ) ;

!"Past tense plural monophthongs to u"
!Vx:u <=> _ Cns:+ %^uAB: ;
!	where Vx in ( i e ø ) ;

!"Past tense plural monophthongs to o"
!Vx:o <=> _ Cns:+ %^oAB: ;
!	where Vx in ( i e ø ) ;

"Supine u"
!! __@RULENAME@__ 
Vx:u <=> _ Cns:+ %^AB4: ;
	where Vx in ( i e ø ó ú ) ;

!"Supine u"
!Vx:u <=> Cns+ _ %^AB4: %>: ;
!	where Vx in ( i e ) ;

"Supine o"
!! __@RULENAME@__ 
Vx:o <=> _ Cns:+ %^AB5: ;
	where Vx in ( a i e ø ó ú ) ;

!"Supine o"
!Vx:o <=> Cns+ _ %^AB5: %>: ;
!	where Vx in ( í ó ú ø i e a ) ;

"Supine i"
!! __@RULENAME@__ 
Vx:i <=> _ Cns:+ %^AB6: ;
	where Vx in ( í a e á ) ;

!"Supine i"
!Vx:i <=> Cns+ _ %^AB6: %>: ;
!	where Vx in ( í a e ) ;

"Present tense ý"
!! __@RULENAME@__ 
Vx:ý <=> _ Cns:+ %^AB7: ;
	where Vx in ( ó ) ;

!"Adjective neuter after ð"
!t:0 <=> Vow Cns+ Vow ð %^EDH: %>: _ #: ;

! brekað>t
! brekað00


!! !Adjectival Sandhi rule

"Vowel shortening in Neuter"
!! __@RULENAME@__ 
ó:o <=> _ ð: %>: t ;
!ó:o <= _ :t %>: t ; ! check this one.

!!€ góð>t
!!€ got0t

!!€ skjót>t
!!€ skjót0t


!! !!Other rules

!! !Morphological passive rules

"u in ur Deletion in front of Pass"
!! __@RULENAME@__ 
 u:0 <=> %>: _ r: %^PASS:0 %>: s t #: ;

"r Deletion in front of Pass"
!! __@RULENAME@__ 
 r:0 <=> [i:|u:] _ %^PASS:0 %>: s t #: ;

"ð Deletion in front of Pass"
!! __@RULENAME@__ 
 ð:0 <=> _ %^PASS:0 %>: s t #: ;


! Remove these when the dust has settled
!"Vowel-deletion in definite article suffix"
! Vx:0 <=> _ (Dummy:) %>: (Dummy:) [ i [ n | ð ] | u m | s %>: i n s | a | u ] ;
!      where Vx in ( i a ) ;
!"Deleting initial i in definite suffix"
! i:0 <=> %>:  Vow                              %>: _ [ n u m | n s | n | ð | n i | n a | n n a r ] #: ; 
!             [ a | u | i ] r (Special:*) %>:*  %>: _ [ n [i|a] r #: ]; 
!               Vow %>:+                            _ [ n | ð | n i ] #: ;
!                                                   _ %>:+ [ a | u m ] ;
!"Deleting initial i in definite suffix after vowel, ð or g last in stem"
! i:0 <=> Vow %>: %>:                                _         n u m #: ; 
!         Vow: %>:                                   _ %>: i:0 n u m #: ; 
!         Vow: %>: i:0 %>:                           _         n u m #: ;
!         Vow: [ ð | g ] (Special:*) %>: ( i:0 ) %>: _         n u m #: ; 
!         Vow: [ ð | g ] (Special:*) %>:             _ %>:  i: n u m #: ;

