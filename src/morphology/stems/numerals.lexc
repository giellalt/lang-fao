! ======== !
! Numerals !
! ======== !



LEXICON Numeral
basic ;
ordinals ;
NUM_PREFIXES ;

LEXICON basic
eitt+Num:ei EITT ;
eini n ;
tvey+Num:tv TVEY ;
trý+Num:tr TRÝ ;
einir+Num:ein PAIRNUM ;
tvinnir+Num:tvinn PAIRNUM ;
trinnir+Num:trinn PAIRNUM ;
fýra n ;
fimm n ;
seks n ;
sjey n ;
átta n ;
áttir n ;
níggju n ;
tíggju n ;
ellivu n ;
tólv n ;
trettan n ;
fjúrtan n ;
fimtan n ;
sekstan n ;
seytan n ; ! XXX check
seytjan n ;
átjan n ;
nítjan n ;
tjúgu n ;
tretivu n ;
tríati n ;     ! 1Mos
fjøruti n ;
hálvtrýss n ;
fimmti n ;     ! 1Mos
trýss n ;
seksti n ;     ! 1Mos
hálvfjerðs n ;
sjeyti n ;     ! 1Mos
fýrs n ;
áttati n ;     ! 1Mos
hálvfems n ;
níti n ;       ! 1Mos
hundrað n ;
túsund n ;
millión n ;
átjanhundrað n ;
ein n ;
nítjanhundrað n ;
tveytúsund n ;
níggjundi n ;
tjúgur n ;
fýrsir n ;



LEXICON EITT
+Msc+Sg+Nom:n # ;
+Msc+Sg+Acc:n # ;
+Msc+Sg+Dat:num # ;
+Msc+Sg+Gen:ns # ;
+Fem+Sg+Nom:n # ;
+Fem+Sg+Acc:na # ;
+Fem+Sg+Dat:ni # ;
+Fem+Sg+Dat:nari # ;
+Fem+Sg+Gen:nar # ;
+Neu+Sg+Nom:tt # ;
+Neu+Sg+Acc:tt # ;
+Neu+Sg+Dat:num # ;
+Neu+Sg+Gen:ns # ;

LEXICON TVEY
+Msc+Sg+Nom:eir # ;
+Msc+Sg+Acc:eir # ;
+Msc+Sg+Dat:eimum # ;
+Msc+Sg+Gen:eggja # ;
+Fem+Sg+Nom:ær # ;
+Fem+Sg+Acc:ær # ;
+Fem+Sg+Dat:eimum # ;
+Fem+Sg+Gen:ggja # ;
+Neu+Sg+Nom:ey # ;
+Neu+Sg+Acc:ey # ;
+Neu+Sg+Dat:eimum # ;
+Neu+Sg+Gen:ggja # ;

LEXICON TRÝ
+Msc+Sg+Nom:íggir # ;
+Msc+Sg+Acc:íggjar # ;
+Msc+Sg+Dat:imum # ;
+Msc+Sg+Gen:íggja # ;
+Fem+Sg+Nom:íggjar # ;
+Fem+Sg+Acc:íggjar # ;
+Fem+Sg+Dat:imum # ;
+Fem+Sg+Gen:íggja # ;
+Neu+Sg+Nom:ý # ;
+Neu+Sg+Acc:ý # ;
+Neu+Sg+Dat:imimum # ;
+Neu+Sg+Gen:íggja # ;

LEXICON PAIRNUM
+Msc+Sg+Pl+Nom:ir # ;
+Msc+Sg+Pl+Acc:ar # ;
+Msc+Sg+Pl+Dat:um # ;
+Msc+Sg+Pl+Gen:a # ;
+Fem+Sg+Pl+Nom:ar # ;
+Fem+Sg+Pl+Acc:ar # ;
+Fem+Sg+Pl+Dat:um # ;
+Fem+Sg+Pl+Gen:a # ;
+Neu+Sg+Pl+Nom:i # ;
+Neu+Sg+Pl+Acc:i # ;
+Neu+Sg+Pl+Dat:um # ;
+Neu+Sg+Pl+Gen:a # ;

LEXICON n
+Num: # ;


! Ordinals
! ========

LEXICON ordinals
fyrsti:fyrst ord_decl ;
annar: ANNAR ;
triði:trið ord_decl ;
triðji:triðj ord_decl ;
fjórði:fjórð ord_decl ;
fimti:fimt ord_decl ;
sætti:sætt ord_decl ;
sjeyndi:sjeynd ord_decl ;
áttandi:áttand ord_decl ;
nággjundi:níggjund ord_decl ;
tíggjundi:tíggjund ord_decl ;
ellivti:ellivt ord_decl ;
ellinti:ellint ord_decl ;
tólvti:tólvt ord_decl ;
trettandi:trettand ord_decl ;
fjúrtandi:fjúrtand ord_decl ;
fimtandi:fimtand ord_decl ;
sekstandi:sekstand ord_decl ;
seytjandi:seytjand ord_decl ;
átjandi:átjand ord_decl ;
nítjandi:nítjand ord_decl ;
tjúgundi:tjúgund ord_decl ;
tretivti:tretivt ord_decl ;
tretivundi:tretivund ord_decl ;
fjørutiundi:fjørutiund  ord_decl ;
hálvtrýsinstrúgundi:hálvtrýsinstrúgund ord_decl ;
hálvtrýssundi:hálvtrýssund ord_decl ;
trýsinstjúgundi:trýsinstjúgund ord_decl ;
trýssundi:trýssund ord_decl ;
hálvfjerðsinstjúgundi:hálvfjerðsinstjúgund ord_decl ;
hálvfjerðsundi:hálvfjerðsund ord_decl ;
fýrsinstjúgundi:fýrsinstjúgund ord_decl ;
fýrsundi:fýrsund ord_decl ;
hálvfemsinstjúgundi:hálvfemsinstjúgund ord_decl ;
hálvfemsundi:hálvfemsund ord_decl ;
hundrandi:hundrand ord_decl ;
túsundi:túsund ord_decl ;

LEXICON ord_decl
+A: adj_def ;

LEXICON ANNAR
+A: ANNARMORPH ;
+Pron+Indef: ANNARMORPH ;

LEXICON ANNARMORPH
+Msc+Nom:annar # ;
+Msc+Acc:annan # ;
+Msc+Dat:øðrum # ;
+Msc+Gen:annars # ;
+Fem+Nom:onnur # ;
+Fem+Acc:aðra # ;
+Fem+Acc:aðru # ;
+Fem+Dat:aðri # ;
+Fem+Dat:aðru # ;
+Fem+Gen:annarar # ;
+Neu+Nom:annað # ;
+Neu+Acc:annað # ;
+Neu+Dat:øðrum # ;
+Neu+Gen:annars # ;
+Msc+Pl+Nom:aðrir # ;
+Msc+Pl+Acc:aðrar # ;
+Msc+Pl+Dat:øðrum # ;
+Msc+Pl+Gen:annara # ;
+Fem+Pl+Nom:aðrar # ;
+Fem+Pl+Acc:aðrar # ;
+Fem+Pl+Dat:øðrum # ;
+Fem+Pl+Gen:annara # ;
+Neu+Pl+Nom:onnur # ;
+Neu+Pl+Acc:onnur # ;
+Neu+Pl+Dat:øðrum # ;
+Neu+Pl+Gen:annara # ;


! Arabic numerals
! ===============

LEXICON NUM_PREFIXES                            !^C^                               
        ARABIC ;
§       ARABIC ;      ! §24                     !^C^                               
§§      ARABIC ;     ! §§24                     !^C^                               
§%      ARABIC ;    ! § 24                      !^C^                               
§§%     ARABIC ;   ! §§ 24                      !^C^                               
%-      ARABIC ;     ! -24                      !^C^                               
%‒      ARABIC ;     ! -24 U+2012 Figure line   !^C^                               
%–      ARABIC ;     ! -24 U+2013 en dash       !^C^                               
%—      ARABIC ;     ! -24 U+2014 em dash       !^C^                               
%―      ARABIC ;     ! -24 U+2015 horizontal l  !^C^                               
%+      ARABIC ;     ! +24                      !^C^                               
%-%     ARABIC ;        ! - 24                  !^C^                               
%‒%     ARABIC ;        ! - 24 2012             !^C^                               
%–%     ARABIC ;        ! - 24 2013             !^C^                               
%—%     ARABIC ;        ! - 24 2014             !^C^                               
%―%     ARABIC ;        ! - 24 2015             !^C^                               
%+%     ARABIC ;        ! + 24                  !^C^                               
%*      ARABIC ;     ! *24                      !^C^                               
%$      ARABIC ;     ! $24  ! And the Euro?     !^C^                               
!%<      ARABIC ;     ! <24                      !^C^                               
!%>     ARABIC ;     ! >24                      !^C^                               
s%.     ARABIC ;     ! s.24                     !^C^                               



LEXICON ARABIC                                  !^C^                               
1 ARABICLOOP ;                                  !^C^                               
2 ARABICLOOP ;                                  !^C^                               
3 ARABICLOOP ;                                  !^C^                               
4 ARABICLOOP ;                                  !^C^                               
5 ARABICLOOP ;                                  !^C^                               
6 ARABICLOOP ;                                  !^C^                               
7 ARABICLOOP ;                                  !^C^                               
8 ARABICLOOP ;                                  !^C^                               
9 ARABICLOOP ;                                  !^C^                               
%0 ARABICLOOP ;                                 !^C^                               



LEXICON ARABICLOOP                              !^C^                                                                              
        ARABICEND ;
        ARABIC ;                                !^C^                                                                              
%-      ARABIC ;              ! 1-2    multipart numbers        !^C^                                                              
%.%-    ARABIC ;              ! 1.-2   multipart numbers        !^C^                                                              
%-%     ARABIC ;              ! 1- 2   multipart numbers        !^C^                                                              
% %-    ARABIC ;              ! 1 -2   multipart numbers        !^C^                                                              
% %-%   ARABIC ;              ! 1 - 2  multipart numbers        !^C^                                                              
% %-%-%   ARABIC ;            ! 1 -- 2 multipart numbers        !^C^                                                              
%. ARABIC ;                                                     !^C^                                                              
%, ARABIC ;                                                     !^C^                                                              
%: ARABIC ;                                                     !^C^                                                              
%  ARABIC ;   ! Allowing for "23 500".                     !^C^                                                              
   ARABIC ;   ! Allowing for "23 500" w/nbsp (initial char = alt-space) !^C^                                              
%/ ARABIC ;   ! 24/12                                      !^C^                                                              
+A+Ord:%. # ;                                                    !^C^                                                             
%-+Num:%- # ;         ! 10-  (high on missing list)                            !^C^                                                             
,%-+Num:,%- # ;       ! 10,-                                   !^C^                                                             
%%+Num+Nom:%% # ;     ! 50%. It is wrong, but 75 % of us write "75%".    !^C^                                              
% %%+Num+Nom:% %% # ; ! We must analyse the corr. ones too... 50 % etc. !^C^                                              
+Num+Cmpnd#: NUMBERCMPND ;

LEXICON ARABICEND
+Num+Nom: # ;
+Num+Acc: # ;
+Num+Dat: # ;
+Num+Gen: # ;

LEXICON NUMBERCMPND
ár:ár kv6 ;
tal:tal h5 ;
mannafar:mannafar h5 ;
metri:metr k1 ;
oyra:oyr h1 ;
siffur:siffur h13 ;
partur:part k6 ;
ferð:ferð kv2e ;
pláta:plát kv1 ;
