!! =================================== !
!! The Faroese morphophonological file !
!! =================================== !

Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 g2:g i2:i j2:j v2:v 
 %^UUML:0 %^IUML:0 %^eIUML:0 %^W:0 %^EPH:0 %^JI:0 %^OEA:0 
 %^EDH:0 %^VSH:0

 %^GDEL:0 %^GGDEL:0 %^GVDEL:0 %^VDEL:0 %^JDEL:0 %^RDEL:0
 %^EIO:0 %^OA:0 %^WVV:0 %^NGKK:0 %^PASS:0

 %^AB1:0 %^AB2:0 %^AB3:0 %^AB4:0 %^AB5:0 %^AB6:0 %^AB7:0
 %^aAB:0 %^uAB:0
 %>:0 ;

Sets

 Vow = a e i o u y æ ø å 
       á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý ;
 Cns = b c d f g h j k l m n p q r s t v w x z ð þ ;
 
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z ð þ ;
 
 Dummy = %^UUML %^IUML %^eIUML %^W %^EPH %^JI %^OEA
 %^EDH %^VSH %^GDEL %^GGDEL %^GVDEL %^VDEL %^JDEL
 %^RDEL %^EIO %^OA %^WVV %^NGKK %^AB1 %^AB2 %^AB3
 %^AB4 %^AB5 %^AB6 %^AB7 %^aAB %^uAB %^PASS %> ; 

 Special =  %^UUML %^IUML %^W %^EPH %^JI %^OEA %^GDEL %^GGDEL 
 %^GVDEL %^VDEL  %^JDEL %^RDEL;


Rules


! Verschärferung

"Deleting g"
 g:0 <=> Vow     _     v:0 [ %^GVDEL: | %^GDEL: %>: ] ;   ! Deleting the gv Verschärferung 1
         Vow     _ g:0 j:0  %^GGDEL: %>: s ;              ! Deleting gg in ggj Genitive I
         Vow g:0 _     j:0 [ %^GDEL: | %^GGDEL: ] %>: s ; ! Deleting gg in ggj Genitive II

"Deleting v in gv sequences"
 v:0 <=> Vow g: _ [ %^GVDEL: | [(%^AB1:)| (%^AB5:)|(%^VSH:) | (%^IUML:)] %^VDEL: | %^IUML: %>: i ] ;


!€ bógv%^IUML%>i
!€ bøg000i

!€ flúgv%^IUML%^VDEL
!€ flýg000

!€ flúgv%VHS%VDEL%>u
!€ flug0000u

!€ búgv%^GVDEL%>s
!€ bú0000s

!€ bógv%^VDEL%>s
!€ bóg000s

!€ skógv%^GVDEL%>m
!€ skó0000m

!€ skýggj%^GGDEL%>s
!€ ský00000s

!€ kríggj%^GDEL%>s
!€ kríg0000s

!€ sjógv%>ar
!€ sjó0v0ar

"Deleting r in Genitive of ur stems"
 r:0 <=> u: _ %^EPH: %^RDEL: %>: a r ;

!€ brúður%^EPH%^%RDEL%>ar
!€ brúð00000ar


"Deleting m in um%>num "
 m:0 <=>     %>: u _ %>: n u m ; 
         g v %>:   _ %>: n u m ; 

!ris%>um%>num
!ris00u000num

!skógv%>m%>num
!skó0000000num



!! The following rule is fishy - the test cases below don't fit the context
!! requirements, and the >s# in the right context seems to indicate passive.
!! The rule conflicts with the "Cns Deletion in front of Pass" rule at the
!! end of the file - but only when using the Xerox tools! XXX - please have a look!
"Deleting Double Consonant in Front of Consonant"
 Cx:0 <=> Vow Cx _ (%>:)  (u: r:) ([Dummy: - %^EPH:]) %>: [d|ð|s|t] ([a|i|u]) ; 
! Cx:0 <=> Vow Cx _ (%>:) (u: r:) (Dummy:) %>: [d|ð|s|(s) t] ([a|i|u]) #:0 ; 
! Cx:0 <=> Vow Cx _ ( %>: u: r: ) ([Dummy: - %^PASS:]) %>: [s|t] #:0 ; 
      where Cx in ( b d f g k l m n p r s t v ) ;

!"Double consonant simplification in front of t"
!Cx:0 <=> Vow Cx _ %>: t ;
!     where Cx in ( b g l m n p r k ) ;

!€ all%>t
!€ al00t


!"Deleting Double Consonant in Front of t + vow"
! Cx:0 <=> Vow Cx _ ( (%>:) ( u: r: ) Dummy:) %>: t [a|i|u]  ; 
!      where Cx in ( b d f g k l m n p r s t v ) ;
! added t to right context, check this!!

!€ hjall%>s
!€ hjal00s

!€ rygg%>s
!€ ryg00s

!€ hjall%>ar
!€ hjall0ar

! Verbal Sandhi rules

"Geminate Assimilation in Past Tense d"
 Cx:0 <=> Vow Cy _ %>: d ;
 	where Cx in ( d ð t )
 	      Cy in ( n r l )
! 	where Cx in ( d ð n p g k t ) ! removing geminates due to prev rule.
! 	      Cy in ( n r n p g k l )
 	      matched ;

"Geminate Assimilation in Past Tense t"
 Cx:0 <=> Vow Cy _ %>: t ;
 	where Cx in ( d ð ) ! t:0 is covered by the next rule
 	      Cy in ( n r ) ! l   in front of all consonants
 	      matched ; 

!€ send%>di
!€ sen00di

!€ hirð%>di
!€ hir00di

!"Geminate Assimilation After t Final Stem Cluster"
! t:0 <=> Vow Cns _ %>: t ;
 	      
!€ sett%>ti
!€ set00ti 	      


"ð Assimilation in Front of Dental Past Suffix -d(i)"
 ð:d <=> Vow _ %>: d (%>:) [ #: | Vow ] ;
 
!€ leið%>di
!€ leid0di 

"Deleting Double Consonant in Front of Epenthesis mark"
 Cx:0 <=> Cx _ [ i:0 | a:0 | u:0 ] [ l | r ] ( %^EPH: ) %>: Vow ;
      where Cx in ( b d f g k l m n p r s t v ) ;

!€ summar%^EPH%>i
!€ sum00r00i

!€ himmal%^EPH%^UUML%>um
!€ him00l000um

!tunnil%>i
!tun00l%>i

!himmal%>i
!him00l0i

"Deleting stem-final s in s genitive"
 s:0 <=> Vow s %>: _ #:0 ; ! added #:0

!€ primus%>s
!€ primus000

!grís%>s
!grís000


"Double ð Deletion"
ð:0 <=> _ %>:0 ð ;


"ð Assimilation in Front of Supine Suffix -t"
 ð:t <=> Vow _ %>: t #: ;

!€ leið%>t
!€ leit0t

"Adjusting Dental Past Suffix -d(i)"
 d:t <=> Vow [ p | t | k | s ] %>: _ (%>:) [ #: | Vow ] ;

!€ keyp%>di
!€ keyp0ti

"Adjective neuter after nlr 1"
t:ð <=> Vow Cns+ i [ r: | l: | n: ] ( %^EPH: ) %>: _ #:  ;

"Adjective neuter after nlr 2"
Cx:0 <=> Vow Cns+ i _ ( %^EPH: ) %>: :ð ;
     where Cx in ( l n r ) ; 	      

!€ mikil%^EPH%>t
!€ miki000ð

!€ gamal%^EPH%>t
!€ gamal00t

"t Deletion in Neuter"
 t:0 <=> [ Cns t | Vow Cns+ Vow ð %^EDH: ] %>: _ #: ;
!t:0 <=> [ r t | Vow Cns+ Vow ð %^EDH: ] %>: _ ;

 
"ng to kk Part 1"
 n:k <=> Vow _ g:k %^NGKK: ;

"ng to kk Part 2"
 g:k <=> Vow n: _ %^NGKK: ;



"Deleting j"
 j:0 <=>           _     [ %^JI: | %^JDEL: ] ;          ! j for j-deletion dummies
         Vow g g:  _ %>: [ Cns | i ] ;                  ! j in j-stems
         Vow g: g: _     [ %^GDEL: | %^GGDEL: ] %>: s ; ! j in ggj genitive
             Cns   _     [ :i | :ý | :e :y ] Cns:* %^IUML: ;
         
!€ beiggj%^JI%>i
!€ beigg000i

!€ verkj%^JDEL%>ur
!€ verk000ur

!€ heyggj%>i
!€ heygg00i


"Realising j in front of vowels"
 j2:j => _ %>: [ a | u ] ;

!€ hylj2%>ar
!€ hylj0ar




!   ===========  !
!   Vowel rules  !
!   ===========  !


"Realising i2 as i"
 i2:i => _ %>: ;

!svii2%>
!svii%>



"Epenthetic deletion"
 Vx:0 <=> Vow: Cns:* (%>:) _ (%>:) [ r | l | n ] %^EPH: ( Special:* ) %>: Vow ; !Vow ;
      where Vx in (a i u) ;
 !Vow was commented out?

!€ ak0r00
!€ økur%^EPH%^UUML%>um
!€ øk0r000um

!€ lykil%^EPH%>an
!€ lyk0l00an

!€ aftan%^EPH%>
!€ aft0n00


"U-umlaut of Epenthetic vowel"
 a:u <=> a: m _ Cns: %^EPH: %^UUML: (%^UUML:) #:0 ;
! check the double umlaut marking...
! question is whether Fem umlaut is always there.

!€ gamal%^EPH%^UUML
!€ gomul00

!€ gamal%^EPH%^UUML%>u
!€ goml000u


"U-umlaut in Front of Nasal"
 a:o <=> _ Nas (Cns:)  Vow: (Cns:)  %^EPH: %^UUML: ;
         _ Nas (Cns:)       (Cns:)         %^UUML: ;
!        _ Nas (Cns:)  Vow: (Cns:)         %^UUML: ; !Needed???

!€ tank%^UUML
!€ tonk0
!den skal heller ikke klare det. den skal klare tonkum, og det gjør den.

!€ band^UUML
!€ bond00

!€ hamar%^EPH%^UUML%>um
!€ hom0r000um

"General U-umlaut"
  a:ø <=> _ NonNas: NonNas:* (Nas) (Vow:) %^UUML: ;
  
!€ dag%>%^UUMLum
!€ døg00um

!€ sag%>%^UUMLa
!€ søg00a

!€ all%>%^UUML
!€ øll00


"U-umlaut for akur"
  a:ø <=> _ k u:0 r (%^EPH:) %^UUML: %>: u ;

!€ akur%^EPH%^UUML%>um
!€ øk0r000um

"I-umlaut"
 Vx:Vy <=> _ Cns:* %^IUML: ; !(Dummy:) [ %>: i | %>: u r | (%>: Cns:*) #: | %>: g ] ;
     where Vx in ( a o ø á ó ú )
	   Vy in ( e y i æ ø ý )
	   matched ;

!€ dag%^IUML%>i
!€ deg00i

!€ son%^IUML%>i
!€ syn00i

!€ bógv%^IUML%>i
!€ bøg000i

"eI-umlaut"
 Vx:Vy <=> _ Cns:* %^eIUML: ;
    where Vx in ( o á i )
          Vy in ( e e e )
   matched;

"I-umlaut for bróðir"
 ó:ø <=> _ Cns:* u: r ( %^EPH: ) %^IUML: ;

!€ fjørð%^IUML%>i
!€ f0irð00i

!€ bjóð%^IUML
!€ b0ýð0

"Inverted U-umlaut from ø"
 ø:a <=> _ Cns* (Vow: Cns) ( %^EPH: ) %^OEA: ;

!€ fløtt%^ØAa
!€ flatt0a

"Inverted U-umlaut from o"
 o:a <=> _ Cns* (Vow: Cns) ( %^EPH: ) %^OA: ;

!€ fonn%^OA%>ar
!€ fann00ar


"o/ei-Umlaut I"
 e:o <=> _ i: Cns:+ %^EIO: ;

"o/ei-Umlaut II"
 i:0 <=> e: _ Cns:+ %^EIO: ;

!€ dreing%^EIO%>i
!€ dro0ng00i




"Vowel deletion in front of na"
 a:0 <=> _ %^W: n ;

!"Special Dative v Deletion"
! v:0 <=> g _ %^IUML: %>: i ;

!"Plural i Deletion in faðir"
! i:0 <=> :e ð _ r %^IUML: %>: ;

!€ faðir%^IUML%>ar
!€ feð0r00ar


"u Deletion in unum after Stressed Vowel"
 u:0 <=> Vx %>: _ n u m ;
 	where Vx in (á é í ó ú ý æ) ;

!€ bý%>unum
!€ by00num 	


! Verbal vowel alternations

"Stem vowel change in Weak Verbs"
 Vx:Vy <=> _ Cns:+ %^WVV: ; ! WVV = Verb
 	where Vx in (e y)
 		  Vy in (a u)
 		  matched ;
 		  
!€ flek%^WVV%>t
!€ flak00t 

!€ flek%^WVV%>t
!€ flak00t 

!€ vel%^WVV%>di
!€ val00di 


"Stem Vowel Shortening in Supine and Participle"
Vx:Vy <=> _ Cns:+ %^VSH:  ; !ok, men flúgv > flýg funker fremdeles ikke ...
	where Vx in ( á ó í ú )
		  Vy in ( a o i u )
		  matched ;


! Adjectival Sandhi rule

"Vowel shortening in Neuter"
ó:o <=> _ ð: %>: t ;
!ó:o <= _ :t %>: t ; ! check this one.

!€ góð%>t
!€ got0t

!€ skjót%>t
!€ skjót0t



! Verbal ablaut rules

! Past singular

"Past tense singular diphthongs I"
0:Vx <=> Cns _ Vy:Vz Cns:+ %^AB1: ;
	where Vx in ( e e e e )
	      Vy in ( í ó u ú )
	      Vz in ( i y y y )
	matched ;

"Past tense singular diphthongs II"
Vy:Vz <=> Cns :Vx _  Cns:+ %^AB1: ;
	where Vx in ( e e e e )
	      Vy in ( í ó u ú )
	      Vz in ( i y y y )
	matched ;

"Past tense singular monophthongs"
Vx:Vy <=> _  (Cns:+) %^AB3: ;
	where Vx in ( e ø a o á i )
	      Vy in ( a a ó a æ a )
	matched ;

!€ gev%^AB3
!€ gav0

"Past tense plural monophthongs"
Vx:Vy <=> _ Cns:+ %^AB2: %>: u ;
          _ %^AB2: ;	
        where Vx in ( á í ó ú i e a ø )
    	      Vy in ( ó i u u ó ó i u )
	matched ;

!"Past tense plural monophthongs to ó"
!Vx:ó <=> _ Cns:+ %>: u ;
!	where Vx in ( i e a o á ) ;

"Past tense plural monophthongs to a"
Vx:a <=> _ Cns:+ (%^NGKK:) %^aAB: ;
	where Vx in ( i e ø ) ;

!"Past tense plural monophthongs to u"
!Vx:u <=> _ Cns:+ %^uAB: ;
!	where Vx in ( i e ø ) ;

!"Past tense plural monophthongs to o"
!Vx:o <=> _ Cns:+ %^oAB: ;
!	where Vx in ( i e ø ) ;
	      
"Supine u"
Vx:u <=> _ Cns:+ %^AB4: ;
	where Vx in ( i e ø ú ) ;

!"Supine u"
!Vx:u <=> Cns+ _ %^AB4: %>: ;
!	where Vx in ( i e ) ;

"Supine o"
Vx:o <=> _ Cns:+ %^AB5: ;
	where Vx in ( a i e ø ó ú ) ;

!"Supine o"
!Vx:o <=> Cns+ _ %^AB5: %>: ;
!	where Vx in ( í ó ú ø i e a ) ;

"Supine i"
Vx:i <=> _ Cns:+ %^AB6: ;
	where Vx in ( í a e á ) ;

!"Supine i"
!Vx:i <=> Cns+ _ %^AB6: %>: ;
!	where Vx in ( í a e ) ;

"Present tense ý"
Vx:ý <=> _ Cns:+ %^AB7: ;
	where Vx in ( ó ) ;

!"Adjective neuter after ð"
!t:0 <=> Vow Cns+ Vow ð %^EDH: %>: _ #: ;

!€ brekað%>t
!€ brekað00




! Morphological passive rules

"u in ur Deletion in front of Pass"
 u:0 <=> %>: _ r: %^PASS:0 %>: s t #: ;

"r Deletion in front of Pass"
 r:0 <=> [i:|u:] _ %^PASS:0 %>: s t #: ;

"ð Deletion in front of Pass"
 ð:0 <=> _ %^PASS:0 %>: s t #: ;


! Remove these when the dust has settled
!"Vowel-deletion in definite article suffix"
! Vx:0 <=> _ (Dummy:) %>: (Dummy:) [ i [ n | ð ] | u m | s %>: i n s | a | u ] ;
!      where Vx in ( i a ) ;
!"Deleting initial i in definite suffix"
! i:0 <=> %>:  Vow                              %>: _ [ n u m | n s | n | ð | n i | n a | n n a r ] #: ; 
!             [ a | u | i ] r (Special:*) %>:*  %>: _ [ n [i|a] r #: ]; 
!               Vow %>:+                            _ [ n | ð | n i ] #: ;
!                                                   _ %>:+ [ a | u m ] ;
!"Deleting initial i in definite suffix after vowel, ð or g last in stem"
! i:0 <=> Vow %>: %>:                                _         n u m #: ; 
!         Vow: %>:                                   _ %>: i:0 n u m #: ; 
!         Vow: %>: i:0 %>:                           _         n u m #: ;
!         Vow: [ ð | g ] (Special:*) %>: ( i:0 ) %>: _         n u m #: ; 
!         Vow: [ ð | g ] (Special:*) %>:             _ %>:  i: n u m #: ;




