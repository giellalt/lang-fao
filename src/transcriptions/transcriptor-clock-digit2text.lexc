! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! ==================== !
! The North Sámi clock !
! ==================== !


Multichar_Symbols

@U.TIME.1@
@U.TIME.2@
@U.TIME.3@
@U.TIME.4@
@U.TIME.5@
@U.TIME.6@
@U.TIME.7@
@U.TIME.8@
@U.TIME.9@
@U.TIME.10@
@U.TIME.11@
@U.TIME.12@

+Use/NG
+Use/NA


LEXICON Root
      %0: smallhour ;
 +Use/NA: smallhour ;
          largehour ;

LEXICON smallhour
  @U.TIME.1@1:@U.TIME.1@  AFTpunkt ; !01:00 - 01:19
  @U.TIME.2@1:@U.TIME.2@  BEFpunkt ; !01:20 - 01:59
  @U.TIME.2@2:@U.TIME.2@  AFTpunkt ; !02:00 - 02:19
  @U.TIME.3@2:@U.TIME.3@  BEFpunkt ; !02:20 - 02:59
  @U.TIME.3@3:@U.TIME.3@  AFTpunkt ; !03:00 - 03:19
  @U.TIME.4@3:@U.TIME.4@  BEFpunkt ; !03:20 - 03:59
  @U.TIME.4@4:@U.TIME.4@  AFTpunkt ; !04:00 - 04:19
  @U.TIME.5@4:@U.TIME.5@  BEFpunkt ; !04:20 - 04:59
  @U.TIME.5@5:@U.TIME.5@  AFTpunkt ; !05:00 - 05:19
  @U.TIME.6@5:@U.TIME.6@  BEFpunkt ; !05:20 - 05:59
  @U.TIME.6@6:@U.TIME.6@  AFTpunkt ; !06:00 - 06:19
  @U.TIME.7@6:@U.TIME.7@  BEFpunkt ; !06:20 - 06:59
  @U.TIME.7@7:@U.TIME.7@  AFTpunkt ; !07:00 - 07:19
  @U.TIME.8@7:@U.TIME.8@  BEFpunkt ; !07:20 - 07:59
  @U.TIME.8@8:@U.TIME.8@  AFTpunkt ; !08:00 - 08:19
  @U.TIME.9@8:@U.TIME.9@  BEFpunkt ; !08:20 - 08:59
  @U.TIME.9@9:@U.TIME.9@  AFTpunkt ; !09:00 - 09:19
 @U.TIME.10@9:@U.TIME.10@ BEFpunkt ; !09:20 - 09:59

LEXICON largehour
  @U.TIME.10@1%0:@U.TIME.10@            AFTpunkt ; !10:00 - 10:19
  @U.TIME.11@1%0:@U.TIME.11@            BEFpunkt ; !10:20 - 10:59
   @U.TIME.11@11:@U.TIME.11@            AFTpunkt ; !11:00 - 11:59
   @U.TIME.12@11:@U.TIME.12@            BEFpunkt ; !11:19 - 11:59
   @U.TIME.12@12:@U.TIME.12@            AFTpunkt ; !12:00 - 12:19
    @U.TIME.1@12:@U.TIME.1@             BEFpunkt ; !12:19 - 12:59
    @U.TIME.1@13:@U.TIME.1@             AFTpunkt ; !13:00 - 13:19
    @U.TIME.2@13:@U.TIME.2@             BEFpunkt ; !13:19 - 13:59
    @U.TIME.2@14:@U.TIME.2@             AFTpunkt ; !14:00 - 114:19
    @U.TIME.3@14:@U.TIME.3@             BEFpunkt ; !14:19 - 14:59
    @U.TIME.3@15:@U.TIME.3@             AFTpunkt ; !15:00 - 15:19
    @U.TIME.4@15:@U.TIME.4@             BEFpunkt ; !15:19 - 15:59
    @U.TIME.4@16:@U.TIME.4@             AFTpunkt ; !16:00 - 16:19
    @U.TIME.5@16:@U.TIME.5@             BEFpunkt ; !16:19 - 16:59
    @U.TIME.5@17:@U.TIME.5@             AFTpunkt ; !17:00 - 17:19
    @U.TIME.6@17:@U.TIME.6@             BEFpunkt ; !17:19 - 17:59
    @U.TIME.6@18:@U.TIME.6@             AFTpunkt ; !18:00 - 18:19
    @U.TIME.7@18:@U.TIME.7@             BEFpunkt ; !18:19 - 18:59
    @U.TIME.7@19:@U.TIME.7@             AFTpunkt ; !19:00 - 19:19
    @U.TIME.8@19:@U.TIME.8@             BEFpunkt ; !19:20 - 19:59
   @U.TIME.8@2%0:@U.TIME.8@             AFTpunkt ; !20:00 - 20:19
   @U.TIME.9@2%0:@U.TIME.9@             BEFpunkt ; !20:20 - 20:59
    @U.TIME.9@21:@U.TIME.9@             AFTpunkt ; !21:00 - 21:19
   @U.TIME.10@21:@U.TIME.10@            BEFpunkt ; !21:20 - 21:59
   @U.TIME.10@22:@U.TIME.10@            AFTpunkt ; !22:00 - 22:19
   @U.TIME.11@22:@U.TIME.11@            BEFpunkt ; !22:19 - 22:59
   @U.TIME.11@23:@U.TIME.11@            AFTpunkt ; !23:00 - 23:19
   @U.TIME.12@23:@U.TIME.12@            BEFpunkt ; !23:19 - 23:59
 @U.TIME.12@%0%0:@U.TIME.12@            AFTpunkt ; !00:00 - 00:19
  @U.TIME.1@%0%0:@U.TIME.1@             BEFpunkt ; !00:19 - 00:59
             24%:%0%0:guoktenuppelohkái #        ; !24:00
 @U.TIME.12@%0%0:@U.TIME.12@            AFTpunkt ; !00:00 - 00:19
  @U.TIME.1@%0%0:@U.TIME.1@             BEFpunkt ; !00:19 - 00:59


LEXICON BEFpunkt
         %:: BEF ; ! +Use/NA
 %.+Use/NA:  BEF ;

LEXICON AFTpunkt
         %:: AFT  ; ! 2:00 = göökte ! +Use/NA
 %.+Use/NA:  AFT  ; ! 2.00 = göökte
   +Use/NA:  HOUR ; ! 2 = göökte


LEXICON BEF
       2%0:logi%                 TOHALF   ;
        21:ovcci%                TOHALF   ;
        22:gávcci%               TOHALF   ;
        23:čieža%                TOHALF   ;
        24:guhtta%               TOHALF   ;
        25:vihtta%               TOHALF   ;
        26:njeallje%             TOHALF   ;
        27:golbma%               TOHALF   ;
        28:guokte%               TOHALF   ;
        29:okta%                 TOHALF   ;
 29+Use/NG:minuhta%              TOHALF   ;
       3%0:beal%                 HOUR     ;
        31:okta%                 OVERHALF ;
 31+Use/NG:minuhta%              OVERHALF ;
        32:guokte%               OVERHALF ;
        33:golbma%               OVERHALF ;
        34:njeallje%             OVERHALF ;
        35:vihtta%               OVERHALF ;
        36:guhtta%               OVERHALF ;
        37:čieža%                OVERHALF ;
        38:gávcci%               OVERHALF ;
        39:ovcci%                OVERHALF ;
       4%0:logi%                 OVERHALF ;
        41:ovccinuppelohkái%     TO       ;
        42:gávccinuppelohkái%    TO       ;
        43:čiežanuppelohkái%     TO       ;
        44:guhttanuppelohkái%    TO       ;
        45:kvárta%               TO       ;
        46:njealljenuppelohkái%  TO       ;
        47:golbmanuppelohkái%    TO       ;
        48:guoktenuppelohkái%    TO       ;
        49:oktanuppelohkái%      TO       ;
       5%0:logi%                 TO       ;
        51:ovcci%                TO       ;
        52:gávcci%               TO       ;
        53:čieža%                TO       ;
        54:guhtta%               TO       ;
        55:vihtta%               TO       ;
        56:njeallje%             TO       ;
        57:golbma%               TO       ;
        58:guokte%               TO       ;
        59:okta%                 TO       ;
 59+Use/NG:minuhta%              TO       ;

LEXICON AFT
       %0%0:                      HOUR ;
        %01:okta%                 OVER ;
 %01+Use/NG:minuhta%              OVER ;
        %02:guokte%               OVER ;
        %03:golbma%               OVER ;
        %04:njeallje%             OVER ;
        %05:vihtta%               OVER ;
        %06:guhtta%               OVER ;
        %07:čieža%                OVER ;
        %08:gávcci%               OVER ;
        %09:ovcci%                OVER ;
        1%0:logi%                 OVER ;
         11:oktanuppelohkái%      OVER ;
         12:guoktenuppelohkái%    OVER ;
         13:golbmanuppelohkái%    OVER ;
         14:njealljenuppelohkái%  OVER ;
         15:kvárta%               OVER ;
         16:guhttanuppelohkái%    OVER ;
         17:čiežanuppelohkái%     OVER ;
         18:gávccinuppelohkái%    OVER ;
         19:ovccinuppelohkái%     OVER ;

LEXICON TOHALF
 :váile% beal%  HOUR ;

LEXICON OVERHALF
 :badjel% beal%  HOUR ;

LEXICON TO
 :váile%  HOUR ;

LEXICON OVER
 :badjel%  HOUR ;

LEXICON HOUR
  NOMHOUR  ; !^N^
  !LOCHOUR ; !^L^

LEXICON NOMHOUR
   @U.TIME.1@:@U.TIME.1@okta               # ;
   @U.TIME.2@:@U.TIME.2@guokte             # ;
   @U.TIME.3@:@U.TIME.3@golbma             # ;
   @U.TIME.4@:@U.TIME.4@njeallje           # ;
   @U.TIME.5@:@U.TIME.5@vihtta             # ;
   @U.TIME.6@:@U.TIME.6@guhtta             # ;
   @U.TIME.7@:@U.TIME.7@čieža              # ;
   @U.TIME.8@:@U.TIME.8@gávcci             # ;
   @U.TIME.9@:@U.TIME.9@ovcci              # ;
  @U.TIME.10@:@U.TIME.10@logi              # ;
  @U.TIME.11@:@U.TIME.11@oktanuppelohkái   # ;
  @U.TIME.12@:@U.TIME.12@guoktenuppelohkái # ;

! Outcommented due to Oahpa. Insert again ad lib.
! LEXICON LOCHOUR
!  @U.TIME.1@:ovttas@U.TIME.1@             # ;
!  @U.TIME.2@:guovttis@U.TIME.2@           # ;
!  @U.TIME.3@:golmmas@U.TIME.3@            # ;
!  @U.TIME.4@:njealjis@U.TIME.4@           # ;
!  @U.TIME.5@:viđas@U.TIME.5@              # ;
!  @U.TIME.6@:guđas@U.TIME.6@              # ;
!  @U.TIME.7@:čiežas@U.TIME.7@             # ;
!  @U.TIME.8@:gávccis@U.TIME.8@            # ;
!  @U.TIME.9@:ovccis@U.TIME.9@             # ;
! @U.TIME.10@:logis@U.TIME.10@             # ;
! @U.TIME.11@:oktanuppelogis@U.TIME.11@    # ;
! @U.TIME.12@:guoktenuppelogis@U.TIME.12@  # ;


