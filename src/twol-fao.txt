! =================================== !
! The Faroese morphophonological file !
! =================================== !

Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 g2:g j2:j v2:v 
 
 %^UUML:0 %^IUML:0 %^eIUML:0 %^W:0 %^EPH:0 %^JI:0 %^OEA:0 
 %^GDEL:0 %^GGDEL:0 %^GVDEL:0 %^VDEL:0 %^JDEL:0 %^RDEL:0
 %^EIO:0 %^OA:0 %^WVV:0
 %^AB1:0 %^AB2:0 %^AB3:0 %^AB4:0 %^AB5:0 %^AB6:0 %^AB7:0
 %^aAB:0 %^uAB:0
 %>:0 ;

Sets

 Vow = a e i o u y æ ø å 
       á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý ;
 Cns = b c d f g h j k l m n p q r s t v w x z ð þ ;
 Nas = m n ;
 NonNas = b c d f g h j k l p q r s t v w x z ð þ ;
 Dummy = %>  %^UUML %^IUML %^eIUML %^W %^EPH %^JI ;


Rules

"Final j as i"
 j:0 <=> _ %^JI: ;
!€ beiggj%^JI%>i
!€ beigg000i

"Deleting j in exceptional stems"
 j:0 <=> _ %^JDEL: ;

!€ verkj%^JDEL%>ur
!€ verk000ur

"Deleting j in j-stems"
  j:0 <=> Vow g g _ %>: [ Cns | i ] ;
!          Vow [ Cns - j ]  _ %>: [ u r | Cns | i ] ;
!€ heyggj%>i
!€ heygg00i



! Verschärferung

"Deleting the gv Verschärferung 1"
 g:0 <=> Vow _ v: %^GVDEL:  ;

"Deleting the gv Verschärferung 2"
 v:0 <=> Vow g: _ [ %^GVDEL: | %^VDEL: ] ;

!€ búgv%^GVDEL%>s
!€ bú0000s

!€ bógv%^VDEL%>s
!€ bóg000s

!€ skógv%^GVDEL%>m
!€ skó0000m

"Deleting ggj in Genitive I"
 g:0 <=> Vow _ g: j:  %^GGDEL: %>: s ;

"Deleting ggj in Genitive II"
 g:0 <=> Vow g: _ j: [ %^GDEL: | %^GGDEL: ] %>: s ;

"Deleting ggj in Genitive III"
 j:0 <=> Vow g: g: _  [ %^GDEL: | %^GGDEL: ] %>: s ;

!€ skýggj%^GGDEL%>s
!€ ský00000s

!€ kríggj%^GDEL%>s
!€ kríg0000s

"Deleting g in gv strings"
 g:0 <=> Vow _ v: %^GDEL: %>: ;

!€ sjógv%>ar
!€ sjó0v0ar

"Deleting r in Genitive of ur stems"
 r:0 <=> u: _ %^EPH:  %^RDEL: %>: a r ;

!€ brúður%^EPH%^%RDEL%>ar
!€ brúð00000ar


"Vowel-deletion in Vowel-initial suffix"
 Vx:0 <=> _ %>: [ i n | i ð | u m | s %>: i n s | a ] ;
      where Vx in ( i a ) ;
!€ dømi%>um
!€ døm00um

!€ dømi%>ið
!€ dømi00ð

!€ grann%>a%>in
!€ grann0a00n

!€ ris%>i%>in
!€ ris0i00n

"Epenthetic deletion"
 Vx:0 <=> _ [ r | l | n ]  %^EPH: %>: ; !Vow ;
      where Vx in (a i u) ;
      
!€ akur%^EPH%>
!€ ak0r00

!€ lykil%^EPH%>
!€ lyk0l00

!€ aftan%^EPH%>
!€ aft0n00

"U-umlaut in Front of Nasal"
 a:o <=> _ Nas (Vow:) (Cns:*)  (%^EPH:) %^UUML:  ;

!€ tank%^UUML
!€ tonk0

!€ hamar%^EPH%^UUML%>um
!€ hom0r000um


"General U-umlaut"
 a:ø <=> _ NonNas:* (Nas) %^UUML: ;

!€ dag%>%^UUMLum
!€ døg00um

!€ sag%>%^UUMLa
!€ søg00a

"I-umlaut"
 Vx:Vy <=> _ Cns* %^IUML: ;
     where Vx in ( a o ø á ó ú )
	   Vy in ( e y i æ ø ý )
	   matched ;

!€ dag%^IUML%>i
!€ deg00i

!€ son%^IUML%>i
!€ syn00i

"eI-umlaut"
 Vx:Vy <=> _ Cns* %^eIUML: ;
     where Vx in ( o )
	   Vy in ( e )
	   matched ;




"j-Deletion in i-umlaut"
 j:0 <=> Cns _  [ :i | :ý | :e :y ] Cns:* %^IUML: ;

!€ fjørð%^IUML%>i
!€ f0irð00i

!€ bjóð%^IUML
!€ b0ýð0

"Inverted U-umlaut from ø"
 ø:a <=> _ Cns* (Vow: Cns) ( %^EPH: ) %^OEA: ;

!€ fløtt%^ØAa
!€ flatt0a

"Inverted U-umlaut from o"
 o:a <=> _ Cns* (Vow: Cns) (%^EPH: ) %^OA: ;

!€ fonn%^OA%>ar
!€ fann00ar


"o/ei-Umlaut I"
 e:o <=> _ i: Cns:+ %^EIO: ;

"o/ei-Umlaut II"
 i:0 <=> e: _ Cns:+ %^EIO: ;

!€ dreing%^EIO%>i
!€ dro0ng00i


"Deleting Double Consonant in Front of Consonant"
 Cx:0 <=> Cx _ %>: ( j: ) Cns  ;
	  Cx _ Vow:0 Cns Dummy:* Vow ;
      where Cx in ( b d f k l m n p r s t v ) 
      matched ;
!€ hjall%>s
!€ hjal00s

!€ summar%>i
!€ sum00r0i

!€ hjall%>ar
!€ hjall0ar

!€ himmal%^EPH%^UUML%>um
!€ him00l000um


"Deleting stem-final s in s genitive"
 s:0 <=> Vow s %>: _ ;

!€ primus%>s
!€ primu00a

"Realising j in front of vowels"
 j2:j <=> _ %>: [ a | u ] ;

!€ hylj2%>ar
!€ hylj0ar

"Vowel deletion in front of na"
 a:0 <=> _ %^W: n ;

"Special Dative v Deletion"
 v:0 <=> g _ %^IUML: %>: i ;

!€ bógv%^IUML%>i
!€ bøg000i

"Plural i Deletion in faðir"
 i:0 <=> :e ð _ r %^IUML: %>: ;

!€ faðir%^IUML%>ar
!€ feð0r00ar


! Verbal vowel alternations

"Stem vowel change in Weak Verbs"
 Vx:Vy <=> _ Cns:+ %^WVV: %>: t ; ! WVV = Verb
 	where Vx in (e y)
 		  Vy in (a u)
 		  matched ;
 		  
!€ flek%^WVV%>t
!€ flak00t 

!€ flek%^WVV%>t
!€ flak00t 



! Verbal Sandhi rules

"ð Assimilation in Front of Dental Past Suffix -d(i)"
 ð:d <=> Vow _ %>: d [ #: | i | u ] ;
 
!€ leið%>di
!€ leid0di 

"ð Assimilation in Front of Supine Suffix -t"
 ð:t <=> Vow _ %>: t #: ;

!€ leið%>t
!€ leit0t

"Adjusting Dental Past Suffix -d(i)"
 d:t <=> [ p | t | k | s ] %>: _ [ #: | i | u ] ;

!€ keyp%>di
!€ keyp0ti


"Geminate Assimilation in Past Tense"
 Cx:0 <=> Cy _ %>: [ d | t ] ;
 	where Cx in ( d ð n p g k t )
 	      Cy in ( n r n p g k l )
 	      matched ;
 	      
!€ send%>di
!€ sen00di

!€ hirð%>di
!€ hir00di
 	      

! Verbal ablaut rules

! Past singular

" Past tense singular diphthongs I"
0:Vx <=> Cns _ Vy:Vz Cns:+ %^AB1: ;
	where Vx in ( e e e )
	      Vy in ( í ó u )
	      Vz in ( i y y )
	matched ;

" Past tense singular diphthongs II"
Vy:Vz <=> Cns :Vx _  Cns:+ %^AB1: ;
	where Vx in ( e e e )
	      Vy in ( í ó u )
	      Vz in ( i y y )
	matched ;

"Past tense singular monophthongs"
Vx:Vy <=> _ Cns:+ %^AB3: [ #: | %>: i ] ;
	where Vx in ( e ø a o á i )
	      Vy in ( a a ó a æ e )
	matched ;

!€ gev%^AB2

!"Past tense singular monophthongs to ó"
!Vx:Vy <=> _ Cns:+ %>: i ;
!	where Vx in ( a á o i )
!	      Vy in ( ó ó o a )
!	matched ;

"Past tense plural monophthongs"
Vx:Vy <=> _ Cns:+ %^AB2: %>: u ;
	where Vx in ( á í ó ú i e a ø )
    	      Vy in ( ó i u u e u i u )
	matched ;

!"Past tense plural monophthongs to ó"
!Vx:ó <=> _ Cns:+ %>: u ;
!	where Vx in ( i e a o á ) ;


"Past tense plural monophthongs to a"
Vx:a <=> _ Cns:+ %^aAB:  ;
	where Vx in ( i e ø ) ;

"Past tense plural monophthongs to u"
Vx:u <=> _ Cns:+ %^uAB:  ;
	where Vx in ( i e ø ) ;

	      
"Supine u"
Vx:u <=> Cns+ _ %^AB4: %>: ;
	where Vx in ( i e ) ;


"Supine o"
Vx:o <=> Cns+ _ %^AB5: %>: ;
	where Vx in ( í ó ú ø i e a ) ;

"Supine i"
Vx:i <=> Cns+ _ %^AB6: %>: ;
	where Vx in ( í a e ) ;


 	      
 	      
! Possessive Sandhi rules

"n Deletion in Neuter"
 n:0 <=> _ %>: t t ;

"t Deletion in Neuter"
 t:o <=> r %>: _ t ;
 
