Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 a2:a a3:a ó2:ó u2:u i2:i
! j2:j j2:0
 
 %^U:0 %^W:0
 %>:0 ;

Sets

 Vow = a e i o u y æ ø å a2 a3 ó2 i2 u2 
       á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý ;
 Cns = b c d f g h j j2 k l m n p q r s t v w x z ð þ ;
 Dummy = %> ;

Rules

"Deleting the gv Verschärferung 1"
 g:0 <=> Vow _ v: %>: s ;

"Deleting the gv Verschärferung 2"
 v:0 <=> Vow g: _ %>: s ;
!!búgv%>s
!!bú000s

"Deleting j in j-stems"
  j:0 <=> Vow g g _ %>: [ Cns | i ] ;
!          Vow [ Cns - j ]  _ %>: [ u r | Cns | i ] ;
!!heyggj%>i
!!heygg00i


!"Deleting Vowel in Front of -um"
! i:0 <=> _ %>: [ u m | s %>: i n s | a ] ;
!!dømi%>um
!!døm00um

"Vowel-deletion in Vowel-initial suffix"
 i:0 <=> Vow %>: _ [ n | ð | # ] ;
	 %>: Vow _ [ n | ð | # ] ;
         _ %>: [ u m | s %>: i n s | a ] ;
!!dømi%>ið
!!dømi00ð
!!grann%>a


"Epenthetic deletion"
 Vx:0 <=> _ [ r | l | n ]  %>: Vow ;
      where Vx in (a2 i2 u2);
!!aku2r%>a
!!ak0r0a
!!lyki2l%>a
!!lyk0l0a
!!afta2n%>a
!!aft0n0a

"U-umlaut"
 a3:ø <=> _ Cns* %>:  %^U:  ;
!!da3g%>%^Uum
!!døg00um
!!sa3g%>%^Ua
!!søg00a

"I-umlaut"
 a3:e <=> _ Cns* %>: i ( %>: | #: ) ;
!!da3g%>i
!!deg%>i

"Deleting Double Consonant in Front of Consonant"
 Cx:0 <=> Cx _ %>: ( j: ) Cns  ;
          Cx _ Vow:0 Cns %>: Vow ;
      where Cx in ( b d f k l m n p r s t v ) ;
      matched ;
!! hjall%>s
!! hjal00s
!!summa3r%>i
!!sum00r0i
!!hjall%>ar
!!hjall0ar

"Realising j in front of vowels"
 j2:j <=> _ %>: [ a | u ] ;
!!hylj2%>ar
!!hylj0ar

"Vowel deletion in front of -na"
 a:0 <=> _ %^W n ;