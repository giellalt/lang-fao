



! =============== !
! Verb morphology !
! =============== !


LEXICON s1  ! nevna
standard-ir ;  ! Verbal forms
:%>d l7 ;      ! Adjectival forms

LEXICON s2  ! keypa
standard-ir ;
:%>d l39 ;

LEXICON s3  ! leiða
standard-ir ;
:%>d l18 ;

LEXICON s4  ! frøa
inf ;
pres-ir ;
prt-dd ;
imp-prsptc ;
sup-tt ;
:%>d l7 ;

LEXICON s5  ! senda
standard-ir ;
:%>d l7 ;

LEXICON s6  ! hirða
standard-ir ;
:%>d l7 ;

LEXICON s7  ! rista
standard-ir ;
:%>d l5 ;

LEXICON s8  ! kenna
standard-ir ;
:%>d l7 ;

LEXICON s9  ! klippa
standard-ir ;
:%>d l5 ;

LEXICON s10 ! fylgja
standard-j-ir ;
:%>d l7 ;

LEXICON s11 ! roykja
standard-j-ir ;
:%>d l5 ;

LEXICON s12 ! boyggja
standard-j-ir ;
:%>d l7 ;

LEXICON s13 ! søkkja
standard-j-ir ;
:%>d l5 ;

LEXICON s14 ! heingja
jinf ;
pres-ir ;
:%^EIO prt-d ;
:%^EIO imp-prsptc ;
:%^EIO sup ;
:%>d l7 ;

LEXICON s15 ! skeinkja
jinf ;
pres-ir ;
:%^EIO prt-d ;
:%^EIO imp-prsptc ;
:%^EIO sup ;
:%>d l5 ;

LEXICON s16 ! flekja
jinf ;
pres-ir ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l5 ;

LEXICON s17 ! berja
jinf ;
pres-ir ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s18 ! krevja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s19 ! dylja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s20 ! leggja
:egg   jinf ;
:egg   pres-ur ;
:eg    prt-ði ; 
:øg    prt-du ;
:ag    imp-prsptc ;
:ag    sup ;
:ag%>d l8 ;

LEXICON s21 ! selja
jinf ;
:%>d l7 ;

LEXICON s22 ! ryðja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l18 ;

LEXICON s23 ! smyrja
jinf ;
pres-null ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s24 ! flysa
jinf ;
pres-null ;
:%^WVV prt-d ;
:%^WVV imp-prsptc ;
:%^WVV sup ;
:%>d l5 ;

LEXICON s25 ! liva
inf ;
pres-ir ;
prt-d ;
imp-prsptc ;
:a sup-a ;
:%>d l6 ;

LEXICON s26 ! plaga
inf ;
pres-ar ;
prt-d ;
imp-prsptc ;
sup ;
:a sup-a ;

LEXICON s27 ! spáa
inf ;
pres-ar ;
prt-dd ;
imp-prsptc ;
sup-tt ;
:%>d l18 ;

LEXICON s28 ! skaða
inf ;
pres-ar ;
prt-d ;
imp-prsptc ;
sup ;
:%>d l18 ;

LEXICON s29 ! brúka
inf ;
pres-ar ;
prt-d ;
imp-prsptc ;
sup ;
:%>t l5 ;

LEXICON s30 ! kalla
inf ;
pres-ar ;
prt-a ;
:a impsg ; !deviant kalla
imppl ;
prsptc ;
sup-a ;
:%>að l6 ;

LEXICON s31 ! only the verb gera and Xgera
:er inf ;
:er pres-null ;
:er imp-prsptc ;
:jør prt-d ;
:jør sup ;
:jørd%> l7 ;

LEXICON s32 ! búgva
:gv      inf ;
:gv      pres-strong-s1 ;
:r%^IUML pres-strong-s23 ;
:gv      pres-pl ; 
:gv      imp-prsptc ; 
         prt-a ;
:gvin  l32 ;

LEXICON s33 ! rógva
:gv      inf ;
:gv      pres-strong-s1 ;
:r%^IUML pres-strong-s23 ;
:gv      pres-pl ; 
:gv      imp-prsptc ; 
         prt-a ;
:%>gvin  l32 ;


LEXICON s34 ! goyggja
:ggj    inf ;
:r       pres-strong-s1 ;
:r       pres-strong-s23 ;
         pres-pl ; 
:ggj     imp-prsptc ; 
         prt-a ;
         sup-a ;



! Strong verbs starting here
! ==========================

LEXICON s35 ! bíta
          inf ;
          pres-ur ;
:%^AB1    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:%^VSHi        sup-a ;
+V+PrtPtc:%^VSHin    l26 ;

LEXICON s36 ! svíkja
:j        inf ;
          pres-ur ;
:%^AB1    prt-null-s ;
:%^AB2    prt-u-p ;
:j        imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;


LEXICON s37 ! bróta
          inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;


LEXICON s38 ! skjóta
          inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s39 ! bjóða
	  inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>d l26 ;

LEXICON s40 ! fúka
inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>d l26 ;

LEXICON s41 ! flúgva
inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
l26 ;

LEXICON s42 ! klúgva
inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
l26 ;

LEXICON s43 ! njóta
inf ;
          pres-strong-s1 ;
:%^IUML   pres-strong-s23 ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
l26 ;

LEXICON s44 ! finna
inf ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
l26 ;

LEXICON s45 ! binda
inf ;
          pres-ur ;
:%^aAB    prt-null-s ;
:%^uAB    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
l26 ;

LEXICON s46 ! stinga
          inf ;
          pres-ur ;
:%^aAB    prt-null-s ;
:%^uAB    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s47 ! svimja
inf ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s48 ! drekka
inf ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s49 ! detta
inf ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s50 ! røkka
          inf ;
          pres-ur ;
:%^AB3    prt-null-s ;
:%^AB2    prt-u-p ;
          imp-prsptc ;
:i        sup-a ;
:%>in     l26 ;

LEXICON s51 ! ganga
:ang       inf ;
:ang       pres-strong-s1 ;
:ong       pres-strong-s23 ; !2?
:ekk       prt-null-s ;
:ing       prt-u-p ;
           imp-prsptc ;
:ingi      sup-a ;
:ing%>in   l26 ;

LEXICON s52 ! veva
:vev   inf ;
:vev   pres-ur ;
:vav   prt-null-s ;
:vóv   prt-u-p ;
:vev   imp-prsptc ;
:vovi  sup-a ;
:vovin l26 ;

LEXICON s53 ! leypa
:leyp   inf ;
:loyp   pres-strong-s1 ;
:loyp   pres-strong-s23 ;
:leyp   prt-null-s ;
:leyp   imp-prsptc ;
:lup    prt-u-p ;
:lopi   sup-a ;
:lopin  l26 ;

LEXICON s54 ! bera
:ber       inf ;
:ber       pres-null;
:bar       prt-null-s ;
:bór       prt-u-p ;
:ber       imp-prsptc ;
:bóri      sup-a ;
:bór%>in   l26 ;

LEXICON s55 ! fara
:far       inf ;
:fer       pres-null ;
:fór       prt-null-s ;
:fór       prt-u-p ;
:fari      sup-a ;
:far       imp-prsptc ;
:far%>in   l26 ;

LEXICON s56 ! geva
:gev       inf ;
:gev       pres-ur ;
:gav       prt-null-s ;
:góv       prt-u-p ;
:givi      sup-a ;
:gev       imp-prsptc ;
:giv%>in   l26 ;

LEXICON s57 ! sita
:sit      inf ;
:sit      pres-ur ;
:sat      prt-null-s ;
:sót      prt-u-p ;
:siti     sup-a ;
:sit      imp-prsptc ;
:sit%>in  l26 ;

LEXICON s58 ! mala
:mal      inf ;
:mel      pres-strong-s1 ;
:mel      pres-strong-s23 ;
:mól      prt-null-s ;
:mól      prt-u-p ;
:mal      imp-prsptc ;
:mali     sup-a ;
:mal%>in  l26 ;

LEXICON s59 ! stjala
:stjal    inf ;
:stjel    pres-strong-s1 ;
:stjel    pres-strong-s23 ;
:stjól    prt-null-s ;
:stjól    prt-u-p ;
:stoli    sup-a ;
:stjel    imp-prsptc ;
:stol%>in     l26 ;

LEXICON s60 ! taka, aka
:ak       inf ;
:ek       pres-strong-s1 ;
:ek       pres-strong-s23 ;
:ók       prt-null-s ;
:ók       prt-u-p ;
:ak       imp-prsptc ;
:iki      sup-a ;
:ik%>in   l26 ;

LEXICON s61 ! halda
:hald     inf ;
:held     pres-strong-s1 ;
:held     pres-strong-s23 ;
:helt     prt-null-s ;
:hild     prt-u-p ;
:hildi    sup-a ;
:hald     imp-prsptc ;
:hild%>in l26 ;

LEXICON s62 ! sova
:sov      inf ;
:svev     pres-strong-s1 ;
:svev     pres-strong-s23 ;
:svav     prt-null-s ;
:svóv     prt-u-p ;
:sovi     sup-a ;
:sov      imp-prsptc ;
:%>in     l26 ;

LEXICON s63 ! koma
:kom      inf ;
:kom      pres-strong-s1 ;
:kem      pres-strong-s23 ;
:kom      prt-null-s ;
:kom      prt-u-p ;
:komi     sup-a ;
:kom      imp-prsptc ;
:kom%>in  l26 ;

LEXICON s64 ! láta
:lát      inf ;
:lát      pres-strong-s1 ;
:let      pres-strong-s23 ;
:læt      prt-null-s ;
:lót      prt-u-p ;
:láti     sup-a ;
:lát      imp-prsptc ;
:lát%>in  l26 ;

LEXICON s65 ! standa
:stand    inf ;
:stand    pres-strong-s1 ;
:stend    pres-strong-s23 ;
:stóð     prt-null-s ;
:stóð     prt-u-p ;
:staði    sup-a ;
:stand    imp-prsptc ;


LEXICON s66 ! biðja
:bið      jinf ;
:bið      pres-ur ;
:bað      prt-null-s ;
:bóð      prt-u-p ;
:biði     sup-a ;
:bið%>in     l26 ;

LEXICON s67 ! draga
:drag     inf ;
:drag     pres-strong-s1 ;
:dreg     pres-strong-s23 ;
:dró      prt-null-s ;
:dróg     prt-u-p ;
:drigi    sup-a ;
:drag     imp-prsptc ;
:drig%>in l26 ;

LEXICON s68 ! hvørva
:hvørv  inf ;
:hvørv  pres-strong-s1 ;
:hvørv  pres-strong-s23 ;
:hvarv  prt-null-s ;
:hvurv  prt-u-p ;
:hvorvi sup-a ;
:horvi  sup-a ;
:hvørv  imp-prsptc ;
:hvorv%>in  l26 ;
:hvorv%>in  l26 ;

LEXICON s69 ! sláa
:slá     inf ;
:slá     pres-strong-s1 ;
:slær    pres-strong-s23-t0 ; 
:sló     prt-null-s ;
:slerdi  prt-null-s ;
:slóg    prt-u-p ;
:slerd   prt-u-p ;
:sligi   sup-a ;
:sligin  l26 ;

LEXICON s70 ! siga
:sig        inf ;
:sig        pres-ur ;
:segði      prt-null-s ;
:søgð       prt-u-p ;
:søgd	    prt-u-p ; !XXX SUB?
:sig        imp-prsptc ;
:sag        sup ;
:sagd%>     l8 ;

LEXICON s71 ! skerja
:sker   jinf ;
:sker   pres-ir ;
:sker   prt-d ;
:skert  sup-a ;
:sker   imp-prsptc ;
:skerd  l7 ;

LEXICON s72 ! eta
:et       inf ;
:et       pres-ur ;
:át       prt-null-s ;
:ót       prt-u-p ;
:eti      sup-a ;
:et       imp-prsptc ;
:et%>in   l26 ;

LEXICON s73 ! læa
:læ    inf ;
:lær   pres-ur ;
:læ    prt-d ;
:ló    prt-null-s ;
:læð   sup-a ;
:læið  sup-a ;
:læ    imp-prsptc ;
:læin  l26 ; !?

LEXICON s74 ! grindast
reflinf ;
pres-ast ;


LEXICON s75 ! balast
reflinf ;


LEXICON s76 ! ræðast
reflinf ;


LEXICON s77 ! minnast
reflinf ;


LEXICON s78 ! farast
reflinf ;


LEXICON s79 ! skjótast
reflinf ;


LEXICON s80 ! trivast
reflinf ;


LEXICON s81 ! kíkjast
reflinf ;


LEXICON s82 ! f´ylast
reflinf ;



! Split lexica
! ============

LEXICON s1/26
s1 ;
s26 ;

LEXICON s1/30
s1 ;
s30 ;

LEXICON s11/15
s11 ;
s15 ;

LEXICON s15/11
s15 ;
s11 ;

LEXICON s18/16
s18 ;
s16 ;

LEXICON s19/30
s19 ;
s30 ;

LEXICON s2/1
s2 ;
s1 ;

LEXICON s2/30
s2 ;
s30 ;

LEXICON s21/11
s21 ;
s11 ;

LEXICON s21/18
s21 ;
s18 ;

LEXICON s24/2
s24 ;
s2 ;

LEXICON s25/1
s25 ;
s1 ;

LEXICON s27/30
s27 ;
s30 ;

LEXICON s28/30
s28 ;
s30 ;

LEXICON s29/2
s29 ;
s2 ;

LEXICON s29/30
s29 ;
s30 ;

LEXICON s3/30
s3 ;
s30 ;

LEXICON s30/1
s30 ;
s1 ;

LEXICON s30/11
s30 ;
s11 ;

LEXICON s30/19
s30 ;
s19 ;

LEXICON s30/2
s30 ;
s2 ;

LEXICON s30/22
s30 ;
s22 ;

LEXICON s30/27
s30 ;
s27 ;

LEXICON s30/28
s30 ;
s28 ;

LEXICON s30/29
s30 ;
s29 ;

LEXICON s30/40
s30 ;
s40 ;

LEXICON s30/5
s30 ;
s5 ;

LEXICON s30/6
s30 ;
s6 ;

LEXICON s30/9
s30 ;
s9 ;

LEXICON s36/2
s36 ;
s2 ;

LEXICON s39/30
s30 ;
s39 ;

LEXICON s47/19
s47 ;
s19 ;

LEXICON s49/52
s49 ;
s52 ;

LEXICON s49/9
s49 ;
s9 ;

LEXICON s5/30
s5 ;
s30 ;

LEXICON s52/30
s52 ;
s30 ;

LEXICON s6/30
s6 ;
s30 ;

LEXICON s60/30
s60 ;
s30 ;

LEXICON s7/49
s7 ;
s49 ;

LEXICON s74/75
s74 ;
s75 ;

LEXICON s77/74
s77 ;
s74 ;

LEXICON s8/30
s8 ;
s30 ;

LEXICON s9/30
s9 ;
s30 ;





! Intermediate lexicon groups
! ===========================

LEXICON standard-ir
 inf ;
 ir-verb ;

LEXICON standard-j-ir
jinf ;
ir-verb ;

LEXICON ir-verb
pres-ir ;
prt-d ;
imp-prsptc ;
sup ;


! Suffix lexica
! =============

! Infinitive

LEXICON jinf
+V+Inf:%>ja # ;
+V+Inf+Pass:%>jast #;

LEXICON inf
+V+Inf:%>a # ;
+V+Inf+Pass:%>ast # ;

LEXICON reflinf
+V+Inf+Pass:%>ast # ;

! Present

LEXICON pres-ir
+V+Ind+Prs+1Sg:%>i  # ;
+V+Ind+Prs+2Sg:%>ir # ;
+V+Ind+Prs+3Sg:%>ir # ;
pres-pl ;

LEXICON pres-ar
+V+Ind+Prs+1Sg:%>a # ;
+V+Ind+Prs+2Sg:%>ar # ;
+V+Ind+Prs+3Sg:%>ar # ;
pres-pl ;

LEXICON pres-ur
+V+Ind+Prs+1Sg:%>i # ;
+V+Ind+Prs+2Sg:%>ur # ;
+V+Ind+Prs+3Sg:%>ur # ;
pres-pl ;

LEXICON pres-null
+V+Ind+Prs+1Sg:%> # ;
+V+Ind+Prs+2Sg:%> # ;
+V+Ind+Prs+3Sg:%> # ;
pres-pl ;

LEXICON pres-strong-s1
+V+Ind+Prs+1Sg:%>i  # ;
!pres-pl ;

LEXICON pres-strong-s23
+V+Ind+Prs+2Sg:%>ur # ;
+V+Ind+Prs+3Sg:%>ur # ;
!pres-pl ;

LEXICON pres-strong-s23-t0
+V+Ind+Prs+2Sg:%>t # ;
+V+Ind+Prs+3Sg:%> # ;
!pres-pl ;

LEXICON pres-pl
+V+Ind+Prs+Pl:%>a # ;

LEXICON pres-ast
+V+Ind+Prs+1Sg:%> # ;
+V+Ind+Prs+2Sg:%>ast # ;
+V+Ind+Prs+3Sg:%>ast # ;
+V+Ind+Prs+Pl:%>ast # ;



! Preterite

LEXICON prt-d
 prt-di ;
 prt-du ;
 
LEXICON prt-di 
+V+Ind+Prt+Sg:%>di # ;

LEXICON prt-ði 
+V+Ind+Prt+Sg:%>ði # ;

LEXICON prt-du
+V+Ind+Prt+Pl:%>du # ;

!LEXICON prt-ðu
!+V+Ind+Prt+Pl:%>ðu # ;

LEXICON prt-dd
+V+Ind+Prt+Sg:%>ddi # ;
+V+Ind+Prt+Pl:%>ddu # ;

LEXICON prt-a
+V+Ind+Prt+Sg:%>aði # ;
+V+Ind+Prt+Pl:%>aðu # ;

LEXICON prt-null-s
+V+Ind+Prt+Sg: # ;

LEXICON prt-u-p
+V+Ind+Prt+Pl:%>u # ;



! Imperative and present participle

LEXICON imp-prsptc
imp ;
prsptc ;

LEXICON imp
impsg ;
imppl ;

LEXICON impsg
+V+Imp+Sg: # ;

LEXICON imppl
+V+Imp+Pl:%^JI%>ið # ;

LEXICON prsptc
+V+PrsPtc:%>andi # ;

 
! Supine and preterite participle
 
LEXICON sup
+V+Sup:%>t # ;
+V+PrtPtc:%>dur # ;

LEXICON sup-tt
+V+Sup:%>tt # ;
+V+PrtPtc:%>ddur # ;

LEXICON sup-a ! kalla
+V+Sup:%>ð # ;
+V+PrtPtc:%>ður # ;



! Ad hoc, irregular
! =================

LEXICON BLÍVA
+Inf:blíva # ;
+Ind+Prs+1Sg:blívi # ;
+Ind+Prs+2Sg:blívur # ;
+Ind+Prs+3Sg:blívur # ;
+Ind+Prs+Pl:blíva # ;
+Ind+Prt+1Sg:bleiv # ;
+Ind+Prt+2Sg:bleivst # ;
+Ind+Prt+3Sg:bleiv # ;
+Ind+Prt+Pl:blivu # ;
+Imp+Sg:blív # ;
+Imp+Pl:blívið # ;
+PrsPtc:blívandi # ;
+PrtPtc:blívin # ;
+Sup:blívið # ;

LEXICON EIGA
+Inf:eiga # ;
+Ind+Prs+1Sg:eigi # ;
+Ind+Prs+2Sg:eigur # ;
+Ind+Prs+2Sg:eigur # ;
+Ind+Prt+Sg:átti # ;
+Ind+Prt+Pl:áttu # ;
+PrsPtc:eigandi # ;
+PrtPtc:eigin # ;
+Sup:eigið  # ;

LEXICON HAVA
+Inf:hava # ;
+Ind+Prs+1Sg:havi # ;
+Ind+Prs+2Sg:hevur # ;
+Ind+Prs+3Sg:hevur # ;
+Ind+Prs+Pl:hava # ;
+Ind+Prt+Sg:hevði # ;
+Ind+Prt+Pl:høvdu # ;
+Imp+Sg:hav # ;
+Imp+Pl:havið # ;
+PrsPtc:havandi # ;
+PrtPtc:havdur # ;
+Sup:havt # ;

LEXICON KUNNA
+Inf:kunna # ;
+Ind+Prs+1Sg:kann # ;
+Ind+Prs+2Sg:kanst # ;
+Ind+Prs+3Sg:kann # ;
+Ind+Prs+Pl:kunnu # ;
+Ind+Prs+Pl:kunna # ;
+Ind+Prt+Sg:kundi # ;
+Ind+Prt+Pl:kundu # ;
+Sup:kunnað # ;

LEXICON MEGA
+Inf:mega # ;
+Ind+Prs+1Sg:má # ;
+Ind+Prs+2Sg:mást # ;
+Ind+Prs+3Sg:má # ;
+Ind+Prs+Pl:mugu # ;
+Ind+Prs+Pl:muga # ;
+Ind+Prt+Sg:mátti # ;
+Ind+Prt+Pl:máttu # ;

LEXICON MUNNA
+Inf:munna # ;
+Ind+Prs+1Sg:man # ;
+Ind+Prs+2Sg:manst # ;
+Ind+Prs+3Sg:man # ;
+Ind+Prs+Pl:munnu # ;
+Ind+Prs+Pl:munna # ;
+Ind+Prt+Sg:mundi # ;
+Ind+Prt+Pl:mundu # ;
+Sup:munnað # ;

LEXICON SKULA
+Inf:skula # ;
+Ind+Prs+1Sg:skal # ;
+Ind+Prs+2Sg:skalt # ;
+Ind+Prs+3Sg:skal # ;
+Ind+Prs+Pl:skulu # ;
+Ind+Prs+Pl:skula # ;
+Ind+Prt+Sg:skuldi # ;
+Ind+Prt+Pl:skuldu # ;
+Sup:skulað # ;

LEXICON VERA
+Inf:vera # ;
+Ind+Prs+1Sg:eri # ;
+Ind+Prs+2Sg:ert # ;
+Ind+Prs+3Sg:er # ;
+Ind+Prs+Pl:eru # ;
+Ind+Prt+1Sg:var # ;
+Ind+Prt+2Sg:varst # ;
+Ind+Prt+3Sg:var # ;
+Ind+Prt+Pl:vóru # ;
+Sup:verið # ;

LEXICON VERÐA
+Inf:verða # ;
+Ind+Prs+1Sg:verði # ;
+Ind+Prs+2Sg:verður # ;
+Ind+Prs+3Sg:verður # ;
+Ind+Prs+Pl:verða # ;
+Ind+Prt+3Sg:varð # ;
+Ind+Prt+3Sg:varðst # ;
+Ind+Prt+3Sg:vórð # ;
+Ind+Prt+Pl:vórðu # ;
+Imp+Sg:verð # ;
+Sbj+3Sg:verði # ; !XXX verði ljós
+Imp+Pl:verðið # ;
+PrsPtc:verðandi # ;
+PrtPtc:vorðin # ;

LEXICON VILJA
+Inf:vilja # ;
+Ind+Prs+1Sg:vil # ;
+Ind+Prs+2Sg:vilt # ;
+Ind+Prs+3Sg:vil # ;
+Ind+Prs+Pl:vilja # ;
+Ind+Prt+Sg:vildi # ;
+Ind+Prt+Pl:vildu # ;
+Sup:viljað # ;

LEXICON VITA
+Inf:vita # ;
+Ind+Prs+1Sg:veit # ;
+Ind+Prs+2Sg:veitst # ;
+Ind+Prs+3Sg:veit # ;
+Ind+Prs+Pl:vita # ;
+Ind+Prt+Sg:visti # ;
+Ind+Prt+Pl:vistu # ;
+Sup:vitað # ;


LEXICON SÍGGJA
+Inf:síggja # ;
+Ind+Prs+1Sg:síggi # ;
+Ind+Prs+2Sg:sært # ;
+Ind+Prs+3Sg:sær # ;
+Ind+Prs+Pl:síggja # ;
+Ind+Prt+1Sg:sá # ;
+Ind+Prt+2Sg:sást # ;
+Ind+Prt+3Sg:sá # ;
+Ind+Prt+Pl:sóu # ;
+Imp+Sg:síggj # ;
+Imp+Sg:sí # ;
+Imp+Pl:síggið # ;
+PrsPrc:síggjandi # ;
+PrfPrc:sæddur # ;
+Sup:sæð # ;
+Sup:sætt # ;

LEXICON FÁA
+Inf:fáa # ;
+Ind+Prs+1Sg:fái # ;
+Ind+Prs+2Sg:fært # ;
+Ind+Prs+3Sg:fær # ;
+Ind+Prs+Pl:fáa # ;
+Ind+Prt+1Sg:fekk # ;
+Ind+Prt+2Sg:fekst # ;
+Ind+Prt+3Sg:fekk # ;
+Ind+Prt+Pl:fingu # ;
+Imp+Sg:fá # ;
+Imp+Pl:fáið # ;
+PrfPrc:fingin # ;
+Sup:fingið # ;

!LEXICON 
!+Inf:
!+Ind+Prs+1Sg:
!+Ind+Prs+2Sg:
!+Ind+Prs+3Sg:
!+Ind+Prs+Pl:
!+Ind+Prt+Sg:
!+Ind+Prt+Pl:
!+Imp+Sg:
!+Imp+Pl:
!+PrsPrc:
!+PrfPrc:
!+Sup:
