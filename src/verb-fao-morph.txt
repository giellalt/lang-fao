



! =============== !
! Verb morphology !
! =============== !


LEXICON s1  ! nevna
standard-ir ;
:%>d l7 ;

LEXICON s2  ! keypa
standard-ir ;
:%>d l39 ;

LEXICON s3  ! leiða
standard-ir ;
:%>d l18 ;

LEXICON s4  ! frøa
inf ;
pres-ir ;
prt-dd ;
imp-prsprt ;
sup-tt ;
:%>d l7 ;

LEXICON s5  ! senda
standard-ir ;
:%>d l7 ;

LEXICON s6  ! hirða
standard-ir ;
:%>d l7 ;

LEXICON s7  ! rista
standard-ir ;
:%>d l5 ;

LEXICON s8  ! kenna
standard-ir ;
:%>d l7 ;

LEXICON s9  ! klippa
standard-ir ;
:%>d l5 ;

LEXICON s10 ! fylgja
standard-j-ir ;
:%>d l7 ;

LEXICON s11 ! roykja
standard-j-ir ;
:%>d l5 ;

LEXICON s12 ! boyggja
standard-j-ir ;
:%>d l7 ;

LEXICON s13 ! søkkja
standard-j-ir ;
:%>d l5 ;

LEXICON s14 ! heingja
jinf ;
pres-ir ;
:%^EIO prt-d ;
:%^EIO imp-prsprt ;
:%^EIO sup ;
:%>d l7 ;

LEXICON s15 ! skeinkja
jinf ;
pres-ir ;
:%^EIO prt-d ;
:%^EIO imp-prsprt ;
:%^EIO sup ;
:%>d l5 ;

LEXICON s16 ! flekja
jinf ;
pres-ir ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l5 ;

LEXICON s17 ! berja
jinf ;
pres-ir ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s18 ! krevja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s19 ! dylja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s20 ! leggja
:%>d l8 ;

LEXICON s21 ! selja
:%>d l7 ;

LEXICON s22 ! ryðja
jinf ;
pres-ur ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l18 ;

LEXICON s23 ! smyrja
jinf ;
pres-null ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l7 ;

LEXICON s24 ! flysa
jinf ;
pres-null ;
:%^WVV prt-d ;
:%^WVV imp-prsprt ;
:%^WVV sup ;
:%>d l5 ;

LEXICON s25 ! liva
inf ;
pres-ir ;
prt-d ;
imp-prsprt ;
:a sup-a ;
:%>d l6 ;

LEXICON s26 ! plaga
inf ;
pres-ar ;
prt-d ;
imp-prsprt ;
sup ;
:a sup-a ;

LEXICON s27 ! spáa
inf ;
pres-ar ;
prt-dd ;
imp-prsprt ;
sup-tt ;
:%>d l18 ;

LEXICON s28 ! skaða
inf ;
pres-ar ;
prt-d ;
imp-prsprt ;
sup ;
:%>d l18 ;

LEXICON s29 ! brúka
inf ;
pres-ar ;
prt-d ;
imp-prsprt ;
sup ;
:%>d l5 ;

LEXICON s30 ! kalla
inf ;
pres-ar ;
prt-a ;
:a impsg ; !deviant kalla
imppl ;
prsptc ;
sup-a ;
:%>d l5 ;

LEXICON s31 ! only the verb gera and Xgera
:er inf ;
:er pres-null ;
:jør prt-d ;
:%>d l7 ;

LEXICON s32 ! búgva
:%>d l32 ;

LEXICON s33 ! rógva
:%>d l32 ;

LEXICON s34 ! goyggja
:j inf ;
# ;

LEXICON s35 ! bíta
inf ;
:%>d l26 ;

LEXICON s36 ! svíkja
inf ;
:%>d l26 ;

LEXICON s37 ! bróta
inf ;
:%>d l26 ;

LEXICON s38 ! skjóta
inf ;
:%>d l26 ;

LEXICON s39 ! bjóða
inf ;
:%>d l26 ;

LEXICON s40 ! fúka
inf ;
:%>d l26 ;

LEXICON s41 ! flúgva
inf ;
l26 ;

LEXICON s42 ! klúgva
inf ;
l26 ;

LEXICON s43 ! njóta
inf ;
l26 ;

LEXICON s44 ! finna
inf ;
l26 ;

LEXICON s45 ! binda
inf ;
l26 ;

LEXICON s46 ! stinga
inf ;
l26 ;

LEXICON s47 ! svimja
inf ;
l26 ;

LEXICON s48 ! drekka
inf ;
l26 ;

LEXICON s49 ! detta
inf ;
l26 ;

LEXICON s50 ! røkka
inf ;
l26 ;

LEXICON s51 ! ganga
inf ;
l26 ;

LEXICON s52 ! veva
inf ;
l26 ;

LEXICON s53 ! leypa
inf ;
l26 ;

LEXICON s54 ! bera
inf ;
l26 ;

LEXICON s55 ! fara
inf ;
l26 ;

LEXICON s56 ! geva
inf ;
l26 ;

LEXICON s57 ! sita
inf ;
l26 ;

LEXICON s58 ! mala
inf ;
l26 ;

LEXICON s59 ! stjala
inf ;
l26 ;

LEXICON s60 ! taka
inf ;
l26 ;

LEXICON s61 ! halda
inf ;
l26 ;

LEXICON s62 ! sova
inf ;
l26 ;

LEXICON s63 ! koma
inf ;
l26 ;

LEXICON s64 ! láta
inf ;
l26 ;

LEXICON s65 ! standa
inf ;
l26 ;

LEXICON s66 ! biðja
inf ;
l26 ;

LEXICON s67 ! draga
inf ;
l26 ;

LEXICON s68 ! hvørva
inf ;
l26 ;

LEXICON s69 ! sláa
inf ;
l26 ;

LEXICON s70 ! siga
inf ;
l26 ;

LEXICON s71 ! skerja
inf ;
l26 ;

LEXICON s72 ! eta
inf ;
l26 ;

LEXICON s73 ! læa
inf ;
l26 ;

LEXICON s74 ! grindast
reflinf ;


LEXICON s75 ! balast
reflinf ;


LEXICON s76 ! ræðast
reflinf ;


LEXICON s77 ! minnast
reflinf ;


LEXICON s78 ! farast
reflinf ;


LEXICON s79 ! skjótast
reflinf ;


LEXICON s80 ! trivast
reflinf ;


LEXICON s81 ! kíkjast
reflinf ;


LEXICON s82 ! f´ylast
reflinf ;



! Split lexica
! ============

LEXICON s1/26
s1 ;
s26 ;

LEXICON s1/30
s1 ;
s30 ;

LEXICON s11/15
s11 ;
s15 ;

LEXICON s15/11
s15 ;
s11 ;

LEXICON s18/16
s18 ;
s16 ;

LEXICON s19/30
s19 ;
s30 ;

LEXICON s2/1
s2 ;
s1 ;

LEXICON s2/30
s2 ;
s30 ;

LEXICON s21/11
s21 ;
s11 ;

LEXICON s21/18
s21 ;
s18 ;

LEXICON s24/2
s24 ;
s2 ;

LEXICON s25/1
s25 ;
s1 ;

LEXICON s27/30
s27 ;
s30 ;

LEXICON s28/30
s28 ;
s30 ;

LEXICON s29/2
s29 ;
s2 ;

LEXICON s29/30
s29 ;
s30 ;

LEXICON s3/30
s3 ;
s30 ;

LEXICON s30/1
s30 ;
s1 ;

LEXICON s30/11
s30 ;
s11 ;

LEXICON s30/19
s30 ;
s19 ;

LEXICON s30/2
s30 ;
s2 ;

LEXICON s30/22
s30 ;
s22 ;

LEXICON s30/27
s30 ;
s27 ;

LEXICON s30/28
s30 ;
s28 ;

LEXICON s30/29
s30 ;
s29 ;

LEXICON s30/40
s30 ;
s40 ;

LEXICON s30/5
s30 ;
s5 ;

LEXICON s30/6
s30 ;
s6 ;

LEXICON s30/9
s30 ;
s9 ;

LEXICON s36/2
s36 ;
s2 ;

LEXICON s39/30
s30 ;
s39 ;

LEXICON s47/19
s47 ;
s19 ;

LEXICON s49/52
s49 ;
s52 ;

LEXICON s49/9
s49 ;
s9 ;

LEXICON s5/30
s5 ;
s30 ;

LEXICON s52/30
s52 ;
s30 ;

LEXICON s6/30
s6 ;
s30 ;

LEXICON s60/30
s60 ;
s30 ;

LEXICON s7/49
s7 ;
s49 ;

LEXICON s74/75
s74 ;
s75 ;

LEXICON s77/74
s77 ;
s74 ;

LEXICON s8/30
s8 ;
s30 ;

LEXICON s9/30
s9 ;
s30 ;





! Intermediate lexicon groups
! ===========================

LEXICON standard-ir
 inf ;
 ir-verb ;

LEXICON standard-j-ir
jinf ;
ir-verb ;

LEXICON ir-verb
pres-ir ;
prt-d ;
imp-prsprt ;
sup ;


! Suffix lexica
! =============

! Infinitive

LEXICON jinf
+V+Inf:%>ja # ;

LEXICON inf
+V+Inf:%>a # ;

LEXICON reflinf
+V+Inf:%>ast # ;

! Present

LEXICON pres-ir
+V+Ind+Prs+1Sg:%>i  # ;
+V+Ind+Prs+2Sg:%>ir # ;
+V+Ind+Prs+3Sg:%>ir # ;

LEXICON pres-ar
+V+Ind+Prs+1Sg:%>a # ;
+V+Ind+Prs+2Sg:%>ar # ;
+V+Ind+Prs+3Sg:%>ar # ;

LEXICON pres-ur
+V+Ind+Prs+1Sg:%>i # ;
+V+Ind+Prs+2Sg:%>ur # ;
+V+Ind+Prs+3Sg:%>ur # ;

LEXICON pres-null
+V+Ind+Prs+1Sg:%> # ;
+V+Ind+Prs+2Sg:%> # ;
+V+Ind+Prs+3Sg:%> # ;


! Preterite

LEXICON prt-d
+V+Ind+Prt+Sg:%>di # ;
+V+Ind+Prt+Pl:%>du # ;

LEXICON prt-dd
+V+Ind+Prt+Sg:%>ddi # ;
+V+Ind+Prt+Pl:%>ddu # ;

LEXICON prt-a
+V+Ind+Prt+Sg:%>ði # ;
+V+Ind+Prt+Pl:%>ðu # ;


! Imperative and present participle

LEXICON imp-prsprt
impsg ;
imppl ;
prsptc ;

LEXICON impsg
+V+Imp+Sg: # ;

LEXICON imppl
+V+Imp+Pl:%>ið # ;

LEXICON prsptc
+V+PrsPtc:%>andi # ;

 
! Supine and preterite participle
 
LEXICON sup
+V+Sup:%>t # ;
+V+PrtPtc:%>dur # ;

LEXICON sup-tt
+V+Sup:%>tt # ;
+V+PrtPtc:%>ddur # ;

LEXICON sup-a ! kalla
+V+Sup:%>ð # ;
+V+PrtPtc:%>ður # ;



! Ad hoc, irregular
! =================

LEXICON VERA
+V+Inf:vera # ;
+V+Ind+Prs+3Sg:er # ;
+V+Ind+Prs+Pl:eru # ;
+V+Ind+Prt+3Sg:var # ;

LEXICON VERÐA
+V+Inf:verða # ;
+V+Ind+Prs+3Sg:verður # ;
+V+Ind+Prs+Pl:verða # ;
+V+Ind+Prt+3Sg:varð # ;