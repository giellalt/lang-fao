# ===================== #
#!! # Faroese grammarchecker
# ===================== #

#!! This is work in progress. The main focus is on **ð** errors, 
#!! 

# ---------------------- #
#   Development setup:   #
# ---------------------- #
#
# cd $GTHOME/langs/fao
# ./autogen.sh
# ./configure --enable-apertium --with-hfst --enable-syntax --enable-grammarchecker --enable-tokenisers --enable-alignment --enable-reversed-intersect
# make
# cd tools/grammarcheckers
# make dev
#
# ---------------------- #
#   Then edit/test as:   #
# ---------------------- #
#
# emacs grammarchecker.cg3  # and C-c C-i / C-c C-c if you have cg-mode installed
# echo "Eg farið avstað." | sh modes/faogram.mode  # from the terminal
#
# ---------------------- #
#   Other debug-pipes:   #
# ---------------------- #
#
# ls modes/
#
# ---------------------- #

# echo "Hei." | hfst-tokenise -g ../tokenisers/tokeniser-gramcheck-gt-desc.pmhfst | vislcg3 -g disambiguator.cg3 -t | vislcg3 -g grammarchecker.cg3 -t  


#!! This file contains two parts: **Definitions** and **rules**

#!! # Definition section

# ========== #
#!! ## Delimiters 
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<¶>" sent ;



# ============= #
#!! ## Grammatical tags
# ============= #

#!! Here we declare all grammatical tags


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

LIST N = N ;
LIST V = V ;
LIST A = A ;
LIST A* = A* ;
LIST Prop = Prop ;
LIST Adv = Adv ;
LIST Num = Num ;
LIST CC = CC ;
LIST CS = CS ;
LIST Interj = Interj ;
LIST Abbr = Abbr ;
LIST ACR = ACR ;
LIST Pr = Pr ;
LIST Pron = Pron ;
LIST Pers = Pers ;
LIST Det = Det ;
LIST Dem = Dem ;
LIST Refl = Refl ;
LIST Recipr = Recipr ;
LIST Poss = Poss ;
LIST Interr = Interr ;
LIST IM = IM ;

LIST Nom = Nom ;
LIST Acc = Acc ;
LIST Gen = Gen ;
LIST Dat = Dat ;
LIST Msc = Msc ;
LIST Fem = Fem ;
LIST Neu = Neu ;
LIST Sg = Sg ;
LIST Pl = Pl ;
LIST Def = Def ;
LIST Indef = Indef ;
LIST Cmp = Cmp ;
LIST Superl = Superl ;
LIST Prs = Prs ;
LIST Prt = Prt ;
LIST 1Sg = 1Sg ;
LIST 2Sg = 2Sg ;
LIST 3Sg = 3Sg ;
LIST Inf = Inf ;
LIST PrfPtc = PrfPrc PrfPtc ;
LIST PrsPtc = PrsPrc PrfPtc ;
LIST Ind = Ind ;
LIST Imp = Imp ;
LIST Pass = Pass ;
LIST Sbj = Sbj ;
LIST Sup = Sup ;
LIST Cmpnd = Cmpnd ;
LIST Rom = Rom ;
LIST CLB = CLB ;
LIST PUNCT = PUNCT ;
LIST LEFT = LEFT ;
LIST RIGHT = RIGHT ;
LIST Guess = Guess ;

LIST Sem/Fem = Sem/Fem ;
LIST Sem/Mal = Sem/Mal ;
LIST Sem/Plc = Sem/Plc ;
LIST Sem/Sur = Sem/Sur ;
LIST Sem/Org = Sem/Org ;
LIST Sem/Veh = Sem/Veh ;
LIST Sem/Year = Sem/Year ;
LIST Sem/ID = Sem/ID ;
LIST Sem/Time-clock = Sem/Time-clock ;

LIST TAG = N V A Adv CC CS Interj Pr Pron Pers Det Refl Recipr Poss Nom Acc Gen
Dat Msc Fem Neu Sg Pl Def Indef Cmp Sup Prs Prt 1Sg 2Sg 3Sg Inf PrfPtc
PrsPtc Sup Imp CLB PUNCT LEFT RIGHT Sem/Fem Sem/Mal Sem/Plc Sem/Sur Sem/Org Sem/Veh Guess ;


LIST NAGD = Nom Acc Gen Dat ;
LIST AGD = Acc Gen Dat ;
LIST GENDER = Msc Fem Neu ;
LIST NUMBER = Sg Pl ;
SET NAGDNUMBERGENDER = (Msc Sg Nom) OR (Msc Sg Acc) OR (Msc Sg Gen) OR (Msc Sg Dat) OR (Msc Pl Nom) OR (Msc Pl Acc) OR (Msc Pl Gen) OR (Msc Pl Dat) OR (Fem Sg Nom) OR (Fem Sg Acc) OR (Fem Sg Gen) OR (Fem Sg Dat) OR (Fem Pl Nom) OR (Fem Pl Acc) OR (Fem Pl Gen) OR (Fem Pl Dat) OR (Neu Sg Nom) OR (Neu Sg Acc) OR (Neu Sg Gen) OR (Neu Sg Dat) OR (Neu Pl Nom) OR (Neu Pl Acc) OR (Neu Pl Gen) OR (Neu Pl Dat) ;


# Sets
# ====

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;


# Noun sets


SET NounMscFem = (N Msc) OR (N Fem) ;
SET NounMscNeu = (N Msc) OR (N Neu) ;
SET NounFemNeu = (N Fem) OR (N Neu) ;

SET MscFem = Msc OR Fem ;
SET MscNeu = Msc OR Neu ;
SET FemNeu = Fem OR Neu ;

LIST 3PERS = "hann" "hon" "tað" ;

# Adjective sets

SET REALADJ = A OR A* ;


# Nominal sets

LIST NOMINALHEAD = N Num Pers Refl Recipr "Pron Interr" "Pron Indef" ;

LIST NorA = N A ;

LIST PRENAGR = Det A ;

LIST MIDJA = "miðja" ; # in the middle of - construction. More N in here, also bottle constr.

SET NOT-ACC = TAG - Acc ;


# Verb sets

LIST COPULA = "vera" "verða" ;

LIST HAVA = "hava" ;

LIST MODV = "kunna" "láta" "skula" "vilja" "munna" "mega" ;

SET AUX = COPULA OR HAVA OR MODV ;

LIST VFIN = Prs Prt Imp Sbj ;
LIST INDSBJ = Ind Sbj ;
#LIST Ind = Prs Prt ;

LIST NONTHIRDV = 1Sg 2Sg 1Pl 2Pl ;

LIST NOT-AD-SUP = "afturgjalda" "aka" "ákæra" "ala" "áleggja" "algilda" "allýsa" "almannakunngera" "aloyða" "ama" "ámæla" "áminna" "ana" "andkveikja" "andøva" "andrógva" "andrøða" "andstevna" "annleggja" "æra" "arbeiða" "árfesta" "ærumeiða" "æruskemma" "áseta" "átaka" "atkvøða" "ættgreina" "ættleiða" "avdøma" "avegna" "avferma" "avgera" "avgeva" "avgreiða" "avhanga" "avhenda" "avhoyra" "avhylja" "ávísa" "avláta" "avlætta" "avlesa" "avloysa" "avlýsa" "avmøða" "avoyða" "avreiða" "avrøkja" "avsiga" "avskapa" "avskøða" "avtaka" "avtapa" "baga" "bakbinda" "bakbíta" "bandtaka" "bannføra" "bannlýsa" "bannseta" "basa" "bæsa" "bata" "bátsrógva" "begrava" "begynna" "behalda" 
"beina" "beinbróta" "beinfrysta" "beinskerja" "beita" "beiða" "bekenna" "belgja" "benda" "bera" "bergtaka" "berja" "bertelva" "besøkja" "bestilla" "betala" "betjena" "betýða" "bevísa" "bila" "bíleggja" "bína" "binda" "birgja" "birta" "bíta" "biðja" "bjóða" "blása" "blaðfesta" "blaðskoyta" "bleikja" "blíva" "blómuprýða" "bloyta" "bløða" "blóðræna" "blóðtroyta" "bóghefta" "bókføra" "bøkja" "bøla" "bølbiðja" "bólføra" "bønhoyra" "borgseta" "borðklæða" "borðreiða" "bøta" "botnfrysta" "botngrava" "botnloysa" "botnseta" "botnvelta" "boyggja" "boðsenda" "brævatkvøða" "bræða" "bráðdoyggja" "bráðdoypa" "bráðdrepa" "bráðfremja" "bráðfrysta" "bráðhvessa" "bráðhvørva" "bráðkøla" "bráðsnúgva" "bráðvaksa" "bráðvenda" "breiða" "brenna" "brennimerkja" "bresta" "bretta" "breyðføða" 
"bróta" "broyta" "brúka" "brýna" "brynna" "bryðja" "búgva" "burda" "burturflyta" "burtursíggja" "búseta" "byggibúgva" "byggja" "byrgja" "býta" "dagfesta" "dagføra" "dagmerkja" "dagseta" "dæla" "dáma" "dampspræna" "danguldríva" "dátuviðgera" "deila" "deingja" "demba" "demma" "detta" "deyðrógva" "dialysuviðgera" "díkja" "dimma" "ditta" "djúpfrysta" "døggfalla" "døma" "dómfella" "doya" "doyggja" "doypa" "doyva" "doyða" "draga" "drekka" "drepa" "drifta" "dríta" "dríva" "dropadríva" "droyma" "droypa" "drúpa" "drýpa" "dustsúgva" "dvína" "dvølja" "dýkja" "dylja" "dyngja" "dýpa" "dyrgja" "dýrmeta" "dýva" "eftirbúgva" "eftirhyggja" "eftirmeta" "eftirtína" "eftirútbúgva" "egna" "eiga" "einkisvirða" "einskilja" "eira" "eita" "eiturherða" "eiðfesta" "elda" "elgja" "elta" 
"elva" "enda" "endafara" "endareisa" "endavenda" "endurbúgva" "endurdoypa" "endurferma" "endurføða" "endurgalda" "endurgera" "endurgeva" "endurgjalda" "endurgreina" "endurloysa" "endurnýta" "endurreisa" "endursenda" "endursiga" "endurskapa" "endurtaka" "endurútbúgva" "endurvinna" "enduryrkja" "enta" "erta" "erva" "eta" "evna" "eydmýkja" "eygbera" "eygfara" "eygfesta" "eygleiða" "eyglýsa" "eygna" "eyknevna" "eyðkenna" "eyðlýsa" "eyðmerkja" "eyðmýkja" "eyðræna" "eyðsýna" "fáa" "falla" "fallbenda" "fara" "fasthalda" "fastseta" "feila" "feingja" "fella" "fergja" "ferma" "festa" "fetta" "fevna" "finna" "fíntelja" "fíra" "físa" "fjala" "fjararbeiða" "fjarstýra" "flæa" 
"flagbyrgja" "flagvelta" "flakaskera" "flana" "flæsa" "fleingja" "flekja" "flenna" "fletta" "fløkja" "flórgrína" "flóta" "floyggja" "floyma" "floyta" "fløða" "flúgva" "flýggja" "flysa" "flysja" "flyta" "fnýsa" "føla" "føra" "forbinda" "forbjóða" "fordeila" "fordøma" "forføra" "forfylgja" "forganga" "forgera" "forherða" "forholva" "forhoyra" "forkeypa" "forkoma" "forláta" "førleikamenna" "forlíta" "formbroyta" "formerkja" "formgeva" "formskera" "forrenna" "forseta" "forskjóta" "forsmáa" "forsøma" "forsova" "forstanda" "forstilla" "forsvinna" "fortelja" "fortjena" "forveita" "forvinna" "forvíta" "fossfella" "foykja" "foysa" "føða" "frámerkja" "framføra" "framganga" "framleingja" "framleiða" "framsenda" "fræsa" "frætta" "frelsa" "fremja" "frídøma" "frífinna" "fríganga" "frígeva" "fríkenna" "friðlýsa" "frøa" "frosteta" "froysa" "froyða" "frumframføra" "frumsýna" "frýsa" "frysta" "fúka" 
"fullføra" "fullgera" "fullinna" "fullkoma" "fullnýta" "fullsiga" "fygla" "fylgja" "fylkja" "fylla" "fyribúgva" "fyribyrgja" "fyridøma" "fyriganga" "fyrigera" "fyrigeva" "fyrihalda" "fyriláta" "fyriliggja" "fyritrekkja" "fyrna" "fýsa" "gáa" "gagngera" "gagnmeta" "gagnnýta" "gala" "galda" "gandbinda" "ganga" "gæra" "gæta" "geislafara" "gelda" "gella" "gera" "gerða" "geva" "gifta" "ginna" "girða" "gista" "gita" "gjala" "gjalda" "gjalla" "gjøgnumføra" "gjøgnumganga" "gjøgnumlýsa" "gjósa" "glæma" "glana" "glæsa" "glenna" "gleppa" "glerstoyta" "gletta" "gleða" "glima" "glíma" "glimma" "glíða" "gløa" "glotta" "gløtta" "gloyma" "gloypa" "gløða" "glymja" "gnella" "gnísta" "gøllhalda" "gólvseta" "gosgrava" "goyggja" "goyma" "goysa" "gøða" "góðkenna" "góðskumeta" "góðtaka" "granskufara" "grasbinda" "grasgrógva" "grasvelta" "gráta" "grava" "gravleggja" "gravseta" "greina" 
"greiða" "gremja" "gretta" "grima" "gríma" "grína" "grípa" "gróa" "grógva" "grøta" "grovhøgga" "groypa" "grøða" "gróðurseta" "grulvisvimja" "grundbroyta" "grundfesta" "grundgeva" "grundleggja" "grunnbróta" "grúvudrekka" "grynna" "grýta" "gryðja" "gyggja" "gylla" "gyrða" "gýsa" "gýta" "hábinda" "hagfesta" "hagreiða" "hagrøkja" "halda" "haldbúgva" "hálmtekja" "hálsfevna" "hálshøgga" "hálvdrepa" "hálvgráta" "hálvlæa" "hálvrenna" "hálvrópa" "hálvsiga" "hálvsova" "hámeta" "handbera" "handfara" "handtaka" "hanga" "hæra" "hárroyta" "hæsa" "hátíðarhalda" "hætta" "hava" "hávirða" "hæða" "háðflenna" "háðlæa" "hefta" "hegna" "heimsøkja" "heingja" "heita" "heitroykja" 
"heiðursmerkja" "helganga" "hella" "helseta" "heltaka" "helvtarbýta" "henda" "henvenda" "hera" "herbúgva" "herkja" "herklæða" "herleiða" "herma" "herseta" "hertaka" "hervenja" "herða" "hevna" "heygseta" "híma" "hirða" "hitta" "hjálpa" "hjartleggja" "høgga" "høla" "holva" "horva" "hótta" "høva" "hoykja" "hoyra" "hoyvenja" "hugbinda" "hugbíta" "hugfara" "hugfesta" "hugkveikja" "hugleiða" "huglyfta" "hugnema" "hugsæra" "hugtaka" "hugtøla" "hugvilla" "húka" "hundbeita" "hundfara" "hundleggja" "hundræða" "hundtaka" "hundvevja" "húðberja" "húðfleingja" "húðfletta" "hválva" "hvæsa" "hvella" "hveppa" "hvessa" "hvíla" "hvína" "hvíta" "hvøkka" "hvøkkja" "hvølva" "hvøppa" "hvørkisgera" "hvørva" "hvørviskilja" "hyggja" "hylja" "hylkja" "hylla" "hýsa" "íbirta" "íganga" "ígera" "íhylkja" "íleiða" "illflenna" "illgita" "illkenna" "illteinkja" "ímerkja" "inna" "innannøra" "innflýggja" "innflyta" "innføra" "innganga" "innibera" "innihalda" "innlesa" "innseta" "innsjóða" "inntøkuføra" "innvíga" "ísáa" "ítøkiliggera" "jarnbera" "jarnbinda" "jarðfesta" "jarðleggja" "jarðsláa" "javnbjóða" "javnflyta" "javnføra" "javnmeta" "javnseta" "javnsýna" 
"káfetta" "kala" "kaldroykja" "kantsláa" "kappbera" "kappdrekka" "kappeta" "kappfletta" "kappganga" "kappgita" "kappkoyra" "kapprenna" "kappríða" "kapprógva" "kapprøða" "kappsigla" "kappskjóta" "kappsláa" "kappsleikja" "kappstoyta" "kappsúpa" "kappsvimja" "kapptelva" "kappvelta" "kappyrkja" "kæra" "kæsa" "kavstoyta" "keikja" "kelka" "kelkja" "kemba" "kenna" "keturoykja" "keypa" "keða" "kílaskera" "kína" "kippa" "kirva" "kjaftbera" "kjaftbinda" "kjaftbróta" "kjaftsláa" "kjafttaka" "kjølfesta" "kjølseta" "kjósa" "kjøtbíta" "klártína" "klæða" "kleina" "kleingja" "klekja" "klekkja" "kleppa" "klikkja" "klína" "klípa" "klippa" "klíva" "kloykja" "kloyna" "kloyva" "klúgva" "klykkja" "knæhefta" "knæseta" "knæskjóta" "knævelta" "knella" "kneppa" "knerkja" "knípa" "knívbera" "knokkroyta" "knúgvaskerja" "knúsa" "knykkja" "knylla" "knýta" "køkja" "køla" "kolldøma" "kollganga" "kollgita" "kollrenna" "kollsigla" "kollvelta" "koma" "kondirenna" "koparklæða" "koparleggja" "koppseta" "koppsigla" "kórseta" "kortleggja" "kortmerkja" "køva" "koyra" "koyrilsfleingja" "kreingja" "kreinkja" "kreppa" "krevja" "kringlumala" "kringluvenda" "kringrenna" "kringseta" "krøkja" "krókseta" "króleggja" "kromleggja" "króseta" "krossfesta" "krosshefta" "krossklippa" 
"krossklúgva" "krossleggja" "kroysta" "krúpa" "krykja" "krýkja" "krýna" "kryvja" "kunngera" "kúra" "kúvenda" "kvala" "kvæða" "kveikja" "kveita" "kvetta" "kvinka" "kvøldseta" "kvøtta" "kvøða" "kyla" "kylja" "kylla" "kynda" "kyrkja" "kyssa" "kýta" "læa" "læga" "lagdraga" "lakafrysta" "lamdoyva" "lamleggja" "lamskerja" "lamskjóta" "lamsláa" "lána" "læna" "læra" "lærlesa" "læsa" "lata" "láta" "lætta" "lava" "leggja" "leika" "leikgera" "leingja" "leinkja" "leiva" "leiða" "leiðbeina" "leka" "lekja" "lemba" "lemja" "lenda" "leppa" "lesa" "leypa" "leysgeva" "leyskeypa" "leyvprýða" "liggja" "limtoyggja" "lissa" "listaføra" "líta" "lítilsmeta" "lítilsvirða" "litprýða" "líva" "líða" "ljóslesa" "ljóstaka" "ljóðtaka" "logagylla" "løgbjóða" "lógfesta" "løgfesta" "lóggeva" "løggilda" "lógsøkja" "løna" "loyna" "loypa" "loysa" "loyva" "løða" "luftkøla" "lúka" "lúra" "lutræna" 
"luttaka" "lútviðgera" "lyfta" "lygna" "lykkja" "lynna" "lýsa" "lysta" "lýta" "lýða" "magadraga" "magnstjala" "makaskifta" "mala" "mála" "mæla" "málbera" "málbinda" "málfara" "marknaðarføra" "markskilja" "matføða" "matgera" "máttaka" "máttsúgva" "mega" "meina" "meinbjóða" "meinsøkja" "meinstinga" "meita" "meiða" "menna" "mergstjala" "mergsúgva" "merja" "merkja" "meskja" "messingleggja" "meta" "míga" "millumlenda" "minna" "minnishalda" "minuleggja" "minuspreingja" "misbjóða" "misbrúka" "misbýta" "mishalda" "mishoyra" "miskenna" "misleggja" "misleiða" "mislesa" "mismeta" "misnýta" "misprýða" "misrøkja" "missa" "missiga" "misskifta" "misskilja" "misstíga" "mistaka" "mistrúgva" "mistýða" "misunna" "misvirða" "moldbera" "mønustinga" "møta" "mótarbeiða" "mótmæla" "motorbúgva" "mótsiga" "móttaka" "møða" "múlabinda" "múlbinda" "muna" "munnbera" 
"munnhøgga" "mýkja" "myla" "mýla" "mylja" "myndprýða" "myndskrýða" "mynna" "myrja" "myrkaleggja" "myrkja" "myrða" "náa" "naglreka" "nágreina" "næla" "náttgista" "navngeva" "navngreina" "navnkenna" "negla" "nema" "nenna" "nerta" "netfesta" "nevbinda" "nevna" "neyðbróta" "neyðdoypa" "neyðelta" "neyðgifta" "neyðhalda" "neyðlenda" "neyðrætta" "neyðroyta" "neyðskræða" "neyðtaka" "neyðysta" "níga" "nípa" "nippa" "nísta" "níta" "níva" "níða" "niðurfella" "niðurgjalda" "niðurleggja" "niðurraðfesta" "niðurseta" "njella" "njósa" "njóta" "nøra" "noyða" "nykkja" "nýmeta" "nýta" "nýútbúgva" "øga" "ognartaka" "óhvessa" "økja" "ongulfesta" "ønta" "ónýta" "óra" "óráðskemma" "ørdrepa" "órína" "órøkja" "ørskera" "ørskjóta" "ørstjala" "orðbera" "orðbýta" "orðfella" "orðfesta" "orðføra" "orðleiða" "orðleypa" "orðtaka" "øsa" "øva" "ovarbeiða" "ovdrekka" "oveta" "ovfara" "ovfylla" "ovganga" "ovgoyma" "ovkeypa" "ovlýsa" "ovmeta" "ovmøða" "ovrenna" "ovrógva" "ovsiga" 
"ovsova" "ovsteikja" "ovsúpa" "ovtaka" "oysa" "oyða" "oyðdrepa" "oyðeta" "oyðidrepa" "oyðileggja" "oyðræna" "øða" "óðalsbýta" "pallseta" "pantseta" "parallellflyta" "pástanda" "pína" "píra" "pirkja" "plaga" "planleggja" "pløga" "pøsa" "prestvíga" "prísmerkja" "prístalsviðgera" "próvdøma" "próvføra" "prýða" "púrgera" "raka" "rakdoyggja" "ræla" "ráma" "ræma" "rána" "ræna" "rangsnúgva" "rangtýða" "rangvenda" "ránroyna" "ræsa" "ráskera" "rætta" "rættleiða" "rættlesa" "rættvísgera" "rátýða" "ráða" "ræða" "ráðagera" "ráðaleggja" "raðbinda" "raðfesta" "ráðføra" "ráðgeva" "ráðleggja" "raðsáa" "raðseta" "reglugera" "reinavelta" "reingja" "reinhalda" "reisa" "reita" "reiða" "reka" "rekja" "rekkja" "remba" "rena" "renda" "renna" "rigna" "ríkja" "rína" "rinda" "ringa" "ringja" "ringmerkja" 
"rippa" "rísa" "rista" "riststeikja" "ritbera" "ritstýra" "ríva" "riða" "ríða" "rógva" "røkja" "røkka" "rópa" "røra" "rósa" "rótbíta" "rótfesta" "roykja" "royna" "royndarflúgva" "royndarkoyra" "royra" "royta" "royva" "røða" "roðklæða" "rúka" "rúma" "rúnabinda" "rundfrysta" "rustviðgera" "ryggbróta" "ryggja" "rygna" "rykkja" "rylkja" "rýma" "rymja" "rýta" "rýða" "ryðja" "sáa" "saka" "sakfella" "sálargreina" "sálda" "sælda" "saltspreingja" "saltstroya" "sáma" "samanbera" "samanseta" "samarbeiða" "sambera" "sambinda" "sambreiða" "sameina" "samføra" "sammeta" "samrøða" "samskifta" "samsýna" "samsyrgja" "samtykja" "samtykkja" "sandblása" "sandstroya" "sannføra" "sannkenna" "sannroyna" "sannspyrja" "sannvísa" "særa" "sæta" "sætta" "savngoyma" "seigpína" "selja" "semja" 
"senda" "serbúgva" "sergreina" "sermerkja" "serútbúgva" "seta" "siga" "síga" "síggja" "sigla" "sita" "sitja" "sjálvdoyggja" "sjálvdrepa" "sjálvhvørva" "sjálvvirkja" "sjófesta" "sjóleggja" "sjóseta" "skáklesa" "skáksigla" "skákvenda" "skala" "skammbíta" "skammdeila" "skammfara" "skammleypa" "skammrósa" "skammskemma" "skammskera" "skapa" "skaða" "skefta" "skeina" "skeinkja" "skeita" "skelkja" "skella" "skelva" "skemma" "skera" "skerda" "skerja" "skerpa" "skifta" "skildøma" "skilja" "skilmeta" "skína" "skíra" "skíta" "skjalfesta" "skjálvtagráta" "skjaða" "skjóla" "skjóta" "skjúla" "skølseta" "skonkja" "skøra" "skørseta" "skortbróta" "skotkvøða" "skotloysa" "skotvelta" "skøva" "skoyna" "skoyta" "skøða" "skráloysa" "skræma" "skræpa" "skráseta" "skráskera" "skræða" "skreiða" "skreiðgera" "skrekkja" "skremja" "skreppa" "skreytbúgva" "skriftbera" "skriftfesta" "skríma" "skríða" "skroyva" "skrykkja" "skrýva" "skrýða" "skryðja" "skuldbinda" "skuldseta" "skundvaksa" "skúrseta" "skurðviðgera" "skvetta" "skvøtta" "skyggja" "skýla" "skylja" "skýma" "skynna" "skýra" 
"sláa" "slagbeita" "slaggreina" "slava" "sleikja" "sleingja" "sleppa" "sletta" "slíta" "sløga" "sløkkja" "sløða" "slúka" "smáflenna" "smágráta" "smálæa" "smámýla" "smástjala" "smæða" "smeita" "smella" "smíla" "smíða" "smoyggja" "smoykja" "smúgva" "smyrja" "snarfrysta" "sneiða" "snerkja" "snerpa" "snerta" "sníkja" "sniðgeva" "snjella" "snøggklippa" "snøra" "snoypa" "snúgva" "snýsa" "snýta" "søkja" "søkka" "søkkja" "sokna" "sólbrenna" "sølubjóða" "søma" "sova" "søva" "soyma" "soyra" "spáa" "spæla" "spara" "speireka" "spekja" "spenna" "sperra" "sperrubinda" "spilla" "spinna" "spjaða" "spøkja" "spónleggja" "sporleypa" "sporðleypa" "sporðreisa" "spottflenna" "spottlæa" "spottreka" "spræna" "spreingja" "spreiða" "spretta" "spríkja" "springa" "spýggja" "spyrja" "spyrna" "spýta" "stæla" "stáleypa" "stálherða" "stálseta" "standa" "stangarræða" "stara" "stavseta" "staðbinda" "staðdoyva" "staðfesta" "staðkenna" "staðseta" "steikja" "steindoyggja" "steindrepa" "steinfrysta" "steingja" "steinrenna" "steinseta" "steinskjóta" "steinsøkka" "steinsova" 
"steinstoyta" "stemma" "stevna" "stíga" "stigbenda" "stigbýta" "stigmerkja" "stílføra" "stilla" "stinga" "stinna" "stjala" "støddarskilja" "støkka" "støkkja" "støleypa" "stóreta" "stórgráta" "stórlæa" "stórleypa" "stovnfesta" "stovnseta" "støvsúgva" "stoypa" "stoyta" "støða" "stóðleypa" "strádoyggja" "streingja" "strekkja" "stríða" "stroya" "stroyma" "strúka" "stúra" "styggja" "stynja" "stýra" "styrkismeta" "styrkja" "stytta" "stýva" "súgva" "sundurbýta" "súpa" "súrleggja" "súðfella" "súðtekja" "svala" "svæva" "sveipa" "svevja" "svíkja" "svímasláa" "svimja" "svínabinda" "svinna" "svíva" "svíða" "svølgja" "svølla" "svølta" "svørja" "syfta" "sýla" "sýna" "syngja" "syrgja" "sýta" "taka" "talmerkja" "talseta" "tæna" "tapa" "tæra" "tætta" "teingja" "teinkja" "tekja" "tekkja" "telgja" "telja" "telva" "temba" "temja" "tenja" "tenna" "teppa" "tetta" "tilbiðja" "tilgeva" "tilhoyra" "tilmæla" "tilnevna" "tilreiða" "tilteinkja" "tíma" "tína" "tinglesa" "tinglýsa" "tippa" "tíðarfesta" "tíðarmerkja" "tíðarseta" "tjena" "tjóðartaka" "tola" "tøla" "tollgreiða" "tøma" 
"tómlæa" "tonkja" "tora" "tornakrýna" "tøta" "tøva" "toyggja" "toyta" "tøðbera" "tøðbreiða" "trábeita" "trábiðja" "tráganga" "trælabinda" "trælbinda" "trálesa" "trámæla" "tránýta" "trároyna" "tráspyrja" "trástara" "trásúgva" "træta" "trátroyta" "træða" "tráðdraga" "treffa" "treingja" "trekkja" "treskja" "trína" "trískera" "tríva" "trøllbinda" "trølltaka" "troysta" "troyta" "troða" "trøða" "trúgva" "trykkja" "trylla" "trýsta" "trýstviðgera" "turrleggja" "turroysa" "turrsleikja" "turrsúgva" "turrvinda" "turva" "tvætta" "tveita" "tvekkja" "tvíbýta" "tvíhalda" "tvílæsa" "tvíleypa" "tvíreisa" "tvískera" "tvíta" "tvítaka" "tvørbera" "tvørbinda" "tvørbróta" "tvørmýla" "tvørstoyta" "tvørvelta" "tyggja" "tykja" "týna" "tyngja" "tynna" "tyrpa" "tyrsta" "tyrva" "tysa" "tysja" "tysta" "týða" "uggaskera" "umbera" "umbiðja" "umbroyta" "umbyggja" "umferma" "umkoma" "umleggja" "ummæla" "umrøða" "umseta" "umsita" "umskapa" "umskera" "umvæla" "umvenda" 
"undangóðkenna" "undirbyggja" "undirgrava" "undirhalda" "undirkøla" "undirmeta" "undirsøkja" "undirstanda" "undirstinga" "undirvísa" "unna" "uppbyggja" "uppdeila" "uppdraga" "uppdríva" "uppfinna" "uppføra" "uppfylla" "uppihalda" "upplæra" "uppleggja" "upplíva" "upploysa" "upplýsa" "uppnevna" "uppraðfesta" "upprunagreina" "uppsiga" "uppstanda" "upptaka" "úrleiða" "úrløða" "úrnýta" "úrskilja" "útbetala" "útbúgva" "útbyggja" "útflýggja" "útflyta" "útføra" "útgalda" "útgeva" "útgreina" "úthella" "útihýsa" "útinna" "útkoma" "útlata" "útleggja" "útnevna" "útsenda" "útseta" "útstanda" "úttýða" "útveita" "útvinna" "vága" "vágbinda" "vaka" "vakja" "vaksa" "væla" "valda" "vanæra" "vanbjóða" "vanbýta" "vanmeta" "vanrøkja" "vanskapa" "vantrúgva" "vanvirða" "vara" "várbeita" "várreka" "varða" "varðveita" "væta" "vatnbera" "vatnblása" "vatnkøla" "vatnrenna" "vatnveita" "vegleggja" "vegleiða" "veikja" "veita" "veiða" "vekja" "veksa" "velja" "velkja" "vella" "velta" "venda" "venja" "vera" "verja" "verkja" "verma" "verpa" "verða" "vetrarføða" "veva" "vevja" 
"veðhalda" "veðseta" "víga" "víggirða" "víkja" "villa" "villleiða" "vinna" "virkja" "virða" "virðisløna" "virðismeta" "vísa" "vitnisføra" "viðganga" "viðgera" "viðkoma" "viðlíkahalda" "viðmæla" "viðmerkja" "viðtaka" "viðurbera" "viðurganga" "viðurkenna" "viðvíkja" "volda" "vongskerja" "ýa" "ýla" "ylja" "ymja" "yngja" "ynna" "ynskja" "ynta" "yppa" "yrkja" "ysta" "ýta" "ýva" "yvirbjóða" "yvirdríva" "yvirgeva" "yvirhalda" "yvirhoyra" "yvirmeta" "yvirseta" "yvirtaka" "yvirvinna" "yðja" "ýðja" ;
# remove "mella" due to "melda"

LIST VINFIN = Inf Sup PrfPtc PrsPtc ;


LIST DATV = "bjóða" "hýsa" "sleppa" "smakka" "takka" ; # "undirvísa" 
 # sleppa3 = sleppa manninum leysum
 
LIST DATPREPV = "liggja" "vera" "standa" "fiska" "vera"; # use only ACCPREPV
LIST ACCPREPV = "leggja" "fara" "renda" "koma" "liða" "seta" ;

LIST OBJPREDVERB = "kalla" "doypa" "nevna" "taka"  ;

LIST MOVEMENTVERB = "koma" "koyra" ;

SET TV = V - COPULA - MODV ;

# Noun-Verb sets

LIST THIRDSG = Sg 3Sg ;
LIST THIRDPL = Sg 3Pl ;
LIST THIRD = Sg 3Sg Pl 3Pl ;

# Number sets

SET NUMBERS = Num - ("eitt") - ("1") ;

# Preposition sets

LIST ACCPREP = "aftan" "aftanvert" "ábeint" "áraka" "foruttan" "gjøgnum" "hóast" "inntil" "ígjøgnum" "íkring" "kring" "niðan" "oman" "síðan" "umframt" "umhvørvis" "umkring" "báðumegin" "hasumegin" "hvørgumegin" "høgrumegin" "sínumegin" "skeivumegin" "somumegin" "vinstrumegin" "øðrumegin" "eystan" "norðan" "sunnan" "sum" "vestan" ;

LIST DATPREP = "hjá" "með" "aftrat" "afturat" "afturímóti" "andstøðis" "andsýnis" "frá" "mótvegis" "nær" "sambært" "viðvíkjandi" "úr" ; # úr?

LIST ACCDATPREP = "í" "á" "undir" "yvir" "eftir" "fyri" "við" ;

LIST ACCGENPREP = "innan" "uttan" "millum" "til" "vegna" ;

LIST ACCDATGENPREP = "at" "av" "hjá" "móti" "um" "undan" "úr" ;


SET SOMEACCPREP = ACCPREP | ACCDATPREP | ACCGENPREP | ACCDATGENPREP ;

SET SOMEDATPREP = DATPREP | ACCDATPREP | ACCDATGENPREP ;

SET SOMEGENPREP = ACCGENPREP | ACCDATGENPREP ;

SET SOMEACCDATPREP = ACCDATPREP | ACCDATGENPREP ;

# Boundary sets

SET S-BOUNDARY = CS | Interr | (";") | (":") | BOS | EOS ;
SET S-BOUNDARY2 = CS | Interr | (";") | (":") | BOS | EOS  ;

# Complementary set



# Case sets

# Some case, but not...
SET NOTNOM = Dat | Gen | Acc ;
SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

# Anything but the following case...
SET NOACC = WORD - Acc ;
SET NODAT = WORD - Dat ;
SET NOACCDAT = WORD - Acc - Dat ;


#SET NO

SET PRE-N =  A | Det | (N Gen) | Num | (Pron Gen) | CC ; # Det???


LIST COMMA = (",") ;
SET MARK =  COMMA | ("\\") | ("\;") ; #"

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;
SET PRE-APP   = COMMA OR PUNCT-LEFT ;


SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv - CC ; # adding CC
SET NOT-PROP = WORDMARK - Prop - CC ;
SET LEX-ADV = Adv - (A*) ;
SET NOT-A = WORDMARK - A ;
SET NOT-CC = WORDMARK - CC ;
SET NOUNADJ = N | A ;

SET NP-MEMBER = PRE-N | N | Pron ;

LIST TIME = "sunnudagur" ;



# Semantic sets


LIST ABSTRACT = "ráð" "byráð" ;

LIST SGMEASURE = "fótur"

LIST BAREPLURALS = "barn" "fólk" "bygdafólk" "konufólk" ;
# these words are usually plurals when used alone. All countable neuters?
#$ Tú sært barn standa.

SET NOT-1SG = WORD - 1Sg - (Prt Sg) ;


########################                  #########################
########################                  #########################
                        # disambiguation #
########################                  #########################
########################                  #########################

#!! Declaring all the error tags

LIST &lex-lat-laet = &lex-lat-laet ;
LIST &lex-liti-litid = &lex-liti-litid ;
LIST &lex-ta-tad = &lex-ta-tad ;
LIST &msyn-inf-sup = &msyn-inf-sup ;
LIST &msyn-nomindef-nomdef = &msyn-nomindef-nomdef ;
LIST &msyn-nsg-npl = &msyn-nsg-npl ;
LIST &msyn-prfptc-sg1 = &msyn-prfptc-sg1 ;
LIST &msyn-sup-inf = &msyn-sup-inf ;
LIST &msyn-supsup-supinf = &msyn-supsup-supinf ;
LIST &msyn-supinf-supsup = &msyn-supinf-supsup ;
LIST &msyn-sup-prspl = &msyn-sup-prspl ;
LIST &msyn-sup-sg1 = &msyn-sup-sg1 ;
LIST &msyn-imppl-sg1 = &msyn-imppl-sg1 ;

#!! # Rule section

SECTION

# Speller suggestions rule - make sure the suggestions survive the cg mangling:
ADD (&typo &SUGGESTWF) (<spelled>) ;


#!! ## Verbs


#!! ### Sg1 target forms

#!! ### **RULE:** Sup should be 1Sg
ADD:msyn-sup-sg1 (&msyn-sup-sg1) TARGET (V Sup) IF (*-1 ("eg" Pron Pers Sg Nom BARRIER NOT-ADV LINK -1 NOT-1SG));
COPY:msyn-sup-sg1 (Ind 1Sg &SUGGEST) EXCEPT (Sup &msyn-sup-sg1) TARGET (V Sup &msyn-sup-sg1) ;

## - Harumframt kiksaði ein týdningarmikil útgávukonsert, tí eg fingið ikki neyðuga visumið.
#$ - Harumframt kiksaði ein týdningarmikil útgávukonsert, tí eg fingi ikki neyðuga visumið.
## - Eg farið avstað.
#$ - Eg fari avstað.

#!! ### **RULE:** Sup  should be 1Sg
ADD:msyn-sup-sg1 (&msyn-sup-sg1) TARGET (V Sup) IF 
	(-1 ("eg" Pron Pers Sg Nom))
#	(NOT 0 A + Neu + Sg + Indef)
	(NOT 0 A + Neu + Sg + Indef OR Adv LINK *1 1Sg)
	(NOT -2 (V Prs) OR (V Prt));
COPY:msyn-sup-sg1 (Ind Prs 1Sg &SUGGEST) EXCEPT (Sup &msyn-sup-sg1) TARGET (V Sup &msyn-sup-sg1) ;



LIST noun-inf-lookalike = ("manna" N) ("ótti" N) ("endi" N) ("skúli" N) ("veiði" N) ("veiða" N) ("vilji" N) ("illgruni" N);

	
#!! ### **RULE:** sup > inf
ADD:msyn-sup-inf (&msyn-sup-inf) TARGET (V PrfPtc) IF
	(*-1 MODV + Ind 
		BARRIER NOT-ADV LINK NOT 0 Sup OR Pron + Indef + Msc + Sg + Nom
		LINK NOT *-1 ("hava") BARRIER S-BOUNDARY)
	(0 ("<(.*)að>"r))
	(NOT 0 noun-inf-lookalike OR ("virka")); # auxiliaries ok: kunnu virkað
COPY:msyn-sup-inf (Inf &SUGGEST) EXCEPT (PrfPtc Neu Pl Acc Indef &msyn-sup-inf) TARGET (V PrfPtc &msyn-sup-inf) ;
COPY:msyn-sup-inf (Inf &SUGGEST) EXCEPT (PrfPtc Neu Pl Nom Indef &msyn-sup-inf) TARGET (V PrfPtc &msyn-sup-inf) ;
COPY:msyn-sup-inf (Inf &SUGGEST) EXCEPT (PrfPtc Fem Sg Nom Indef &msyn-sup-inf) TARGET (V PrfPtc &msyn-sup-inf) ;
ADDRELATION:msyn-sup-inf ($2) MODV + Ind FROM (*1 V LINK 0 &msyn-sup-inf);
## - Eg vil komað.
#$ - Eg vil koma.

#"<dálkað>"
#	"dálka" V PrfPtc Fem Sg Nom Indef <W:0.0> &msyn-sup-inf ADD:361:msyn-sup-inf
#msyn-sup-inf
#	"dálka" V Fem Sg <W:0.0> Inf &SUGGEST ADD:361:msyn-sup-inf COPY:366:msyn-sup-inf
#dálka+V+Fem+Sg+Inf	?
#	"dálka" V Fem Sg Nom <W:0.0> Inf &SUGGEST ADD:361:msyn-sup-inf COPY:365:msyn-sup-inf
#dálka+V+Fem+Sg+Nom+Inf	?

ADD:msyn-supsup-supinf (&msyn-supsup-supinf) TARGET (V Sup) IF
	(*-2 MODV + Ind OR HAVA + Ind BARRIER NOT-ADV LINK NOT 0 Sup)
	(-1 Sup - COPULA - MODV - ("fáa"))
	(NOT -1 (N Neu Sg Nom Def))
	(0 ("<(.*)að>"r))
	(NOT 0 ("virka"));
COPY:msyn-supsup-supinf (Inf &SUGGEST) EXCEPT (Sup &msyn-supsup-supinf) TARGET (V &msyn-supsup-supinf) ;
ADDRELATION:msyn-supsup-supinf ($2) Sup FROM (1 V LINK 0 (&msyn-supsup-supinf));
## - Eg havi ikki kunnað kastað tann.
#$ -  Eg havi ikki kunnað kasta tann.


ADD:msyn-supinf-supsup (&msyn-supinf-supsup) TARGET (V Inf) IF
	(*-2 MODV + Ind OR HAVA + Ind BARRIER NOT-ADV LINK NOT 0 Sup)
	(-1 Sup - COPULA - MODV - ("fáa"))
	(NOT -1 (N Neu Sg Nom Def))
	(0 ("<(.*)að>"r));
COPY:msyn-supsup-supinf (Inf &SUGGEST) EXCEPT (Sup &msyn-supsup-supinf) TARGET (V &msyn-supsup-supinf) ;
ADDRELATION:msyn-supsup-supinf ($2) Sup FROM (1 V LINK 0 (&msyn-supsup-supinf));
## Fólk høvdu kunnað klára seg
#$ Fólk høvdu kunnað klárað seg


#PrfPtc Neu Pl Acc Indef

#!! ### **RULE:** Neu should be 1Sg
ADD:msyn-prfptc-sg1 (&msyn-prfptc-sg1) TARGET (V PrfPtc Neu Sg Nom Indef) IF
	(*-1 ("eg" Pron Pers Sg Nom ) BARRIER NOT-ADV LINK -1 BOS OR CS OR Adv OR PUNCT)
	(NOT 0 A + Neu + Sg + Indef OR Adv LINK *1 1Sg)
	;
COPY:msyn-prfptc-sg1 (Ind Prs 1Sg &SUGGEST) EXCEPT (PrfPtc Neu Sg Nom Indef &msyn-prfptc-sg1) TARGET (&msyn-prfptc-sg1) ;
## Eg takið tað.
#$ Eg taki tað.

#!! ### **RULE:** Imp Pl should be 1Sg
ADD:msyn-imppl-sg1 (&msyn-imppl-sg1) TARGET (V Imp Pl) IF (*-1 ("eg" Pron Pers Sg Nom ) BARRIER NOT-ADV LINK -1 BOS OR CS OR Adv OR PUNCT);
COPY:msyn-imppl-sg1 (Ind Prs 1Sg &SUGGEST) EXCEPT (Imp Pl &msyn-imppl-sg1) TARGET (V &msyn-imppl-sg1) ;
## Eg lesið tað.
#$ Eg lesi tað.


#!! ### Plural forms

#!! ### **RULE:** Sup should be Pl -- marginal??
ADD:msyn-sup-prspl (&msyn-sup-prspl) TARGET (V Sup) IF
	(*-1 ("eg") + Pron + Pers + Pl + Nom BARRIER NOT-ADV LINK NOT -1 V + Pl)
	(0 ("<(.*)að>"r))
	;
COPY:msyn-sup-prspl (Ind Prs Pl &SUGGEST) EXCEPT (Sup &msyn-sup-prspl) TARGET (V Sup &msyn-sup-prspl) ;

# Vit skrivað til tín í dag um hetta málið.
#$ Vit skriva til tín í dag um hetta málið.

#!! ### **RULE:** Sup should be Pl -- marginal??
ADD:msyn-sup-prspl (&msyn-sup-prspl) TARGET (V Sup) IF
	(*1 ("eg") + Pron + Pers + Pl + Nom BARRIER NOT-ADV LINK NOT -1 V + Pl);
COPY:msyn-sup-prspl (Ind Prs Pl &SUGGEST) EXCEPT (Sup &msyn-sup-prspl) TARGET (V Sup &msyn-sup-prspl) ;



#!! ### Supine forms

#!! ### **RULE:**s for Pl should be Sup are not written






#!! ### **RULE:** Inf should be Sup
ADD:msyn-inf-sup (&msyn-inf-sup) TARGET (V) IF
	(*-1 ("hava" V Ind) BARRIER NOT-ADV OR PrfPtc OR ("at") OR CC) # Linda: Warum: OR ("at"). Weil wir brauchen es. T.
	(0 Ind + Prs + Pl OR Inf)
	(NOT 0 ("<[A-ZÄÖÅÁÉIÓÚ].*>"r) OR Pass OR Sup OR Adv OR NOT-AD-SUP OR A + Acc OR N + Acc OR noun-inf-lookalike OR ("verða"))
	(NEGATE 0 A + $$NAGD LINK 1 N + $$NAGD) # norska toskakvotan
	;

#$ ... at virða tær grundleggjandi reglur, ið náttúran og tað náttúruskapta umhvørvið hava at laga seg eftir. # at !

#!! ### **RULE:** Inf should be Sup
ADD:msyn-inf-sup (&msyn-inf-sup) TARGET (Inf) IF
	(*-1 ("hava" V Ind) BARRIER NOT-ADV OR CC OR ("at"))
	(NOT 0 ("<[A-ZÄÖÅÁÉIÓÚ].*>"r) OR Pass OR Sup OR Adv OR NOT-AD-SUP OR ("verða"))
	(NEGATE 0 NOT-AD-SUP LINK NOT 1 Sup) 
	(NEGATE 0 A LINK 1 N)
	;
COPY:msyn-inf-sup (Sup &SUGGEST) EXCEPT (Ind Prs Pl &msyn-inf-sup) TARGET (Ind &msyn-inf-sup) ;
COPY:msyn-inf-sup (Sup &SUGGEST) EXCEPT (Inf &msyn-inf-sup) TARGET (Inf &msyn-inf-sup) ;

## Hava vit skriva til tín í dag um hetta málið?
#$ Hava vit skrivað til tín í dag um hetta málið?

#!! ### **RULE:** Inf should be Sup
ADD:msyn-inf-sup (&msyn-inf-sup) TARGET V + Inf IF 
	(*-1 ("eg" Pron Pers Pl Nom) BARRIER NOT-ADV OR ("at") LINK -1 ("hava" V Ind)) 
	(NOT 0 Pass OR ("verða") OR A + Acc OR N + Acc)
	(NOT 1 Sup) 
#	(NOT *1 Sup BARRIER CC ) 
	;
ADD:msyn-inf-sup (&msyn-inf-sup) TARGET V + Ind + Prs + Pl IF 
	(*-1 ("eg" Pron Pers Pl Nom) BARRIER NOT-ADV OR ("at") LINK -1 ("hava" V Ind)) 
	(NOT 0 Pass OR ("verða") OR A + Acc OR N + Acc)
	(NOT 1 Sup) 
#	(NOT *1 Sup BARRIER CC ) 
	;
COPY:msyn-inf-sup (Sup &SUGGEST) EXCEPT (Ind Prs Pl &msyn-inf-sup) TARGET (V &msyn-inf-sup) ;


#!! ## Specific verbs

#!! ### **RULE:** Past tens of láta is læt not lat
#ADD:lex-lat-laet (&lex-lat-laet) TARGET ("lata" V Imp Sg) IF (-1 Adv OR Nom) ;
#COPY:lex-lat-laet ("láta" V Ind Prt 3Sg &SUGGEST) EXCEPT ("lata" V Imp Sg &lex-lat-laet) TARGET ("lata");
## Tað lat stygt.
## Tað læt stygt.

# Problem: láta - letur - læt - látu  men lata - letur - læt - lótu (la)t.
# "lata" V Imp Sg = "lata" V Prt 1Sg 2Sg 3Sg = lat
# Vi klarer ikkje skilje láta frå lata.

#!! ## Nouns


#!! ### Definiteness

#!! ### **RULE:** Neu Indef should be Neu Def
#!! We turn off this rule for now, it is too hard to avoid false alarms.
#ADD:msyn-nomindef-nomdef (&msyn-nomindef-nomdef) TARGET (N Neu Sg Dat Indef) IF
#	(NOT 0 Prop)
#	(-1 ("at"))
#	(1 VFIN OR CS OR Pr) ;
#COPY:msyn-nomindef-nomdef (Nom Def &SUGGEST) EXCEPT (Dat Indef &msyn-nomindef-nomdef) TARGET (N Neu &msyn-nomindef-nomdef) ;
## .. fyri at samfelagi skal fylgja altjóða menningini.
#$ .. fyri at samfelagið skal fylgja altjóða menningini.

## at skifti millum útbúgvingarskiparnirnar er væl skipað.
#$ at skiftið millum útbúgvingarskiparnirnar er væl skipað.


#!! ### Quantor phrases

#!! ### **RULE:** Num + N Sg should be Num + N Pl
#ADD:msyn-nsg-npl (&msyn-nsg-npl) TARGET N  IF
#	(*-1 Num BARRIER NOT-A LINK NOT 0 ("ein") OR ("eini") OR ("einir") OR ("eitt") OR ("1") OR Sem/Year OR Sem/ID OR Rom OR Sem/Time-clock)
#	(0 (N Msc Sg Acc Indef) LINK NOT 0 Pl OR SGMEASURE) ;
#COPY:msyn-nsg-npl (Pl Acc Indef  &SUGGEST) EXCEPT (Sg Acc Indef &msyn-nsg-npl) TARGET (N Msc &msyn-nsg-npl) ;
#ADDRELATION:msyn-nsg-npl ($2)  (&msyn-nsg-npl)  TO (*-1 Num) ; # this ADDRELATION rule is to be able to refer to two words to the user.
##  6 skipara hljópu fyrir borð með kistur sinar ok våru allir drepnir
#$  6 skiparar hljópu fyrir borð með kistur sinar ok våru allir drepnir

#!! Num + N Sg should be Num + N Pl (We need arabic tag here)
#ADD:msyn-nsg-npl (&msyn-nsg-npl) TARGET (N Msc Pl Gen Indef) IF (*-1 Num BARRIER NOT-A LINK NOT 0 ("ein") OR ("eitt") OR ("1") OR Sem/Year); # (NOT *-1 Num LINK -1 SOMEGENPREP);
#COPY:msyn-nsg-npl (Nom Indef  &SUGGEST) EXCEPT (Gen Indef &msyn-nsg-npl) TARGET (N Msc Pl &msyn-nsg-npl) ;
#ADDRELATION:msyn-nsg-npl ($2)  (&msyn-nsg-npl)  TO (*-1 Num) ; 
# Gen Pl?? Denne forstår eg ikkje logikken attom.


#!! ## Subjunctives

#!! Nothing here.

## Tey, ið hava manna arbeiðsbólkin, eru:
#$ Tey, ið hava mannað arbeiðsbólkin, eru:

#!! ## ta / tað rules

#!! ### **RULE:** ta should be tað
ADD:lex-ta-tad (&lex-ta-tad) TARGET (<ta> "tann" Pron Dem Fem Sg Acc) IF (-1 (Ind))(1 Det OR Pron)(NOT 0 ("<tí>"r)) ;
COPY:lex-ta-tad ("tað" Det Neu Sg Nom &SUGGEST) EXCEPT ("tann" Pron Dem Fem Sg Acc &lex-ta-tad) TARGET (&lex-ta-tad) ;

## Er ta onkur sum hevur eina troyggju til føroyskan búna til sølu? 
#$ Er tað onkur sum hevur eina troyggju til føroyskan búna til sølu?


#!! ## Adjectives

#!! ### **RULE:** líti should be lítið
ADD:lex-liti-litid (&lex-liti-litid) TARGET ("líta" V Ind Prs 1Sg) IF (-1 Det OR Adv OR Pr)(1 N + Neu + Indef);
COPY:lex-liti-litid ("lítil" A Neu Sg Nom Indef &SUGGEST) EXCEPT ("líta" V Ind Prs 1Sg &lex-litid) TARGET (&lex-liti-litid) ;

## at ov líti samfesti er í verandi vegleiðingarskipan.
#$ at ov lítið samfesti er í verandi vegleiðingarskipan.



# Local Variables:
# cg-pre-pipe: "sh modes/faogram-disam.mode"
# End:
