# ===================== #
# Faroese grammarchecker
# ===================== #

# ---------------------- #
#   Development setup:   #
# ---------------------- #
#
# cd $GTHOME/langs/fao
# ./autogen.sh
# ./configure --enable-apertium --with-hfst --enable-syntax --enable-grammarchecker --enable-tokenisers --enable-alignment --enable-reversed-intersect
# make
# cd tools/grammarcheckers
# make dev
#
# ---------------------- #
#   Then edit/test as:   #
# ---------------------- #
#
# emacs grammarchecker.cg3  # and C-c C-i / C-c C-c if you have cg-mode installed
# echo "Eg farið avstað." | sh modes/faogram.mode  # from the terminal
#
# ---------------------- #
#   Other debug-pipes:   #
# ---------------------- #
#
# ls modes/
#
# ---------------------- #

# echo "Hei." | hfst-tokenise -g ../tokenisers/tokeniser-gramcheck-gt-desc.pmhfst | vislcg3 -g disambiguator.cg3 -t | vislcg3 -g grammarchecker.cg3 -t  


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<¶>" sent ;



# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

LIST N = N ;
LIST V = V ;
LIST A = A ;
LIST A* = A* ;
LIST Prop = Prop ;
LIST Adv = Adv ;
LIST Num = Num ;
LIST CC = CC ;
LIST CS = CS ;
LIST Interj = Interj ;
LIST Abbr = Abbr ;
LIST ACR = ACR ;
LIST Pr = Pr ;
LIST Pron = Pron ;
LIST Pers = Pers ;
LIST Det = Det ;
LIST Dem = Dem ;
LIST Refl = Refl ;
LIST Recipr = Recipr ;
LIST Poss = Poss ;
LIST Interr = Interr ;
LIST IM = IM ;

LIST Nom = Nom ;
LIST Acc = Acc ;
LIST Gen = Gen ;
LIST Dat = Dat ;
LIST Msc = Msc ;
LIST Fem = Fem ;
LIST Neu = Neu ;
LIST Sg = Sg ;
LIST Pl = Pl ;
LIST Def = Def ;
LIST Indef = Indef ;
LIST Cmp = Cmp ;
LIST Superl = Superl ;
LIST Prs = Prs ;
LIST Prt = Prt ;
LIST 1Sg = 1Sg ;
LIST 2Sg = 2Sg ;
LIST 3Sg = 3Sg ;
LIST Inf = Inf ;
LIST PrfPrc = PrfPrc ;
LIST PrsPrc = PrsPrc ;
LIST Ind = Ind ;
LIST Imp = Imp ;
LIST Pass = Pass ;
LIST Sbj = Sbj ;
LIST Sup = Sup ;
LIST Cmpnd = Cmpnd ;
LIST CLB = CLB ;
LIST PUNCT = PUNCT ;
LIST LEFT = LEFT ;
LIST RIGHT = RIGHT ;
LIST Guess = Guess ;

LIST Sem/Fem = Sem/Fem ;
LIST Sem/Mal = Sem/Mal ;
LIST Sem/Plc = Sem/Plc ;
LIST Sem/Sur = Sem/Sur ;
LIST Sem/Org = Sem/Org ;
LIST Sem/Veh = Sem/Veh ;
LIST Sem/Year = Sem/Year ;

LIST TAG = N V A Adv CC CS Interj Pr Pron Pers Det Refl Recipr Poss Nom Acc Gen
Dat Msc Fem Neu Sg Pl Def Indef Cmp Sup Prs Prt 1Sg 2Sg 3Sg Inf PrfPrc
PrsPrc Sup Imp CLB PUNCT LEFT RIGHT Sem/Fem Sem/Mal Sem/Plc Sem/Sur Sem/Org Sem/Veh Guess ;


LIST NAGD = Nom Acc Gen Dat ;
LIST AGD = Acc Gen Dat ;
LIST GENDER = Msc Fem Neu ;
LIST NUMBER = Sg Pl ;
SET NAGDNUMBERGENDER = (Msc Sg Nom) OR (Msc Sg Acc) OR (Msc Sg Gen) OR (Msc Sg Dat) OR (Msc Pl Nom) OR (Msc Pl Acc) OR (Msc Pl Gen) OR (Msc Pl Dat) OR (Fem Sg Nom) OR (Fem Sg Acc) OR (Fem Sg Gen) OR (Fem Sg Dat) OR (Fem Pl Nom) OR (Fem Pl Acc) OR (Fem Pl Gen) OR (Fem Pl Dat) OR (Neu Sg Nom) OR (Neu Sg Acc) OR (Neu Sg Gen) OR (Neu Sg Dat) OR (Neu Pl Nom) OR (Neu Pl Acc) OR (Neu Pl Gen) OR (Neu Pl Dat) ;


# Sets
# ====

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;


# Noun sets


SET NounMscFem = (N Msc) OR (N Fem) ;
SET NounMscNeu = (N Msc) OR (N Neu) ;
SET NounFemNeu = (N Fem) OR (N Neu) ;

SET MscFem = Msc OR Fem ;
SET MscNeu = Msc OR Neu ;
SET FemNeu = Fem OR Neu ;

LIST 3PERS = "hann" "hon" "tað" ;

# Adjective sets

SET REALADJ = A OR A* ;


# Nominal sets

LIST NOMINALHEAD = N Num Pers Refl Recipr "Pron Interr" "Pron Indef" ;

LIST NorA = N A ;

LIST PRENAGR = Det A ;

LIST MIDJA = "miðja" ; # in the middle of - construction. More N in here, also bottle constr.

SET NOT-ACC = TAG - Acc ;


# Verb sets

LIST COPULA = "vera" "verða" ;

LIST HAVA = "hava" ;

LIST MODV = "kunna" "láta" "skula" "vilja" "munna" "mega" ;

SET AUX = COPULA OR HAVA OR MODV ;

LIST VFIN = Prs Prt Imp Sbj ;
LIST INDSBJ = Ind Sbj ;
#LIST Ind = Prs Prt ;

LIST NONTHIRDV = 1Sg 2Sg 1Pl 2Pl ;

LIST VINFIN = Inf Sup PrfPrs PrsPrc ;


LIST DATV = "bjóða" "hýsa" "sleppa" "smakka" "takka" ; # "undirvísa" 
 # sleppa3 = sleppa manninum leysum
 
LIST DATPREPV = "liggja" "vera" "standa" "fiska" "vera"; # use only ACCPREPV
LIST ACCPREPV = "leggja" "fara" "renda" "koma" "liða" "seta" ;

LIST OBJPREDVERB = "kalla" "doypa" "nevna" "taka"  ;

LIST MOVEMENTVERB = "koma" "koyra" ;

SET TV = V - COPULA - MODV ;

# Noun-Verb sets

LIST THIRDSG = Sg 3Sg ;
LIST THIRDPL = Sg 3Pl ;
LIST THIRD = Sg 3Sg Pl 3Pl ;

# Number sets

SET NUMBERS = Num - ("eitt") - ("1") ;

# Preposition sets

LIST ACCPREP = "aftan" "aftanvert" "ábeint" "áraka" "foruttan" "gjøgnum" "hóast" "inntil" "ígjøgnum" "íkring" "kring" "niðan" "oman" "síðan" "umframt" "umhvørvis" "umkring" "báðumegin" "hasumegin" "hvørgumegin" "høgrumegin" "sínumegin" "skeivumegin" "somumegin" "vinstrumegin" "øðrumegin" "eystan" "norðan" "sunnan" "sum" "vestan" ;

LIST DATPREP = "hjá" "með" "aftrat" "afturat" "afturímóti" "andstøðis" "andsýnis" "frá" "mótvegis" "nær" "sambært" "viðvíkjandi" "úr" ; # úr?

LIST ACCDATPREP = "í" "á" "undir" "yvir" "eftir" "fyri" "við" ;

LIST ACCGENPREP = "innan" "uttan" "millum" "til" "vegna" ;

LIST ACCDATGENPREP = "at" "av" "hjá" "móti" "um" "undan" "úr" ;


SET SOMEACCPREP = ACCPREP | ACCDATPREP | ACCGENPREP | ACCDATGENPREP ;

SET SOMEDATPREP = DATPREP | ACCDATPREP | ACCDATGENPREP ;

SET SOMEGENPREP = ACCGENPREP | ACCDATGENPREP ;

SET SOMEACCDATPREP = ACCDATPREP | ACCDATGENPREP ;

# Boundary sets

SET S-BOUNDARY = CS | Interr | (";") | (":") | BOS | EOS ;
SET S-BOUNDARY2 = CS | Interr | (";") | (":") | BOS | EOS  ;

# Complementary set



# Case sets

# Some case, but not...
SET NOTNOM = Dat | Gen | Acc ;
SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

# Anything but the following case...
SET NOACC = WORD - Acc ;
SET NODAT = WORD - Dat ;
SET NOACCDAT = WORD - Acc - Dat ;


#SET NO

SET PRE-N =  A | Det | (N Gen) | Num | (Pron Gen) | CC ; # Det???


LIST COMMA = (",") ;
SET MARK =  COMMA | ("\\") | ("\;") ; #"

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;
SET PRE-APP   = COMMA OR PUNCT-LEFT ;


SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv - CC ; # adding CC
SET NOT-PROP = WORDMARK - Prop - CC ;
SET LEX-ADV = Adv - (A*) ;
SET NOT-A = WORDMARK - A ;
SET NOT-CC = WORDMARK - CC ;
SET NOUNADJ = N | A ;

SET NP-MEMBER = PRE-N | N | Pron ;

LIST TIME = "sunnudagur" ;



# Semantic sets


LIST ABSTRACT = "ráð" "byráð" ;

LIST BAREPLURALS = "barn" "fólk" "bygdafólk" "konufólk" ;
# these words are usually plurals when used alone. All countable neuters?
#$ Tú sært barn standa.

SET NOT-1SG = WORD - 1Sg - (Prt Sg) ;


########################                  #########################
########################                  #########################
                        # disambiguation #
########################                  #########################
########################                  #########################

#!! Declaring all the error tags

LIST &msyn-sg1 = &msyn-sg1 ;
LIST &msyn-prspl = &msyn-prspl ;
LIST &msyn-nomdef = &msyn-nomdef ;
LIST &msyn-sup = &msyn-sup ;
LIST &msyn-npl = &msyn-npl ;
LIST &lex-laet = &lex-laet ;
LIST &lex-tad = &lex-tad ;


#!! !!!Rule section

SECTION

# Speller suggestions rule - make sure the suggestions survive the cg mangling:
ADD (&typo &SUGGESTWF) (<spelled>) ;


#!! !!Verbs


#!! !Sg1 target forms

#!! Sup should be 1Sg
ADD:msyn-sg1 (&msyn-sg1) TARGET (V Sup) IF (*-1 ("eg" Pron Pers Sg Nom BARRIER NOT-ADV LINK -1 NOT-1SG));
COPY:msyn-sg1 (Ind 1Sg &SUGGEST) EXCEPT (Sup &msyn-sg1) TARGET (V Sup &msyn-sg1) ;

## - Harumframt kiksaði ein týdningarmikil útgávukonsert, tí eg fingið ikki neyðuga visumið.
#$ - Harumframt kiksaði ein týdningarmikil útgávukonsert, tí eg fingi ikki neyðuga visumið.
## - Eg farið avstað.
#$ - Eg fari avstað.

#!! Sup  should be 1Sg
ADD:msyn-sg1 (&msyn-sg1) TARGET (V Sup) IF (-1 ("eg" Pron Pers Sg Nom))(NOT -2 (Prt Sg) OR 1Sg);
COPY:msyn-sg1 (Ind Prs 1Sg &SUGGEST) EXCEPT (Sup &msyn-sg1) TARGET (V Sup &msyn-sg1) ;



#!! Neu should be 1Sg
ADD:msyn-sg1 (&msyn-sg1) TARGET (N Neu Sg Nom Def) IF (*-1 ("eg" Pron Pers Sg Nom ) BARRIER NOT-ADV LINK -1 BOS OR CS OR Adv OR PUNCT);
COPY:msyn-sg1 (V Ind Prs 1Sg &SUGGEST) EXCEPT (N Neu Sg Nom Def &msyn-sg1) TARGET (&msyn-sg1) ;
## Eg takið tað.
#$ Eg taki tað.

#!! Imp Pl should be 1Sg
ADD:msyn-sg1 (&msyn-sg1) TARGET (V Imp Pl) IF (*-1 ("eg" Pron Pers Sg Nom ) BARRIER NOT-ADV LINK -1 BOS OR CS OR Adv OR PUNCT);
COPY:msyn-sg1 (Ind Prs 1Sg &SUGGEST) EXCEPT (Imp Pl &msyn-sg1) TARGET (V &msyn-sg1) ;
## Eg lesið tað.
#$ Eg lesi tað.


#!! !Plural forms
#!! Sup should be Pl
ADD:msyn-prspl (&msyn-prspl) TARGET (V Sup) IF (*-1 ("eg") + Pron + Pers + Pl + Nom BARRIER NOT-ADV LINK NOT -1 V + Pl);
COPY:msyn-prspl (Ind Prs Pl &SUGGEST) EXCEPT (Sup &msyn-prspl) TARGET (V Sup &msyn-prspl) ;

## Vit skrivað til tín í dag um hetta málið.
#$ Vit skriva til tín í dag um hetta málið.

#$ Hava vit skrivað til tín í dag um hetta málið?



#!! !Supine forms

#!! Inf should be Sup
ADD:msyn-sup (&msyn-sup) TARGET (Inf) IF (-1 ("hava" V Ind))(NOT 0 Pass)(NOT *1 Sup)(NOT 0 A LINK 1 N);
COPY:msyn-sup (Sup &SUGGEST) EXCEPT (Inf &msyn-sup) TARGET (&msyn-sup) ;


#!! !!Specific verbs

#!! Past tens of láta is læt not lat
ADD:lex-laet (&lex-laet) TARGET ("lata" V Imp Sg) IF (-1 Adv OR Nom) ;
COPY:lex-laet ("láta" V Ind Prt 3Sg &SUGGEST) EXCEPT ("lata" V Imp Sg &lex-laet) TARGET ("lata");
## Tað lat stygt.
## Tað læt stygt.

#!! !!Nouns


#!! !Definiteness

#!! Neu Indef should be Neu Def
ADD:msyn-nomdef (&msyn-nomdef) TARGET (N Neu Sg Dat Indef) IF (NOT 0 Prop)(-1 BOS or CS)(1 VFIN OR CS OR Pr) ;
COPY:msyn-nomdef (Nom Def &SUGGEST) EXCEPT (Dat Indef &msyn-nomdef) TARGET (N Neu &msyn-nomdef) ;
## .. fyri at samfelagi skal fylgja altjóða menningini.
#$ .. fyri at samfelagið skal fylgja altjóða menningini.

#$ at skifti millum útbúgvingarskiparnirnar er væl skipað.
#$ at skiftið millum útbúgvingarskiparnirnar er væl skipað.


#!! !Quantor phrases

#!! Num + N Sg should be Num + N Pl
ADD:msyn-npl (&msyn-npl) TARGET N  IF (*-1 Num BARRIER NOT-A LINK NOT 0 ("ein") OR ("eitt") OR ("1") OR Sem/Year)(0 (N Msc Sg Acc Indef) LINK NOT 0 Pl) ;
COPY:msyn-npl (Pl Acc Indef  &SUGGEST) EXCEPT (Sg Acc Indef &msyn-npl) TARGET (N Msc &msyn-npl) ;
ADDRELATION:msyn-npl ($2)  (&msyn-npl)  TO (*-1 Num) ; # this ADDRELATION rule is to be able to refer to two words to the user.
##  6 skipara hljópu fyrir borð með kistur sinar ok våru allir drepnir
#$  6 skiparar hljópu fyrir borð með kistur sinar ok våru allir drepnir

#!! Num + N Sg should be Num + N Pl (We need arabic tag here)
ADD:msyn-npl (&msyn-npl) TARGET (N Msc Pl Gen Indef) IF (*-1 Num BARRIER NOT-A LINK NOT 0 ("ein") OR ("eitt") OR ("1") OR Sem/Year); # (NOT *-1 Num LINK -1 SOMEGENPREP);
COPY:msyn-npl (Nom Indef  &SUGGEST) EXCEPT (Gen Indef &msyn-npl) TARGET (N Msc Pl &msyn-npl) ;
ADDRELATION:msyn-npl ($2)  (&msyn-npl)  TO (*-1 Num) ; 



#!! !!Subjunctives

## Tey, ið hava manna arbeiðsbólkin, eru:
#$ Tey, ið hava mannað arbeiðsbólkin, eru:

#!! ta should be tað
ADD:lex-tad (&lex-tad) TARGET ("tann" Pron Dem Fem Sg Acc) IF (-1 (Ind))(1 Det OR Pron)(NOT 0 ("tí" CS)) ;
COPY:lex-tad ("tað" Det Neu Sg Nom &SUGGEST) EXCEPT ("tann" Pron Dem Fem Sg Acc &lex-tad) TARGET (&lex-tad) ;

## Er ta onkur sum hevur eina troyggju til føroyskan búna til sølu?
#$ Er tað onkur sum hevur eina troyggju til føroyskan búna til sølu?


#!! !!Adjectives

#!! líti should be lítið
ADD:lex-litid (&lex-litid) TARGET ("líta" V Ind Prs 1Sg) IF (-1 Det OR Adv OR Pr)(1 N + Neu + Indef);
COPY:lex-litid ("lítil" A Neu Sg Nom Indef &SUGGEST) EXCEPT ("líta" V Ind Prs 1Sg &lex-litid) TARGET (&lex-litid) ;

## at ov líti samfesti er í verandi vegleiðingarskipan.
#$ at ov lítið samfesti er í verandi vegleiðingarskipan.



# Local Variables:
# cg-pre-pipe: "sh modes/faogram-disam.mode"
# End:
